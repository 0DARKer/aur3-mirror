# Maintainer: Perberos <perberos@gmail.com>
pkgname=mate-screensaver
pkgver=20120309
pkgrel=1
pkgdesc='Screensaver for MATE'
arch=('i686' 'x86_64')
url="https://github.com/perberos/mate-screensaver"
license=('GPL')
depends=('gtk2' 'dbus' 'mate-conf' 'mate-desktop')
makedepends=('pkgconfig' 'intltool' 'mate-doc-utils')
options=('!libtool' '!emptydirs')
source=(mate-screensaver.pam)
install=mate-screensaver.install
sha256sums=('2744d1fc39da46fc681b7cf1c9230d2035b2bb8d9f510213fbe439c114eb76a2')

_gitroot=git://github.com/mate-desktop/mate-screensaver.git
_gitname=mate-screensaver

build() {
	cd "$srcdir"
	msg "Connecting to GIT server...."

	if [[ -d "$_gitname" ]]; then
		cd "$_gitname" && git pull origin
		msg "The local files are updated."
	else
		git clone "$_gitroot" "$_gitname"
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting build..."

	rm -rf "$srcdir/$_gitname-build"
	git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
	cd "$srcdir/$_gitname-build"


	./autogen.sh \
		--prefix=/usr --sysconfdir=/etc \
		--mandir=/usr/share \
		--disable-static \
		--with-xscreensaverdir=/usr/share/xscreensaver/config \
		--with-xscreensaverhackdir=/usr/lib/xscreensaver \
		--with-mit-ext \
		--libexecdir=/usr/lib/${pkgname} || return 1

	make || return 1
}

package() {
	cd "$srcdir/$_gitname-build"

	make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install || return 1
	
	install -m644 "${srcdir}/mate-screensaver.pam" "${pkgdir}/etc/pam.d/mate-screensaver"
	
	install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
	mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
	rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
