# Contributor: Evangelos Foutras <foutrelis@gmail.com>

pkgname=avatar-factory
pkgver=0.8
pkgrel=1
pkgdesc="A bash script that creates eye candy shortcuts for music albums, photo albums, movie files, and more"
arch=('i686' 'x86_64')
url="https://launchpad.net/avatar-factory/"
license=('custom')
depends=('imagemagick' 'zenity' 'desktop-file-utils')
install=avatar-factory.install
source=(http://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.bz2
        COPYING
        avatar-factory.1)
md5sums=('f35a8046ad888fa1540d7e4a49e4bd6a'
         '780f3968fbcf3de67ef4b4ac4ffbde39'
         'c257cac793373fabfe7c7bc1b67c3740')

build() {
  cd "$srcdir"

  find Avatar-Factory -type f | while read f; do
    target=$(echo $f | sed 's|^[^/]*/||g')
    install -D -m644 "$f" "$pkgdir/usr/share/$pkgname/$target"
  done
  chmod +x "$pkgdir/usr/share/$pkgname/music-widget"
  find "$pkgdir/usr/share/$pkgname/themes" -maxdepth 1 -type f -exec chmod +x {} \;

  install -D -m644 Avatar-Factory/avatar-factory.png \
                   "$pkgdir/usr/share/pixmaps/avatar-factory.png"
  install -D -m644 avatar-factory.desktop \
                   "$pkgdir/usr/share/applications/avatar-factory.desktop"
  sed -i "$pkgdir/usr/share/applications/avatar-factory.desktop" \
      -e '/^Encoding/d' \
      -e 's/^Icon=.*/Icon=avatar-factory/'

  install -D -m644 avatar-factory.1 "$pkgdir/usr/share/man/man1/avatar-factory.1"
  install -D -m755 avatar-factory "$pkgdir/usr/bin/avatar-factory"
  sed 's|usr/local|usr|g' -i "$pkgdir/usr/bin/avatar-factory" \
                          -i "$pkgdir/usr/share/$pkgname/music-widget"

  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
