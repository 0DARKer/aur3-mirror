# Contributor: Iru Cai <mytbk920423@gmail.com>
pkgname=archbest-mod1
pkgver=2.8
pkgrel=3
pkgdesc="The Arch is the best program produces a arch-friendly greeting, English and Chinese only"
arch=('i686' 'x86_64')
url="https://wiki.archlinux.org/index.php/Arch_is_the_best"
license=('custom:WTFPL')
depends=('glibc')
source=("http://mirrors.tuna.tsinghua.edu.cn/gnu/hello/hello-${pkgver}.tar.gz"
        "archbest_en_zh.patch")
md5sums=('6a67cbbbc0420061ef938a9a2736fbd6'
    'cb333b557a3b93510f120b0a3e148931')

INSTALL_PREFIX=/opt/archbest

build() {
  cd "${srcdir}/hello-${pkgver}"
  patch -Np1 -i ../archbest_en_zh.patch
  cd po/
  for LANG in zh_CN zh_TW
  do
    msgfmt -o ${LANG}.gmo ${LANG}.po
  done
  cd ..
  ./configure --prefix=${INSTALL_PREFIX}
  make
}

package() {
  cd "${srcdir}/hello-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}/${INSTALL_PREFIX}/share/info/dir"
  install -d ${pkgdir}/usr/bin
  ln -svf ${INSTALL_PREFIX}/bin/hello ${pkgdir}/usr/bin/archbest
}

# vim:set ts=2 sw=2 et:
