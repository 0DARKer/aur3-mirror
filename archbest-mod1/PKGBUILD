# Contributor: Iru Cai <mytbk920423@gmail.com>
pkgname=archbest-mod1
pkgver=2.8
pkgrel=2
pkgdesc="The Arch is the best program produces a arch-friendly greeting, English and Chinese only"
arch=('i686' 'x86_64')
url="https://wiki.archlinux.org/index.php/Arch_is_the_best"
license=('custom:WTFPL')
depends=('glibc')
source=("http://mirrors.tuna.tsinghua.edu.cn/gnu/hello/hello-${pkgver}.tar.gz"
        "archbest_en_zh.patch")
md5sums=('6a67cbbbc0420061ef938a9a2736fbd6'
    'cb333b557a3b93510f120b0a3e148931')

build() {
  cd "${srcdir}/hello-${pkgver}"
  patch -Np1 -i ../archbest_en_zh.patch
  cd po/
  for LANG in zh_CN zh_TW
  do
    msgfmt -o ${LANG}.gmo ${LANG}.po
  done
  cd ..
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/hello-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}/usr/share/info/dir"
  mv ${pkgdir}/usr/bin/hello ${pkgdir}/usr/bin/archbest
}

# vim:set ts=2 sw=2 et:
