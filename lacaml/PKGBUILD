# Maintainer: Sergei Lebedev <superbobry@gmail.com>
# Contributor: Kostas Andreadis <kmandreadis@gmail.com>

pkgname=lacaml
pkgver=6.0.1
pkgrel=1
pkgdesc="BLAS/LAPACK-interface for OCaml"
arch=('i686' 'x86_64')
url="http://ocaml.info/home/ocaml_sources.html"
license=('LGPL')
depends=('ocaml' 'lapack' 'blas')
makedepends=('ocaml-findlib')
source=("http://forge.ocamlcore.org/frs/download.php/775/$pkgname-$pkgver.tar.gz")
md5sums=('288969619a2c7cdee239bd84798b6889')
options=(!strip)

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make CONFIGUREFLAGS="--prefix /usr --destdir $pkgdir" all
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  install -dm 755 "$OCAMLFIND_DESTDIR/stublibs"
  make install
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
