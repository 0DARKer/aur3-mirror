# Maintainer: Alad Wenter
# base64 -d <<< YWxhZEBhcmNobGludXguaW5mbwo

pkgname=howm-x11
_pkgname=howm
pkgver=0.5
pkgrel=5
pkgdesc='A lightweight, tiling X11 window manager that mimics vi by offering operators, motions and modes.'
arch=('i686' 'x86_64')
url='https://github.com/HarveyHunt/howm'
license=('GPL')
depends=('xcb-util-wm' 'sxhkd' 'cottage')
makedepends=('git')
checkdepends=('linux-headers')
options=('docs')
source=("https://github.com/HarveyHunt/howm/archive/${pkgver}.tar.gz")
sha256sums=('568247262381ee34ba3e48217a42e372fea0b056920b17a2590d927f9f17b846')
install=howm.install

build() {
  cd "$_pkgname-$pkgver"
  make release
}

check() {
  cd "$_pkgname-$pkgver"
  find /usr/lib/modules -name checkpatch.pl -print -quit | xargs -i cp {} .
  make check
}

package() {
  cd "$_pkgname-$pkgver"
  mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/share/xsessions" "$pkgdir/usr/share/howm/examples"  

  find "examples/" -type f -exec install -m644 {} "$pkgdir/usr/share/howm/examples" \;
  install -m644 howm.xsession.desktop "$pkgdir/usr/share/xsessions"
  install -m755 "bin/release/howm" "$pkgdir/usr/bin"
  install -m755 "examples/howmrc" "$pkgdir/usr/share/howm/examples"
}

# vim:set ts=2 sw=2 et:
