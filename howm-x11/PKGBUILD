# Maintainer: Alad Wenter
# base64 -d <<< YWxhZEBhcmNobGludXguaW5mbwo

pkgbase=howm-x11
pkgname=('howm-x11' 'cottage')
pkgver=0.5
_pkgver=0.2
pkgrel=2
pkgdesc='A lightweight, tiling X11 window manager that mimics vi by offering operators, motions and modes.'
arch=('i686' 'x86_64')
url='https://github.com/HarveyHunt/howm'
license=('GPL')
makedepends=('git')
options=('docs')
source=("https://github.com/HarveyHunt/howm/archive/${pkgver}.tar.gz"
	"https://github.com/HarveyHunt/cottage/archive/v${_pkgver}.tar.gz"
	"https://raw.githubusercontent.com/torvalds/linux/master/scripts/checkpatch.pl"
	"https://raw.githubusercontent.com/torvalds/linux/master/scripts/spelling.txt")
install=howm.install
sha256sums=('568247262381ee34ba3e48217a42e372fea0b056920b17a2590d927f9f17b846'
            'ba24c1e2a2e365879653b5d824970dd502cc20375710b1cd2208ba7a3a785934'
            '12f81fca3a770e425fc0226e2bdfad1141391c928dc40ba9e95eb4a42024f9e3'
            '1916fc0139856cc382b37de46375c0a7d3d4c50bd8b954f62eb0f5e2ceefd343')

build() {
  cd "$srcdir/howm"
  make release
  
  cd "$srcdir/cottage"
  make release
}

check() {
  _checkpatch() {
    install -m755 "$srcdir/checkpatch.pl" .
    install -m644 "$srcdir/spelling.txt" .
    make check
  }
  
  cd "$srcdir/howm"
  _checkpatch
  
  cd "$srcdir/cottage"
  _checkpatch
}

package_howm-x11() {
  pkgdesc='A lightweight, tiling X11 window manager that mimics vi by offering operators, motions and modes.'
  depends=('xcb-util-wm' 'sxhkd' 'cottage')
  conflicts=('howm-x11-git')

  cd "$srcdir/howm"
  mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/share/xsessions" "$pkgdir/usr/share/howm"
  install -m755 "bin/release/howm" "$pkgdir/usr/bin"
  install -m644 howm.xsession.desktop "$pkgdir/usr/share/xsessions"
  cp -r "examples/" "$pkgdir/usr/share/howm/"
}

package_cottage() {
  pkgdesc='Use howm commands, operators and set configuration values through a UNIX socket.'
  pkgver=${_pkgver}
  conflicts=('cottage-git')
  
  cd "$srcdir/cottage"
  mkdir -p "$pkgdir/usr/bin"
  install -m755 "bin/release/cottage" "$pkgdir/usr/bin"
}

# vim:set ts=2 sw=2 et:
