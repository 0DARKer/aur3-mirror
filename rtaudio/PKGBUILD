# Maintainer: rtfreedman  (rob<d0t>til<d0t>freedman<aT>googlemail<d0t>com
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: SpepS <dreamspepser@yahoo.it>

pkgname=rtaudio
pkgver=4.0.12
pkgrel=2
pkgdesc="A set of C++ classes that provide a common API for realtime audio input/output."
arch=('i686' 'x86_64')
url="http://www.music.mcgill.ca/~gary/rtaudio/"
license=('GPL')
depends=('jack' 'rtmidi') # 'pulseaudio'
source=("${url}release/${pkgname}-${pkgver}.tar.gz"
		"rtaudio-build.patch"		
		'LICENSE')
md5sums=('72072361b2a0e4186f89fcc5c5a555c2'
         'a62a556fcade58b8bb0692777709bdd5'
         '6a3591e021e8c53f64d359f8f412bccf')

prepare() {
	cd "$srcdir/$pkgname-$pkgver"
	patch -p1 -i "$srcdir/rtaudio-build.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr \
	--with-alsa --with-jack # --with-pulse
  make
  # test & example programs
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  
  # configure scripts
  sed -i 's@Requires:@Requires: jack @' librtaudio.pc
  install -Dm644 librtaudio.pc "$pkgdir/usr/lib/pkgconfig/librtaudio.pc"
  install -Dm755 rtaudio-config "$pkgdir/usr/bin/rtaudio-config"

  # libs
  install -m755 "librtaudio.so.$pkgver" "$pkgdir/usr/lib/librtaudio.so.$pkgver"
  ln -sf librtaudio.so."$pkgver" "$pkgdir/usr/lib/librtaudio.so.4"
  ln -sf librtaudio.so."$pkgver" "$pkgdir/usr/lib/librtaudio.so"

  # RtAudio shares RtError.h with rtmidi:RtMidi.h
  # Thus I left out RtError.h and made rtmidi a dependency
  # to place the header file where other packages will expect it
  install -Dm644 RtAudio.h "$pkgdir/usr/include/RtAudio.h"

  # cosmetic fix
  sed -i 's/-lasound -lasound/-lasound/' rtaudio-config

  # docs
  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  install -m644 readme doc/release.txt "$pkgdir/usr/share/doc/$pkgname/"

  # License
  install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  
  # gcc 4.6 header fix - DO WE NEED IT ANYMORE?
  #sed -i "/inline/s/;$//" "$pkgdir/usr/include/RtAudio.h"

  # if you need anything from below, uncomment it
  #for i in $(find tests -maxdepth 1 -perm 755 -type f); do
  #        install -Dm755 $i "$pkgdir"/usr/bin/$pkgname-${i/*\//}
  #done
  #cd contrib/python/pyrtaudio/
  #python2 setup.py install --root="$pkgdir"
  #install -Dm644 PyRtAudioTest.py "$pkgdir"/usr/share/doc/$pkgname/
}

# vim:set ts=2 sw=2 et:
