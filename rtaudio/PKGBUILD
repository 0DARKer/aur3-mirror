# Maintainer: rtfreedman  (rob<d0t>til<d0t>freedman<aT>googlemail<d0t>com
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: SpepS <dreamspepser@yahoo.it>

pkgname=rtaudio
pkgver=4.1.1
pkgrel=1
pkgdesc="A set of C++ classes that provide a common API for realtime audio input/output."
arch=('i686' 'x86_64')
url="http://www.music.mcgill.ca/~gary/rtaudio/"
license=('MIT')
depends=('jack' 'rtmidi') # 'pulseaudio'
source=("${url}release/${pkgname}-${pkgver}.tar.gz")
md5sums=('99763d3187fbe24ca959a1b711c17984')

prepare() {
  cd "$pkgname-$pkgver"
  ## use system cflags
#  sed -i 's/^CFLAGS   =/CFLAGS   +=/' Makefile.in
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
	--with-alsa --with-jack # --with-pulse --with-oss --enable-debug
  make
  ## test utils
  cd tests && make
}

package() {
  cd "$pkgname-$pkgver"
  
  ## libs
  install -Dm755 librtaudio.so.$pkgver "$pkgdir"/usr/lib/librtaudio.so.$pkgver
  ln -sf librtaudio.so."$pkgver" "$pkgdir"/usr/lib/librtaudio.so.4
  ln -sf librtaudio.so."$pkgver" "$pkgdir"/usr/lib/librtaudio.so

  ## lib header
  install -Dm644 RtAudio.h "$pkgdir"/usr/include/RtAudio.h

  ## lib config
  install -Dm644 librtaudio.pc "$pkgdir"/usr/lib/pkgconfig/librtaudio.pc
  install -Dm755 rtaudio-config "$pkgdir"/usr/bin/rtaudio-config

  ## test utils installed with prefix 'rtaudio-'
  for i in $(find tests -maxdepth 1 -perm 755 -type f); do
      install -Dm755 $i "$pkgdir"/usr/bin/rtaudio-${i/*\//}
  done

  ## text docs
  install -dm755 "$pkgdir"/usr/share/doc/rtaudio
  install -m644 readme doc/release.txt "$pkgdir"/usr/share/doc/rtaudio

  ## html docs
#  install -d "$pkgdir"/usr/share/doc/rtaudio/{html,images}
#  install -Dm644 doc/html/*  "$pkgdir"/usr/share/doc/rtaudio/html
#  install -Dm644 doc/images/*  "$pkgdir"/usr/share/doc/rtaudio/images

  ## License
  csplit -s readme "%LEGAL AND%"
  install -Dm644 xx00 "$pkgdir"/usr/share/licenses/rtaudio/LICENSE

  ## python bindings - doesn't work with 4.1.1
  #cd contrib/python/pyrtaudio/
  #python2 setup.py install --root="$pkgdir"
  #install -Dm644 PyRtAudioTest.py "$pkgdir"/usr/share/doc/$pkgname/
}

# vim:set ts=2 sw=2 et:
