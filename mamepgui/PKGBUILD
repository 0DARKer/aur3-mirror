# Maintainer: TryA <tryagainprod {at} gmail.com>

pkgname=mamepgui
pkgver=1.5.0
pkgrel=2
_date=20100305
pkgdesc="Frontend for SDLMAME/SDLMESS based on MAME Plus!"
arch=('i686' 'x86_64')
url="http://mameicons.free.fr/mame32p"
license=('GPL')
depends=('qt' 'sdl')
optdepends=('sdlmame: port of MAME using SDL'
            'sdlmess: port of MESS using SDL')
source=(http://mameicons.free.fr/mame32p/download/${pkgname}-${pkgver}.src-${_date}.zip)
md5sums=('70903903311130e6d110305bd24dfdea')

build() {
  cd ${srcdir}

  # Edit mamepgui.pro to disable static compilation and SDL 1.3 linking
  sed -e '1,2d' -i mamepgui.pro

  # Edit gamelist.cpp to disable joystick detection, which is a SDL 1.3 feature
  sed -e '42d' -i gamelist.cpp

  # Patches to fix compilation with Qt 4.7
  sed -e 's/ipspath(NULL)/ipspath()/' -i ips.cpp
  sed -e 's/void setGuiStyle(QString = NULL);/void setGuiStyle(QString);/' \
      -e 's/void setBgPixmap(QString = NULL);/void setBgPixmap(QString);/' -i mamepgui_main.h

  lrelease mamepgui.pro
  qmake
  make
}

package() {
  cd ${srcdir}
  install -Dm755 mamepgui ${pkgdir}/usr/bin/mamepgui
}
