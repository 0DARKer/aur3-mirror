# Contributor: Florian "Bluewind" Pritz <flo@xssn.at>
pkgname=spamass-milter
pkgver=0.3.1
pkgrel=2
pkgdesc="Sendmail Milter that pipes incoming mail through SpamAssassin"
arch=(i686 x86_64)
url="http://savannah.nongnu.org/projects/spamass-milt/"
license=('GPL')
depends=(gcc-libs spamassassin)
makedepends=(libmilter)
backup=(etc/conf.d/spamass-milter.conf)
source=("http://savannah.nongnu.org/download/spamass-milt/$pkgname-$pkgver.tar.bz2"
        rc-conf
        rc-script
        popen.diff)

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch < "$srcdir/popen.diff"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -D -m755 spamass-milter "$pkgdir/usr/bin/spamass-milter"
  install -D -m644 spamass-milter.1 "$pkgdir/usr/share/man/man1/spamass-milter.1"
  install -D -m755 "$srcdir/rc-script" "$pkgdir/etc/rc.d/spamass-milter"
  install -D -m644 "$srcdir/rc-conf" "$pkgdir/etc/conf.d/spamass-milter.conf"
}

# vim:set ts=2 sw=2 et:
md5sums=('5f3a441de032c7c044cbb7b922311f1b'
         '854d6d59737c2bed10e2e1bfcae7d5c6'
         'e15bf0676348dc261b310bec4660b885'
         '198962d06c63266b97a08fbefb3f3633')
