# Maintainer:  Pauline123 <321eniluap at gmail dot com>
# Contributor: Shanto <shanto at hotmail>
# Contributor: Piotr Rogo≈ºa <rogoza dot piotr at gmail dot com>
pkgname=conky-colors
_pkgname=conky_colors
pkgver=5.1
pkgrel=3
pkgdesc="An easier way to configure Conky"
arch=('i686' 'x86_64')
url="http://gnome-look.org/content/show.php/CONKY-colors?content=92328"
license=('GPL')
depends=('pystatgrab' 'ttf-liberation' 'hddtemp' 'curl' 'lm_sensors' 'conky' 'ttf-droid')
makedepends=('perl')
optdepends=('conky-lua')
conflicts=('ttf-weather')
options="!strip"

source=("http://gnome-look.org/CONTENT/content-files/92328-$_pkgname-$pkgver.tar.gz"
	)
md5sums=('00a41aafe415a6d272368bb963c27941')



build() {
  cd "$srcdir/$_pkgname"

  make
}

package() {
  cd "$srcdir/$_pkgname"

  install -dm755 ${pkgdir}/usr/share/fonts/TTF
  install -Dm644 fonts/conkycolors/* ${pkgdir}/usr/share/fonts/TTF

  install -dm755 ${pkgdir}/usr/share/conky-colors/{bin,icons,scripts,templates}
  install -dm755 ${pkgdir}/usr/share/conky-colors/icons/{Case,CD,Glassy,oldVinyl,Players,Vinyl}

  for i in Case  CD  Glassy  oldVinyl  Players  Vinyl; do
    install -Dm644 conkycolors/icons/$i/* ${pkgdir}/usr/share/conky-colors/icons/$i/
  done

  for i in bin scripts templates; do
    install -Dm644 conkycolors/$i/* ${pkgdir}/usr/share/conky-colors/$i/
  done

  chmod +x ${pkgdir}/usr/share/conky-colors/bin/*
  chmod +x ${pkgdir}/usr/share/conky-colors/scripts/*
  install -Dm755 conky-colors ${pkgdir}/usr/bin/conky-colors

}
