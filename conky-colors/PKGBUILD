# Maintainer:  Android_808 <android underscore 808 at hotmail dot com>
# Contributor: Pauline123 <321eniluap at gmail dot com>
# Contributor: Shanto <shanto at hotmail>
# Contributor: Piotr Rogo≈ºa <rogoza dot piotr at gmail dot com>
pkgname=conky-colors
_pkgname=conky_colors
pkgver=8.0.1
pkgrel=1
pkgdesc="An easier way to configure Conky"
arch=('i686' 'x86_64')
url="http://gnome-look.org/content/show.php/CONKY-colors?content=92328"
license=('GPL')
depends=('pystatgrab' 'ttf-liberation' 'hddtemp' 'curl' 'lm_sensors' 'conky' 'ttf-droid' 'ttf-ubuntu-font-family')
makedepends=('perl')
optdepends=('conky-lua')
conflicts=('ttf-weather' 'ttf-openlogos')
options="!strip"

source=("http://www.deviantart.com/download/244793180/conky_colors_by_helmuthdu-d41qrmk.zip")
md5sums=('2b7c8685f6dac6e0ca2ed50b3546f0d6')



build() {
  cd "$srcdir/helmuthdu-conky_colors-88c1dbd"

  make
}

package() {
  cd "$srcdir/helmuthdu-conky_colors-88c1dbd"

  install -dm755 ${pkgdir}/usr/share/fonts/TTF
  install -Dm644 fonts/conkycolors/* ${pkgdir}/usr/share/fonts/TTF

  install -dm755 ${pkgdir}/usr/share/conkycolors/{bin,icons,scripts,templates}
  install -dm755 ${pkgdir}/usr/share/conkycolors/icons/{Case,CD,ConkyPhoto,Glassy,Moon,oldVinyl,Players,SLS,Vinyl,Weather,Weather_Moonphases}

  for i in Case  CD  ConkyPhoto  Glassy  Moon  oldVinyl  Players  SLS  Vinyl Weather  Weather_Moonphases; do
    install -Dm644 conkycolors/icons/$i/* ${pkgdir}/usr/share/conkycolors/icons/$i/
  done

  for i in bin scripts templates; do
    install -Dm644 conkycolors/$i/* ${pkgdir}/usr/share/conkycolors/$i/
  done

  chmod +x ${pkgdir}/usr/share/conkycolors/bin/*
  chmod +x ${pkgdir}/usr/share/conkycolors/scripts/*
  install -Dm755 conky-colors ${pkgdir}/usr/bin/conky-colors

}
