# Maintainer:  Pauline123 <321eniluap at gmail dot com>
# Contributor: Shanto <shanto at hotmail>
# Contributor: Piotr Rogo≈ºa <rogoza dot piotr at gmail dot com>
pkgname=conky-colors
_pkgname=conky_colors
pkgver=6.1
pkgrel=2
pkgdesc="An easier way to configure Conky"
arch=('i686' 'x86_64')
url="http://gnome-look.org/content/show.php/CONKY-colors?content=92328"
license=('GPL')
depends=('pystatgrab' 'ttf-liberation' 'hddtemp' 'curl' 'lm_sensors' 'conky' 'ttf-droid' 'ttf-ubuntu-font-family')
makedepends=('perl')
optdepends=('conky-lua')
conflicts=('ttf-weather')
options="!strip"

source=("http://www.deviantart.com/download/244793180/conky_colors_by_helmuthdu-d41qrmk.zip"
	)
md5sums=('375c2e18750efb87ec995633376f4cf5')



build() {
  cd "$srcdir/$_pkgname"

  make
}

package() {
  cd "$srcdir/$_pkgname"

  install -dm755 ${pkgdir}/usr/share/fonts/TTF
  install -Dm644 fonts/conkycolors/* ${pkgdir}/usr/share/fonts/TTF

  install -dm755 ${pkgdir}/usr/share/conkycolors/{bin,icons,scripts,templates}
  install -dm755 ${pkgdir}/usr/share/conkycolors/icons/{Case,CD,Glassy,oldVinyl,Players,Vinyl}

  for i in Case  CD  Glassy  oldVinyl  Players  Vinyl; do
    install -Dm644 conkycolors/icons/$i/* ${pkgdir}/usr/share/conkycolors/icons/$i/
  done

  for i in bin scripts templates; do
    install -Dm644 conkycolors/$i/* ${pkgdir}/usr/share/conkycolors/$i/
  done

  chmod +x ${pkgdir}/usr/share/conkycolors/bin/*
  chmod +x ${pkgdir}/usr/share/conkycolors/scripts/*
  install -Dm755 conky-colors ${pkgdir}/usr/bin/conky-colors

}
