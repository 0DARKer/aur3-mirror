# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Josh Chase <jcjoshuachase@gmail.com>

pkgname=glance
pkgver=2013.1~g2
pkgrel=1
_clientname=python-glanceclient
_clientver=0.7.0
pkgdesc="OpenStack image service"
arch=(any)
url=https://launchpad.net/$pkgname
license=(custom)
depends=(python2-anyjson python2-argparse python2-boto python2-crypto python2-eventlet
    python2-greenlet python2-httplib2 python2-iso8601 python2-kombu python2-lxml python2-migrate
    python2-passlib python2-paste python2-paste-deploy python2-routes python2-sendfile
    python2-sqlalchemy python2-webob python2-wsgiref python2-xattr)
makedepends=(python2-distribute)
source=($url/grizzly/grizzly-2/+download/$pkgname-$pkgver.tar.gz
    http://pypi.python.org/packages/source/${_clientname:0:1}/$_clientname/$_clientname-$_clientver.tar.gz)
sha256sums=('02ea6cf0e1600deeffb4104972a9188477f7c08a92789b7fbd5fc2e1b38e9fd6'
    '57a3c0ab8c48b2e569cb7cfd50fe5709f56d68e52159b14a7d9a24a4221dc617')
sha512sums=('388dc906a53a88e1d860ae3e7b435d3d951fb8ac26b6c028ffa941f0f363249c8fe1764c96806f895ecc1ca3347f275e35635da4b1d36d8b652c9d974afa7802'
    '04820e3206bdc3aca7338de55b4b341c7b2e27a7b96d762e952380b56525018c64b451fc2e3ce7c2ddf244627bfece321ef9e2af0471c663085e2deb63e1dc75')

build() {
    find "$srcdir" -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;

    cd "$srcdir"/$_clientname-$_clientver/
    python2 setup.py build

    cd ../$pkgname-$pkgver
    python2 setup.py build
}

package() {
    cd "$srcdir"/$_clientname-$_clientver/
    python2 setup.py install --root="$pkgdir"

    cd ../$pkgname-$pkgver/
    python2 setup.py install --root="$pkgdir" --optimize=1

    install -d "$pkgdir"/etc/
    cp -r etc/ "$pkgdir"/etc/$pkgname/
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
