# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Josh Chase <jcjoshuachase@gmail.com>

pkgname=glance
pkgver=2012.2.1
pkgrel=2
_clientname=python-glanceclient
_clientver=0.7.0
pkgdesc="OpenStack image service"
arch=(any)
url=https://launchpad.net/$pkgname
license=(custom)
depends=(python-paste-deploy python2-anyjson python2-argparse python2-boto python2-crypto
    python2-eventlet python2-greenlet python2-httplib2 python2-iso8601 python2-kombu python2-lxml
    python2-migrate python2-passlib python2-paste python2-pysendfile python2-routes
    python2-sqlalchemy python2-webob python2-wsgiref python2-xattr)
makedepends=(python2-distribute)
source=($url/folsom/$pkgver/+download/$pkgname-$pkgver.tar.gz
    http://pypi.python.org/packages/source/${_clientname:0:1}/$_clientname/$_clientname-$_clientver.tar.gz)
sha256sums=('1c806f434e4f69bb7c5e30bbabb77df39849baffa310d03e7fd995ece681a2fc'
    '57a3c0ab8c48b2e569cb7cfd50fe5709f56d68e52159b14a7d9a24a4221dc617')
sha512sums=('d88b8108506dc71adc1acc614e036694768fc5564e756cfda8eff05b08e647b061d9b97d78a5561fad88f233fd35549a7728125df9132178b36698f37ebfc3fb'
    '04820e3206bdc3aca7338de55b4b341c7b2e27a7b96d762e952380b56525018c64b451fc2e3ce7c2ddf244627bfece321ef9e2af0471c663085e2deb63e1dc75')

build() {
    find "$srcdir" -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;

    cd "$srcdir"/$_clientname-$_clientver/
    python2 setup.py build

    cd ../$pkgname-$pkgver
    python2 setup.py build
}

package() {
    cd "$srcdir"/$_clientname-$_clientver/
    python2 setup.py install --root="$pkgdir"

    cd "$srcdir"/$pkgname-$pkgver/
    python2 setup.py install --root="$pkgdir" --optimize=1

    install -d "$pkgdir"/etc/
    cp -r etc/ "$pkgdir"/etc/$pkgname/
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
