# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Josh Chase <jcjoshuachase@gmail.com>

pkgname=glance
_relname=havana
pkgver=2013.2.b1
pkgrel=1
pkgdesc="OpenStack image registry and delivery service"
arch=(any)
url=https://launchpad.net/$pkgname
license=(custom)
depends=(python2-anyjson python2-argparse python2-boto python2-crypto python2-eventlet
    python2-jsonpointer python2-glanceclient python2-greenlet python2-httplib2 python2-iso8601
    python2-keystoneclient python2-kombu python2-lxml python2-migrate python2-passlib python2-paste
    python2-paste-deploy python2-pyopenssl python2-routes python2-sendfile python2-sqlalchemy
    python2-warlock python2-webob python2-wsgiref python2-xattr)
makedepends=(python2-distribute)
source=($url/$_relname/$_relname-${pkgver: -1}/+download/$pkgname-$pkgver.tar.gz)
sha256sums=('a9995ffe1e3100e7e8b7231121bd657b03dbcb40ad0b6323acab9111d5c8c2e6')
sha512sums=('1c4c87460a9ca32c52a9b7995d62f73a3bf59046209aca4c204d30fc6fecf766a28d3955d15a117dd5ab02110aafaf41a758dfe71ba49dabd57727c5952d8354')

build() {
    cd $pkgname-$pkgver/
    find -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
    python2 setup.py build
}

package() {
    cd $pkgname-$pkgver/
    python2 setup.py install --root="$pkgdir" --optimize=1

    install -d "$pkgdir"/etc/
    cp -r etc/ "$pkgdir"/etc/$pkgname/
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
