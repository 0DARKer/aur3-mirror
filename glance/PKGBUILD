# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Josh Chase <jcjoshuachase@gmail.com>

pkgname=glance
_relname=juno
pkgver=2014.2.b1
pkgrel=1
pkgdesc="OpenStack image registry and delivery service"
arch=(any)
url=https://launchpad.net/glance
license=(custom)
depends=(python2-jsonpointer python2-glanceclient python2-keystoneclient
    python2-pyopenssl python2-warlock)
makedepends=(python2-setuptools)
source=($url/$_relname/$_relname-1/+download/$pkgname-$pkgver.tar.gz)
sha256sums=('bc4b2c1ddf5492e82bd25da279b3a49082c4174f5590e09311a4634dcfc7cc1e')
sha512sums=('a1068d7fc9bd7d573edbbd4745a651eef7bed2e87e1a6d4c8a634e7a8954c065735db4596b6955373a0f6c93972c77ff27315977be4d60c97170dd6549621d01')

prepare() {
    find $pkgname-$pkgver -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
}

build() {
    cd $pkgname-$pkgver/
    python2 setup.py build
}

package() {
    cd $pkgname-$pkgver/
    python2 setup.py install --root="$pkgdir" --optimize=1

    install -d "$pkgdir"/etc/
    cp -r etc/ "$pkgdir"/etc/$pkgname/
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
