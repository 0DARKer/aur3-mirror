# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Josh Chase <jcjoshuachase@gmail.com>

pkgname=glance
pkgver=2012.1.2
pkgrel=2
_ostackver=essex
_clientname=python-glanceclient
_clientver=0.5.1
pkgdesc="OpenStack image service"
arch=(any)
license=(custom)
url=https://launchpad.net/$pkgname
depends=(python-paste python-paste-deploy python2-anyjson python2-argparse python2-boto
    python2-crypto python2-eventlet python2-greenlet python2-httplib2 python2-iso8601 python2-kombu
    python2-lxml python2-migrate python2-passlib python2-pysendfile python2-routes
    python2-sqlalchemy python2-webob python2-wsgiref python2-xattr)
makedepends=(python2-distribute)
source=(https://launchpad.net/$pkgname/$_ostackver/$pkgver/+download/$pkgname-$pkgver.tar.gz
    http://pypi.python.org/packages/source/${_clientname:0:1}/$_clientname/$_clientname-$_clientver.tar.gz)
sha256sums=('df03f968feb28ad301e5764070776d9e31cf08e4c85ce793e5079fe9b9d3e705'
    '84784b57b939bb2367748d6f290f420a79459774801f753c77fd98af17461b80')
sha512sums=('53a5ecedfe99b2b505d2012fa0f17808e67d944be89c6285eb19ed2daf59c6fe691775ff8e57c6b9176ab71ede8c5bd46d7b2c78a4cc2755ea1ebf43627ea13f'
    '6d326f2d56669bf300c8bcd35bf8359260361594ea092e484b504f4aae64a4f42cc9e25464a11855294f3487a2dd7cac624e8395b1dbc05959b8f7f7e418723a')

build() {
    find "$srcdir" -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;

    cd "$srcdir"/$_clientname-$_clientver/
    python2 setup.py build

    cd ../$pkgname-$pkgver
    python2 setup.py build
}

package() {
    cd "$srcdir"/$_clientname-$_clientver/
    python2 setup.py install --root=$pkgdir

    cd "$srcdir"/$pkgname-$pkgver/
    python2 setup.py install --root="$pkgdir" --optimize=1

    install -dm600 "$pkgdir"/etc/$pkgname
    install -Dm600 etc/* "$pkgdir"/etc/$pkgname/
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
