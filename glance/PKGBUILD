# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Josh Chase <jcjoshuachase@gmail.com>

pkgname=glance
pkgver=2013.1.g3
pkgrel=1
_clientname=python-glanceclient
_clientver=0.8.0
pkgdesc="OpenStack image service"
arch=(any)
url=https://launchpad.net/$pkgname
license=(custom)
depends=(python2-anyjson python2-argparse python2-boto python2-crypto python2-eventlet
    python2-greenlet python2-httplib2 python2-iso8601 python2-kombu python2-lxml python2-migrate
    python2-passlib python2-paste python2-paste-deploy python2-routes python2-sendfile
    python2-sqlalchemy python2-webob python2-wsgiref python2-xattr)
makedepends=(python2-distribute)
source=($url/grizzly/grizzly-${pkgver#*g}/+download/$pkgname-$pkgver.tar.gz
    http://pypi.python.org/packages/source/${_clientname:0:1}/$_clientname/$_clientname-$_clientver.tar.gz)
sha256sums=('c6f0370f645edf9b51c1fb153ceca2bf6ca85e83469380cb19d2882e5d7bb588'
    '46bfe15087437f19499c803394334a470081804f3f23f19dcc5a83608c01d383')
sha512sums=('83301bc06d6ecf68fec7e7052440441c746a6402abf9499103692a4493630d7c6b9bc17d56e91625cf4b6e9f069ee2161c7dbc3f3c846622f51ff36b8d323c31'
    '1ca10562d1acecb8de46e84753328dc3cefe0a7afd554b93b3ed05edca70520677ddbaaa7aaa05e019bfc3ae1bd7ecec879ef961a3fe638c523e8ad41eb133e6')

build() {
    find -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;

    cd $_clientname-$_clientver/
    python2 setup.py build

    cd ../$pkgname-$pkgver/
    python2 setup.py build
}

package() {
    cd $_clientname-$_clientver/
    python2 setup.py install --root="$pkgdir"

    cd ../$pkgname-$pkgver/
    python2 setup.py install --root="$pkgdir" --optimize=1

    install -d "$pkgdir"/etc/
    cp -r etc/ "$pkgdir"/etc/$pkgname/
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
