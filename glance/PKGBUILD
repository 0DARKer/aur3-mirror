# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Josh Chase <jcjoshuachase@gmail.com>

pkgname=glance
_relname=icehouse
pkgver=2014.1.b2
pkgrel=1
pkgdesc="OpenStack image registry and delivery service"
arch=(any)
url=https://launchpad.net/glance
license=(custom)
depends=(python2-jsonpointer python2-glanceclient python2-keystoneclient
    python2-pyopenssl python2-warlock)
makedepends=(python2-setuptools)
source=($url/$_relname/$_relname-2/+download/$pkgname-$pkgver.tar.gz)
sha256sums=('2d6a823a03f1098f7f6e826d08e60030865eb2efd2b3ff3ecda5ed625a95c57a')
sha512sums=('4fb880539898872b97844bad41679cc02a6ab6b97451c591152007ea8fb79150b58876300f420ad62514eafa11c3460295a6be8de1acc42e78d43b76f0b4522d')

prepare() {
    find $pkgname-$pkgver -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
}

build() {
    cd $pkgname-$pkgver/
    python2 setup.py build
}

package() {
    cd $pkgname-$pkgver/
    python2 setup.py install --root="$pkgdir" --optimize=1

    install -d "$pkgdir"/etc/
    cp -r etc/ "$pkgdir"/etc/$pkgname/
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
