# Maintainer: Jesse Jaara <gmail.com: jesse.jaara>

pkgname=gstreamer-vaapi
pkgver=0.4.0
pkgrel=4
pkgdesc="A collection of VA-API based plugins for GStreamer"
url="http://gitorious.org/vaapi/gstreamer-vaapi"
arch=('i686' 'x86_64')
license=('GPL2')
depends=("gstreamer0.10-bad" 'libva')
makedepends=('gtk-doc')
optdepends=('xvba-video: for ATI chipsets'
	    'vdpau-video: for nVidia chipsets'
	    'libva-driver-intel: For Intel chipsets')
source=("http://www.freedesktop.org/software/vaapi/releases/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
options=('!libtool')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  #Sadly the ffmpeg API used is so old that it need ALOT of fixing
  LIBS=-ldl ./configure --disable-ffmpeg --prefix=/usr

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install
}

md5sums=('e58b55bdc196fa7a6b86dca52e18424f')
