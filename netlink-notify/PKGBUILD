# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=netlink-notify
pkgver=0.4.1.r2.gd9031ce
pkgrel=1
pkgdesc="Notify about netlink changes"
arch=('i686' 'x86_64')
url="http://www.eworm.de/"
depends=('libnotify')
makedepends=('git' 'inkscape')
license=('GPL')
install=netlink-notify.install
source=('git://github.com/eworm-de/netlink-notify.git')

pkgver() {
	cd netlink-notify/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[_-+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}


build() {
	cd netlink-notify/

	make

	inkscape --export-png ${pkgname}-connected.png ${pkgname}-connected.svg
	inkscape --export-png ${pkgname}-disconnected.png ${pkgname}-disconnected.svg
}

package() {
	cd netlink-notify/

	install -D -m0755 ${pkgname} ${pkgdir}/usr/bin/${pkgname}
	install -D -m0644 ${pkgname}.desktop ${pkgdir}/etc/xdg/autostart/${pkgname}.desktop
	install -D -m0644 ${pkgname}-connected.svg ${pkgdir}/usr/share/icons/hicolor/scalable/status/${pkgname}-connected.svg
	install -D -m0644 ${pkgname}-connected.png ${pkgdir}/usr/share/icons/hicolor/48x48/status/${pkgname}-connected.png
	install -D -m0644 ${pkgname}-disconnected.svg ${pkgdir}/usr/share/icons/hicolor/scalable/status/${pkgname}-disconnected.svg
	install -D -m0644 ${pkgname}-disconnected.png ${pkgdir}/usr/share/icons/hicolor/48x48/status/${pkgname}-disconnected.png
}

sha256sums=('SKIP')
