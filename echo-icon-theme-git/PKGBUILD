# Maintainer: Charles Bos <charlesbos1 AT gmail>
# Contributor: Michael Wiencek <mwtuea at gmail dot com>

pkgname=echo-icon-theme-git
pkgver=r389.4007669
pkgrel=4
pkgdesc="A new generation of icon set to replace the legacy Bluecurve icon set."
url="https://fedorahosted.org/echo-icon-theme/"
arch=('any')
license=('CCPL')
depends=('icon-naming-utils')
optdepends=(
  'mist-icon-theme: for inheriting missing icons'
)
source=('git://git.fedorahosted.org/echo-icon-theme')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir"/echo-icon-theme
  # Use number of revisions as the version itself is too long.
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir"/echo-icon-theme/base
  aclocal && automake --add-missing && autoconf
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/echo-icon-theme/base
  make prefix="$pkgdir"/usr install
}

