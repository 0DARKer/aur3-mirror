# Maintainer: veox <veox at wemakethings dot net>

pkgname=libsigrokdecode-git
_gitname=libsigrokdecode
pkgver=506.cab2b4e
pkgrel=1
pkgdesc="Client software that supports various hardware logic analyzers, protocol decoders library (git version)"
arch=('i686' 'x86_64')
url="http://sigrok.org/wiki/Main_Page"
license=('GPL3')
depends=('glib2' 'python')
makedepends=('git')
provides=('libsigrokdecode')
conflicts=('libsigrokdecode')
source=("git://sigrok.org/${_gitname}")
md5sums=('SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "$srcdir/${_gitname}"

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/${_gitname}"
  make DESTDIR="$pkgdir" PREFIX=/usr install
}
