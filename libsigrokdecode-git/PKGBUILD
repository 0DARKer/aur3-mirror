# Maintainer: veox <veox at wemakethings dot net>

pkgname=libsigrokdecode-git
_pkgname=libsigrokdecode
pkgver=20120308
pkgrel=1
pkgdesc="Client software that supports various hardware logic analyzers, protocol decoders library"
arch=('i686' 'x86_64')
url="http://sigrok.org/wiki/Main_Page"
license=('GPL3')
depends=('glib2' 'python')
makedepends=('git')
provides=('libsigrokdecode')
conflicts=('libsigrokdecode')
source=()
md5sums=()

_gitroot="git://sigrok.git.sourceforge.net/gitroot/sigrok/sigrok"
_gitname="sigrok"

build() {

  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"

  cd "$srcdir/$_gitname-build/$_pkgname"
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_gitname-build/$_pkgname"
  make DESTDIR="$pkgdir" PREFIX=/usr install
}
