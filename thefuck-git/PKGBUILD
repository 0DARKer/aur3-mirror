# Maintainer: Jonathan Ryan <jryan@curious-computing.com>
pkgname=thefuck-git # '-bzr', '-git', '-hg' or '-svn'
_pkgname=thefuck
pkgver=1.4.r16.636ce7c
pkgrel=3
pkgdesc="Magnificent app which corrects your previous console command, inspired by @liamosaur twit."
arch=(any)
url="https://github.com/nvbn/thefuck"
license=('MIT')
depends=('python' 'python-pathlib')
makedepends=('git' 'python-setuptools' 'python-pytest' 'python-mock')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
install=thefuck-git.install
source=('git+http://github.com/nvbn/thefuck.git' 'thefuck-git.install')
md5sums=('SKIP'
         'ce3955458e1d6993125c2458564c94db')

pkgver() {
  cd "$srcdir/${_pkgname}"
  printf "1.4.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

check() {
  cd "$srcdir/${_pkgname}"
  py.test
}

package() {
  cd "$srcdir/${_pkgname}"
  python setup.py install --root="$pkgdir/" --optimize=1
}
