# Maintainer: Duncan K. <duncank@fastmail.fm>

pkgname=discododgeball-hib
pkgver=0_20131228
_hibver=12202013
pkgrel=1
pkgdesc='Robot Roller-Derby Disco Dodgeball, a team-based multiplayer FPS (Humble Bundle version)'
url='http://www.82apps.com/DiscoDodgeball/'
arch=('i686' 'x86_64')
license=('custom:commercial')
if [ $CARCH == i686 ]; then
  depends=('glu' 'libxcursor' 'alsa-lib')
  optdepends=('alsa-plugins: PulseAudio support'
              'libpulse: PulseAudio support')
else
  depends=('lib32-glu' 'lib32-libxcursor' 'lib32-alsa-lib')
  optdepends=('lib32-alsa-plugins: PulseAudio support'
              'lib32-libpulse: PulseAudio support')
fi
options=('!strip' '!upx')
PKGEXT='.pkg.tar.gz'
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Manually download it to \"$(pwd)\", or set up a hib:// DLAGENT in /etc/makepkg.conf."; exit 1')

_installer="discododgeball_linux_${_hibver}.zip"
source=("hib://${_installer}"
        "discododgeball-hib.desktop")
md5sums=('485fe3407c0b201b624bcf4ff5f5c740'
         'fd48093923124aaec390a7d89f40b4ef')

package() {
  cd $srcdir
  _installdir="/opt/discododgeball"; _target=$pkgdir/$_installdir

  # Install game files
  mkdir -p $_target
  cp -rT "Disco Dodgeball" $_target
  rm "${_target}/.DS_Store"

  # Install desktop entry
  install -Dm644 "${pkgname}.desktop" \
                 "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install icon
  install -Dm644 "Disco Dodgeball/Disco Dodgeball_Data/Resources/UnityPlayer.png" \
                "${pkgdir}/usr/share/pixmaps/discododgeball.png"

  # Link executable
  install -d "${pkgdir}/usr/bin"
  ln -s "${_installdir}/Disco Dodgeball.x86" "${pkgdir}/usr/bin/discododgeball"
}
