# Maintainer: Whitney Marshall <whitney.marshall@gmail.com>

pkgname=go-mtpfs-git
pkgver=2013.04.13
pkgrel=2
pkgdesc="Simple tool for viewing MTP devices as FUSE filesystems"
arch=('x86_64' 'i686')
url="http://github.com/hanwen/go-mtpfs"
license=('BSD')
depends=('libmtp')
makedepends=('go')
options=('!strip' '!emptydirs')
source=('go-mtpfs::git://github.com/hanwen/go-mtpfs.git')
md5sums=('SKIP')
_gourl=github.com/hanwen/go-mtpfs

pkgver() {
  cd go-mtpfs
  # Get the date of the last commit, in YYYY.MM.DD format
  git log -1 --pretty=%cd --date=short | sed 's/-/./g'
}

build() {
  cd "$srcdir"
  GOPATH="$srcdir" go get -v -x ${_gourl}
}

check() {
  source /etc/profile.d/go.sh
  GOPATH="$GOPATH:$srcdir" go test -v -x ${_gourl}
}

package() {
  source /etc/profile.d/go.sh
  mkdir -p "$pkgdir/$GOPATH"
  cp -R --preserve=timestamps "$srcdir"/{src,pkg} "$pkgdir/$GOPATH"

  mkdir -p "$pkgdir/usr"
  cp -R --preserve=timestamps "$srcdir/bin" "$pkgdir/usr"

  # Package license (if available)
  for f in LICENSE COPYING; do
    if [ -e "$srcdir/src/$_gourl/$f" ]; then
      install -Dm644 "$srcdir/src/$_gourl/$f" "$pkgdir/usr/share/licenses/$pkgname/$f"
    fi
  done
}

# vim:set ts=2 sw=2 et:
