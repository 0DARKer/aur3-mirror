# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Guillaume ALAUX <Guillaume at alaux dot net>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=jdk6
pkgver=6u27
_javabuild=07
pkgrel=1
pkgdesc="Sun/Oracle's Java Development Kit"
depends=('jre6')
url='http://java.oracle.com'
license=('custom')
install='jdk6.install'

arch=('i686' 'x86_64')
[ "${CARCH}" = 'i686' ]   && _arch=i586
[ "${CARCH}" = 'x86_64' ] && _arch=x64

source=(
  "http://download.oracle.com/otn-pub/java/jdk/${pkgver}-b${_javabuild}/jdk-${pkgver}-linux-${_arch}.bin"
  'construct.sh::http://java.net/projects/jdk-distros/sources/svn/content/trunk/utils/construct.sh?raw=true'
)
[ "${CARCH}" = 'i686' ] && md5sums=('bdb5f05bd20c6aa9a4729726191bf6fd')
[ "${CARCH}" = 'x86_64' ] && md5sums=('94f93a3ff03f824a238ecd79ad90433e')
md5sums+=('94065b612df0046d9ae758943f9f6a75')

build() {
  cd "${srcdir}"
  rm -rf unbundle-jdk linux-jdk linux-jre
  mkdir unbundle-jdk
  cd unbundle-jdk
  sh ../jdk-${pkgver}-linux-$_arch.bin --accept-license
  cd ..
  sh construct.sh unbundle-jdk linux-jdk linux-jre
}

package() {
  cd "${srcdir}"

  rm -rf linux-jdk/jre
  install -d "${pkgdir}"/opt
  mv linux-jdk "${pkgdir}"/opt/java6

  install -d "${pkgdir}"/usr/bin
  ln -s /opt/java6/bin/javac "${pkgdir}/usr/bin/javac6"

  install -d "${pkgdir}"/usr/share/licenses/jdk6
  install -m644 "${pkgdir}"/opt/java6/COPYRIGHT \
    "${pkgdir}"/usr/share/licenses/jdk6
  install -m644 "${pkgdir}"/opt/java6/LICENSE \
    "${pkgdir}"/usr/share/licenses/jdk6
  install -m644 "${pkgdir}"/opt/java6/THIRDPARTYLICENSEREADME.txt \
    "${pkgdir}"/usr/share/licenses/jdk6
}

