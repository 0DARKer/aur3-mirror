# Maintainer: jtts
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: GordonGR <gordongr@freemail.gr>

_pkgbasename=polkit
pkgname=lib32-$_pkgbasename
pkgver=0.111
pkgrel=1
pkgdesc="Application development toolkit for controlling system-wide privileges (32-bit)"
arch=('x86_64')
license=(LGPL)
url="http://www.freedesktop.org/wiki/Software/polkit"
depends=($_pkgbasename lib32-glib2 lib32-pam lib32-expat lib32-systemd lib32-js185)
makedepends=(gcc-multilib intltool)
options=('!libtool')
# Not needed. This is a lib32-package.
#install=polkit.install
source=(http://www.freedesktop.org/software/polkit/releases/$_pkgbasename-$pkgver.tar.gz)
# Not needed. This is a lib32-package.
#        polkit.pam)
sha256sums=('02ae544547211b687818c97bcbf19bf6b8b5be7fda93000525a8765c7bed1ea1')

build() {
  cd $_pkgbasename-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libdir=/usr/lib32 --libexecdir=/usr/lib32/polkit-1 \
      --with-systemdsystemunitdir=/usr/lib/systemd/system \
      --disable-static --enable-introspection=no --enable-libsystemd-login=yes \
      --enable-man-pages=no --disable-gtk-doc \
      CC="gcc -m32" CXX="g++ -m32" PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  make
}

package() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="$pkgdir" install

  # Not needed. This is a lib32-package.
  #chown 102 "$pkgdir/etc/polkit-1/rules.d"
  #chown 102 "$pkgdir/usr/share/polkit-1/rules.d"
  #
  #install -m644 "$srcdir/polkit.pam" "$pkgdir/etc/pam.d/polkit-1"

  # cleanup for lib32 package
  rm -rf $pkgdir/{etc,usr/{bin,lib,include,share}}
}
