# Maintainer: jtts
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: GordonGR <gordongr@freemail.gr>

_pkgbasename=polkit
pkgname=lib32-${_pkgbasename}
pkgver=0.109
pkgrel=2
pkgdesc="Application development toolkit for controlling system-wide privileges (32-bit)"
arch=('x86_64')
license=('LGPL')
url="http://www.freedesktop.org/wiki/Software/polkit"
depends=("${_pkgbasename}" 'lib32-glib2>=2.30.0' 'lib32-pam' 'lib32-expat' 'lib32-systemd' 'lib32-js')
makedepends=('gcc-multilib' 'intltool')
options=('!libtool')
source=("http://www.freedesktop.org/software/polkit/releases/${_pkgbasename}-${pkgver}.tar.gz"
        "fix-xauthority.patch")
sha256sums=('805d5c75e2adf18fd6622f0b1ea733759fb29d9d4afe8e16adbff5b84ded2ae2'
            'd25e0ed70940207187c6013de396f4d1ac3f6c8f04c8478fb0b1aa7a17cd3cca')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  patch -Np1 -i ../fix-xauthority.patch

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libdir=/usr/lib32 --libexecdir=/usr/lib32/polkit-1 \
      --disable-static --enable-introspection=no --enable-libsystemd-login=yes \
      --enable-man-pages=no --disable-gtk-doc \
      CC="gcc -m32" CXX="g++ -m32" PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # cleanup for lib32 package
  rm -rf "${pkgdir}"/{etc,usr/{bin,lib,include,share}}
}
