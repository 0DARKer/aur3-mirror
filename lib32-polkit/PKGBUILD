# Maintainer: jtts
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: GordonGR <gordongr@freemail.gr>

_pkgbasename=polkit
pkgname=lib32-${_pkgbasename}
pkgver=0.107
pkgrel=4
pkgdesc="Application development toolkit for controlling system-wide privileges (32 bit)"
arch=('x86_64')
license=('LGPL')
url="http://www.freedesktop.org/wiki/Software/PolicyKit"
depends=("${_pkgbasename}" 'lib32-glib2>=2.30.0' 'lib32-pam' 'lib32-expat' 'lib32-js')
makedepends=('gcc-multilib' 'intltool')
options=('!libtool')
source=("http://www.freedesktop.org/software/polkit/releases/${_pkgbasename}-${pkgver}.tar.gz"
	"fix-empty-wheel.patch")
sha256sums=('c826973db2b226dc8f807af5143f7b8be5d0270f46c0c1241a83b067db9d8311'
            'ec59771ea95ac45656bc80bd94ab695990068eded68a340b386f6296f40d2c28')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  # https://bugs.archlinux.org/task/31717
  patch -Np1 -i ../fix-empty-wheel.patch

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libdir=/usr/lib32 --libexecdir=/usr/lib32/polkit-1 \
      --disable-static --enable-introspection=no --enable-libsystemd-login=no \
      --enable-man-pages=no --disable-gtk-doc \
      CC="gcc -m32" CXX="g++ -m32" PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # cleanup for lib32 package
  rm -rf "${pkgdir}"/{etc,usr/{bin,lib,include,share}}
}
