# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=polkit
pkgname=lib32-${_pkgbasename}
pkgver=0.101
pkgrel=2
pkgdesc="Application development toolkit for controlling system-wide privileges (lib32)"
arch=('x86_64')
license=('LGPL')
url="http://www.freedesktop.org/wiki/Software/PolicyKit"
depends=("${_pkgbasename}" 'lib32-glib2' 'lib32-pam' 'lib32-expat')
makedepends=('gcc-multilib' 'intltool' 'gtk-doc')
replaces=('policykit')
options=('!libtool')
source=(http://hal.freedesktop.org/releases/${_pkgbasename}-${pkgver}.tar.gz
        CVE-2011-1485.patch)
md5sums=('f925ac93aba3c072977370c1e27feb7f'
         '4d858b8ab602614d7db2bc8574f6fd29')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  patch -Np1 -i "${srcdir}/CVE-2011-1485.patch"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libdir=/usr/lib32 --libexecdir=/usr/lib32/polkit-1 \
      --disable-static --enable-gtk-doc --enable-introspection=no \
      CC="gcc -m32" CXX="g++ -m32" PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/{etc,usr/{bin,include,share},var}
}
