# Maintainer: jtts
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: GordonGR <gordongr@freemail.gr>

_pkgbasename=polkit
pkgname=lib32-$_pkgbasename
pkgver=0.110
pkgrel=1
pkgdesc="Application development toolkit for controlling system-wide privileges (32-bit)"
arch=('x86_64')
license=('LGPL')
url="http://www.freedesktop.org/wiki/Software/polkit"
depends=($_pkgbasename 'lib32-glib2>=2.30.0' 'lib32-pam' 'lib32-expat' 'lib32-systemd' 'lib32-js')
makedepends=('gcc-multilib' 'intltool')
options=('!libtool')
# Not needed (lib32-package)
#install=polkit.install
source=(http://www.freedesktop.org/software/polkit/releases/$_pkgbasename-$pkgver.tar.gz)
# Not needed (lib32-package)
#        polkit.pam)
sha256sums=('8e5c5044bb968643b7fa379f287fb10582615df760ad2f1cb84be6e19fafe6e8')

build() {
  cd $srcdir/$_pkgbasename-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libdir=/usr/lib32 --libexecdir=/usr/lib32/polkit-1 \
      --with-systemdsystemunitdir=/usr/lib/systemd/system \
      --disable-static --enable-introspection=no --enable-libsystemd-login=yes \
      --enable-man-pages=no --disable-gtk-doc \
      CC="gcc -m32" CXX="g++ -m32" PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  make
}

package() {
  cd $srcdir/$_pkgbasename-$pkgver
  make DESTDIR="$pkgdir" install

  # Not needed (lib32-package)
  #install -m644 "$srcdir/polkit.pam" "$pkgdir/etc/pam.d/polkit-1"
  # cleanup for lib32 package
  rm -rf $pkgdir/{etc,usr/{bin,lib,include,share}}
}
