# Contributor: Dongsheng Cai <imdongsheng@gmail.com>
# Maintainer: Adria Arrufat (swiftscythe) <swiftscythe@gmail.com>

_pkgname=gthumb
pkgname=gthumb-git
pkgver=3.3.2.14.g31b402d
pkgrel=1
pkgdesc="Image browser and viewer for the GNOME Desktop"
arch=(i686 x86_64)
url="http://live.gnome.org/gthumb/"
license=('GPL')
provides=($_pkgname=$pkgver)
conflicts=($_pkgname)
depends=(desktop-file-utils hicolor-icon-theme dconf librsvg clutter-gtk
         gst-plugins-base-libs gsettings-desktop-schemas libwebp libsecret
         libsoup webkitgtk json-glib)
makedepends=(intltool libchamplain libopenraw exiv2 itstool brasero librsvg
             libopenraw liboauth docbook-xsl)
optdepends=('libopenraw: read RAW files'
            'exiv2: metadata support'
            'libchamplain: map viewer'
            'brasero: burn discs'
            'liboauth: web albums')
options=('!emptydirs')
install=gthumb.install
source=(git://git.gnome.org/$_pkgname)
md5sums=('SKIP')

pkgver() {
    cd $_pkgname/
    git describe | sed 's/^v//;s/-/./g'
}

build() {
    cd $_pkgname/
    ./autogen.sh --prefix=/usr CFLAG="-ggdb" || return 1
    make
}

package() {
    cd $_pkgname/
    make DESTDIR="$pkgdir" install
    install -D -m644 $srcdir/gthumb/data/icons/hicolor/48x48/apps/gthumb.png $pkgdir/usr/share/pixmaps/gthumb.png
}
