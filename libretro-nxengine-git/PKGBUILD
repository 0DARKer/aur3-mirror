# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive   <almostalive2003 at gmail dot com>

pkgname=libretro-nxengine-git
pkgver=378.bc15f7b
pkgrel=1
pkgdesc="libretro implementation of NXEngine. (Cave Story)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/nxengine-libretro"
license=('GPL3')
makedepends=('git')

install=libretro-nxengine.install

_gitname=nxengine-libretro
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/nxengine_libretro.info"
        "http://www.cavestory.org/downloads/cavestoryen.zip")
md5sums=('SKIP'
         'SKIP'
         '5aad47f1cb72185d6e7f4c8c392f6b6e')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}"
  make
}

package() {
  install -Dm644 "${_gitname}/nxengine_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-nx.so"
  install -Dm644 "nxengine_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-nx.info"

  install -Dm644 "CaveStory/Doukutsu.exe" "${pkgdir}/usr/share/libretro/libretro-nx/datafiles/Doukutsu.exe"
  cp -r "CaveStory/data/" "${pkgdir}/usr/share/libretro/libretro-nx/datafiles/"
}
