# Contributor: Leslie P. Polzer <polzer@gnu.org>
pkgname=db4.6
pkgver=4.6.21
pkgrel=2
pkgdesc="The Berkeley DB embedded database system 4.6"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.oracle.com/technology/software/products/berkeley-db/index.html"
depends=('gcc-libs')
options=('!libtool' '!makeflags')
source=(http://download.oracle.com/berkeley-db/db-${pkgver}.tar.gz)
md5sums=('718082e7e35fc48478a2334b0bc4cd11')

build() {
  cd ${srcdir}/db-${pkgver}/

  cd build_unix
  ../dist/configure --prefix=/usr --enable-compat185 \
    --enable-shared --disable-static --enable-cxx
  make LIBSO_LIBS=-lpthread || return 1
  make prefix=${pkgdir}/usr \
       includedir=${pkgdir}/usr/include/db4.6 install

  rm -rf ${pkgdir}/usr/docs
  rm -f ${pkgdir}/usr/lib/libdb{,_cxx}.so
  rm -f ${pkgdir}/usr/lib/libdb{,_cxx}-4.so

  cd ${pkgdir}/usr/bin

  for i in *; do
    mv $i db4.6_${i/db_/}
  done

  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ${srcdir}/db-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
