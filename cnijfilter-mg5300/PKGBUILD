# Maintainer: Giancarlo Bianchi <giancarlobianchi76@gmail.com>
# Original Contributors: Fortunato Ventre (voRia) <vorione@gmail.com>
#                        Custom Processing Unlimited (CPUnltd) <CPUnltd 'at' gmail 'dot' com>
pkgname=cnijfilter-mg5300
pkgver=3.60
pkgrel=5
_pkgver=3.60-1
pkgdesc="Canon IJ Printer Driver (for MG5300 series)"
url="http://support-au.canon.com.au/contents/AU/EN/0100392802.html"
arch=('i686' 'x86_64')
license=('custom')
depends=('libcups' 'cups' 'popt' 'ghostscript' 'gsfonts' 'atk>=1.9.0' 'gtk2>=2.8.0' 
				 'pango>=1.12.3' 'libtiff' 'cairo>=1.0.2' 'libxml2>=2.6.24' 
				 'fontconfig>=2.3.0' 'libxinerama')
makedepends=('autoconf>=2.13' 'automake>=1.6' 'tar' 'make' 'gcc')
conflicts=('cnijfilter-common')
install=cnijfilter-mg5300.install
source=("http://gdlp01.c-wss.com/gds/8/0100003928/01/cnijfilter-source-${_pkgver}.tar.gz"
				'build-fixes.patch'
				'cups.patch'
				'id.po.patch'
				'libpng.patch')
md5sums=('70e412331a21f4b573b4e901c89cee18'
	       '5f4b3523f6542f0a90886e7e6264f19a'
	       'd5c3dda759bd8809cd5359872f9ba645'
	       'e228560529d46e1f03617ba7bc9aa657'
	       '3bca06ca61b0c9ffd0c292ed6985375f')

build() {
	if [ "$CARCH" == "x86_64" ]; then
		libdir=libs_bin64
	else
		libdir=libs_bin32
	fi

	## Patches
	msg "Applying patches"
	patch -p0 < build-fixes.patch
	patch -p0 < cups.patch
	patch -p0 < id.po.patch
	patch -p0 < libpng.patch

	## Compile and install mg5300 stuff
	# ppd file
	msg "Building ppd"
	cd ${srcdir}/cnijfilter-source-${_pkgver}/ppd
	./autogen.sh --prefix=/usr --enable-ppdpath=/usr/share/cups/model --program-suffix=mg5300
	make clean
	make
	make DESTDIR=${pkgdir} install

	# cnijfilter
	msg "Building cnijfilter"
	cd ${srcdir}/cnijfilter-source-${_pkgver}/cnijfilter
	autoreconf -i
	./autogen.sh --prefix=/usr --enable-libpath=/usr/lib/bjlib --enable-binpath=/usr/bin --program-suffix=mg5300
	make clean
	make
	make DESTDIR=${pkgdir} install

	# printui
	msg "Building printui"
	cd ${srcdir}/cnijfilter-source-${_pkgver}/printui
	./autogen.sh --prefix=/usr --datadir=/usr/share --program-suffix=mg5300
	make clean
	make
	make DESTDIR=${pkgdir} install

	# lgmon
	msg "Building lgmon"
	cd ${srcdir}/cnijfilter-source-${_pkgver}/lgmon
	./autogen.sh --prefix=/usr --enable-progpath=/usr/bin --program-suffix=mg5300
	make clean
	make
	make DESTDIR=${pkgdir} install

	# cngpijmon
	msg "Building cngpijmon"
	cd ${srcdir}/cnijfilter-source-${_pkgver}/cngpijmon
	./autogen.sh --prefix=/usr --enable-progpath=/usr/bin --datadir=/usr/share --program-suffix=mg5300
	make clean
	make
	make DESTDIR=${pkgdir} install
	
	## Compile and install common stuff
	# libs
	msg "Building common libs"
	cd ${srcdir}/cnijfilter-source-${_pkgver}/libs
	./autogen.sh --prefix=/usr
	make clean
	make
	make DESTDIR=${pkgdir} install

	# cngpij
	msg "Building cngpij"
	cd ${srcdir}/cnijfilter-source-${_pkgver}/cngpij
	./autogen.sh --prefix=/usr --enable-progpath=/usr/bin
	make clean
	make
	make DESTDIR=${pkgdir} install
	
	# pstocanonij
	msg "Building pstocanonij"
	cd ${srcdir}/cnijfilter-source-${_pkgver}/pstocanonij
	./autogen.sh --prefix=/usr --enable-progpath=/usr/bin
	make clean
	make
	make DESTDIR=${pkgdir} install

	# backend
	msg "Building backend"
	cd ${srcdir}/cnijfilter-source-${_pkgver}/backend
	./autogen.sh --prefix=/usr --enable-progpath=/usr/bin
	make clean
	make
	make DESTDIR=${pkgdir} install

	# backendnet
	msg "Building backendnet"
	cd ${srcdir}/cnijfilter-source-${_pkgver}/backendnet
	./autogen.sh --prefix=/usr --enable-progpath=/usr/bin LDFLAGS="-L../../com/${libdir}"
	make clean
	make
	make DESTDIR=${pkgdir} install

	# sm sub process
	msg "Building cnijnpr"
	cd ${srcdir}/cnijfilter-source-${_pkgver}/cngpijmon/cnijnpr
	./autogen.sh --prefix=/usr LDFLAGS="-ldl"
	make clean
	make
	make DESTDIR=${pkgdir} install

	# Install mg5300 libraries
	msg "Installing libraries"
	install -d ${pkgdir}/usr/lib/
	install -m 755 ${srcdir}/cnijfilter-source-${_pkgver}/389/${libdir}/*so.* ${pkgdir}/usr/lib/
	install -d ${pkgdir}/usr/lib/bjlib/
	install -m 644 ${srcdir}/cnijfilter-source-${_pkgver}/389/database/* ${pkgdir}/usr/lib/bjlib/
	# Install common libraries
	install -m 755 ${srcdir}/cnijfilter-source-${_pkgver}/com/${libdir}/*so.* ${pkgdir}/usr/lib/
	install -m 666 ${srcdir}/cnijfilter-source-${_pkgver}/com/ini/* ${pkgdir}/usr/lib/bjlib/
	# Make symbolic links for libraries
	cd ${pkgdir}/usr/lib/
	ln -s libcnnet.so.1.2.2 libcnnet.so
	ln -s libcnbpcmcm389.so.8.10.1 libcnbpcmcm389.so
	ln -s libcnbpcnclapi389.so.3.6.1 libcnbpcnclapi389.so
	ln -s libcnbpcnclbjcmd389.so.3.3.0 libcnbpcnclbjcmd389.so
	ln -s libcnbpcnclui389.so.3.6.1 libcnbpcnclui389.so
	ln -s libcnbpess389.so.4.1.3 libcnbpess389.so
	ln -s libcnbpo389.so.1.0.2 libcnbpo389.so

	# Install license file
	cd ${srcdir}/cnijfilter-source-${_pkgver}
	install -D LICENSE-cnijfilter-${pkgver}EN.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-cnijfilter-${pkgver}EN.txt
}

# vim:set ts=2 sw=2 :et
