# Maintainer: Michael DÃ¼ll <mail@akurei.me> PGP-Key: D6777412 - Fingerprint: C972 8B50 9411 BBE1 5CA5  9906 5103 71F3 D677 7412
# Contributor: Nick B <Shirakawasuna at gmail _dot_com>

pkgname=html2ps
pkgver=1.0b7
pkgrel=5
pkgdesc="An HTML to PostScript converter written in Perl."
arch=('i686' 'x86_64')
url="http://user.it.uu.se/~jan/html2ps.html"
license=('GPL2')
depends=('ghostscript' 'perl' 'texlive-bin')
source=(http://user.it.uu.se/~jan/html2ps-$pkgver.tar.gz)

build() {
    __script=$srcdir/$pkgname-$pkgver/$pkgname

    # Fixes Bug: https://bugzilla.redhat.com/show_bug.cgi?id=822117
    sed -e '/^  local($\[)=0;$/d' -i $__script

    # Set some variables in the script
    sed -i "s#/it/sw/share/www/lib/html2ps/html2psrc#/etc/html2psrc#g" $__script
    sed -i "s#/opt/misc/lib/html2ps/html2psrc#/etc/html2psrc#g" $__script
    sed -i "s#/opt/misc/lib/html2ps/html2ps.html#/usr/share/html2ps/html2ps.html#g" $__script 
    sed -i "s#Ghostscript: 0;#Ghostscript: 1;#g" $__script
    sed -i "s#TeX: 0;#TeX: 1;#g" $__script
    sed -i 's# file: "";# file: "/usr/share/texmf/tex/generic/hyphen/hyphen.tex";#g' $__script

    install -D -m755 $srcdir/$pkgname-$pkgver/$pkgname $pkgdir/usr/bin/$pkgname
    install -D -m644 $srcdir/$pkgname-$pkgver/sample $pkgdir/etc/html2psrc
    install -D -m644 $srcdir/$pkgname-$pkgver/sample $pkgdir/usr/share/$pkgname/etc/html2psrc-sampleconfig
    install -D -m644 $srcdir/$pkgname-$pkgver/$pkgname.1 $pkgdir/usr/share/man/man1/$pkgname.1
    install -D -m644 $srcdir/$pkgname-$pkgver/${pkgname}rc.5 $pkgdir/usr/share/man/man5/${pkgname}rc.5
    install -D -m644 $srcdir/$pkgname-$pkgver/$pkgname.html $pkgdir/usr/share/$pkgname/$pkgname.html

}

sha512sums=('5b50de983eb3acdb70b5b2feac8e0b4ac01e29c0ca36f6ad6a34b92e0ba8c2f359028e5076b47ce07296ad9992a3f889c82372f90199fd4b12f2549f6c779368')
