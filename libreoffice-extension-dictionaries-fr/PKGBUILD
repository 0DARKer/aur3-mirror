# Maintainer: Gilrain <pierre.buard+aur gmail com>
pkgname=libreoffice-extension-dictionaries-fr
pkgver=5.1
_pkgver=${pkgver//./-}
pkgrel=1
pkgdesc="French dictionaries for LibreOffice."
arch=('any')
url="http://extensions.libreoffice.org/extension-center/dictionnaires-francais/"
license=('custom:MPL2' 'LGPL')
depends=('libreoffice-common')
groups=('libreoffice-extensions')
makedepends=('unzip')
changelog=changelog
source=("http://extensions.libreoffice.org/extension-center/dictionnaires-francais/releases/5.1/lo-oo-ressources-linguistiques-fr-v${_pkgver}.oxt"
	"https://www.mozilla.org/MPL/2.0/index.txt")
noextract=(lo-oo-ressources-linguistiques-fr-v${_pkgver}.oxt)
sha256sums=('582d3baca7c03c235416adf05744a39210b21f9ee05c8410bbcb57c2dea1a734'
	    'fab3dd6bdab226f1c08630b1dd917e11fcb4ec5e1e020e2c16f83a0a13863e85')

package() {
  install -dm755 ${pkgdir}/usr/lib/libreoffice/share/extensions
  unzip -q ${srcdir}/lo-oo-ressources-linguistiques-fr-v${_pkgver}.oxt -d ${pkgdir}/usr/lib/libreoffice/share/extensions/dict-fr/

  # fix world writable bit
  find "${pkgdir}/usr/lib/libreoffice/share/extensions/dict-fr/" \( -type d -exec chmod 755 {} \; \) -o \( -type f -exec chmod 644 {} \; \)

  install -Dm644 index.txt ${pkgdir}/usr/share/licences/${pkgname}/LICENSE
}
