# Maintainer: Marcin Karpezo <sirmacik at gmail dot com>
# Contributor: Michael Rosset <mrosset@uarch.org>
# Contributor: Dan McGee <dan@archlinux.org> - Thanks for Your PKGBUILD in extra

pkgname=git-git
pkgver=20100113
pkgrel=1
pkgdesc="Free & open source, distributed version control system"
arch=('i686' 'x86_64')
url="http://git.or.cz/"
license=('GPL2')
depends=('curl' 'expat' 'perl-error' 'perl')
optdepends=('tk: gitk and git gui'
            'perl-libwww: git svn'
            'perl-term-readkey: git svn'
            'subversion: git svn'
            'cvsps: git cvsimport')
makedepends=('git-core' 'asciidoc' 'xmlto' 'docbook-xsl')
replaces=('git-core' 'git')
conflicts=('git-core' 'git')
provides=('git-core' 'git')
install=${pkgname}.install
source=()
md5sums=()

_gitroot="git://git.kernel.org/pub/scm/git/git.git"
_gitname="git"

build() {
    if [ -d ${srcdir}/${_gitname} ]
    then
	msg "Updateing local repository..."
	cd ${_gitname}
	git pull origin master || return 1
	msg "The local files are updated."
    else
	git clone ${_gitroot} ${_gitname}
    fi
    msg "git checkout done or server timeout"
    msg "Starting make..."
    
    cp -r ${srcdir}/${_gitname} ${srcdir}/${_gitname}-build
    cd ${srcdir}/${_gitname}-build
	
    make THREADED_DELTA_SEARCH=1 prefix=/usr all doc || return 1
    make THREADED_DELTA_SEARCH=1 prefix=/usr \
	INSTALLDIRS=vendor DESTDIR=${pkgdir} install install-doc|| return 1
    
# bash completion
    mkdir -p ${pkgdir}/etc/bash_completion.d/
    install -m644 ./contrib/completion/git-completion.bash ${pkgdir}/etc/bash_completion.d/git || return 1

# contrib stuff
    cp -a ./contrib ${pkgdir}/usr/share/git/ || return 1

#remove empty directories
    rm -rf ${pkgdir}/usr/lib/perl5
}

