# Maintainer:  Christoph Vigano <mail at cvigano dot de>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
# Contributor: Michael Rosset <mrosset@uarch.org>
# Contributor: Dan McGee <dan@archlinux.org> - Thanks for Your PKGBUILD in extra

pkgname=git-git
pkgver=20120119
pkgrel=1
pkgdesc="the fast distributed version control system"
arch=('i686' 'x86_64')
url="http://git-scm.com/"
license=('GPL2')
depends=('curl' 'expat' 'perl-error' 'perl')
optdepends=('tk: gitk and git gui'
            'perl-libwww: git svn'
            'perl-term-readkey: git svn'
            'perl-mime-tools: git send-email'
            'perl-net-smtp-ssl: git send-email TLS support'
            'perl-authen-sasl: git send-email TLS support'
            'python2: various helper scripts'
            'subversion: git svn'
            'cvsps: git cvsimport')
makedepends=('git-core' 'asciidoc' 'xmlto' 'docbook-xsl' 'emacs')
replaces=('git-core' 'git')
conflicts=('git-core' 'git')
provides=('git-core' 'git')
backup=('etc/conf.d/git-daemon.conf')
source=('git-daemon'
        'git-daemon.conf')

md5sums=('8e2648910fd5dd4f1c41d3c7fa9e9156'
         '2e42bf97779a1c6411d89043334c9e78')

_gitroot="git://git.kernel.org/pub/scm/git/git.git"
_gitname="git"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone --depth 1 "$_gitroot" "$_gitname" #shallow clone
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."
    
  rm -rf "$srcdir/$_gitname-build"
  mkdir "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname" && ls -A1 --indicator-style=none | grep -v "^.git" | xargs -d '\n' cp -r -t "../$_gitname-build" # do not copy over the .git folder
  cd "$srcdir/$_gitname-build"
	
  export PYTHON_PATH='/usr/bin/python2'
  make prefix=/usr gitexecdir=/usr/lib/git-core \
    CFLAGS="$CFLAGS" LDFLAGS="$LDFLAGS" \
    USE_LIBPCRE=1 \
    NO_CROSS_DIRECTORY_HARDLINKS=1 all man

  cd contrib/emacs
  make prefix=/usr
}
    
package() {
  export PYTHON_PATH='/usr/bin/python2'

  cd "$srcdir/$_gitname-build"
  make prefix=/usr gitexecdir=/usr/lib/git-core \
    CFLAGS="$CFLAGS" LDFLAGS="$LDFLAGS" \
    USE_LIBPCRE=1 \
    NO_CROSS_DIRECTORY_HARDLINKS=1 \
    INSTALLDIRS=vendor DESTDIR="$pkgdir" install install-man

  # bash completion
  mkdir -p "$pkgdir"/etc/bash_completion.d/
  install -m644 ./contrib/completion/git-completion.bash "$pkgdir"/etc/bash_completion.d/git 
  # more contrib stuff
  cp -a ./contrib "$pkgdir"/usr/share/git/ 
  # scripts are for python 2.x
  sed -i 's|#![ ]*/usr/bin/env python|#!/usr/bin/env python2|' \
    $(find "$pkgdir" -name '*.py') \
    "$pkgdir"/usr/share/git/{fast-import/git-p4,gitview/gitview}

  # emacs interface
  cd contrib/emacs
  make prefix=/usr DESTDIR="$pkgdir" install

  # how 'bout some manpages?
  #for mansect in man1 man5 man7; do
  #  for manpage in "$srcdir"/$mansect/*; do
  #    install -D -m644 $manpage "$pkgdir"/usr/share/man/$mansect/$(basename $manpage)
  #  done
  #done

  # remove perllocal.pod, .packlist, and empty directories.
  rm -rf "$pkgdir"/usr/lib/perl5

  # git daemon script
  install -D -m755 "$srcdir"/git-daemon "$pkgdir"/etc/rc.d/git-daemon
  install -D -m644 "$srcdir"/git-daemon.conf "$pkgdir"/etc/conf.d/git-daemon.conf

}

