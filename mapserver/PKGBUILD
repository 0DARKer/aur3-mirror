# Contributor: Marcos Piccinini <x@nofxx.com>
pkgname=mapserver
pkgver=5.6.1
pkgrel=1
pkgdesc="Platform for publishing spatial data and interactive mapping applications to the web"
arch=(i686 x86_64)
license=('MIT')
url="http://www.mapserver.org"
depends=('libpng' 'freetype2' 'gd' 'zlib' 'gdal' 'proj' 'libjpeg' 'libxml2' 'libpqxx' 'pdflib-lite' 'geos' 'agg' 'apache')
makedepends=('cfitsio')
options=()
source=("http://download.osgeo.org/mapserver/$pkgname-$pkgver.tar.gz")
md5sums=('7eca7a1b71d62ad0eee5893df46ec10f')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}

  ./configure   --with-ogr=/usr/bin/gdal-config \
                --with-gdal=/usr/bin/gdal-config \
                --with-httpd=/usr/sbin/httpd \
                --with-curl-config=/usr/bin/curl-config \
                --with-proj=/usr \
                --with-tiff \
                --with-gd=/usr \
                --with-jpeg \
                --with-png \
                --with-freetype=/usr/ \
                --with-threads \
                --with-wcs \
                --with-wfsclient \
                --with-wmsclient \
                --with-libiconv=/usr \
                --with-postgis=/usr/bin/pg_config \
                --with-geos=/usr/bin/geos-config \
                --with-xml2-config=/usr/bin/xml2-config \
                --with-sos \
                --with-agg \
                --enable-debug || return 1

  make || return 1
  mkdir -p ${startdir}/pkg/usr/bin || return 1
  install -m755 mapserv ${startdir}/pkg/usr/bin/mapserv || return 1
  install -m755 mapserver-config ${startdir}/pkg/usr/bin/mapserver-config || return 1
}

# To compile with Arcgis SDE Support:
#   --with-sde-version=90 \
#   --with-sde=/usr/sde/sdeexe90 \

