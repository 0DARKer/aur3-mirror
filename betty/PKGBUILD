# Maintainer: Maxim Andersson <thesilentboatman@gmail.com>

pkgname=betty
pkgver=0.1.6
pkgrel=1
pkgdesc="Friendly English-like interface for your command line"
arch=('any')
url="https://github.com/pickhardt/betty"
license=('Apache')
depends=('ruby')
source=("https://github.com/pickhardt/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('25f195d6a2517481e681cfed735381f5b4e61791215792f3ddf25debbf5afbfa')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Set absolute paths for static files
  sed -i "s^File.dirname(__FILE__) + '/lib^'/usr/lib/${pkgname}^g" main.rb
  sed -i "s^ENV\['HOME'\] + '/.^'/etc/${pkgname}/^g" lib/config.rb
  sed -i "s^ENV\['HOME'\] + '/.^'/var/log/${pkgname}/^g" main.rb
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -d "${pkgdir}/etc/${pkgname}"
  install -d "${pkgdir}/usr/lib/${pkgname}"

  install -Dm666 /dev/null "${pkgdir}/var/log/${pkgname}/betty_history"
  install -D main.rb "${pkgdir}/usr/bin/${pkgname}"
  install -m644 -t "${pkgdir}/usr/lib/${pkgname}" lib/*
}

# vim:set ts=2 sw=2 et:
