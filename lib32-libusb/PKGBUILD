# Maintainer: 3V0LU710N <db_eee-at-hotmail-dot-com>

pkgname=lib32-libusb
pkgver=1.0.18
pkgrel=1
pkgdesc="A cross-platform user library to access USB devices (32-bit)"
arch=('x86_64')
url="http://libusb.info/"
license=('LGPL')
depends=('lib32-glibc' 'libusb' 'lib32-systemd')
makedepends=('gcc-multilib')
source=(http://downloads.sourceforge.net/libusb/libusb-${pkgver}.tar.bz2)
options=(!libtool)
md5sums=('4a6d049923efc6496f1d383054dac1a6')

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  cd "libusb-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 --disable-static
  make
}

package () {
  cd "${srcdir}/libusb-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/include
}
