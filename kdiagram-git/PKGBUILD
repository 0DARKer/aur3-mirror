pkgname=kdiagram-git
_gitname=kdiagram
pkgver=0.72.b883883
pkgrel=1
pkgdesc="Powerful libraries (KChart, KGantt) for creating business diagrams"
arch=('i686' 'x86_64')
url="http://quickgit.kde.org/?p=kdiagram.git"
license=('LGPL')
depends=('extra-cmake-modules' 'qt5-base' 'qt5-svg')
makedepends=('git')
conflicts=('kdiagram')
provides=('kdiagram')
source=("git://anongit.kde.org/kdiagram.git#branch=master")
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/$_gitname"
    echo "0.$(git rev-list --count HEAD).$(git describe --always)"
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../"$_gitname" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DLIB_INSTALL_DIR=lib \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
        -DBUILD_TESTING=OFF
    make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
