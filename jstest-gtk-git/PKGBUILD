# Maintainer: ZekeSulastin <zekesulastin@gmail.com>
# Contributor: MaMuS <adriano.lols@gmail.com>

pkgname=jstest-gtk-git
pkgver=20120606
pkgrel=2
pkgdesc="A simple GTK2-based joystick tester and calibrator"
arch=('i686' 'x86_64')
url="https://github.com/Grumbel/jstest-gtk/"
source=('jstest-gtk.desktop')
md5sums=('82c590be65690f23b5a68496c4bdaf11')
license=('GPL3')
depends=('gtkmm')
makedepends=('git' 'scons')

_gitroot="git://github.com/Grumbel/jstest-gtk/"
_gitname="jstest-gtk"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # Needs a few minor alterations to build; pull request submitted
  sed -i '31a #include <unistd.h>' src/joystick.cpp
  sed -i '20 s/2.0/&\ x11/' SConstruct
  sed -i 's_data/generic.png_/usr/share/jstest-gtk/&_' src/joystick_list_widget.cpp
  scons

}

package() {
  cd "$srcdir/$_gitname-build"
  install -D -m755 jstest-gtk "$pkgdir/usr/bin/jstest-gtk"
  install -D -m644 "./data/generic.png" "${pkgdir}/usr/share/icons/jstest-gtk.png"
  install -D -m644 "${srcdir}/jstest-gtk.desktop" "${pkgdir}/usr/share/applications/jstest-gtk.desktop"
  mkdir -p "${pkgdir}/usr/share/jstest-gtk"
  cp -r "${srcdir}/jstest-gtk/data" "${pkgdir}/usr/share/jstest-gtk/"
}
