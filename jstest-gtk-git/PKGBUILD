# Submiter: ZekeSulastin <zekesulastin@gmail.com>
# Maintainer: MaMuS <adriano.lols@gmail.com>

pkgname=jstest-gtk-git
pkgver=20130109
pkgrel=1
pkgdesc="A simple GTK2-based joystick tester and calibrator"
arch=('i686' 'x86_64')
url="https://github.com/Grumbel/jstest-gtk/"
source=('jstest-gtk.desktop' 'fix_asset_path.patch')
md5sums=('82c590be65690f23b5a68496c4bdaf11'
         'b3f21e3c6942a540d43cbf1654b4ad76')
license=('GPL3')
depends=('gtkmm')
makedepends=('git' 'scons')

_gitroot="git://github.com/Grumbel/jstest-gtk/"
_gitname="jstest-gtk"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi
  msg "GIT checkout done or server timeout"

  msg "Starting build..."
  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  msg "Patching..."
  patch -p2 < "$srcdir/fix_asset_path.patch"

  scons
}

package() {
  cd "$srcdir/$_gitname-build"
  install -D -m755 jstest-gtk "$pkgdir/usr/bin/jstest-gtk"
  install -D -m644 "./data/generic.png" "${pkgdir}/usr/share/icons/jstest-gtk.png"
  install -D -m644 "${srcdir}/jstest-gtk.desktop" "${pkgdir}/usr/share/applications/jstest-gtk.desktop"
  mkdir -p "${pkgdir}/usr/share/jstest-gtk"
  cp -r "${srcdir}/jstest-gtk/data" "${pkgdir}/usr/share/jstest-gtk/"
}

