# Maintainer: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Note: namcap warns that 'libcups' (in the `depends` array) is unnecessary.
# This is false. Splitter will produce the following exception of libcups is
# not installed:
# Exception in thread "main" java.lang.UnsatisfiedLinkError: /usr/lib/jvm/java-7-openjdk/jre/lib/amd64/headless/libmawt.so: libcups.so.2: cannot open shared object file: No such file or directory

pkgname=splitter
pkgver=r300
pkgrel=2
pkgdesc='Utility for splitting OpenStreetMap maps into small tiles'
url='http://www.mkgmap.org.uk/'
license=('GPL')
arch=('any')
depends=('java-environment' 'sh' 'libcups')
source=("http://www.mkgmap.org.uk/snapshots/${pkgname}-${pkgver}.tar.gz")
md5sums=('2fc11dfa7f886ce91596d61a32305d93')
sha256sums=('a55fff620f75b697c72e630518ec39ba3f0ec96ff4c84e957ed8d30a89b4d1ec')

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Install splitter.jar and it's dependencies.
  install -D -m644 \
    ${pkgname}.jar \
    ${pkgdir}/usr/share/java/${pkgname}/${pkgname}.jar
  mkdir ${pkgdir}/usr/share/java/${pkgname}/lib
  install -m644 \
    -t ${pkgdir}/usr/share/java/${pkgname}/lib/ \
    lib/*

  # Create an executable file.
  install -d ${pkgdir}/usr/bin
  echo '#!/bin/sh' > ${pkgdir}/usr/bin/$pkgname
  echo CLASSPATH="/usr/share/java/${pkgname}/${pkgname}.jar" >> ${pkgdir}/usr/bin/$pkgname
  echo java -jar \"\${CLASSPATH}\" \"\$@\" >> ${pkgdir}/usr/bin/$pkgname
  chmod 755 ${pkgdir}/usr/bin/$pkgname
}
