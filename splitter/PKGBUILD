# Maintainer: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
#
# Note: namcap warns that 'libcups' (in the `depends` array) is unnecessary.
# This is untrue. Splitter will produce the following exception of libcups is
# not installed:
#
#     Exception in thread "main" java.lang.UnsatisfiedLinkError: /usr/lib/jvm/java-7-openjdk/jre/lib/amd64/headless/libmawt.so: libcups.so.2: cannot open shared object file: No such file or directory
#

pkgname=splitter
pkgver=r306
pkgrel=1
pkgdesc='Utility for splitting OpenStreetMap maps into tiles.'
url='http://www.mkgmap.org.uk/'
license=('GPL')
arch=('any')
depends=('java-environment' 'sh' 'libcups')
source=("http://www.mkgmap.org.uk/snapshots/${pkgname}-${pkgver}.tar.gz")
md5sums=('1ad2a50d87776b4ef7038c9a02ac0b2f')
sha256sums=('65214b855e1295e55108215050b3754f0062e7c833e696cc83ab943e3e2d271b')

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Install splitter.jar and it's dependencies.
  install -D -m644 \
    ${pkgname}.jar \
    ${pkgdir}/usr/share/java/${pkgname}/${pkgname}.jar
  mkdir ${pkgdir}/usr/share/java/${pkgname}/lib
  install -m644 \
    -t ${pkgdir}/usr/share/java/${pkgname}/lib/ \
    lib/*

  # Create an executable file.
  install -d ${pkgdir}/usr/bin
  echo '#!/bin/sh' > ${pkgdir}/usr/bin/${pkgname}
  echo CLASSPATH="/usr/share/java/${pkgname}/${pkgname}.jar" >> ${pkgdir}/usr/bin/${pkgname}
  echo java -jar \"\${CLASSPATH}\" \"\$@\" >> ${pkgdir}/usr/bin/${pkgname}
  chmod 755 ${pkgdir}/usr/bin/${pkgname}
}

# vim:set ts=2 sw=2 et:
