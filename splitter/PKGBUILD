# Maintainer: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
#
# namcap warns that 'libcups' is an unnecessary dependency. This is untrue.
# Splitter will produce the following exception if libcups is not installed:
#
#     Exception in thread "main" java.lang.UnsatisfiedLinkError:
#     /usr/lib/jvm/java-7-openjdk/jre/lib/amd64/headless/libmawt.so:
#     libcups.so.2: cannot open shared object file: No such file or directory
#

pkgname=splitter
pkgver=r317
pkgrel=1
pkgdesc='Utility for splitting OpenStreetMap maps into tiles.'
url='http://www.mkgmap.org.uk/'
license=(GPL)
arch=(any)
depends=(java-environment sh libcups)
source=("http://www.mkgmap.org.uk/snapshots/${pkgname}-${pkgver}.tar.gz")
sha256sums=('b1032631b552d563957458e7e90143c0336152805b15de175f3602dfb7e8114d')

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Install splitter.jar and it's dependencies.
  install -Dm644 "${pkgname}.jar" \
    "${pkgdir}/usr/share/java/${pkgname}/${pkgname}.jar"
  install -d "${pkgdir}/usr/share/java/${pkgname}/lib/"
  install -m644 -t "${pkgdir}/usr/share/java/${pkgname}/lib/" lib/*

  # Create an executable file.
  install -d "${pkgdir}/usr/bin"
cat << EOF > "${pkgdir}/usr/bin/${pkgname}"
#!/usr/bin/env sh
CLASSPATH=/usr/share/java/${pkgname}/${pkgname}.jar
java -jar "\${CLASSPATH}" "\$@"
EOF
  chmod 755 "${pkgdir}/usr/bin/${pkgname}"
}

# vim:set ts=2 sw=2 et:
