# $Id: PKGBUILD,v 1.14 2008/12/13 10:56:15 abhidg Exp $
# Maintainer: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=gnonlin-git
pkgver=20090517
pkgrel=1
pkgdesc="A library for creating non-linear video editors"
arch=(i686 x86_64)
url="http://gnonlin.sourceforge.net"
depends=('gstreamer0.10')
provides=('gnonlin')
conflicts=('gnonlin')
options=('!libtool')
license=('LGPL')
source=()

_gitroot="git://anongit.freedesktop.org/gstreamer/gnonlin"
_gitname="gnonlin"

build() {

    cd $srcdir

    msg "Connecting to GIT server..."
    if [[ -d $_gitname ]]; then
        (cd $_gitname && git pull origin)
    else
        git clone --depth=1 $_gitroot $_gitname
    fi

    cd $_gitname
    if test ! -f common/gst-autogen.sh;
    then
        echo "+ Setting up common submodule"
        git submodule init
    fi
    git submodule update
    cd ..

    msg "GIT checkout done or server timeout"
    msg "Starting make..."

    rm -rf $_gitname-build
    cp -r $_gitname $_gitname-build
    cd $_gitname-build

    ./autogen.sh
    ./configure --prefix=/usr
    make || return 1
    make DESTDIR="$pkgdir" install || return 1
}
