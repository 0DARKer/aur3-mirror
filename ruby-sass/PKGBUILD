# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Contributor: Matt Harrison <matt at mistbyte dot com>
# Contributor: Renzo Carbonara <gnuk0001@gmail.com>
# Contributor: Bogdan Szczurek <thebodzio@gmail.com>

_pkgname=sass
pkgname=ruby-$_pkgname
pkgver=3.4.6
pkgrel=1
pkgdesc="Tools and Ruby libraries for the CSS3 extension languages: Sass and SCSS."
arch=("any")
url="http://sass-lang.com/"
license=("MIT")
depends=("ruby" "ruby-yard" "ruby-maruku")
makedepends=("rubygems")
# optdepends=("ruby-listen")
source=("http://gems.rubyforge.org/gems/$_pkgname-$pkgver.gem")
noextract=("$_pkgname-$pkgver.gem")
sha256sums=("31d44cc011faad88eae6c22f7121515622764ccb1b48f68a6cef3ce6459494b9")

package() {
	local _gemdir="$(ruby -rubygems -e"puts Gem.default_dir")"
	gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" -n "$pkgdir/usr/bin" "$_pkgname-$pkgver.gem"
	install -D "$pkgdir$_gemdir/gems/$_pkgname-$pkgver/MIT-LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
