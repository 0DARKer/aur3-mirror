# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>
# Contributor: Matt Harrison <matt at mistbyte dot com>
# Contributor: Renzo Carbonara <gnuk0001@gmail.com>
# Contributor: Bogdan Szczurek <thebodzio@gmail.com>

pkgname=ruby-sass
_gemname=${pkgname#ruby-}
pkgver=3.2.9
pkgrel=1
pkgdesc="Tools and Ruby libraries for the CSS3 extension languages: Sass and SCSS."
arch=('any')
url="http://sass-lang.com/"
license=('MIT')
depends=('ruby' 'ruby-yard>=0.5.3' 'ruby-maruku>=0.5.9')
makedepends=('rubygems')
optdepends=('rb-inotify>=0.5.1')
source=("http://gems.rubyforge.org/gems/${_gemname}-${pkgver}.gem")
noextract=("${_gemname}-${pkgver}.gem")
sha256sums=('f88433638d8dcf31366e55451087e04fb1aecc29c554f240647baba8495c9b22')

package() {
	local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"
	gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" -n "$pkgdir/usr/bin" ${_gemname}-$pkgver.gem
	install -D "$pkgdir$_gemdir/gems/${_gemname}-$pkgver/MIT-LICENSE" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
