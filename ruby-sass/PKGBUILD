# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>
# Contributor: Matt Harrison <matt at mistbyte dot com>
# Contributor: Renzo Carbonara <gnuk0001@gmail.com>
# Contributor: Bogdan Szczurek <thebodzio@gmail.com>

_pkgname=sass
pkgname=ruby-$_pkgname
pkgver=3.2.14
pkgrel=1
pkgdesc="Tools and Ruby libraries for the CSS3 extension languages: Sass and SCSS."
arch=('any')
url="http://sass-lang.com/"
license=('MIT')
depends=('ruby' 'ruby-yard' 'ruby-maruku')
makedepends=('rubygems')
optdepends=('rb-inotify>=0.5.1')
source=("http://gems.rubyforge.org/gems/$_pkgname-$pkgver.gem")
noextract=("$_pkgname-$pkgver.gem")
sha256sums=('56155378aa8ce800b7926fcda737a1d52e36235c32e4e70297f7bf524b355cc9')

package() {
	local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"
	gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" -n "$pkgdir/usr/bin" "$_pkgname-$pkgver.gem"
	install -D "$pkgdir$_gemdir/gems/$_pkgname-$pkgver/MIT-LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
