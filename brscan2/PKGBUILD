# Maintainer: libernux <dutchman55@gmx.com>
# Contributor: Olaf Leidinger <leidola@newcon.de>
# Contributor: Hervé Cauwelier <herve ¤ oursours.net>
# Edited by Marc Straube <dev@marcstraube.de>
pkgname=brscan2
pkgver=0.2.5
pkgrel=1
pkgdesc="SANE drivers from Brother for model-2 scanners"
depends=('sane' 'libusb-compat')
arch=('i686' 'x86_64')
if [ "$CARCH" == "i686" ]; then
  _filename="$pkgname-$pkgver-$pkgrel.i386.rpm"
  md5sums=('944432983dcb918704862147b2a27977' '1bd5f896f3e97fe6d53a85b4d5a4ea79')
else
  _filename="$pkgname-$pkgver-$pkgrel.x86_64.rpm"
  md5sums=('88ab217b814ba87ac855b21dc037e0ac' '1bd5f896f3e97fe6d53a85b4d5a4ea79')
fi
source=(
	"http://www.brother.com/pub/bsc/linux/dlf/$_filename"
	'scanner-license.txt'
)
url="http://welcome.solutions.brother.com/bsc/public_s/id/linux/en/index.html"
license=('custom:brother commercial license')
install="$pkgname.install"

package() {
    cp -R "$srcdir/usr" "$pkgdir"
    install -m 644 -D scanner-license.txt "${pkgdir}/usr/share/licenses/${pkgname}/scanner-licence.txt"
    # On x86_64, install in "/usr/lib" instead of "/usr/lib64"
    if [ "$CARCH" == "x86_64" ]; then
      cd "$pkgdir/usr/"
      mv "$pkgdir/usr/lib64" "$pkgdir/usr/lib"
      # some links are wrong now - recreate them
      for fn in lib/libbrcolm2.so.1.0.1 \
          lib/libbrscandec2.so.1.0.0 \
          lib/sane/libsane-brother2.so.1.0.7
      do
          # break, if file does not exist
          if [ ! -f $fn ] ; then
              echo "ERROR: $fn does not exist"
              return 1
          fi
          base=`echo $fn | cut -d. -f1`.so
          major=`echo $fn | cut -d. -f3`
          ln -sf `basename $fn` $base
          ln -sf `basename $fn` $base.$major
      done
    fi
}