# Maintainer: libernux <dutchman55@gmx.com>
# Contributor: Olaf Leidinger <leidola@newcon.de>
# Contributor: Hervé Cauwelier <herve ¤ oursours.net>
# Edited by Marc Straube <dev@marcstraube.de>
pkgname=brscan2
pkgver=0.2.5
pkgrel=1
pkgdesc="SANE drivers from Brother for model-2 scanners"
arch=('i686' 'x86_64')
url="http://welcome.solutions.brother.com/bsc/public_s/id/linux/en/index.html"
license=('custom:brother commercial license')
depends=('sane' 'libusb-compat')
install="$pkgname.install"
if [ "$CARCH" == "i686" ]; then
   source=(
		"http://www.brother.com/pub/bsc/linux/dlf/$pkgname-$pkgver-$pkgrel.i386.rpm"
		'scanner-license.txt'
		)
	md5sums=('944432983dcb918704862147b2a27977' '1bd5f896f3e97fe6d53a85b4d5a4ea79')
else
   source=(
		"http://www.brother.com/pub/bsc/linux/dlf/$pkgname-$pkgver-$pkgrel.x86_64.rpm"
		'scanner-license.txt'
		)
	md5sums=('88ab217b814ba87ac855b21dc037e0ac' '1bd5f896f3e97fe6d53a85b4d5a4ea79')
fi
prepare() {
#  do not install in '/usr/local'
	if [ -d $srcdir/usr/local/Brother ]; then
		install -d $srcdir/usr/share
		mv $srcdir/usr/local/Brother/ $srcdir/usr/share/brother
		rm -rf $srcdir/usr/local
		sed -i 's|/usr/local/Brother|/usr/share/brother|g' `grep -lr '/usr/local/Brother' ./`
		sed -i 's|/usr/local|/usr/share|g' `grep -lr '/usr/local' ./`
		ln -sf /usr/share/brother/sane/brsaneconfig2 $srcdir/usr/bin/brsaneconfig2
        fi
}	
package() {
cp -R "$srcdir/usr" "$pkgdir"
install -m 644 -D scanner-license.txt "${pkgdir}/usr/share/licenses/${pkgname}/scanner-licence.txt"
# On x86_64, install in "/usr/lib" instead of "/usr/lib64"
if	 [ "$CARCH" == "x86_64" ]; then
	cd "$pkgdir/usr/"
	mv "$pkgdir/usr/lib64" "$pkgdir/usr/lib"
# some links are wrong now - recreate them
	cd "$pkgdir/usr/lib"
        ln -sf libbrcolm2.so.1 libbrcolm2.so
        ln -sf libbrcolm2.so.1.0.1 libbrcolm2.so.1
        ln -sf libbrscandec2.so.1 libbrscandec2.so
        ln -sf libbrscandec2.so.1.0.0 libbrscandec2.so.1
	cd "$pkgdir/usr/lib/sane"
	ln -sf libsane-brother2.so.1 libsane-brother2.so
	ln -sf libsane-brother2.so.1.0.7 libsane-brother2.so.1
fi
}