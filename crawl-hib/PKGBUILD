# Maintainer: Duncan K. <duncank@fastmail.fm>

pkgname=crawl-hib
pkgver=0.01.01
_hibver=0-01-01
pkgrel=1
pkgdesc='Local multiplayer dungeon crawler where your friends control the monsters (Humble Bundle version)'
url='http://www.powerhoof.com/crawl/'
arch=('i686' 'x86_64')
license=('custom:commercial')
depends=('glu' 'libxcursor' 'alsa-lib')
optdepends=('libpulse: PulseAudio support')
options=('!strip' '!upx')
PKGEXT='.pkg.tar.gz'
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Manually download it to \"$(pwd)\", or set up a hib:// DLAGENT in /etc/makepkg.conf."; exit 1')

if [ $CARCH == i686 ]; then
	_binext='x86'
else
	_binext='x86_64'
fi

_installer="crawl-${_hibver}-linux.tar.gz"
_bin="Crawl.${_binext}"

source=("hib://${_installer}")
md5sums=('48ee16173a258b8b0f4b35f8352a15e7')

package() {
	cd $srcdir
	_installdir="/opt/crawl"; _target=$pkgdir/$_installdir

	# Install game files
	mkdir -p $_target
	cp -r "Crawl_Data" $_target
	cp $_bin $_target

	# Link executable
	install -d "${pkgdir}/usr/bin"
	ln -s ${_installdir}/${_bin} "${pkgdir}/usr/bin/crawl"
}
