# Mantainer: Jens Staal <staal1978@gmail.com>
# Modified from open_watcom: Felix Stirlitz <m.p.isaev@yandex.com>
# adjust the configuration below to change options
pkgname=open_watcom-v2
pkgver=2.0
pkgrel=1
pkgdesc="The Open Watcom C/C++ compiler, binary distribution -V2 fork"
arch=('i686' 'x86_64')
#url="http://www.openwatcom.org"
url="https://github.com/open-watcom"
provides=("open_watcom")
conflicts=("open_watcom")
replaces=("open_watcom")
license=('custom:OWPL-1')
source=( 'http://downloads.sourceforge.net/project/openwatcom/current-build/open-watcom-2_0-c-linux-x86' )
noextract=( 'open-watcom-2_0-c-linux-x86' )
#md5sums change frequently since it is a snapshot. If it fails, download manually and check md5sum
md5sums=('82a64a50c9186d4c812d4f0ae63d65c0')
options=(!strip)

build() {
    chmod +x ./open-watcom-2_0-c-linux-x86
}

package() {
	_tmpnam="$(mktemp /tmp/options.$$.XXXXXXXXX)"
	sed -e 's/#.*$//g' >"$_tmpnam" <<EOF
ms=true                        # small memory model libraries
mm=true                        # medium memory model libraries
mc=true                        # compact memory model libraries
ml=true                        # large memory model libraries
mh=true                        # huge memory model libraries
doshost=true                   # DOS host binaries
os2host=false                  # OS/2 host binaries
dostarg=true                   # DOS target libraries
os2targ=false                  # OS/2 target libraries
nlmtarg=false                  # Novell Netware target
rdoshost=false                 # RDOS host binaries
rdostarg=false                 # RDOS target libraries
lnxhost=true                   # Linux host binaries
lnxtarg=true                   # Linux target libraries
winhost=false                  # Windows host binaries
wnthost=false                  # Windows NT host binaries
wintarg=true                   # Windows target libraries
wnttarg=true                   # Windows NT target libraries
tools16=true                   # 16-bit tools
samples=true                   # Samples
helpfiles=false                # Help files
win16_helpfiles=false          # Win16 help files
win32_helpfiles=false          # Windows NT help files
htmlhelp_helpfiles=false       # CHM help files
whelp_helpfiles=false          # WHELP help files
os2_helpfiles=false            # OS/2 help files
pdf_helpfiles=true             # PDF help files
rtsource=true                  # Runtime library source
3r=true                        # Register calling convention libraries
3s=true                        # Stack calling convention libraries
cplusplus=true	               # C++ support
EOF
	./open-watcom-2_0-c-linux-x86 -i -dDstDir="$pkgdir/opt/watcom" -dOWDir="/opt/watcom" -f="$_tmpnam"
	install -d "$pkgdir/usr/share/licenses/watcom"
	install -Dm644 "$pkgdir/opt/watcom/license.txt" "$pkgdir/usr/share/licenses/watcom/license.txt"
	_pkgdirsane="$(echo "$pkgdir" | sed -e 's,[\[^$.+*\\?],\\&,g')"
	sed -i -e "s,$_pkgdirsane,," "$pkgdir/opt/watcom/owsetenv.sh"
	rm -f "$_tmpnam"
}
