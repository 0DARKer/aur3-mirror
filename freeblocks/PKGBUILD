# Maintainer: Christoph J. Thompson <cjsthompson@gmail.com>

pkgname=freeblocks
pkgver=r39.g626f950
pkgrel=1
pkgdesc="Tetris Attack-like puzzle game"
arch=('i686' 'x86_64')
url="http://github.com/dorkster/freeblocks"
license=('GPL3')
depends=('sdl' 'sdl_image' 'sdl_mixer' 'sdl_ttf')
makedepends=('git')
source=("${pkgname}::git+https://github.com/dorkster/freeblocks.git")
md5sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "r%s.g%s" \
    "$(git rev-list --count HEAD)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${pkgname}"
  sed -i 's|#CFLAGS+=-DPKGDATADIR=\\\"$(PKGDATADIR)\\\"|CFLAGS+=-DPKGDATADIR=\\\"$(PKGDATADIR)\\\"|g' Makefile
  make PKGDATADIR="/usr/share/freeblocks"
}

package() {
  cd "${pkgname}"
  make DESTDIR="${pkgdir}" PKGDATADIR="/usr/share/freeblocks" PREFIX="/usr" install
  install -D -m644 README "${pkgdir}/usr/share/doc/freeblocks/README"
}
