# Maintainer: Joshua Stiefer <facedelajunk@gmail.com>

pkgname=herrie-git
pkgver=20130329
pkgrel=1
pkgdesc="A minimal music player"
arch=('i686' 'x86_64')
url="http://www.herrie.info" 
license=('GPL')
depends=('libmodplug' 'libsndfile' 'libid3tag' 'libxspf' 'libmad' 'curl' 'dbus-glib')
makedepends=('git')
source=('git://git.80386.nl/herrie'
        'extra-dbus-support.diff')
provides=(herrie)
conflicts=('herrie')
md5sums=('SKIP'
         'f8eebc84ffeeeeaaa3942411354cbb02')

pkgver() {
  cd "$srcdir/herrie"
  git log -1 --format="%cd" --date=short | sed 's|-||g'
}

prepare() {
  cd "$srcdir/herrie"
  patch -p1 < "$srcdir/extra-dbus-support.diff"
}

build() {
  cd "$srcdir/herrie/herrie"
  ./configure
  make
}

package() {
  cd "$srcdir/herrie/herrie"
  make DESTDIR="$pkgdir" install 
}

# vim:set ts=2 sw=2 et:

