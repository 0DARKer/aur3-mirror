# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor:  TDY <tdy@gmx.com>
# Contributor: Massimiliano Torromeo <Massimiliano.Torromeo AT gmail DOT com>

pkgname=qedit
pkgver=2.4.1
pkgrel=1
pkgdesc="A multi-purpose text editor based on NEdit using Qt"
arch=('i686' 'x86_64')
url="http://www.phenix.bnl.gov/WWW/publish/hpereira/software/index.php?page=package&package_list=software_list_qt4&package=qedit&full=1"
license=('GPL')
depends=('aspell' 'qt')
options=('!emptydirs')
install="$pkgname.install"
source=("http://www.phenix.bnl.gov/WWW/publish/hpereira/software/tgz/$pkgname-$pkgver.tar.gz"
        "$pkgname-gcc4.7.patch" "$pkgname.desktop")
md5sums=('e838a943a8480ad056406493d28735b1'
         'c966d82bb4c95a02fc142a90150ab1d1'
         'aacf039bebaa2d60a6451bab00b60537')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # gcc 4.7 patch
  patch -p1 -i ../${source[1]}

  # strip --as-needed flag
  LDFLAGS="${LDFLAGS//,--as-needed}"

  # DSO link fix
  export LIBS="-lX11"

  ./configure --prefix=/usr \
              --with-qt-dir=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # desktop file
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 icon.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
