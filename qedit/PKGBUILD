# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: rtfreedman
# Contributor: SpepS <dreamspepser@yahoo.it>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Massimiliano Torromeo <Massimiliano.Torromeo@gmail.com>

pkgname=qedit
pkgver=2.6.5
pkgrel=1
pkgdesc="A multi-purpose text editor based on NEdit using Qt"
arch=(i686 x86_64)
url=http://www.phenix.bnl.gov/WWW/publish/hpereira/software/index.php?page=package&package_list=software_list_qt4&package=qedit&full=1
license=(GPL2)
depends=(aspell kdelibs qt4)
options=(!emptydirs)
install=$pkgname.install
source=(http://www.phenix.bnl.gov/WWW/publish/hpereira/software/tgz/$pkgname-$pkgver.tar.gz)
sha256sums=('ebe536e24c08052e98351502fb29e09560bc68d5acb937c5330f707875e48ba1')
sha512sums=('00d9eaa947658bfac3626e8863e5b8017e13e563df24e4a86f25fc5d9a590e25a7a9af71dd7fc3b89795a8c7297fc27c36d4cf7835b41f5fdfe7f0cc4dce6fd2')

build() {
    install -d $pkgname-build/
    cd $pkgname-build/
    cmake ../$pkgname-$pkgver/ \
        -DCMAKE_INSTALL_PREFIX=$(kde4-config --prefix) \
        -DCMAKE_BUILD_TYPE=Release \
        -DQT_QMAKE_EXECUTABLE=qmake-qt4
    make
}

package() {
    make -C $pkgname-build DESTDIR="$pkgdir" install

    cd $pkgname-$pkgver/
    install -Dm644 $pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
    install -Dm644 $pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
}
