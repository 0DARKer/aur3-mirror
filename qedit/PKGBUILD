# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: rtfreedman
# Contributor: SpepS <dreamspepser@yahoo.it>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Massimiliano Torromeo <Massimiliano.Torromeo@gmail.com>

pkgname=qedit
pkgver=2.6.0
pkgrel=1
pkgdesc="A multi-purpose text editor based on NEdit using Qt"
arch=(i686 x86_64)
url="http://www.phenix.bnl.gov/WWW/publish/hpereira/software/index.php?page=package&package_list=software_list_qt4&package=qedit&full=1"
license=(GPL2)
depends=(aspell qt4)
options=(!emptydirs)
install=$pkgname.install
source=(http://www.phenix.bnl.gov/WWW/publish/hpereira/software/tgz/$pkgname-$pkgver.tar.gz
    $pkgname.desktop)
sha256sums=('0cd8f02fcae3fbdfbf871c2d8fd6e82483cd52f7ab2388c68d1efac5328033e1'
    '4c7e2e4a62e19402afbc6b9000adf3c62476acbb2ae1c27c7e86034b63253a29')
sha512sums=('ccb6a6a6200d4254ba38918e487b28c7aeaa07df83f46a550eb1ec27260bba53a310626961ca21922072a0d97596669ab77a1aa2beab2c5872b0652ee066c39a'
    '938f59bde19d922c263e94309919a6720ae625df9f38f53989b283be3f4bb95debe6edf997828fa09347be89e9b0a3bd02c4e2ca59c4342d741f0304b7fdff2f')

build() {
    cd "$srcdir"/$pkgname-$pkgver/

    LDFLAGS="${LDFLAGS//,--as-needed}"

    cmake -DCMAKE_INSTALL_PREFIX=/usr .
    make
}

package() {
    cd "$srcdir"/$pkgname-$pkgver/
    make DESTDIR="$pkgdir" install

    install -Dm644 ../$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
    install -Dm644 icon.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
}
