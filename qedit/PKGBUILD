# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: rtfreedman
# Contributor: SpepS <dreamspepser@yahoo.it>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Massimiliano Torromeo <Massimiliano.Torromeo@gmail.com>

pkgname=qedit
pkgver=2.5.6
pkgrel=2
pkgdesc="A multi-purpose text editor based on NEdit using Qt"
arch=(i686 x86_64)
url="http://www.phenix.bnl.gov/WWW/publish/hpereira/software/index.php?page=package&package_list=software_list_qt4&package=qedit&full=1"
license=(GPL2)
depends=(aspell qt4)
options=(!emptydirs)
install=$pkgname.install
source=(http://www.phenix.bnl.gov/WWW/publish/hpereira/software/tgz/$pkgname-$pkgver.tar.gz
    $pkgname.desktop)
sha256sums=('2e10ff6495fa97543176d725d8c4a4a9d2645c1b86e56434a1914c9d0ed59e90'
    '4c7e2e4a62e19402afbc6b9000adf3c62476acbb2ae1c27c7e86034b63253a29')
sha512sums=('0eb3337be995e4e14b02fbb6d70e4c943814ec177cdb7d5d820244f3facd1e8239c96274a3c0c02e8929cf51e3f47ff68e8539556ef16b904ffef355069bdea7'
    '938f59bde19d922c263e94309919a6720ae625df9f38f53989b283be3f4bb95debe6edf997828fa09347be89e9b0a3bd02c4e2ca59c4342d741f0304b7fdff2f')

build() {
    cd "$srcdir"/$pkgname-$pkgver/

    LDFLAGS="${LDFLAGS//,--as-needed}"

    cmake -DCMAKE_INSTALL_PREFIX=/usr .
    make
}

package() {
    cd "$srcdir"/$pkgname-$pkgver/
    make DESTDIR="$pkgdir" install

    install -Dm644 ../$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
    install -Dm644 icon.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
}
