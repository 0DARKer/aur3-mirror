# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: rtfreedman
# Contributor: SpepS <dreamspepser@yahoo.it>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Massimiliano Torromeo <Massimiliano.Torromeo@gmail.com>

pkgname=qedit
pkgver=2.6.8
pkgrel=1
pkgdesc="A multi-purpose text editor based on NEdit using Qt"
arch=(i686 x86_64)
url='http://hugo.pereira.free.fr/software/index.php?page=package&package_list=software_list_qt4&package=qedit'
license=(GPL2)
depends=(aspell qt4)
makedepends=(cmake)
options=(!emptydirs)
install=$pkgname.install
source=(http://hugo.pereira.free.fr/software/tgz/$pkgname-$pkgver.tar.gz)
sha256sums=('d180244cb0d3e3fe7449c538335c196def326b6c2ec4b5d404ef9c0f95ebbf1f')
sha512sums=('167e0b2d1af6a9bbfa2414b307cb1ad3f4cdf3109a8102167690ad8cff0685f24e1e00f8da798b16bb9b4606fc24cc0fbe7bcc826b17529ed890100f75bfe059')

build() {
    install -d $pkgname-build/
    cd $pkgname-build/
    cmake ../$pkgname-$pkgver/ \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DQT_QMAKE_EXECUTABLE=qmake-qt4
    make
}

package() {
    make -C $pkgname-build DESTDIR="$pkgdir" install

    cd $pkgname-$pkgver/
    install -Dm644 $pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
    install -Dm644 $pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
}
