# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: SpepS <dreamspepser@yahoo.it>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Massimiliano Torromeo <Massimiliano.Torromeo@gmail.com>

pkgname=qedit
pkgver=2.5.3
pkgrel=1
pkgdesc="A multi-purpose text editor based on NEdit using Qt"
arch=(i686 x86_64)
url="http://www.phenix.bnl.gov/WWW/publish/hpereira/software/index.php?page=package&package_list=software_list_qt4&package=qedit&full=1"
license=('GPL')
depends=('aspell' 'qt')
options=('!emptydirs')
install="$pkgname.install"
source=(http://www.phenix.bnl.gov/WWW/publish/hpereira/software/tgz/$pkgname-$pkgver.tar.gz
    $pkgname.desktop)
sha256sums=('83bd6a486231c7d067c652744c33ceaec01970b45c4d09f7988d943166d07f7c'
    '4c7e2e4a62e19402afbc6b9000adf3c62476acbb2ae1c27c7e86034b63253a29')
sha512sums=('7e87065345c8322032879d44440e5b5521d16d7b412c8cd9ab26cc9d6959db77e488b80ce210ad511a996422cfa55f0b100a6687fd6a563303f18d19a09fdc60'
    '938f59bde19d922c263e94309919a6720ae625df9f38f53989b283be3f4bb95debe6edf997828fa09347be89e9b0a3bd02c4e2ca59c4342d741f0304b7fdff2f')

build() {
    cd "$srcdir"/$pkgname-$pkgver/

    LDFLAGS="${LDFLAGS//,--as-needed}"

    cmake -DCMAKE_INSTALL_PREFIX=/usr .
    make
}

package() {
    cd "$srcdir"/$pkgname-$pkgver/
    make DESTDIR="$pkgdir" install

    install -Dm644 ../$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
    install -Dm644 icon.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
}
