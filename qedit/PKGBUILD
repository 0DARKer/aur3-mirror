# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: rtfreedman
# Contributor: SpepS <dreamspepser@yahoo.it>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Massimiliano Torromeo <Massimiliano.Torromeo@gmail.com>

pkgname=qedit
pkgver=2.6.7
pkgrel=1
pkgdesc="A multi-purpose text editor based on NEdit using Qt"
arch=(i686 x86_64)
url='http://hugo.pereira.free.fr/software/index.php?page=package&package_list=software_list_qt4&package=qedit'
license=(GPL2)
depends=(aspell kdelibs qt4)
makedepends=(cmake)
options=(!emptydirs)
install=$pkgname.install
source=(http://hugo.pereira.free.fr/software/tgz/$pkgname-$pkgver.tar.gz)
sha256sums=('88baba787bf149865d7dd9670c0f8b35bf9648e7dc28298a5d2c4acc4c0f0d20')
sha512sums=('647b046e8c70e1e8cec7548988a0b583f91a8d7ac05e903ca9ecfd266ee2e3952773b5cd69cb0071c3996308f1fd054a2f6bc5fa46045760b534a7477e5c3680')

build() {
    install -d $pkgname-build/
    cd $pkgname-build/
    cmake ../$pkgname-$pkgver/ \
        -DCMAKE_INSTALL_PREFIX=$(kde4-config --prefix) \
        -DCMAKE_BUILD_TYPE=Release \
        -DQT_QMAKE_EXECUTABLE=qmake-qt4
    make
}

package() {
    make -C $pkgname-build DESTDIR="$pkgdir" install

    cd $pkgname-$pkgver/
    install -Dm644 $pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
    install -Dm644 $pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
}
