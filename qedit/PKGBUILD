# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: rtfreedman
# Contributor: SpepS <dreamspepser@yahoo.it>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Massimiliano Torromeo <Massimiliano.Torromeo@gmail.com>

pkgname=qedit
pkgver=2.6.3
pkgrel=1
pkgdesc="A multi-purpose text editor based on NEdit using Qt"
arch=(i686 x86_64)
url=http://www.phenix.bnl.gov/WWW/publish/hpereira/software/index.php?page=package&package_list=software_list_qt4&package=qedit&full=1
license=(GPL2)
depends=(aspell qt4)
options=(!emptydirs)
install=$pkgname.install
source=(http://www.phenix.bnl.gov/WWW/publish/hpereira/software/tgz/$pkgname-$pkgver.tar.gz
    $pkgname.desktop)
sha256sums=('0e925533fa933b27e0512607938ddc3ae86640f133a4392212f8cab87837eb03'
    '4c7e2e4a62e19402afbc6b9000adf3c62476acbb2ae1c27c7e86034b63253a29')
sha512sums=('703c8a9220475f4c737755d3c2af137869bf625dab0ec4a134264afdb9e388ae269156e1dd8345e3f84009150a997edd54f616e8143e8245bc457d68ff625317'
    '938f59bde19d922c263e94309919a6720ae625df9f38f53989b283be3f4bb95debe6edf997828fa09347be89e9b0a3bd02c4e2ca59c4342d741f0304b7fdff2f')

build() {
    install -d $pkgname-build/
    cd $pkgname-build/
    LDFLAGS="${LDFLAGS//,--as-needed}"
    cmake ../$pkgname-$pkgver/ \
        -DCMAKE_INSTALL_PREFIX=$(kde4-config --prefix) \
        -DCMAKE_BUILD_TYPE=Release \
        -DQT_QMAKE_EXECUTABLE=qmake-qt4
    make
}

package() {
    make -C $pkgname-build DESTDIR="$pkgdir" install
    install -Dm644 $pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
    install -Dm644 $pkgname-$pkgver/$pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
}
