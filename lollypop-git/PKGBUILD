# Maintainer: Ian Brunelli (brunelli) <ian@brunelli.me>

pkgname=lollypop-git
_gitname=lollypop
pkgver=1198.7bef11b
pkgrel=4
pkgdesc='Music player for GNOME'
arch=('i686' 'x86_64')
license=('GPLv3')
url="https://github.com/gnumdk/${_gitname}"
depends=('gobject-introspection' 'gtk3' 'python-dbus' 'python-gobject' 'python-cairo' 'totem-plparser')
makedepends=('git' 'gnome-common' 'intltool' 'itstool' 'python' 'yelp-tools')
optdepends=('python-pylast: last.fm support')
options=('!emptydirs')
install=lollypop.install
source=("git://github.com/gnumdk/${_gitname}.git")
md5sums=('SKIP')
conflicts=('lollypop')
provides=("lollypop=$pkgver")

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "$srcdir/${_gitname}"
  ./autogen.sh --prefix=/usr --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/${_gitname}"
  make DESTDIR="${pkgdir}" install
}
