# Maintainer: Ian Brunelli (brunelli) <ian@brunelli.me>

pkgname=lollypop-git
pkgver=175.11592aa
pkgrel=1
pkgdesc='Music player for GNOME'
arch=('i686' 'x86_64')
license=('GPL3')
url='https://github.com/gnumdk/lollypop'
depends=('python-dbus' 'gtk3' 'python-gobject' 'python-cairo' 'python-mutagen')
makedepends=('git' 'intltool' 'itstool' 'python')
install=lollypop.install
source=("$pkgname"::'git+https://github.com/gnumdk/lollypop')
md5sums=('SKIP')
conflicts=('lollypop')
provides=("lollypop=$pkgver")

pkgver() {
  cd "$srcdir/$pkgname"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd "$srcdir/$pkgname"
  git submodule init
  git submodule update
}

build() {
  cd "$srcdir/$pkgname"
  ./autogen.sh --prefix=/usr --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="${pkgdir}" install
}
