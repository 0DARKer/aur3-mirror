# Maintainer: Billy Wayne McCann <thebillywayne ~et~ gmail.com>

pkgname=molekel
pkgver=5.4.0
_pkgvm_=5.4
pkgrel=1
pkgdesc="Molecular visualization program" 
arch=('x86_64')
url="http://molekel.cscs.ch/wiki/pmwiki.php/Main/HomePage"
license=('GPL')
install=${pkgname}.install
groups=('science')
depends=('qt4' 'glu' 'mesa' 'libxcursor' 'gcc-libs-multilib' 'libxinerama' 'openbabel')
provides=('molekel')
source=(ftp://ftp.cscs.ch/out/molekel/molekel_${_pkgvm_}/molekel_5_4_0_linux_x86_64.tar.gz)
sha1sums=('63f6492c4ff2adb2a6a411d25991b0654e33c595')

_pkgver_=${pkgver//./_}
_longname_=${pkgname}_${_pkgver_}_linux_${arch}

package() {

    # install desktop file and icon
    install -m755 -d "$pkgdir"/usr/share/applications
    install -m644 -t "$pkgdir"/usr/share/applications/ ../$pkgname.desktop
    install -m755 -d "$pkgdir"/usr/share/pixmaps
    install -m644 -t "$pkgdir"/usr/share/pixmaps/ "$srcdir"/${_longname_}/resources/Molekel.png 

    # install licenses
    install -m755 -d "$pkgdir"/usr/share/licenses/$pkgname
    install -m644 -t "$pkgdir"/usr/share/licenses/$pkgname "$srcdir"/${_longname_}/license

    # this package is a self contained unit. move it to /opt/
    install -m755 -d "$pkgdir"/opt/$pkgname
    mv "$srcdir"/${_longname_}/{bin,data,doc,lib,resources,shaders,notes,*sh}  "$pkgdir"/opt/$pkgname 

}
