# Maintainer: archtux <antonio dot arias99999 at gmail dot com>

pkgname=xix-music-player
pkgver=0.37
pkgrel=1
pkgdesc="Multi-platform music player."
arch=('i686' 'x86_64')
url="http://xixmusicplayer.org/"
license=('GPL3')
depends=('gtk2' 'libpulse')

# If you want the Qt4 version uncomment next line. Default is GTK2. Also change dependency from gtk2 -> qt4.
#_de='QT'

if [ "$CARCH" == "x86_64" ] && [ "$_de" == '' ]; then
  _pkgarch='_x64'
  _md5sum='5674900670ce74279600308a3d68e1d7'
fi
if [ "$CARCH" == "x86_64" ] && [ "$_de" == 'QT' ]; then
  _pkgarch='_x64'
  _md5sum='eb04f2e6346f238e6f59d3836f859ffb'
fi
if [ "$CARCH" == "i686" ] && [ "$_de" == '' ]; then
  _md5sum='af97ed50f47e3a447abfdb8627cea625'
fi
if [ "$CARCH" == "i686" ] && [ "$_de" == 'QT' ]; then
  _md5sum='c1aa50ca573377de649c0717afef2ab7'
fi
source=(http://www.zittergie.be/download/linux/XiXMusicPlayer${_de}${_pkgarch}.zip
        $pkgname
        XiX-Music-Player.desktop
        XiX-Music-Player.ico)
md5sums=($_md5sum
         'ad690e1d0cae85c23d73e1f013711eef'
         'e6855b7d4b2efaecadef8038ca3b4831'
         '6e4ec1161919499307b244c5e7dee693')
optdepends=('fbcmd: Facebook integration in XiX Music Player'
            'lame: encode CD-Tracks to MP3'
            'flac: encode CD-Tracks to FLAC'
            'mplayer: to rip DVD-Tracks')

package() {
  cd $srcdir/XiXMusicPlayer*
  
  # Data
  install -dm755 $pkgdir/usr/share/XiX-Music-Player/{lib,Local}
  cp Local/* $pkgdir/usr/share/XiX-Music-Player/Local
  
  # Install libbass libraries from XiX Music Player
  cp lib/lib* $pkgdir/usr/share/XiX-Music-Player/lib
 
  # Binary
  install -Dm755 XiXMusicPlayer* $pkgdir/usr/share/XiX-Music-Player/XiXMusicPlayer

  # Start file
  cd ..
  install -Dm755 $pkgname $pkgdir/usr/bin/$pkgname
  
  # Desktop icon
  install -Dm644 XiX-Music-Player.desktop $pkgdir/usr/share/applications/XiX-Music-Player.desktop
  install -Dm644 XiX-Music-Player.ico $pkgdir/usr/share/pixmaps/XiX-Music-Player.ico
}