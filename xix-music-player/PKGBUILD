# Maintainer: archtux <antonio dot arias99999 at gmail dot com>

pkgname=xix-music-player
pkgver=0.32h
pkgrel=1
pkgdesc="Multi-platform music player."
arch=('i686' 'x86_64')
url="http://xixmusicplayer.org/"
license=('CCPL:cc-by-nc-nd-3.0')
depends=('gtk2' 'libpulse')

# If you want the Qt4 version uncomment next line. Default is GTK.
#_de='QT'

if [ "$CARCH" == "x86_64" ] && [ "$_de" == '' ]; then
  _pkgarch='_x64'
  _md5sum='b9bf727421ce23c5e1742c433d1567d9'
fi
if [ "$CARCH" == "x86_64" ] && [ "$_de" == 'QT' ]; then
  _pkgarch='_x64'
  _md5sum='81bd6ced2f7db451436037bdd3a9aeeb'
fi
if [ "$CARCH" == "i686" ] && [ "$_de" == '' ]; then
  _pkgarch=''
  _md5sum='6e844eef5280bfb327cb0442ea7ae8c0'
fi
if [ "$CARCH" == "i686" ] && [ "$_de" == 'QT' ]; then
  _pkgarch=''
  _md5sum='94a4c4c7b48c7dab236fc6e108ac0338'
fi
source=(http://www.zittergie.be/download/linux/XiXMusicPlayer${_de}${_pkgarch}.zip
        $pkgname
        XiX-Music-Player.desktop
        XiX-Music-Player.ico)
md5sums=($_md5sum
         'ad690e1d0cae85c23d73e1f013711eef'
         'e6855b7d4b2efaecadef8038ca3b4831'
         '6e4ec1161919499307b244c5e7dee693')
optdepends=('fbcmd: Facebook integration in XiX Music Player'
            'lame: encode CD-Tracks to MP3'
            'flac: encode CD-Tracks to FLAC'
            'mplayer: to rip DVD-Tracks')

package() {
  cd $srcdir/XiXMusicPlayer*
  
  # Data
  install -dm755 $pkgdir/usr/share/XiX-Music-Player/{lib,Local}
  cp Local/* $pkgdir/usr/share/XiX-Music-Player/Local
  
  # Install libbass libraries from XiX Music Player
  cp lib/lib* $pkgdir/usr/share/XiX-Music-Player/lib
 
  # Binary
  install -Dm755 XiXMusicPlayer $pkgdir/usr/share/XiX-Music-Player/XiXMusicPlayer

  # Start file
  cd ..
  install -Dm755 $pkgname $pkgdir/usr/bin/$pkgname
  
  # Desktop icon
  install -Dm644 XiX-Music-Player.desktop $pkgdir/usr/share/applications/XiX-Music-Player.desktop
  install -Dm644 XiX-Music-Player.ico $pkgdir/usr/share/pixmaps/XiX-Music-Player.ico
}