# Maintainer: archtux <antonio dot arias99999 at gmail dot com>

pkgname=xix-music-player
pkgver=0.33c
pkgrel=1
pkgdesc="Multi-platform music player."
arch=('i686' 'x86_64')
url="http://xixmusicplayer.org/"
license=('GPL3')
depends=('gtk2' 'libpulse')

# If you want the Qt4 version uncomment next line. Default is GTK. Also change dependency from gtk2 -> qt4.
#_de='QT'

if [ "$CARCH" == "x86_64" ] && [ "$_de" == '' ]; then
  _pkgarch='_x64'
  _md5sum='12a7990e4559fe6876f2520c2d15d09e'
fi
if [ "$CARCH" == "x86_64" ] && [ "$_de" == 'QT' ]; then
  _pkgarch='_x64'
  _md5sum='9c8bcb835381cdf95d0c9f1bd95ca8c6'
fi
if [ "$CARCH" == "i686" ] && [ "$_de" == '' ]; then
  _md5sum='d6b01e991837bef1adf0b8777eebac77'
fi
if [ "$CARCH" == "i686" ] && [ "$_de" == 'QT' ]; then
  _md5sum='77dec3e63ecc0699b6369a271011cd1b'
fi
source=(http://www.zittergie.be/download/linux/XiXMusicPlayer${_de}${_pkgarch}.zip
        $pkgname
        XiX-Music-Player.desktop
        XiX-Music-Player.ico)
md5sums=($_md5sum
         'ad690e1d0cae85c23d73e1f013711eef'
         'e6855b7d4b2efaecadef8038ca3b4831'
         '6e4ec1161919499307b244c5e7dee693')
optdepends=('fbcmd: Facebook integration in XiX Music Player'
            'lame: encode CD-Tracks to MP3'
            'flac: encode CD-Tracks to FLAC'
            'mplayer: to rip DVD-Tracks')

package() {
  cd $srcdir/XiXMusicPlayer*
  
  # Data
  install -dm755 $pkgdir/usr/share/XiX-Music-Player/{lib,Local}
  cp Local/* $pkgdir/usr/share/XiX-Music-Player/Local
  
  # Install libbass libraries from XiX Music Player
  cp lib/lib* $pkgdir/usr/share/XiX-Music-Player/lib
 
  # Binary
  install -Dm755 XiXMusicPlayer $pkgdir/usr/share/XiX-Music-Player/XiXMusicPlayer

  # Start file
  cd ..
  install -Dm755 $pkgname $pkgdir/usr/bin/$pkgname
  
  # Desktop icon
  install -Dm644 XiX-Music-Player.desktop $pkgdir/usr/share/applications/XiX-Music-Player.desktop
  install -Dm644 XiX-Music-Player.ico $pkgdir/usr/share/pixmaps/XiX-Music-Player.ico
}