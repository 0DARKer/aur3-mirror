# Maintainer: archtux <antonio dot arias99999 at gmail dot com>

pkgname=xix-music-player
pkgver=0.38c
pkgrel=3
pkgdesc="Multi-platform music player."
arch=('i686' 'x86_64')
url="http://xixmusicplayer.org/"
license=('GPL3')
depends=('gtk2' 'libbass_aac' 'libbass_alac' 'libbass_ape' 'libbass_fx' 'libbasscd' 'libbassdsd' 'libbassflac' 'libbassopus' 'libpulse')

# If you want the Qt4 version uncomment next line. Default is GTK2. Also change dependency from gtk2 -> qt4.
#_de='QT'

if [ "$CARCH" == "x86_64" ] && [ "$_de" == '' ]; then
  _pkgarch='_x64'
  _md5sum='343e3a5e3f21a9f8770916be3a2a870f'
fi
if [ "$CARCH" == "x86_64" ] && [ "$_de" == 'QT' ]; then
  _pkgarch='_x64'
  _md5sum='47a49d840592a0ed9b1b7258535862b2'
fi
if [ "$CARCH" == "i686" ] && [ "$_de" == '' ]; then
  _md5sum='bb7dcb5621ce0efc5bb0aecd163051c5'
fi
if [ "$CARCH" == "i686" ] && [ "$_de" == 'QT' ]; then
  _md5sum='a8f3170b577bdb11c13805351745c196'
fi
source=(http://www.zittergie.be/download/linux/XiXMusicPlayer${_de}${_pkgarch}.zip
        $pkgname
        XiX-Music-Player.desktop
        XiX-Music-Player.ico)
md5sums=($_md5sum
         'a9dc33519ca6426d574054404dafe6b0'
         'e6855b7d4b2efaecadef8038ca3b4831'
         '6e4ec1161919499307b244c5e7dee693')
optdepends=('fbcmd: Facebook integration in XiX Music Player'
            'lame: encode CD-Tracks to MP3'
            'flac: encode CD-Tracks to FLAC'
            'mplayer: to rip DVD-Tracks')

package() {
  cd $srcdir/XiXMusicPlayer*

  # Data
  install -dm755 $pkgdir/usr/share/XiX-Music-Player/Local
  cp Local/* $pkgdir/usr/share/XiX-Music-Player/Local

  # Binary
  install -Dm755 XiXMusicPlayer* $pkgdir/usr/share/XiX-Music-Player/XiXMusicPlayer

  # Start file
  cd ..
  install -Dm755 $pkgname $pkgdir/usr/bin/$pkgname

  # Desktop icon
  install -Dm644 XiX-Music-Player.desktop $pkgdir/usr/share/applications/XiX-Music-Player.desktop
  install -Dm644 XiX-Music-Player.ico $pkgdir/usr/share/pixmaps/XiX-Music-Player.ico
}