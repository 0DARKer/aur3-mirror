# Maintainer: Moritz Lipp <mlq@pwmt.org>

pkgname=oclint
pkgver=0.7
pkgrel=1
pkgdesc="static code analysis tool for improving quality and reducing defects"
arch=('i686' 'x86_64')
url="http://oclint.org/"
license=('BSD')
makedepends=('cmake' 'subversion' 'python2' 'libxml2')
source=(http://archives.oclint.org/releases/$pkgver/$pkgname-$pkgver-src.tar.gz)
md5sums=('8bbccef58be384f4a4cfc2ed9b72c69a')

prepare() {
  cd "$srcdir/$pkgname-$pkgver-src/oclint-scripts"

  msg "Checkout LLVM/Clang"
  ./checkoutLLVMClang.sh tags/RELEASE_33/final

  # Use python2
  mkdir -p "$srcdir/python2-path"
  ln -s /usr/bin/python2 "$srcdir/python2-path/python"
  export PATH="$srcdir/python2-path:$PATH"
}

build() {
  cd "$srcdir/$pkgname-$pkgver-src/oclint-scripts"

  msg "Build LLVM/Clang"
  ./buildClang.sh release

  msg "Build OCLint"
  ./buildAll.sh

  # Cleanup python
  rm -r $srcdir/python2-path
}

package() {
	cd "$srcdir/$pkgname-$pkgver-src"

  mkdir -p $pkgdir/usr/bin
  cp ./build/oclint-release/bin/oclint* $pkgdir/usr/bin/

  mkdir -p $pkgdir/usr/lib
  cp -r ./build/oclint-release/lib/* $pkgdir/usr/lib/
}
