# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=cqrlogo-git
pkgver=0.3.6.r2.gb6b5be9
pkgrel=1
pkgdesc="CGI QR-Code logo for web services - git checkout"
arch=('i686' 'x86_64')
url="https://github.com/eworm-de/cqrlogo"
depends=('iniparser' 'libpng' 'qrencode')
makedepends=('git' 'markdown' 'fcgi')
optdepends=('fcgi: for FastCGI version')
checkdepends=('zbar')
license=('GPL')
conflicts=('cqrlogo')
provides=('cqrlogo')
install=cqrlogo.install
backup=('etc/cqrlogo.conf')
source=('git://github.com/eworm-de/cqrlogo.git')
sha256sums=('SKIP')

pkgver() {
	cd cqrlogo/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

build() {
	cd cqrlogo/

	make
}

check() {
	cd cqrlogo/

	make check
}

package() {
	cd cqrlogo/

	make DESTDIR="${pkgdir}" install
}

