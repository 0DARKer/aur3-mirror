# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=cqrlogo-git
pkgver=0.3.4.r0.g4235212
pkgrel=1
pkgdesc="CGI QR-Code logo for web services"
arch=('i686' 'x86_64')
url="https://github.com/eworm-de/cqrlogo"
depends=('libpng' 'qrencode')
makedepends=('git' 'markdown')
checkdepends=('zbar')
license=('GPL')
conflicts=('cqrlogo')
install=cqrlogo.install
source=('git://github.com/eworm-de/cqrlogo.git')
sha256sums=('SKIP')

pkgver() {
	cd cqrlogo/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

build() {
	cd cqrlogo/

	make
}

check() {
	cd cqrlogo/

	make check
}

package() {
	cd cqrlogo/

	make DESTDIR="${pkgdir}" install
}

