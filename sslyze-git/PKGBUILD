# Maintainer: Fabian Zaremba <fabian at youremail dot eu>
pkgname=sslyze-git
pkgver=0.10.412
pkgrel=2
pkgdesc="Fast and full-featured SSL scanner."
arch=('i686' 'x86_64')
url="https://github.com/nabla-c0d3/sslyze"
license=('GPL2')
depends=('python2')
makedepends=('git')
provides=('sslyze')
conflicts=('sslyze')
options=('!makeflags')
source=("git://github.com/nabla-c0d3/nassl.git"
"git://github.com/nabla-c0d3/sslyze.git"
"http://zlib.net/zlib-1.2.8.tar.gz"
"https://www.openssl.org/source/openssl-1.0.1m.tar.gz")
sha256sums=('SKIP'
            'SKIP'
            '36658cb768a54c1d4dec43c3116c27ed893e88b02ecfcb44f2166f9c0b7f2a0d'
            '095f0b7b09116c0c5526422088058dc7e6e000aa14d22acca6a4e2babcdfef74')


pkgver() {
cd "$srcdir/sslyze"
echo $(grep "##" CHANGELOG.md | head -n 1 | sed 's/## v//').$(git rev-list --count HEAD)
} 

build() {

cd "$srcdir/"

mv "$srcdir/openssl-1.0.1m" "$srcdir/nassl/"
mv "$srcdir/zlib-1.2.8" "$srcdir/nassl/"

cd "$srcdir/nassl"

#Update to new openssl version
sed -i "s/openssl-1.0.1i/openssl-1.0.1m/" "$srcdir/nassl/buildAll_config.py"

#Change python invocations to use python2
#Force linking with libz seems to be needed, add LDFLAGS for setup_unix.py invocation
sed -i "s/python -m unittest discover --pattern/python2 -m unittest discover --pattern/" "$srcdir/nassl/buildAll_unix.py"
sed -i 's/python setup_unix.py build/LDFLAGS="-Wl,--no-as-needed -lz" python2 setup_unix.py build/' "$srcdir/nassl/buildAll_unix.py"

python2 ./buildAll_unix.py

cd "$srcdir/nassl/test/nassl"
rm *.pyc
sed -i 's#/usr/bin/python#/usr/bin/python2#' *.py



mv "$srcdir/nassl/test/nassl" "$srcdir/sslyze/"

}

package() {

#Packaging routine derived from sslyze PKGBUILD by goll

# Install files in /opt
mkdir -p "$pkgdir/opt/sslyze"
cp -a "$srcdir/sslyze/." "$pkgdir/opt/sslyze"

rm -rf "$pkgdir/opt/sslyze/.git"

find "$pkgdir/opt/sslyze" -type f -name '*.py' -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#'

# Create an indirect launcher in /usr/bin
mkdir -p "$pkgdir/usr/bin"

cat << EOF > "$pkgdir/usr/bin/sslyze"
#!/usr/bin/bash
cd /opt/sslyze && python2 sslyze.py \$@
EOF

chmod 755 "$pkgdir/usr/bin/sslyze"

}
