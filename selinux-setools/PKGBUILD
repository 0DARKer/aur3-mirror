# Maintainer: Nicky726 <Nicky726 [at] gmail [dot] com>

pkgname=selinux-setools
_origname=setools
pkgver=3.3.7
pkgrel=4
pkgdesc="SELinux SETools GUI and CLI tools and libraries for SELinux policy analysis"
groups=('selinux' 'selinux-extras')
arch=('i686' 'x86_64')
url="http://oss.tresys.com/projects/setools/"
license=('GPL' 'LGPL')
depends=('flex' 'bison' 'selinux-usr-libselinux>=2.0.87' 'selinux-usr-libsepol>=2.0.38' 'selinux-usr-libsepol' 'libxml2' 'sqlite3>=3.6.20' 'tcl>=8.4.9' 'tk>=8.4.9' 'libglade>=2.6.4')
optdepends=('glib2:needed to build graphical tools'
'gtk2>=2.8:needed to build graphical tools'
'bwidget>=1.8:needed to build graphical tools')
makedepends=('swig>=1.3.28' 'python' 'java-environment' 'pkgconfig' 'autoconf>=2.60')
source=(http://oss.tresys.com/projects/${_origname}/chrome/site/dists/${_origname}-${pkgver}/${_origname}-${pkgver}.tar.bz2)
md5sums=('0377d7a06028825434cd7b41a80865a5')

build() {
  cd "${srcdir}/${_origname}-${pkgver}"
  # Fix expected version of SWIG
  sed -i -e "s|AC_PROG_SWIG(1.3.28)|AC_PROG_SWIG(2.0.0)|g" configure.ac
  autoreconf -i -s
  #Arch uses rather nonstandard directory for policy sources
  ./configure	--enable-swig-java \
		--with-java-prefix="$JAVA_HOME" \
 		--with-default-policy=/etc/selinux/refpolicy/src/policy \
		--disable-bwidget-check \
  		--prefix=/usr || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
