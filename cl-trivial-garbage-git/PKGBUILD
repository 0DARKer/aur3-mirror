# Maintainer: DavidK <david_king at softhome dot net>
# Contributor:  mrshpot <mrshpot at gmail dot com>
# Contributor:  veox <box 55 [shift-two] mail [dot] ru>

pkgname=cl-trivial-garbage-git
_clname=trivial-garbage   # used in CL scope, not package scope
pkgver=20130225.58
pkgrel=1
pkgdesc="Several portable Common Lisp extensions"
arch=('any')
url="http://common-lisp.net/project/trivial-garbage/"
license=('custom')
depends=('common-lisp')
makedepends=('git')
optdepends=('cl-rt: For the test suite')
provides=('cl-trivial-garbage')
install=cl-trivial-garbage.install
source=('git://github.com/trivial-garbage/trivial-garbage.git')
md5sums=('SKIP')
_gitname=trivial-garbage
options=(docs)

pkgver() {
  cd "$srcdir"/$_gitname
  echo "$(git log -1 --format="%cd" --date=short | sed 's|-||g').$(git rev-list --count master)"
}

package() {
        cd "$srcdir"/$_gitname
        sed -i "s/:rtest/:rt/g" trivial-garbage.asd

	install -d "${pkgdir}"/usr/share/common-lisp/source/${_clname}
	install -d "${pkgdir}"/usr/share/common-lisp/systems
        install -d "${pkgdir}"/usr/share/licenses/${pkgname}

	install -m 644 -t "${pkgdir}"/usr/share/common-lisp/source/${_clname} \
		"${srcdir}"/$_gitname/*.lisp
	install -m 644 -t "${pkgdir}"/usr/share/common-lisp/source/${_clname} \
		"${srcdir}"/$_gitname/*.asd
        install -m 644 -T README "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

	cd "${pkgdir}"/usr/share/common-lisp/systems
	ln -s ../source/${_clname}/${_clname}.asd .
}
