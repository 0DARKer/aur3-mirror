# Maintainer: Lara Maia <lara@craft.net.br>
# Contributor: BlackICE <manfredi at gmail.com>
# Contributor: Daenyth <Daenyth+Arch AT gmail DOT com>

pkgname=pkgtools
pkgver=25
pkgrel=3
pkgdesc="A collection of scripts for Arch Linux packages"
arch=('any')
url="https://bbs.archlinux.org/viewtopic.php?pid=384196"
license=('GPL')
depends=('bash>=4' 'pcre' 'libarchive' 'python' 'namcap' 'pkgfile' 'gem2arch')
optdepends=('abs: Provides PKGBUILD prototypes for newpkg')
provides=('newpkg')
backup=('etc/pkgtools/newpkg.conf' 'etc/pkgtools/pkgfile.conf' 'etc/pkgtools/spec2arch.conf')
install=pkgtools.install
source=("${pkgname}-v${pkgver}.tar.gz::https://github.com/Daenyth/pkgtools/tarball/v$pkgver"
        "https://raw.githubusercontent.com/bluepeppers/pip2arch/master/pip2arch.py")
md5sums=('9e9d7aad5ba8ecee1b798c3ed4e0a0a7'
         '9267993a72489019ec1536c887304374')

prepare() {
	cp -f pip2arch.py Daenyth-"$pkgname"-*/scripts/pip2arch/
}

package() {
	cd Daenyth-"$pkgname"-*

	make DESTDIR="$pkgdir" install
}
