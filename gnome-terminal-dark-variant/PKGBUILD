# $Id$
# Maintainer: Ian Hernandez <ihernandezs@openmailbox.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-terminal-dark-variant
_pkgname=gnome-terminal
pkgver=3.16.0
pkgrel=1
pkgdesc="The GNOME Terminal Emulator with patch to bring back 'Use dark theme variant' preference"
arch=(i686 x86_64)
license=(GPL)
depends=(vte3 gsettings-desktop-schemas dconf)
makedepends=(gnome-doc-utils intltool itstool docbook-xsl desktop-file-utils
             libnautilus-extension appdata-tools gnome-shell gconf vala yelp-tools)
optdepends=('gconf: settings migration when upgrading from older version')
options=('!emptydirs')
url="http://www.gnome.org"
groups=('gnome')
install=gnome-terminal.install
source=(http://ftp.gnome.org/pub/gnome/sources/$_pkgname/${pkgver:0:4}/$_pkgname-$pkgver.tar.xz gnome-terminal-restore-dark.patch)
sha256sums=('2d633017a0eba202a99ab4c3dda4b502d6750fb2471f6a60c53433a23be67fe8' '5ef48574b93ec5530ce2747012fc0838a5e1ffa265803069a8da79b1be5e1eff')

prepare() {
    cd $_pkgname-$pkgver
    patch -p1 -i ../gnome-terminal-restore-dark.patch
    autoreconf -f -i
}

build() {
  cd $_pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$_pkgname --disable-static --with-nautilus-extension
  make
}

check() {
  cd $_pkgname-$pkgver
  make check
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
