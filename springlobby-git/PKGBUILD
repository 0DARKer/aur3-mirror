# Maintainer: Erdbeerkaese (erdbeerkaese AT gawab DOT com)
# Contributor: DuGi <dugi@irc.pl>

pkgname=springlobby-git
pkgver=20110820
pkgrel=1
pkgdesc="A free cross-platform lobby client for the Spring RTS project, development version."
arch=('i686' 'x86_64')
url="http://springlobby.info/"
license=('GPL2')
depends=('wxgtk' 'libnotify' 'openal' 'curl' 'hicolor-icon-theme' 'libtorrent-rasterbar')
optdepends=('sdl' 'sdl_sound' 'sdl_mixer') 
makedepends=('boost' 'asio' 'cmake' 'git')
conflicts=('springlobby')
provides=('springlobby')
install=springlobby.install
source=()
_gitroot="git://springlobby.info/git/buildbot/springlobby.git"
_gitname="springlobby"

build() {
    cd "$srcdir"
    msg "Connecting to GIT server...."

    if [ -d $_gitname ] ; then
        cd $_gitname && git pull origin
        msg "The local files are updated."
    else
        git clone $_gitroot $_gitname --depth=1
    fi

    msg "GIT checkout done or server timeout"
    msg "patching source..."
    
    cd "$srcdir/$_gitname"
    sed -i "s|/usr/bin/awk|/bin/awk|g" tools/test-susynclib.awk
    sed -i "s/boost_system-mt/boost_system/g" CMakeLists.txt
    
    msg "Starting make..."
    rm -rf "$srcdir/$_gitname-build"
    mkdir "$srcdir/$_gitname-build"
    cd "$srcdir/$_gitname-build"
    export CXXFLAGS="${CXXFLAGS} -DBOOST_FILESYSTEM_VERSION=2"
    cmake -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
	  "$srcdir/$_gitname"
    make
}

package() {
    cd "$srcdir/$_gitname-build"
    make DESTDIR=$pkgdir install || return 1
}

# vim: sw=2:ts=2 et:
