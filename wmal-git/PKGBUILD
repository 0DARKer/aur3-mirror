# Maintainer: Joost Bremmer <toost dot b at gmail dot com>
# Contributor: carstene1ns <url/mail: arch carsten-teibes de>

pkgname=wmal-git
pkgver=0.2.r237.gcdabdfb
pkgrel=1
pkgdesc="A lightweight and simple updater for MyAnimeList, Melative or VNDB."
arch=('any')
url="http://z411.github.io/wmal-python/"
license=('GPL3')
depends=('python2'
         'lsof')
        
makedepends=('python2'
             'desktop-file-utils'
             'git'
            )
optdepends=('pygtk: gtk frontend'
			'python2-pillow: thumbnail images in gtkfrontend'
            'python2-urwid: ncurses frontend')

install=wmal-git.install
source=(${pkgname}::'git+https://github.com/z411/wmal-python.git'
        'wmal-gtk.desktop'
        'wmal-curses.desktop')

sha256sums=('SKIP'
            '475ee49e7d8a5ac6a864a3d4f55515aee98f12a085b348a98262e97e0400fb7a'
            '198be3c48a2ae92fcb3657695b6d7dbf4b3c7fbae34a0bf9f1466b6fc46d969e')


pkgver() {
  cd ${pkgname}
  # as there are no tags, use some black magic to get version...
  printf "%s.r%s.g%s" "$(grep version setup.py | sed -e 's|.*version=.\(.*\).,|\1|')" \
    "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}


package() {
   cd ${pkgname}
   python2 setup.py install --prefix=/usr --root="$pkgdir/" --optimize=1
   install -Dm644 wmal/data/wmal_icon.png "$pkgdir"/usr/share/pixmaps/wmal.png
   install -Dm644 ../wmal-gtk.desktop "$pkgdir"/usr/share/applications/wmal-gtk.desktop
   install -m644 ../wmal-curses.desktop "$pkgdir"/usr/share/applications/wmal-curses.desktop
}
