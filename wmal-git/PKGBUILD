# Maintainer: Joost Bremmer <toost dot b at gmail dot com>
# Contributor: carstene1ns <url/mail: arch carsten-teibes de>

pkgname=wmal-git
pkgver=0.2.r377.g004925d
pkgrel=1
pkgdesc="A lightweight and simple updater for MyAnimeList, Melative or VNDB."
arch=('any')
url="http://z411.github.io/wmal-python/"
license=('GPL3')
depends=('python2'
         'lsof')
        
makedepends=('python2'
             'desktop-file-utils'
             'git'
            )
optdepends=('pygtk: gtk frontend'
			'python2-pillow: thumbnail images for GUI frontends'
			'python2-pyqt4: Qt frontend'
            'python2-urwid: ncurses frontend')

install=wmal-git.install
source=(${pkgname}::'git+https://github.com/z411/wmal-python.git'
        'wmal-curses.desktop'
        'wmal-gtk.desktop'
		'wmal-qt.desktop')

sha256sums=('SKIP'
            '198be3c48a2ae92fcb3657695b6d7dbf4b3c7fbae34a0bf9f1466b6fc46d969e'
            '3f582306a9ab30c6c583a091d2b8d2b9669baee2c2865807d7ddcf5c990a8c01'
            '84ce42695932fb70ee7c1ba51e0bb2db50edd3e8953d645fffd4a5cab1d508a4')


pkgver() {
  cd ${pkgname}
  git describe --long --tags | sed -e 's/([^-]*-g)/r\1/;s/-/./g'
}


package() {
   cd ${pkgname}
   python2 setup.py install --prefix=/usr --root="$pkgdir/" --optimize=1
   install -Dvm644 wmal/data/wmal_icon.png "${pkgdir}"/usr/share/pixmaps/wmal.png
   install -Dvm644 "${srcdir}"/wmal-curses.desktop "${pkgdir}"/usr/share/applications/wmal-curses.desktop
   install -vm644  "${srcdir}"/wmal-gtk.desktop "${pkgdir}"/usr/share/applications/wmal-gtk.desktop
   install -vm644  "${srcdir}"/wmal-qt.desktop "${pkgdir}"/usr/share/applications/wmal-qt.desktop
}
