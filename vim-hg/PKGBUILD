pkgname=vim-hg
pkgver=4036
pkgrel=1
pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor'
arch=('i686' 'x86_64')
license=('custom:vim')
url="http://www.vim.org"
depends=('gpm' 'perl' 'python' 'python2')
makedepends=('wget' 'sed' 'grep' 'gettext' 'mercurial')
conflicts=('vim' 'vim-runtime')
provides=('vim=7.3' 'vim-runtime=7.3')
source=('pythoncomplete.vim::http://www.vim.org/scripts/download_script.php?src_id=10872'
        'vimrc'
        'archlinux.vim')
md5sums=('6e7adfbd5d26c1d161030ec203a7f243'
         '20fceda56badf201695c57999b0bc609'
         '10353a61aadc3f276692d0e17db1478e')
backup=('etc/vimrc')

_hgroot="http://vim.googlecode.com/hg/"
_hgrepo="vim"
_hgbranch="default"

fetch() {
  # update source
  cd $srcdir

  # NOTE: makepkg likes updating at the beginning so I guess we don't have to
  if [ ! -d $_hgrepo ]; then
    hg clone $_hgroot/$_hgrepo || return 1
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."
  
  rm -rf "$srcdir/$_hgrepo-build"
  cp -a "$srcdir/$_hgrepo" "$srcdir/$_hgrepo-build"
}

build() {
  fetch

  cd "$srcdir"

  # define the place for the global (g)vimrc file (set to /etc/vimrc)
  sed -i 's|^.*\(#define SYS_.*VIMRC_FILE.*"\) .*$|\1|' \
    vim-build/src/feature.h
  sed -i 's|^.*\(#define VIMRC_FILE.*"\) .*$|\1|' \
    vim-build/src/feature.h

  cd vim-build/src && autoconf

  ./configure \
    --prefix=/usr \
    --localstatedir=/var/lib/vim \
    --with-features=big \
    --with-compiledby=ArchLinux \
    --enable-gpm \
    --enable-acl \
    --with-x=no \
    --disable-gui \
    --enable-multibyte \
    --enable-cscope \
    --disable-netbeans \
    --enable-perlinterp \
    --disable-pythoninterp \
    --disable-python3interp \
    --disable-rubyinterp \
    --disable-luainterp

  make
}

package() {
  cd "$srcdir/$_hgrepo-build"
  make VIMRCLOC=/etc DESTDIR=$pkgdir install

  # provided by (n)vi in core
  rm "${pkgdir}"/usr/bin/{ex,view}

  # delete some manpages
  find "${pkgdir}"/usr/share/man -type d -name 'man1' 2>/dev/null | \
    while read _mandir; do
    cd ${_mandir}
    rm -f ex.1 view.1 # provided by (n)vi
    rm -f evim.1    # this does not make sense if we have no GUI
  done

  # patch runtime
  cd $pkgdir/usr/share/vim/vim73/
  sed -i "s/rpmsave/pacsave/;s/rpmnew/pacnew/;s/,\*\.ebuild/\0,PKGBUILD*,*.install/" filetype.vim

  # fix FS#17216
  sed -i 's|messages,/var|messages,/var/log/messages.log,/var|' \
    ${pkgdir}/usr/share/vim/vim73/filetype.vim

  install -Dm644 $srcdir/vimrc  $pkgdir/etc/vimrc
  install -Dm644 $srcdir/archlinux.vim  \
                 $pkgdir/usr/share/vim/vimfiles/archlinux.vim
  install -dm755 $pkgdir/usr/share/licenses/$pkgname
  cd $pkgdir/usr/share/licenses/$pkgname
  ln -s ../../vim/vim73/doc/uganda.txt license.txt

  # license
  install -Dm644 "${srcdir}"/vim-build/runtime/doc/uganda.txt \
    "${pkgdir}"/usr/share/licenses/${pkgname}/license.txt
}

# vim:set ts=2 sw=2 et:
