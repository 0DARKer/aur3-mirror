# Maintainer:  Christopher Arndt <chris -at- chrisarndt -dot- de>
# $Id: PKGBUILD 1241 2014-11-21 15:33:12Z carndt $

pkgname=xcftools-git
pkgver=1.0.7r16.2c4d3fc
_pkgver=1.0.7
pkgrel=1
pkgdesc="Command line tools for extracting images and information from GIMP XCF files (Git version)."
url="https://github.com/j-jorge/xcftools/"
arch=('x86_64' 'i686')
license=('Custom')
depends=('libpng' 'perl')
conflicts=('xcftools')
provides=('xcftools')
source=("$pkgname::git+https://github.com/j-jorge/xcftools.git"
        "LICENSE"
        "xcftools.patch")
sha256sums=('SKIP'
            'ad91bc83ff4050aca9867ee0d9b5921f8a31fe50d37b9aaf0e260aa0b4e8987a'
            '49a7bcc2b93d53629fcc31c88d9495b3a98c4ebc7c6fed0f02630600d39df196')

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo ${_pkgver}r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
    cd "${srcdir}/${pkgname}"

    patch -i "${srcdir}/xcftools.patch"
}

build() {
    cd "${srcdir}/${pkgname}"

    ./configure --prefix=/usr
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install

    install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
