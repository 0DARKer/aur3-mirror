# Maintainer: Antonio Garcia-Dominguez <antonio.garciadominguez@uca.es>
pkgname=bison++
pkgver=1.21.11
pkgrel=1
pkgdesc="Parser generator in C or C++ from BNF notation"
arch=('i686' 'x86_64')
url="http://packages.debian.org/bison++"
license=('GPL')
source=('http://ftp.de.debian.org/debian/pool/main/b/bison++/bison++_1.21.11.orig.tar.gz' 'http://ftp.de.debian.org/debian/pool/main/b/bison++/bison++_1.21.11-3.diff.gz')
md5sums=('5cdeae3478207d5e1052e4616daa41cd'
         'c290793fb5ff7c35ad9b424b90b167d0')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Apply the Debian patch
  patch -p1 < ../*.diff

  # Fix the broken Makefile.am script
  sed -e '
    s/datadir/DESTDIR\)\$(datadir/
    s/bindir/DESTDIR\)\$(bindir/
    /^pkgdata_DATA/s/ Example$//
    /install\-exec\-hook/i\install-data-hook:\
\tcp -r $(srcdir)/Example $(DESTDIR)$(pkgdatadir)\
' -i Makefile.am

  # Bootstrap the autotools and compile
  aclocal
  automake --add-missing
  autoreconf
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # Remove or rename files that could conflict with regular Bison
  rm "$pkgdir/usr/bin/bison"
  mv "$pkgdir/usr/share/info/"{bison,bison++}.info
  rm "$pkgdir/usr/share/man/man1/bison.1"
}

# vim:set ts=2 sw=2 et:
