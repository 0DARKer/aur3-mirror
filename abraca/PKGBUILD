# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Johan Slikkie <johan@slikkie.nl>
# Maintainer: J. W. Birdsong <jwbirdsong AT gamil DOT com> 

pkgname=abraca
pkgver=0.6.0
pkgrel=1
pkgdesc="A GTK2 client for the XMMS2 music player, with a focus on collections"
arch=('i686' 'x86_64')
url="http://abraca.xmms.se/"
license=('GPL')
depends=('gtk2' 'xmms2' 'libgee' 'hicolor-icon-theme')
makedepends=('scons' 'vala' 'python' 'gettext')
install=$pkgname.install
md5sums=('62ba2634678bb9490a83030d47206c22')

source=( http://abraca.xmms.se/attachments/download/134/${pkgname}-${pkgver}.tar.gz)

build() {
  cd "$srcdir"/${pkgname}-${pkgver}

  # scons and install
  scons PREFIX=/usr 
  scons DESTDIR="$pkgdir" install 

  # install icons
  mkdir -p "$pkgdir"/usr/share/icons/hicolor/scalable/apps
  mv -f "$pkgdir"/usr/share/icons/{,hicolor/scalable/apps/}$pkgname.svg
  for _width in '16' '32' '128' '192'; do
    install -Dm644 data/$pkgname-${_width}.png \
    "$pkgdir"/usr/share/icons/hicolor/${_width}x${_width}/apps/$pkgname.png
  done
}

# vim:set ts=2 sw=2 et:

