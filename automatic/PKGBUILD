# Contributor: Matias De la Puente <mfpuente.ar@gmail.com>
pkgname=automatic
pkgver=0.7.1
pkgrel=1
pkgdesc="RSS downloader for Linux based systems, capable of adding downloaded torrents directly to Transmission"
arch=('i686' 'x86_64')
url="http://kylek.is-a-geek.org:31337/Automatic/"
license=('GPLv2')
depends=('curl' 'libxml2' 'pcre')
optdepends=('transmission-cli')
backup=('etc/conf.d/automaticd')
source=(http://kylek.is-a-geek.org:31337/files/$pkgname-$pkgver-src.tar.gz
        automaticd.rc
        automaticd.conf)
sha256sums=('7bfc5c9da80030b12a19f2b9b72dd3b9d7cc6f576ee6b3c37b7c747a23ebac8e'
            '6830dd6b19d233c3d7a7b83ead466b0ea2cb23ee88cd15d5aeffa50716c02d9e'
            'ed4cdf8ef72840fdb57b7670d114372bb68153cdfae731d1102687b568a71940')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package () {
  cd "$srcdir/$pkgname-$pkgver"
  
  make DESTDIR="$pkgdir/" install
  install -d "$pkgdir/etc/rc.d"
  install -m755 "$srcdir/automaticd.rc" "$pkgdir/etc/rc.d/automaticd"
  install -D -m644 "$srcdir/automaticd.conf" "$pkgdir/etc/conf.d/automaticd"
}

