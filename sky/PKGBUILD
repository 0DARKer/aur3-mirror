# Maintainer: 
# Contributor: Jameson Pugh <imntreal@gmail.com>
# Contributor: stef312 <stef312_at_gmail_dot_com>
# Contributor: Gaspar de Elías <caspercba_at_hotmail_dot_com>
# Contributor: Artem Sheremet <dot_doom_at_gmail_dot_com>

pkgname=sky
pkgver=2.0.342
pkgrel=3
pkgdesc="Lync & Skype for business on Linux"

arch=('x86_64')
if [[ $CARCH == 'x86_64' ]]; then
    _arch=64
elif [[ $CARCH == 'i686' ]]; then
    _arch=32
fi

url="http://tel.red"

license=('custom: Copyright © 2015 TEL.RED LLC')
options=('!strip')
install="${pkgname}.install"

depends=('openssl' 'nss' 'gstreamer0.10-base' 'libpulse' 'alsa-lib' 'v4l-utils' 
    'libxinerama' 'libxss' 'libxcb')
optdepends=('ibus: Intelligent Input Bus (IBus) support')
replaces=('wync')
makedepends=('binutils' 'chrpath')

source=("sky.ibus.patch")
source_x86_64=("http://tel.red/linux/sky_ubuntu${_arch}_v${pkgver}.deb")
source_i686=("http://tel.red/linux/sky_ubuntu32_v2.0.340.deb")

sha256sums=("3e83d6347b140bf5db14194df105fd49fe8766d60a48d6366da7fdbb81a9b1e3")
sha256sums_x86_64=("ff2f1f197a91df04805e6827714155da93f56c8359e6d6925ca7d316c4d7786e")
sha256sums_i686=("3c23a2d29840c14a32fa83033d9a54104c8f8596fa76251256c86b3d2292e66c")

package() {
    cd "${srcdir}"
    ar x "sky_ubuntu${_arch}_v${pkgver}.deb" >/dev/null
    tar -zxf data.tar.gz

    local icu_libs
    icu_libs="libicudata libicudata libicudata libicule libiculx libicutest 
        libicutu libicuuc libicui18n libicuio"

    # fix broken rpaths of bundled icu libs
    for lib in ${icu_libs}; do
        chrpath -d "${srcdir}"/opt/sky_linux/lib/${lib}.so*
    done
    
    pwd
    patch -p1 < sky.ibus.patch

    cp -rf "${srcdir}/etc" "${pkgdir}/"
    install -Dm 644 "${srcdir}/usr/share/applications/sky.desktop" "${pkgdir}/usr/share/applications/sky.desktop"
    install -Dm 644 "${srcdir}/usr/share/pixmaps/sky.png" "${pkgdir}/usr/share/pixmaps/sky.png"
    mv "${srcdir}/opt" "${pkgdir}/"
    find "${pkgdir}/" -type d -exec chmod 0755 {} \;

    mkdir -m 755 "${pkgdir}/usr/bin"
    ln -sr "${pkgdir}/opt/sky_linux/sky.sh" "${pkgdir}/usr/bin/sky"
}

# vim: set ts=2 sw=2 ft=sh noet:
