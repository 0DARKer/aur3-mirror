# Maintainer: Gergely Imreh <imrehg@gmail.com>
# http://github.com/imrehg/aur
# Contributor: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
pkgname=aerofs
pkgver=EA21.1
pkgrel=1
pkgdesc="Synchronize your files without servers and build private cloud storage out of your own devices"
arch=('i686' 'x86_64')
license=('GPL')
depends=('java-runtime>=6' 'sharutils')
url="https://www.aerofs.com"
source=('https://www.aerofs.com/downloads/aerofs-installer.tgz')
package() {
  _destdir="opt/$pkgname"
  _docdir="usr/share/doc/$pkgname"
  _desktopdir="usr/share/applications"

  # Install everything to /opt
  cd $srcdir/$pkgname
  mkdir -p $pkgdir/$_destdir/
  cp -r * $pkgdir/$_destdir

  # Create binary links
  mkdir -p $pkgdir/usr/bin/
  ln -sf /$_destdir/aerofs $pkgdir/usr/bin/
  ln -sf /$_destdir/aerofs-cli $pkgdir/usr/bin/
  ln -sf /$_destdir/aerofs-gui $pkgdir/usr/bin/
  ln -sf /$_destdir/aerofs-sh $pkgdir/usr/bin/

  # Docs into correct location
  mkdir -p $pkgdir/$_docdir/
  mv "$pkgdir/$_destdir/Getting Started.pdf" $pkgdir/$_docdir

  # Cleanup unrequired files
  rm -f $pkgdir/$_destdir/INSTALL

  # Put the .desktop file in the correct location
  mkdir -p $pkgdir/$_desktopdir/
  mv $pkgdir/$_destdir/aerofs.desktop $pkgdir/$_desktopdir/
}
md5sums=('be404f62486ef396ea9f89bce47c1246')
