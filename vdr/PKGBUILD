# Maintainer: David Spicer <azleifel at googlemail dot com>
# Contributor: Maik Opitz <maik@fjordblau.de>

pkgname=vdr
pkgver=1.6.0
pkgrel=16
arch=('i686' 'x86_64')
pkgdesc="The video disk recorder (VDR)"
url="http://www.tvdr.de/"
license=('GPL2')
depends=('fontconfig' 'gettext' 'libjpeg' 'perl')
optdepends=('lirc: To use a remote control'
            'runvdr-extreme: An alternative runvdr script'
            'vdrsymbols-ttf: Symbols that some VDR plugins and patches use')
backup=('etc/vdr/channels.conf'
        'etc/vdr/diseqc.conf'
        'etc/vdr/keymacros.conf'
        'etc/vdr/sources.conf'
        'etc/vdr/svdrphosts.conf')
options=(!emptydirs)
install="${pkgname}.install"
source=(ftp://ftp.tvdr.de/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        ftp://ftp.tvdr.de/vdr/Developer/${pkgname}-${pkgver}-1.diff
        ftp://ftp.tvdr.de/vdr/Developer/${pkgname}-${pkgver}-2.diff
        http://www.udo-richter.de/vdr/files/${pkgname}-${pkgver}-2-tsplay-0.2.diff
        MainMenuHooks-v1_0_1.diff
        Make.config.${pkgname}-${pkgver}
        timercmd-0.1_1.6.0.diff
        rc-runvdr
        uk-CrystalPalace-channels.conf
        vdr-1.6.0-const.diff
        vdr-1.6.0-kernel-2.6.29.diff
        vdr-1.6.0-ncurses.diff
        vdr-1.6.0-sky-lirc-device.patch
        vdr-1.6.0-pluginparam.patch
        vdr-shutdown.sh)
md5sums=('76f4ebe6525a35e33313d27a3f8e80c0'
         '4a6be2d47afa3a275837f1703c4df77b'
         '875b66a90d1c6f2e25aa1221b319136b'
         '3c8af4c8199da16eaa38c406bbd8f531'
         '9eb0a30f480ff7761b95194dd02ec5e8'
         'e1b132e62353788e10b2ab482e8f3523'
         'e9b82b88c1fa21b6fa9734ca240f6192'
         'dc40bfb16e2ecd9c69043d754b401bfa'
         '9f2765aaf441f6a9b1f72fabebb5fee0'
         '21ce4dfe0e6455f90c37cd17e68bc7bd'
         'f2d23231b7d9dc5847f9adcc3c64f61a'
         'b8e6162119306b77a48a8517872dbcb5'
         'f026ac9819ba21e48f8db5cab22ab409'
         'e4ca729c0cff072ebaf5a8e8ff4e4a20'
         '6426eac608143bacbf7233c15af86193')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Patches
  patch -Np1 -i "${srcdir}/${pkgname}-${pkgver}-1.diff"
  patch -Np1 -i "${srcdir}/${pkgname}-${pkgver}-2.diff"
  patch -Np1 -i "${srcdir}/vdr-1.6.0-const.diff"
  patch -Np1 -i "${srcdir}/vdr-1.6.0-kernel-2.6.29.diff"
  patch -Np1 -i "${srcdir}/vdr-1.6.0-sky-lirc-device.patch"
  patch -Np0 -i "${srcdir}/vdr-1.6.0-ncurses.diff"
  patch -Np1 -i "${srcdir}/MainMenuHooks-v1_0_1.diff"
  patch -Np1 -i "${srcdir}/timercmd-0.1_1.6.0.diff"
  patch -Np1 -i "${srcdir}/vdr-1.6.0-pluginparam.patch"
  # NB. The tsplay patch is currently supported by vdr-streamdev (>=0.5.0) and
  #     vdr-xineliboutput-cvs plugins.  It may break the vdr-radio plugin.
  #patch -Np1 -i "${srcdir}/vdr-1.6.0-2-tsplay-0.2.diff"

  # Update lirc socket location in man page
  sed -i "s|/dev/lircd|/var/run/lirc/lircd|g" vdr.1

  # Don't install runvdr
  sed -i "s|runvdr ||g" Makefile

  # VDR directory environment (Make.config):
  # MANDIR       = /usr/share/man
  # BINDIR       = /usr/bin
  # LOCDIR       = /usr/share/locale
  # PLUGINLIBDIR = /usr/lib/vdr
  # VIDEODIR     = /var/spool/video
  # CONFDIR      = /etc/vdr
  # Also:
  # VDR_USER = vdr

  install -d -m755 "${pkgdir}/etc/rc.d"
  install -d -m755 "${pkgdir}/usr/bin"
  install -d -m755 "${pkgdir}/usr/include/vdr/include"
  install -d -m755 "${pkgdir}/usr/include/vdr/libsi"
  install -d -m755 "${pkgdir}/usr/share/doc/${pkgname}"

  cp "${srcdir}/Make.config.${pkgname}-${pkgver}" Make.config

  make
  make DESTDIR="${pkgdir}" install

  # Install utility scripts
  for _file in *.pl PLUGINS/src/sky/getskyepg.pl; do
    install -m755 "${_file}" "${pkgdir}/usr/bin/${_file##*/}"
  done

  # Install Make.config and vdr header files
  install -m644 Make.config "${pkgdir}/usr/include/vdr"
  install -m644 *.h "${pkgdir}/usr/include/vdr"
  install -m644 libsi/*.h "${pkgdir}/usr/include/vdr/libsi"
  ln -sf .. "${pkgdir}/usr/include/vdr/include/vdr"
  ln -sf ../libsi "${pkgdir}/usr/include/vdr/include/libsi"

  # Install daemon and shutdown scripts
  install -m755 "${srcdir}/rc-runvdr" "${pkgdir}/etc/rc.d/runvdr"
  install -m755 "${srcdir}/vdr-shutdown.sh" "${pkgdir}/usr/bin/vdr-shutdown.sh"

  # Install documents
  for _document in channels.conf* *.template CONTRIBUTORS HISTORY INSTALL MANUAL PLUGINS.html README* runvdr UPDATE*; do
    install -m644 ${_document} "${pkgdir}/usr/share/doc/${pkgname}"
  done

  # Maintainer's Perk (install custom channels.conf)
  install -m644 "${srcdir}/uk-CrystalPalace-channels.conf" "${pkgdir}/etc/vdr/channels.conf"
}

