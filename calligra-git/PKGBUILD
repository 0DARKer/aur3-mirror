# Maintainer: archtux <antonio.arias99999@gmail.com>

pkgname=calligra-git
true && pkgname=('calligra-filters-git' 'calligra-interfaces-git' 'calligra-libs-git' 'calligra-plugins-git' \
		 'calligra-pics-git' 'calligra-servicetypes-git' 'calligra-templates-git' 'calligra-tools-git' \
		 'calligra-karbon-git' 'calligra-karbon-doc-git' 'calligra-kexi-git' 'calligra-kexi-doc-git' \
		 'calligra-kformula-git' 'calligra-kformula-doc-git' 'calligra-kounavail-git' 'calligra-kplato-git' \
		 'calligra-kpresenter-git' 'calligra-krita-git' 'calligra-tables-git' 'calligra-tables-doc-git' \
		 'calligra-words-git' 'calligra-handbook-git' 'calligra-flow-git' 'calligra-braindump-git')
pkgver=20110403
pkgrel=1
arch=('i686' 'x86_64')
license=('FDL1.2' 'GPL2' 'LGPL')
makedepends=('kdelibs' 'kdepimlibs' 'qca' 'lcms2' 'hicolor-icon-theme' 'sqlite3' 'gsl' \
	 'glew' 'qimageblitz' 'poppler-qt' 'shared-mime-info' 'openexr' 'libqtgtl' \
	 'libpqxx' 'freetds' 'fftw' 'libspnav' 'automoc4' 'boost' 'cmake' 'eigen' \
	 'git' 'pkgconfig' 'pstoedit' 'libwpd' 'libwpg' 'kdegraphics-libs' 'docbook-xml' 'docbook-xsl')
install=calligra.install
groups=('calligra-git')
conflicts=('koffice')

_gitroot="git://anongit.kde.org/calligra"
_gitname="calligra"

build() {

cd $srcdir
  msg "Connecting to the GIT server...."
  
  if [[ -d $srcdir/$_gitname ]] ; then
    cd $_gitname
    git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi
  
  msg "GIT checkout done"
  msg "Starting make..."
  if [[ -d ${srcdir}/build ]]; then
       msg "Cleaning the previous build directory..."
       rm -rf ${srcdir}/build
  fi
  mkdir $srcdir/build
  cd $srcdir/build
  cmake $startdir/src/$_gitname -DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
  make
}

package_calligra-filters-git() {
pkgdesc="Filters for the KDE office suite"
optdepends=('libgsf: Microsoft Word document importer' 'libwpd: WordPerfect document importer'
	'libwpg: Corel WordPerfect Graphics image importer' 'pstoedit: Karbon EPS import filter'
	'kdegraphics-libs: support for raw images')
        cd "${srcdir}/build/filters"
        make DESTDIR="${pkgdir}" install
}

package_calligra-interfaces-git() {
pkgdesc="Interfaces for the KDE office suite"
	depends=('kdelibs' 'calligra-libs-git')
	cd "${srcdir}/build/interfaces"
	make DESTDIR="${pkgdir}" install
}

package_calligra-libs-git(){
	pkgdesc="Libs for the KDE office suite"
	depends=('kdepimlibs' 'qca' 'lcms' 'calligra-servicetypes-git')
        cd "${srcdir}/build/libs"
        make DESTDIR="${pkgdir}" install
}

package_calligra-plugins-git(){
	pkgdesc="Plugins for the KDE office suite"
	depends=('calligra-libs-git')
        cd "${srcdir}/build/plugins"
        make DESTDIR="${pkgdir}" install
}

package_calligra-pics-git(){
	pkgdesc="Icons for the KDE office suite"
	install=calligra.install
        cd "${srcdir}/build/pics"
        make DESTDIR="${pkgdir}" install
}

package_calligra-servicetypes-git(){
	pkgdesc="Servicetypes for the KDE office suite"
        cd "${srcdir}/build/servicetypes"
        make DESTDIR="${pkgdir}" install
}

package_calligra-templates-git(){
	pkgdesc="Templates for the KDE office suite"
        cd "${srcdir}/build/templates"
        make DESTDIR="${pkgdir}" install
}

package_calligra-tools-git(){
	pkgdesc="Tools for the KDE office suite"
	depends=('calligra-libs-git')
        cd "${srcdir}/build/tools"
        make DESTDIR="${pkgdir}" install
}

package_calligra-karbon-git(){
	pkgdesc="Vector drawing application for KDE"
	depends=('hicolor-icon-theme' 'calligra-libs-git' 'calligra-templates-git')
        optdepends=('calligra-filters-git: import/export filters')
	install=calligra.install
        cd "${srcdir}/build/karbon"
        make DESTDIR="${pkgdir}" install
}

package_calligra-karbon-doc-git(){
	pkgdesc="Documentation for karbon"
        cd "${srcdir}/build/doc/karbon"
        make DESTDIR="${pkgdir}" install
}

package_calligra-kexi-git(){
        pkgdesc="Integrated data management application for the KDE office suite"
        depends=('kdelibs' 'calligra-libs-git' 'calligra-tables-git' 'sqlite3')
        cd "${srcdir}/build/kexi"
        make DESTDIR="${pkgdir}" install
}

package_calligra-kexi-doc-git(){
        pkgdesc="Documentation for kexi"
        cd "${srcdir}/build/doc/kexi"
        make DESTDIR="${pkgdir}" install
}

package_calligra-kformula-git(){
        pkgdesc="Formula editor for the KDE office suite"
	depends=('kdelibs' 'calligra-libs-git')
        cd "${srcdir}/build/kformula"
        make DESTDIR="${pkgdir}" install
}

package_calligra-kformula-doc-git() {
	pkgdesc="Documentation for kformula"
	cd "${srcdir}/build/doc/kformula"
        make DESTDIR="${pkgdir}" install
}

package_calligra-kounavail-git(){
	pkgdesc="Kounavail for the KDE office suite"
	depends=('kdelibs' 'calligra-libs-git')
        cd "${srcdir}/build/kounavail"
        make DESTDIR="${pkgdir}" install
}

package_calligra-kplato-git(){
	pkgdesc="Project management application for KDE"
	depends=('hicolor-icon-theme' 'calligra-libs-git' 'kdepimlibs' 'calligra-templates-git' \
		'calligra-plugins-git' 'calligra-words-git')
	install=calligra.install
        cd "${srcdir}/build/kplato"
        make DESTDIR="${pkgdir}" install
}

package_calligra-kpresenter-git(){
	pkgdesc="Presentation tool for KDE"
	depends=('hicolor-icon-theme' 'calligra-libs-git' 'calligra-templates-git')
        optdepends=('calligra-filters-git: import/export filters')
	install=calligra.install
        cd "${srcdir}/build/kpresenter"
        make DESTDIR="${pkgdir}" install
}

package_calligra-krita-git(){
	pkgdesc="Graphics editor for KDE"
	depends=('hicolor-icon-theme' 'glew' 'qimageblitz' 'calligra-libs-git' \
		'calligra-templates-git' 'calligra-plugins-git' 'poppler-qt' 'shared-mime-info' \
		'openexr' 'opengtl' 'lcms2' 'libqtgtl')
        optdepends=('calligra-filters-git: import/export filters')
        cd "${srcdir}/build/krita"
        make DESTDIR="${pkgdir}" install
}

package_calligra-tables-git(){
	pkgdesc="Spreadsheet application for KDE"
	depends=('hicolor-icon-theme' 'gsl' 'calligra-libs-git' 'calligra-templates-git' 'calligra-plugins-git')
	optdepends=('calligra-filters-git: import/export filters')
	install=calligra.install
        cd "${srcdir}/build/tables"
        make DESTDIR="${pkgdir}" install
}

package_calligra-tables-doc-git(){
	pkgdesc="Documentation for tables"
	cd "${srcdir}/build/doc/tables"
        make DESTDIR="${pkgdir}" install
}

package_calligra-words-git(){
	pkgdesc="Wordprocessor for KDE"
	depends=('hicolor-icon-theme' 'calligra-libs-git' 'calligra-templates-git' 'calligra-pics-git' \
		'calligra-kounavail-git' 'calligra-plugins-git')
        optdepends=('calligra-filters-git: import/export filters')
	install=calligra.install
        cd "${srcdir}/build/words"
        make DESTDIR="${pkgdir}" install

        # some sed lines to have python2 in extension scripts
        sed -i "s|/usr/bin/env python|/usr/bin/env python2|" "${pkgdir}/usr/share/apps/words/scripts/extensions/oouno.py"
        sed -i "s|/usr/bin/env python|/usr/bin/env python2|" "${pkgdir}/usr/share/apps/words/scripts/extensions/importdoxyxml.py"
        sed -i "s|/usr/bin/env python|/usr/bin/env python2|" "${pkgdir}/usr/share/apps/words/scripts/extensions/onlinehelp.py"
}

package_calligra-handbook-git() {
	pkgdesc="Documentation for calligra"
	cd "${srcdir}/build/doc/calligra"
        make DESTDIR="${pkgdir}" install
}

package_calligra-flow-git() {
	pkgdesc="For diagramming and flowcharting"
	depends=('calligra-libs-git')
	cd "${srcdir}/build/flow"
	make DESTDIR="${pkgdir}" install
}

package_calligra-braindump-git() {
	pkgdesc="Tool to dump and organize the content of your brain"
	depends=('calligra-libs-git')
	cd "${srcdir}/build/braindump"
	make DESTDIR="${pkgdir}" install
}
url="http://www.calligra-suite.org/"
pkgdesc="Calligra Suite is a set of applications written to help you to accomplish your work."
