# Maintainer: archtux <antonio.arias99999@gmail.com>

pkgname=calligra-git
true && pkgname=('calligra-filters-git'
		 'calligra-libs-git'
		 'calligra-plugins-git'
		 'calligra-extras-git'
		 'calligra-devtools-git'
		 'calligra-karbon-git'
		 'calligra-kexi-git'
		 'calligra-plan-git'
		 'calligra-stage-git'
		 'calligra-krita-git'
		 'calligra-sheets-git'
		 'calligra-words-git'
		 'calligra-handbook-git'
		 'calligra-flow-git'
		 'calligra-braindump-git')
pkgver=20130305
pkgrel=1
arch=('i686' 'x86_64')
license=('FDL1.2' 'GPL2' 'LGPL')

makedepends=('kdepimlibs' 'lcms2' 'gsl' 'glew' 'libqtgtl'  'fftw' 'libpqxx<4.0'
	     'libspnav' 'automoc4' 'boost' 'cmake' 'eigen2' 'git' 'pstoedit'
	     'libwpd' 'libwpg' 'docbook-xml' 'libgsf' 'kdegraphics-okular'
	     'kdeedu-marble' 'libkdcraw' 'freetds' 'libwps' 'libvisio-git'
	     'opencolorio' 'openimageio' 'libmysqlclient' 'xbase'
	     'openjpeg' 'cauchy-hg' )
groups=('calligra-git')

_gitroot="git://anongit.kde.org/calligra"
_gitname="calligra"

build() {

cd $srcdir
 msg "Connecting to the GIT server...."
  
  if [[ -d $srcdir/$_gitname ]] ; then
    cd $_gitname
    git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot --depth=1
  fi
    msg "GIT checkout done"
    msg "Starting make..."
  if [[ -d ${srcdir}/build ]]; then
      msg "Cleaning the previous build directory..."
      rm -rf ${srcdir}/build
  fi
  mkdir $srcdir/build
  cd $srcdir/build  
  cmake $srcdir/$_gitname -DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DQT_QMAKE_EXECUTABLE=qmake-qt4 \
		-DBUILD_active=OFF
  make
}

package_calligra-filters-git() {
  pkgdesc="Filters for the Calligra office suite"
  optdepends=('libgsf: Microsoft Word document importer'
            'libwpd: WordPerfect document importer'
            'libwpg: Corel WordPerfect Graphics image importer'
            'pstoedit: Karbon EPS import filter'
            'libkdcraw: support for raw images'
	    'libwps: Words WPS import filter'
	    'libvisio-git: Visio Import Filter Library')
  conflicts=('koffice-filters' 'calligra-filters')
  install=calligra.install

    cd "${srcdir}/build/filters"
    make DESTDIR="${pkgdir}" install
}

package_calligra-libs-git(){
  pkgdesc="Libraries for the Calligra office suite"
  depends=('kdepimlibs' 'lcms2' 'kactivities')
  conflicts=('koffice-libs' 'calligra-libs' 'calligra-interfaces-git' 'calligra-pics-git' 'calligra-servicetypes-git'  'calligra-templates-git')

  for d in interfaces libs pics servicetypes templates; do
    cd "${srcdir}"/build/${d}
    make DESTDIR="${pkgdir}" install
  done
}

package_calligra-plugins-git(){
  pkgdesc="Plugins for the Calligra office suite"
  depends=('calligra-libs-git' 'calligra-filters-git')
  optdepends=('kdeedu-marble: map shape for Calligra')
  conflicts=('koffice-plugins' 'koffice-kchart' 'calligra-plugins')

    cd "${srcdir}/build/plugins"
    make DESTDIR="${pkgdir}" install
}

package_calligra-extras-git(){
  pkgdesc="Lots of extra stuff that uses the Calligra libs/modules/programs"
  depends=('calligra-libs-git')
  conflicts=('koffice-tools' 'calligra-tools' 'calligra-tools-git')

    cd "${srcdir}/build/tools"
    make DESTDIR="${pkgdir}" install
}

package_calligra-devtools-git(){
  pkgdesc="Lots of tools only interesting to developers working on Calligra"
  depends=('calligra-libs-git')
  conflicts=('koffice-tools' 'calligra-tools' 'calligra-tools-git')

    cd "${srcdir}/build/tools"
    make DESTDIR="${pkgdir}" install
}

package_calligra-karbon-git(){
  pkgdesc="Create scalable vector drawings"
  depends=('calligra-libs-git')
  conflicts=('koffice-karbon' 'koffice-karbon-doc' 'calligra-karbon-doc' 'calligra-karbon')
  install=calligra.install

  cd "${srcdir}/build/karbon"
  make DESTDIR="${pkgdir}" install
}

package_calligra-kexi-git(){
  pkgdesc="Develop desktop database applications"
  depends=('calligra-libs-git' 'glew' 'fftw')
  optdepends=('libmysqlclient: MySQL driver'
              'freetds: Sybase & MS SQL driver'
              'xbase: XBase driver'
              'calligra-sheets-git: Spreadsheet-to-Kexi-table import plugin'
              'kdeedu-marble: map form widget'
              'calligra-plugins-git: extra calligra plugins')
  conflicts=('koffice-kexi' 'koffice-kexi-doc' 'calligra-kexi-doc' 'calligra-kexi')

    cd "${srcdir}/build/kexi"
    make DESTDIR="${pkgdir}" install
    cd "${srcdir}"/build/doc/kexi
    make DESTDIR="${pkgdir}" install
}

package_calligra-plan-git(){
  pkgdesc="Calligra Project Management Component"
  depends=('calligra-words-git')
  conflicts=('calligra-kplato-git' 'koffice-kplato' 'calligra-kplato' 'calligra-plan')
  replaces=('calligra-kplato-git')
  install=calligra.install

    cd "${srcdir}/build/plan"
    make DESTDIR="${pkgdir}" install
    cd "${srcdir}"/build/3rdparty/kdgantt
    make DESTDIR="${pkgdir}" install
}

package_calligra-stage-git(){
  pkgdesc="Write presentation documents"
  depends=('calligra-plugins-git' 'kdebase-runtime')
  conflicts=('calligra-kpresenter-git' 'calligra-stage')
  install=calligra.install

    cd "${srcdir}/build/stage"
    make DESTDIR="${pkgdir}" install
    cd "${srcdir}"/build/doc/stage
    make DESTDIR="${pkgdir}" install
}

package_calligra-krita-git(){
  pkgdesc="Edit and paint images"
  depends=('calligra-plugins-git' 'kdebase-runtime' 'eigen2' 'libqtgtl' 
           'opencolorio' 'libkdcraw' 'fftw' 'glew' 'opengtl' 'openimageio')
  conflicts=('koffice-krita' 'calligra-krita')
  install=calligra.install

    cd "${srcdir}/build/krita"
    make DESTDIR="${pkgdir}" install
}

package_calligra-sheets-git(){
  pkgdesc="Write spreadsheet documents"
  depends=('gsl' 'calligra-libs-git' 'kdebase-runtime')
  optdepends=('calligra-filters-git: import/export filters'
              'calligra-plugins-git: extra calligra plugins'
              'calligra-extras-git: extra calligra tools')
  conflicts=('koffice-kspread' 'calligra-sheets' 'calligra-tables-git')
  install=calligra.install

    cd "${srcdir}/build/sheets"
    make DESTDIR="${pkgdir}" install
    cd "${srcdir}"/build/doc/sheets
    make DESTDIR="${pkgdir}" install
}

package_calligra-words-git(){
  pkgdesc="Word processor"
  depends=('calligra-plugins-git' 'kdegraphics-okular')
  optdepends=('calligra-extras-git: extra calligra tools')
  conflicts=('koffice-kword' 'calligra-words')
  install=calligra.install

    cd "${srcdir}/build/words"
    make DESTDIR="${pkgdir}" install
}

package_calligra-handbook-git() {
  pkgdesc="Documentation for Calligra"
  conflicts=('koffice-handbook' 'calligra-handbook')

    cd "${srcdir}/build/doc/calligra"
    make DESTDIR="${pkgdir}" install
}

package_calligra-flow-git() {
  pkgdesc="Flowchart & Diagram Editing"
  depends=('calligra-words-git')
  conflicts=('calligra-flow')
  install=calligra.install

    cd "${srcdir}/build/flow"
    make DESTDIR="${pkgdir}" install
}

package_calligra-braindump-git() {
  pkgdesc="Notes and idea gathering"
  depends=('calligra-libs-git' 'kdebase-runtime')
  conflicts=('calligra-braindump')
  install=calligra.install
	
    cd "${srcdir}/build/braindump"
    make DESTDIR="${pkgdir}" install
}

url="http://www.calligra-suite.org/"
pkgdesc="Calligra Suite is a set of applications written to help you to accomplish your work."
