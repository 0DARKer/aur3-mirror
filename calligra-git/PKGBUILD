# Maintainer: archtux <antonio.arias99999@gmail.com>

pkgname=calligra-git
true && pkgname=('calligra-filters-git' 'calligra-interfaces-git' 'calligra-libs-git' 'calligra-plugins-git' \
		 'calligra-pics-git' 'calligra-servicetypes-git' 'calligra-templates-git' 'calligra-tools-git' \
		 'calligra-karbon-git' 'calligra-kexi-git' 'calligra-kounavail-git' \
		 'calligra-plan-git' 'calligra-stage-git' 'calligra-krita-git' 'calligra-sheets-git' \
		 'calligra-sheets-doc-git' 'calligra-words-git' 'calligra-handbook-git' 'calligra-thesaurus-doc-git' \
		 'calligra-flow-git' 'calligra-braindump-git' 'calligra-mobile-git' 'calligra-active-git')
pkgver=20120417
pkgrel=1
arch=('i686' 'x86_64')
license=('FDL1.2' 'GPL2' 'LGPL')

makedepends=('kdepimlibs' 'lcms2' 'gsl' 'glew' 'libqtgtl' 'libpqxx' 'fftw' \
	     'libspnav' 'automoc4' 'boost' 'cmake' 'eigen2' 'git' 'pstoedit' \
	     'libwpd' 'libwpg' 'docbook-xml' 'libgsf' 'kdegraphics-okular' \
	     'kdeedu-marble' 'libkdcraw' 'freetds' 'libwps' 'libvisio-git')
groups=('calligra-git')

_gitroot="git://anongit.kde.org/calligra"
_gitname="calligra"

build() {

cd $srcdir
 msg "Connecting to the GIT server...."
  
  if [[ -d $srcdir/$_gitname ]] ; then
   cd $_gitname
   git pull origin
   msg "The local files are updated."
  else
   git clone $_gitroot
  fi
 msg "GIT checkout done"
 msg "Starting make..."
 if [[ -d ${srcdir}/build ]]; then
      msg "Cleaning the previous build directory..."
      rm -rf ${srcdir}/build
fi
  mkdir $srcdir/build
  cd $srcdir/build  
  cmake $startdir/src/$_gitname -DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DIHAVEPATCHEDQT=1
  make
}

package_calligra-filters-git() {
  pkgdesc="Filters for the Calligra office suite"
  optdepends=('libgsf: Microsoft Word document importer'
            'libwpd: WordPerfect document importer'
            'libwpg: Corel WordPerfect Graphics image importer'
            'pstoedit: Karbon EPS import filter'
            'libkdcraw: support for raw images'
	    'libwps: Words WPS import filter'
	    'libvisio-git: Visio Import Filter Library')
  conflicts=('koffice-filters' 'calligra-filters')
  install=calligra.install

    cd "${srcdir}/build/filters"
    make DESTDIR="${pkgdir}" install
}

package_calligra-interfaces-git() {
  pkgdesc="Interfaces for the Calligra office suite"
  depends=('calligra-libs-git')
  conflicts=('koffice-interfaces' 'calligra-interfaces')

    cd "${srcdir}/build/interfaces"
    make DESTDIR="${pkgdir}" install
}

package_calligra-libs-git(){
  pkgdesc="Libraries for the Calligra office suite"
  depends=('kdepimlibs' 'lcms2' 'calligra-servicetypes-git' 'kdeedu-marble')
  conflicts=('koffice-libs' 'calligra-libs')

    cd "${srcdir}/build/libs"
    make DESTDIR="${pkgdir}" install
}

package_calligra-plugins-git(){
  pkgdesc="Plugins for the Calligra office suite"
  depends=('calligra-libs-git')
  conflicts=('koffice-plugins' 'koffice-kchart' 'calligra-plugins')

    cd "${srcdir}/build/plugins"
    make DESTDIR="${pkgdir}" install
}

package_calligra-pics-git(){
  pkgdesc="Icons for the Calligra office suite"
  depends=('calligra-libs-git')
  conflicts=('koffice-pics' 'calligra-pics')
  install=calligra.install

    cd "${srcdir}/build/pics"
    make DESTDIR="${pkgdir}" install
}

package_calligra-servicetypes-git(){
  pkgdesc="Servicetypes for the Calligra office suite"
  conflicts=('koffice-servicetypes' 'calligra-servicetypes')

    cd "${srcdir}/build/servicetypes"
    make DESTDIR="${pkgdir}" install
}

package_calligra-templates-git(){
  pkgdesc="Templates for the Calligra office suite"
  conflicts=('koffice-templates' 'calligra-templates')

    cd "${srcdir}/build/templates"
    make DESTDIR="${pkgdir}" install
}

package_calligra-tools-git(){
  pkgdesc="Tools for the Calligra office suite"
  depends=('calligra-libs-git')
  conflicts=('koffice-tools' 'calligra-tools')

    cd "${srcdir}/build/tools"
    sed -i '/mobile/d' cmake_install.cmake
    make DESTDIR="${pkgdir}" install
}

package_calligra-karbon-git(){
  pkgdesc="Create scalable vector drawings"
  depends=('calligra-libs-git' 'calligra-templates-git')
  conflicts=('koffice-karbon' 'koffice-karbon-doc' 'calligra-karbon-doc' 'calligra-karbon')
  install=calligra.install

  cd "${srcdir}/build/karbon"
  make DESTDIR="${pkgdir}" install
}

package_calligra-kexi-git(){
  pkgdesc="Develop desktop database applications"
  depends=('calligra-libs-git' 'calligra-templates-git' 'freetds')
  conflicts=('koffice-kexi' 'koffice-kexi-doc' 'calligra-kexi-doc' 'calligra-kexi')

    cd "${srcdir}/build/kexi"
    make DESTDIR="${pkgdir}" install
}

package_calligra-kounavail-git(){
  pkgdesc="Unavailable Calligra Document"
  depends=('calligra-libs-git')
  conflicts=('koffice-kounavail' 'calligra-kounavail')

    cd "${srcdir}/build/kounavail"
    make DESTDIR="${pkgdir}" install
}

package_calligra-plan-git(){
  pkgdesc="Calligra Project Management Component"
  depends=('calligra-words-git')
  conflicts=('calligra-kplato-git' 'koffice-kplato' 'calligra-kplato' 'calligra-plan')
  replaces=('calligra-kplato-git')
  install=calligra.install

    cd "${srcdir}/build/plan"
    make DESTDIR="${pkgdir}" install
}

package_calligra-stage-git(){
  pkgdesc="Write presentation documents"
  depends=('calligra-libs-git' 'calligra-templates-git')
  optdepends=('calligra-filters-git: import/export filters')
  conflicts=('calligra-kpresenter-git' 'calligra-stage')
  install=calligra.install

    cd "${srcdir}/build/stage"
    make DESTDIR="${pkgdir}" install
}

package_calligra-krita-git(){
  pkgdesc="Edit and paint images"
  depends=('calligra-libs-git' 'calligra-templates-git' 'calligra-plugins-git' 'glew' \
	 'qimageblitz' 'poppler-qt' 'openexr' 'libqtgtl')
  optdepends=('calligra-filters-git: import/export filters')
  conflicts=('koffice-krita' 'calligra-krita')
  install=calligra.install

    cd "${srcdir}/build/krita"
    make DESTDIR="${pkgdir}" install
}

package_calligra-sheets-git(){
  pkgdesc="Write spreadsheet documents"
  depends=('gsl' 'calligra-libs-git' 'calligra-templates-git' 'calligra-plugins-git')
  optdepends=('calligra-filters-git: import/export filters')
  conflicts=('koffice-kspread' 'calligra-sheets' 'calligra-tables-git')
  install=calligra.install

    cd "${srcdir}/build/sheets"
    make DESTDIR="${pkgdir}" install
}

package_calligra-sheets-doc-git(){
  pkgdesc="Documentation for sheets"
  conflicts=('calligra-tables-doc-git')

    cd "${srcdir}/build/doc/sheets"
    make DESTDIR="${pkgdir}" install
}

package_calligra-words-git(){
  pkgdesc="Word processor"
  depends=('calligra-libs-git' 'calligra-templates-git' 'calligra-pics-git' \
	   'calligra-kounavail-git' 'kdegraphics-okular')
  optdepends=('calligra-filters-git: import/export filters')
  conflicts=('koffice-kword' 'calligra-words')
  install=calligra.install

    cd "${srcdir}/build/words"
    make DESTDIR="${pkgdir}" install
}

package_calligra-handbook-git() {
  pkgdesc="Documentation for Calligra"
  conflicts=('koffice-handbook' 'calligra-handbook')

    cd "${srcdir}/build/doc/calligra"
    make DESTDIR="${pkgdir}" install
}

package_calligra-thesaurus-doc-git() {
  pkgdesc="Documentation for Thesaurus"
  conflicts=('koffice-thesaurus-doc' 'calligra-thesaurus-doc')
 
    cd "${srcdir}"/build/doc/thesaurus
    make DESTDIR="${pkgdir}" install
}

package_calligra-flow-git() {
  pkgdesc="Flowchart & Diagram Editing"
  depends=('calligra-libs-git')
  conflicts=('calligra-flow')
  install=calligra.install

    cd "${srcdir}/build/flow"
    make DESTDIR="${pkgdir}" install
}

package_calligra-braindump-git() {
  pkgdesc="Notes and idea gathering"
  depends=('calligra-libs-git')
  conflicts=('calligra-braindump')
  install=calligra.install
	
    cd "${srcdir}/build/braindump"
    make DESTDIR="${pkgdir}" install
}

package_calligra-mobile-git() {
  pkgdesc="Calligra Mobile is a mobile user interaction of Calligra Suite"
  depends=('calligra-tools-git')

    cd "${srcdir}/build/tools/mobile"
    make DESTDIR="${pkgdir}" install
}

package_calligra-active-git() {
  pkgdesc="QtQuick GUI for Calligra targeted for mobile devices"
  depends=('calligra-libs-git')

    cd "${srcdir}/build/active"
    make DESTDIR="${pkgdir}" install
}

url="http://www.calligra-suite.org/"
pkgdesc="Calligra Suite is a set of applications written to help you to accomplish your work."
