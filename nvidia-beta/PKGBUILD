# Maintainer: Det
# Contributors: Ng Oon-Ee, Dan Vratil
# Based on [extra]'s nvidia: https://www.archlinux.org/packages/extra/x86_64/nvidia/

pkgname=nvidia-beta
pkgver=337.12
pkgrel=1
pkgdesc="NVIDIA driver for linux (beta)"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux' 'nvidia-libgl-beta' "nvidia-utils-beta=${pkgver}")
makedepends=('linux-headers')
conflicts=('nvidia-96xx' 'nvidia-173xx' 'nvidia')
provides=('nvidia')
license=('custom:NVIDIA')
install=${pkgname}.install
options=('!strip')

_arch='x86'
_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
if [ "${CARCH}" = 'x86_64' ]; then
    _arch='x86_64'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
fi

source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
# source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
md5sums=('c610d5bff8f7f758dc25fdd51b1b65eb')  # NVIDIA-Linux-${_arch}-${pkgver}
[ "${CARCH}" = 'x86_64' ] && md5sums[0]='4c2aaac75c1ff01a09ac8f4ff84084cd' # NVIDIA-Linux-${_arch}-${pkgver}-no-compat32

prepare() {
    # Remove previous builds
    if [ -d ${_pkg} ]; then
        rm -rf ${_pkg}
    fi

    # Extract
    msg2 "Self-Extracting ${_pkg}.run.."
    sh ${_pkg}.run --extract-only
    cd ${_pkg}

#     # Patch
#     _major=$(pacman -Q linux | grep -Po "(\d+\.)\d+")
#     if [ ${_major} = 3.14 ]; then
#         msg2 "Updating for linux 3.14..."
#         patch -p1 -i "${srcdir}"/nvidia-linux-3.14.patch
#     fi
}

build() {
    # Version of 'linux'
    _major=$(pacman -Q linux | grep -Po "(\d+\.)\d+")
    _extramodules=extramodules-${_major}-ARCH
    _kernel=$(cat /usr/lib/modules/${_extramodules}/version)

    # Main module
    cd ${_pkg}/kernel
    msg2 "Building Nvidia module for ${_kernel}..."
    make SYSSRC=/usr/lib/modules/${_kernel}/build module

    # UVM
    cd uvm
    msg2 "Building UVM module for ${_kernel}...: https://devblogs.nvidia.com/parallelforall/unified-memory-in-cuda-6/"
    make SYSSRC=/usr/lib/modules/${_kernel}/build module
}

package() {
    # Version of 'linux'
    _major=$(pacman -Q linux | grep -Po "(\d+\.)\d+")
    _extramodules=extramodules-${_major}-ARCH

    # Install
    install -Dm644 ${_pkg}/kernel/nvidia.ko \
                   "${pkgdir}"/usr/lib/modules/${_extramodules}/nvidia.ko

    # Unified Memory
    install -Dm644 ${_pkg}/kernel/uvm/nvidia-uvm.ko \
                   "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia-uvm.ko"

    # Compress
    gzip "${pkgdir}"/usr/lib/modules/${_extramodules}/nvidia*.ko

    # Write _extramodules to .install
    sed -i "s/_extramodules='.*'/_extramodules='${_extramodules}'/" "${startdir}"/${install}

    # Blacklist Nouveau
    install -d "${pkgdir}"/usr/lib/modprobe.d/
    echo "blacklist nouveau" >> "${pkgdir}"/usr/lib/modprobe.d/nvidia.conf
}