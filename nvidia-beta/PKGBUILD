# Maintainer: Det
# Contributors: Ng Oon-Ee, Dan Vratil
# Based on [extra]'s nvidia: https://www.archlinux.org/packages/extra/x86_64/nvidia/

pkgname=nvidia-beta
pkgver=334.16
pkgrel=1
pkgdesc="NVIDIA driver for linux (beta)"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux' 'nvidia-libgl-beta' "nvidia-utils-beta=${pkgver}")
makedepends=('linux-headers')
conflicts=('nvidia-96xx' 'nvidia-173xx' 'nvidia')
provides=('nvidia')
license=('custom:NVIDIA')
install=${pkgname}.install
options=('!strip')

if [ "${CARCH}" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    md5sums=('2503a9ce1c17154099b2bfbdde86caf7')
elif [ "${CARCH}" = "x86_64" ]; then
    _arch='x86_64'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    md5sums=('54be06165c88cda2235c2c7eee7a2575')
fi

#source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
        'nvidia-linux-3.13.patch'
        'nvidia-linux-3.14.patch')
md5sums+=('9761c3f33c47f4ce29d31d6fbaba7559'  # nvidia-linux-3.13.patch
          '4b30d667e50f963f7cd2a118ffe17523') # nvidia-linux-3.14.patch

prepare() {
    # Remove previous builds
    if [ -d ${_pkg} ]; then
        rm -rf ${_pkg}
    fi

    # Extract
    msg2 "Self-Extracting ${_pkg}.run.."
    sh ${_pkg}.run --extract-only

    # Patch
    _major=$(pacman -Q linux | grep -Po "(\d+\.)\d+")

    cd ${_pkg}

    # 3.13/.14
   if [ ${_major} = 3.13 ]; then
       msg2 "Updating for linux 3.13..."
       patch -p1 -i "${srcdir}/nvidia-linux-3.13.patch"
   elif [ ${_major} = 3.14 ]; then
       msg2 "Updating for linux 3.14..."
       patch -p1 -i "${srcdir}/nvidia-linux-3.14.patch"
   fi
}

build() {
    # Version of 'linux'
    _major=$(pacman -Q linux | grep -Po "(\d+\.)\d+")
    _extramodules=extramodules-${_major}-ARCH
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

    # Build
    msg2 "Starting make module..."
    cd ${_pkg}/kernel
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
    # Version of 'linux'
    _major=$(pacman -Q linux | grep -Po "(\d+\.)\d+")
    _extramodules=extramodules-${_major}-ARCH

    # Install/compress module
    install -Dm644 ${_pkg}/kernel/nvidia.ko "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"

    # Write ${_EXTRAMODULES} to .install
    sed -i "s/_extramodules='.*'/_extramodules='${_extramodules}'/" "${startdir}/${install}"

    # Blacklist Nouveau
    install -d "${pkgdir}/usr/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia.conf"
}
