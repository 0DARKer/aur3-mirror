# Maintainer : Dan Vratil <vratil@progdansoft.com>

pkgname=nvidia-beta 
pkgver=295.20
pkgrel=1
pkgdesc="NVIDIA beta drivers for linux." 
arch=('i686' 'x86_64') 
[ "$CARCH" = x86_64 ] && ARCH=x86_64 &&_srcname=NVIDIA-Linux-x86_64-${pkgver}-no-compat32 && md5sums=('79b05cffa482234833ae1c4bb40664c3')
[ "$CARCH" = "i686" ] && ARCH=x86 && _srcname=NVIDIA-Linux-x86-${pkgver} && md5sums=('a6f702271da49930a17e28b5928fe75a')
provides=("nvidia=${pkgver}")
url="http://www.nvidia.com/" 
depends=("nvidia-utils-beta=${pkgver}") 
makedepends=('linux-headers')
conflicts=('nvidia-96xx' 'nvidia-71xx' 'nvidia-legacy' 'nvidia') 
license=('custom') 
install=nvidia.install
source=("ftp://download.nvidia.com/XFree86/Linux-$ARCH/${pkgver}/${_srcname}.run")

_extramodules=extramodules-3.2-ARCH
_kernver="$(cat /lib/modules/${_extramodules}/version || true)"

build()
{
	# Extract the nvidia drivers
	cd "${srcdir}"
	if [ -d ${_srcname} ]; then
		rm -rf ${_srcname}
	fi
	sh ${_srcname}.run --extract-only
	cd ${_srcname}/kernel

	make SYSSRC=/usr/src/linux-${_kernver} module
}

package() 
{
	cd "${srcdir}/${_srcname}/kernel"

	# Install kernel module
	mkdir -p "${pkgdir}/lib/modules/${_extramodules}/"
	install -m644 nvidia.ko "${pkgdir}/lib/modules/${_extramodules}/"

	# Blacklist nouveau since 2.6.34
	mkdir -p "${pkgdir}/etc/modprobe.d/"
	echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist.conf"
                      
}

