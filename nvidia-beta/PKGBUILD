# Maintainer : Ng Oon-Ee <ngoonee.talk@gmail.com>
# Contributor : Dan Vratil <vratil@progdansoft.com>

pkgname=nvidia-beta 
pkgver=313.18
_kernel="$(uname -r | cut -d'.' -f-2)"
_extramodules=extramodules-${_kernel}-ARCH
pkgrel=2
pkgdesc="NVIDIA beta drivers for linux." 
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
provides=("nvidia=${pkgver}")
depends=("nvidia-utils-beta=${pkgver}")
makedepends=('linux-headers' 'kmod>=9-2')
conflicts=('nvidia-96xx' 'nvidia-71xx' 'nvidia-legacy' 'nvidia') 
license=('custom')
install=nvidia.install
options=(!strip)

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    md5sums=('780c37c28a6e06e9571cafe348b7da64'
             '2627107ebbd930389b00a9a2cd5e6f42')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
   _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    md5sums=('fa17a260793a38b4b8ae367db2e03b39'
             '2627107ebbd930389b00a9a2cd5e6f42')
fi
source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
        "linux-3.7.6.patch")
#source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
    cd "${srcdir}"
    if [ -d ${_pkg} ]; then
      rm -rf ${_pkg}
    fi
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}/kernel"
    patch -Np3 -i "${srcdir}"/linux-3.7.6.patch
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
