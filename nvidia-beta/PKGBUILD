# Maintainer: Det
# Contributors: Ng Oon-Ee, Dan Vratil
# Based on [extra]'s nvidia: https://www.archlinux.org/packages/extra/x86_64/nvidia/

pkgname=nvidia-beta
pkgver=319.32
_major=$(uname -r | cut -d '.' -f-2)
_extramodules=extramodules-${_major}-ARCH
pkgrel=1
pkgdesc="NVIDIA driver for linux (beta)" 
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux' 'nvidia-libgl-beta' "nvidia-utils-beta=${pkgver}")
makedepends=('linux-headers')
conflicts=('nvidia-96xx' 'nvidia-173xx' 'nvidia') 
provides=('nvidia')
license=('custom:NVIDIA')
install=${pkgname}.install
options=('!strip')

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    md5sums=('978339ff37c3207f3a39d3952721dcdb')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    md5sums=('d181d856768868823df3bda4607a5315')
fi

#source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
        'linux-3.7.patch'
        'linux-3.8.patch')
md5sums+=('7c684fab691b15d55a79fd8f9b1a224d'  # linux-3.7.patch
          '9b20edcefc8ad28f40bdf9a5e26ef731') # linux-3.8.patch

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

    # Remove previous builds
    if [ -d ${_pkg} ]; then
        rm -rf ${_pkg}
    fi

    # Extract
    sh ${_pkg}.run --extract-only
    cd ${_pkg}/kernel

    # Patch
    _verstring=$(echo ${_kernver} | cut -d '-' -f1 | tr -d .)
    if [ ${_verstring} -ge 370 ]; then
        msg2 "Applying linux-3.7.patch"
        patch -p3 -i "${srcdir}/linux-3.7.patch"
        if [ ${_verstring} -lt 382 ]; then
            msg2 "Applying linux-3.8.patch"
            patch -p1 -i "${srcdir}/linux-3.8.patch"
        fi
    fi

    # Build module
    msg2 "Starting make module..."
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
    # Install/compress module
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"

    # Write $_extramodules to .install
    sed -i "s/_extramodules='.*'/_extramodules='${_extramodules}'/" "${startdir}/${install}"

    # Blacklist Nouveau
    install -d "${pkgdir}/usr/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia.conf"
}