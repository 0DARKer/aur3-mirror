# Maintainer : Dan Vratil <vratil@progdansoft.com>

pkgname=nvidia-beta 
pkgver=304.51
pkgrel=1
pkgdesc="NVIDIA beta drivers for linux." 
arch=('i686' 'x86_64') 
[ "$CARCH" = x86_64 ] && ARCH=x86_64 &&_srcname=NVIDIA-Linux-x86_64-${pkgver}-no-compat32
[ "$CARCH" = "i686" ] && ARCH=x86 && _srcname=NVIDIA-Linux-x86-${pkgver}
provides=("nvidia=${pkgver}")
url="http://www.nvidia.com/" 
depends=("nvidia-utils-beta=${pkgver}") 
makedepends=('linux-headers' 'kmod>=9-2')
conflicts=('nvidia-96xx' 'nvidia-71xx' 'nvidia-legacy' 'nvidia') 
license=('custom') 
install=nvidia.install
source=("ftp://download.nvidia.com/XFree86/Linux-$ARCH/${pkgver}/${_srcname}.run")

_kernver="$(uname -r | cut -d'.' -f-2)"
_extramodules=extramodules-${_kernver}-ARCH
_kernname="$(cat /usr/lib/modules/${_extramodules}/version)"

[ "$CARCH" = "i686" ] && md5sums=('4fde294e139a31528ea7f6efd8885a09' )
[ "$CARCH" = "x86_64" ] && md5sums=('2ca10e05cd3b5d2a87caaaad9fd93c06' )

build()
{
	# Extract the nvidia drivers
	cd "${srcdir}"
	if [ -d ${_srcname} ]; then
		rm -rf ${_srcname}
	fi
	sh ${_srcname}.run --extract-only
	cd ${_srcname}/kernel

	make SYSSRC=/usr/src/linux-${_kernname} module
}

package() 
{
	cd "${srcdir}/${_srcname}/kernel"

	# Install kernel module
	mkdir -p "${pkgdir}/usr/lib/modules/${_extramodules}/"
	install -m644 nvidia.ko "${pkgdir}/usr/lib/modules/${_extramodules}/"

	# Blacklist nouveau since 2.6.34
	mkdir -p "${pkgdir}/etc/modprobe.d/"
	echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist.conf"
                      
}

