pkgname=loliwm-git
pkgver=r821.aa62a3c
pkgrel=1

pkgdesc='Wayland window manager based on wlc'
url='https://github.com/Cloudef/loliwm'
arch=('i686' 'x86_64')
license=('GPL')

depends=('wayland-git' 'pixman')
makedepends=('git' 'cmake')
optdepends=('weston-git: To test weston clients in loliwm')

source=('git://github.com/Cloudef/loliwm'
        'git://github.com/Cloudef/wlc')

md5sums=('SKIP' 'SKIP')

pkgver() {
    cd
    printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git describe --always)"
}

prepare() {
    cd loliwm
    git submodule init
    git config submodule.lib/wlc.url "$srcdir"/wlc
    git submodule update lib/wlc
}

build() {
    cd loliwm
    cmake -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd loliwm
    install -Dm755 src/loliwm "$pkgdir"/usr/bin/loliwm
}
