# Maintainer: Magnus Therning <magnus@therning.org>
# Contributor: Thomas Pani <thomas.pani@gmail.com>

pkgname=ocaml-fileutils
pkgver=0.4.2
pkgrel=1
pkgdesc="A library to provide pure OCaml functions to manipulate real file and filename."
arch=('i686' 'x86_64')
url="http://forge.ocamlcore.org/projects/ocaml-fileutils"
license=('LGPL')
depends=('ocaml' 'ocaml-findlib')
changelog=$pkgname.changelog

source=(
    https://forge.ocamlcore.org/frs/download.php/462/ocaml-fileutils-${pkgver}.tar.gz
    "destdir.patch"
    )

build() {
    cd "$srcdir/$pkgname-$pkgver"

    patch -p3 < ${srcdir}/destdir.patch

    ./configure --prefix=/usr
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"

    install -d $pkgdir/$(ocamlfind printconf destdir)
    make DESTDIR="$pkgdir/" OCAMLFIND_DESTDIR="$pkgdir/$(ocamlfind printconf destdir)" install
}

sha256sums=('ec996a9dd4f374785f4bc99adc9a83936ed862715d6a679fb2ced90aa6ca38b0'
            'df8e40c9786cfa89c02f0a57ff760fe771e61e4666082d1ae89f992e8a6b8425')
