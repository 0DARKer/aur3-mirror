# Maintainer: jjacky
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>

_pkgname=xfwm4
pkgname=$_pkgname-hover-inactive
pkgver=4.8.2
pkgrel=1
pkgdesc="Xfce window manager w/ hover effect for inactive windows"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.xfce.org/"
groups=('xfce4')
depends=("libxfce4ui" 'libwnck' 'hicolor-icon-theme')
makedepends=('pkgconfig' 'intltool')
options=('!libtool')
provides=('xfwm4=4.8.2')
conflicts=('xfwm4')
install=${pkgname}.install
source=(http://archive.xfce.org/src/xfce/${_pkgname}/4.8/${_pkgname}-${pkgver}.tar.bz2
	hover-inactive.patch)
md5sums=('41b730d5abf39a69fc3b221728ddaa75'
         'fa2855007fa2a55eb55c3a626a366244')
sha1sums=('7822408eaddd9fc4937d9e6825eda7d56dc46b06'
          '1399b8036121ffe027bdcd726985bac5751df51f')

build() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  patch -p1 -i ../hover-inactive.patch
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--enable-startup-notification \
	--enable-randr \
	--enable-compositor \
	--enable-xsync \
	--disable-debug
  make
}

package() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
