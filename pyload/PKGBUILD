# Maintainer: RaNaN <ranan@pyload.org>
# Maintainer: onny <onny@project-insanity.org>
# Contributer: prophy <4prophy@gmail.com>
# Contributor: carstene1ns <carsten.1 gmx net>

pkgname='pyload'
pkgver=0.4.9
pkgrel=14
pkgdesc="Downloadtool for One-Click-Hoster written in python."
url="http://www.pyload.org"
license='GPL'
arch=('any')
conflicts=('pyload-hg' 'pyload-git')
replaces=('pyload-hg' 'pyload-git')
depends=('python2' 'python2-crypto' 'python2-pycurl' 'tesseract' 
         'python2-imaging' 'python-jinja' 'python2-beaker'
         'python2-pyopenssl')
optdepends=('python2-pyqt: Gui'
            'python2-flup: for additional webservers'
            'python2-notify: Notifications for GUI'
            'js: ClickNLoad')
source=(http://get.pyload.org/get/src/$pkgver
'http://bitbucket.org/ranan/pyload-dist/raw/bf705af8f412/debian/pyload/usr/share/applications/pyload-gui.desktop'
'http://bitbucket.org/ranan/pyload-dist/raw/bf705af8f412/debian/pyload/usr/share/applications/pyload.desktop'
'http://bitbucket.org/ranan/pyload-dist/raw/bf705af8f412/debian/pyload/usr/share/pixmaps/pyload-gui.png'
'http://bitbucket.org/ranan/pyload-dist/raw/bf705af8f412/debian/pyload/usr/share/pixmaps/pyload.svg'
'pyload.service'
'pyload.conf') 
install='pyload.install'

sha512sums=('6265602073279763c5393b958ec2cc6825d51e872556d2f2141db1e2afd9a2aaded8275aac0576526f96863d768bc9ff3669e7ff31e7b9fb5c5f655bba920a8e'
            '86eb27f042adaf7096dec6c50bfd8f2ca0d099cbb1a27a47af276a3f42b6aebc5807022fbd9f5ca543e329543fe5ab1c3ee873163b9d90b8b3242523082b7a0d'
            '3f5d237d8e8ca469fa138e37b2118154146526340d151e8e75c79630c6bd01baab5d21f912d97fdce62ef6ff2e7c4c479d69ba8b66d3f17dbd4adb2636eddb08'
            '07ed25d6b1b62f6589f350854ac8e864bcb94a8b497d00522c3e7481d6ec2a25ba1a072b947872c7b82ca4de03ac30ffd96811bfae64df2f5dc586e4960282c6'
            '96fb4952e2fe178e6f2c0a79213b5f3c82401b0edd386698fbddfe5002d9ec676ba71cfc2cbb9fd773749fb4c3173d445cef851635d8ee981536ba7946ec8bf6'
            '463bb10711d2f47d14d1495bd1e77fad7f9b57fbe8fee9759e5b61f400bc6854472922d663103ca4bfdaa355f2fbf7a23c9976b640f048221d0b150687db3d3e'
            '37b6e41bdd493c5652c484a4c772e3d70da197f724ad48901451049eda019698b923a561173162771bd06ac4cf6d9da162b085535684195bb57502b5205df424')
prepare() {
    cd $srcdir

    sed -i 's_#!/usr/bin/env python$_#!/usr/bin/env python2_' pyload/pyLoad*.py
}

package(){
    cd $srcdir

    install -d ${pkgdir}/opt/pyload
    cp -r pyload/* ${pkgdir}/opt/pyload

    install -d ${pkgdir}/usr/share/applications
    cp pyload.desktop ${pkgdir}/usr/share/applications
    cp pyload-gui.desktop ${pkgdir}/usr/share/applications

    install -d ${pkgdir}/usr/share/pixmaps
    cp pyload.svg ${pkgdir}/usr/share/pixmaps
    cp pyload-gui.png ${pkgdir}/usr/share/pixmaps

    install -d ${pkgdir}/usr/bin
    ln -s /opt/pyload/pyLoadCore.py ${pkgdir}/usr/bin/pyLoadCore
    ln -s /opt/pyload/pyLoadCli.py ${pkgdir}/usr/bin/pyLoadCli
    ln -s /opt/pyload/pyLoadGui.py ${pkgdir}/usr/bin/pyLoadGui

    # Create PyLoad service
    install -Dm 644 ${srcdir}/pyload.service ${pkgdir}/usr/lib/systemd/system/pyload.service
    install -Dm 644 ${srcdir}/pyload.conf ${pkgdir}/var/lib/pyload/pyload.conf
}

# vim:set ts=4 sw=4 et:
