# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=yubico-c-client-git
pkgver=20120616
pkgrel=1
pkgdesc="Yubico yubikey client C library"
arch=('i686' 'x86_64')
url="https://github.com/Yubico/yubico-c-client"
license=('BSD')
depends=('curl' 'libyubikey')
makedepends=('git')
provides=('ykclient' 'yubico-c-client')
conflicts=('ykclient' 'yubico-c-client')
source=('automake.patch')

_gitroot="git://github.com/Yubico/yubico-c-client.git"
_gitname="yubico-c-client"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  patch -Np1 < ${srcdir}/automake.patch

  autoreconf --install
  ./configure --prefix=/usr
  make
  make check
}

package() {
  cd "${srcdir}/${_gitname}-build"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  install -D -m644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
  make DESTDIR="${pkgdir}/" install
} 
sha256sums=('40bd149af1d00ad9427c03b2ebb411541c190810b02fc2853cee28eef1f01073')
