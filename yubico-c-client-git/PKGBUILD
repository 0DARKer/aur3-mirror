# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=yubico-c-client-git
pkgver=2.9.r46.g5a34ab5
pkgrel=1
pkgdesc="Yubico yubikey client C library"
arch=('i686' 'x86_64')
url="https://github.com/Yubico/yubico-c-client"
license=('BSD')
depends=('curl' 'libyubikey')
makedepends=('git' 'help2man')
provides=('ykclient' 'yubico-c-client')
conflicts=('ykclient' 'yubico-c-client')
source=('git://github.com/Yubico/yubico-c-client.git'
        'ykclient-man.patch')

pkgver() {
	cd yubico-c-client/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[_-+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

build() {
	cd yubico-c-client/

	patch -Np1 < ${srcdir}/ykclient-man.patch

	autoreconf --install
	./configure --prefix=/usr
	make
}

check() {
	cd yubico-c-client/

	make check
}

package() {
	cd yubico-c-client/
  
	install -D -m644 COPYING "${pkgdir}/usr/share/licenses/yubico-c-client/COPYING"
	install -D -m644 README "${pkgdir}/usr/share/doc/yubico-c-client/README"
	make DESTDIR="${pkgdir}/" install
} 

sha256sums=('SKIP'
            'abfb0199e2761824fb09f2c49c10bfc1d5ca248489f5477372070c38c29361dd')
