# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=yubico-c-client-git
pkgver=2.9.17.g3526fe7
pkgrel=1
pkgdesc="Yubico yubikey client C library"
arch=('i686' 'x86_64')
url="https://github.com/Yubico/yubico-c-client"
license=('BSD')
depends=('curl' 'libyubikey')
makedepends=('git')
provides=('ykclient' 'yubico-c-client')
conflicts=('ykclient' 'yubico-c-client')
source=('git://github.com/Yubico/yubico-c-client.git')

pkgver() {
	cd yubico-c-client/
	git describe --tags --long | sed 's|^[^0-9]*||;s|-|.|g'
}

build() {
	cd yubico-c-client/

	autoreconf --install
	./configure --prefix=/usr
	make
}

check() {
	cd yubico-c-client/

	make check
}

package() {
	cd yubico-c-client/
  
	install -D -m644 COPYING "${pkgdir}/usr/share/licenses/yubico-c-client/COPYING"
	install -D -m644 README "${pkgdir}/usr/share/doc/yubico-c-client/README"
	make DESTDIR="${pkgdir}/" install
} 

sha256sums=('SKIP')
