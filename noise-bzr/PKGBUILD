# Maintainer: Ner0

pkgname=noise-bzr
pkgver=1367
pkgrel=1
pkgdesc="The official audio player of elementary OS."
arch=('i686' 'x86_64')
url="https://launchpad.net/noise"
license=('GPL3')
depends=('desktop-file-utils' 'granite-bzr' 'gstreamer0.10-base' 'libgpod'
         'libindicate' 'libnotify' 'libpeas' 'libsoup' 'sqlheavy' 'taglib')
makedepends=('bzr' 'cmake' 'vala')
optdepends=('gstreamer0.10-plugins: Plugins for various audio formats')
provides=('noise')
conflicts=('noise')
install=noise.install
source=('noise::bzr+http://bazaar.launchpad.net/~elementary-apps/noise/trunk/')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/noise"
  bzr revno
}

build() {
  cd "$srcdir/noise"
  rm -rf build/
  mkdir build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF
  make
}

package() {
  cd "$srcdir/noise/build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install
}
