# Maintainer: Ner0

pkgname=noise-bzr
pkgver=683
pkgrel=1
pkgdesc="The official audio player of elementary OS."
arch=('i686' 'x86_64')
url="https://launchpad.net/noise"
license=('GPL3')
depends=('glib2' 'gtk3' 'gstreamer0.10' 'gstreamer0.10-base' 'taglib' 'sqlheavy-git'
         'libxml2' 'libnotify' 'libsoup' 'json-glib' 'granite-bzr' 'libgpod'
         'libpeas' 'desktop-file-utils' 'dconf' 'hicolor-icon-theme')
makedepends=('bzr' 'cmake' 'vala')
install=$pkgname.install

_bzrtrunk=lp:noise
_bzrmod=noise

build() {
  msg "Connecting to Bazaar server...."

  if [ -d $_bzrmod ]; then
    cd $_bzrmod && bzr pull $_bzrtrunk -r $pkgver && cd ..
    msg "The local files are updated."
  else
    bzr branch $_bzrtrunk $_bzrmod -r $pkgver
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  [ -d $_bzrmod-build ] && rm -rf $_bzrmod-build
  cp -r $_bzrmod $_bzrmod-build
  cd $_bzrmod-build
  sed -i 's/sqlheavy-0.1/sqlheavy-0.2/' CMakeLists.txt
  #sed -i 's|Mutex ()|Mutex (with --target-glib=2.32)|g' src/*.vala src/Devices/*.vala
  #sed -i 's|Mutex()|Mutex(with --target-glib=2.32)|g' src/*.vala src/Devices/*.vala
  [ -d build ] && rm -rf build
  mkdir build
  cd build
  CFLAGS="${CFLAGS} -Wno-deprecated-declarations"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF -DSQLHEAVY_USE_2=1
  make ${MAKEFLAGS}
}

package() {
  cd "$srcdir/$_bzrmod-build/build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install
}
