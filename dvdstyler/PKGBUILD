# $Id: PKGBUILD 57686 2011-11-01 09:55:36Z ebelanger $
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Previous Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>
# Maintainer: Mario J Steele <mario@ruby-im.net>

pkgname=dvdstyler
pkgver=2.2b1
pkgrel=1
pkgdesc="Cross-platform DVD authoring application"
arch=('i686' 'x86_64')
url="http://www.dvdstyler.de/"
license=('GPL')
depends=('cdrkit' 'dvdauthor' 'dvd+rw-tools' 'wxsvg' 'xine-ui' 'mjpegtools' 'libgnomeui')
makedepends=('zip' 'xmlto' 'docbook-xsl')
optdepends=('dvdisaster: ECC support')
options=('!makeflags')
source=(http://downloads.sourceforge.net/dvdstyler/DVDStyler-${pkgver}.tar.bz2)
sha1sums=('c38a3467509189cbfe32eac64e618d107c364d13')

build() {
  cd "${srcdir}/DVDStyler-${pkgver}"
  sed -i 's/rmdir "$$t"//' docs/Makefile.in
  sed -i 's/vsink_buffer.h/buffersink.h/' src/mediatrc_ffmpeg.cpp
  ./configure --prefix=/usr
  sed -i 's/libwxvilla.a/libwxvilla.a \/usr\/lib\/libjpeg.so.8/' src/Makefile
  # The author of the program did not include these, therfore fails compilation
  sed -i 's/MainWin.cpp/VideoFrameDlg.cpp MainWin.cpp/' src/Makefile
  sed -i 's/MainWin.$(OBJEXT)/VideoFrameDlg.$(OBJEXT) MainWin.$(OBJEXT)/' src/Makefile
  make
}

package() {
  cd "${srcdir}/DVDStyler-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
