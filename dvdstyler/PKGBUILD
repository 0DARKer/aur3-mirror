# Contributor: Alexander Fehr <pizzapunk gmail com>
# Previous Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>
# Previous Maintainer: Mario J Steele <mario@ruby-im.net>
# Maintainer: Miguel Revilla <yo@miguelrevilla.com>

pkgname=dvdstyler
pkgver=2.4.3
pkgrel=1
pkgdesc="Cross-platform DVD authoring application"
arch=('i686' 'x86_64')
url="http://www.dvdstyler.de/"
license=('GPL')
depends=('cdrkit' 'dvdauthor' 'dvd+rw-tools' 'zip' 'xmlto' 'wxgtk2.9' 'wxsvg>=1.1.14' 'xine-ui' 'mjpegtools' 'libgnomeui' 'libexif')
makedepends=('docbook-xsl' 'flex')
optdepends=('dvdisaster: ECC support')
options=('!makeflags')
source=(http://downloads.sourceforge.net/dvdstyler/${pkgver}/DVDStyler-${pkgver}.tar.bz2)
sha1sums=('41358eabbfc051daf4c724df94b9cd972cbd85f3')

build() {
  cd "${srcdir}/DVDStyler-${pkgver}"
  chmod 755 configure
  ./configure --prefix=/usr --with-wx-config=/usr/bin/wx-config-2.9
  sed -i 's/libwxvilla.a/libwxvilla.a \/usr\/lib\/libjpeg.so.8/' src/Makefile
  make
}

package() {
  cd "${srcdir}/DVDStyler-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m 644 -D ${pkgdir}/usr/share/doc/dvdstyler/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
