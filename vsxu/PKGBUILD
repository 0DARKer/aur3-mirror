# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=vsxu
pkgver=0.4.2
pkgrel=1
pkgdesc="A free to use program that lets you create and perform real-time audio visual presets."
arch=(i686 x86_64)
url="http://www.vsxu.com/"
license=('GPL' 'custom')
depends=('jack' 'glew' 'glfw2' 'opencv' 'libpng12' 'desktop-file-utils' 'xdg-utils')
makedepends=('cmake')
install="$pkgname.install"
source=("https://github.com/vovoid/vsxu/archive/$pkgver.tar.gz"
        vsxu-rtaudio-default-on-{alsa,jack}.patch)
md5sums=('bbd73438386aa3eede5c3b104d706104'
         'b5914ed1af3e4486ebc1ee1df1d9a30f'
         '64988fbc8bf4804d05a13ea6b14b4f86')

prepare() {
  cd $pkgname-$pkgver

  # use ALSA (or JACK) by default as rtaudio backend
  # (since PulseAudio segfaults when not present)
  #
  ## uncomment the desidered patch (NOT both)
  ## commont both patches for using PulseAudio

#  patch -p1 -i ../${source[1]} ## ALSA
  patch -p1 -i ../${source[2]} ## JACK
}

build() {
  cd $pkgname-$pkgver

  [[ -d b ]] || mkdir b && cd b
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DGLFW_LIBRARY=/usr/lib/libglfw2.so \
           -DPNG_LIBRARY=/usr/lib/libpng12.so \
           -DPNG_PNG_INCLUDE_DIR=/usr/include/libpng12
  make
}

package() {
  cd $pkgname-$pkgver/b
  make DESTDIR="$pkgdir/" install

  # license
  install -Dm644 ../COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
} 
