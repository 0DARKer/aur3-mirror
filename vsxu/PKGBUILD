# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=vsxu
pkgver=0.3.1
pkgrel=1
pkgdesc="A free to use program that lets you create and perform real-time audio visual presets."
arch=(i686 x86_64)
url="http://www.vsxu.com/"
license=('GPL' 'custom')
depends=('glew' 'glfw' 'opencv' 'libpng12' 'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('cmake')
install="$pkgname.install"
source=("https://github.com/vovoid/vsxu/tarball/$pkgver")
md5sums=('f0c3c6ee1a2e324e2bdbfa4799d96b42')

build() {
  cd "$srcdir/vovoid-$pkgname-"*

  # do not use /usr/lib64  
  sed -i "s/lib64/lib/g" `grep -rl lib64 .`

  # right libpng version is 1.2 and not 12
  find . -name cmake_packages.txt -exec sed -i 's/PNG 12/PNG 1.2.50/' {} \;

  [[ -d bld ]] || mkdir bld && cd bld
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DPNG_LIBRARY=/usr/lib/libpng.so.3 \
           -DPNG_PNG_INCLUDE_DIR=/usr/include/libpng12
  make
}

package() {
  cd "$srcdir/vovoid-$pkgname-"*/bld
  make DESTDIR="$pkgdir/" install

  # license
  install -Dm644 ../COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
} 
