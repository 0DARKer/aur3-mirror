# Maintainer: Piotr Rogoża <rogoza dot piotr at gmail dot com>
# Contributor: Piotr Rogoża <rogoza dot piotr at gmail dot com>
# vim:set ts=2 sw=2 et ft=sh tw=100: expandtab

_author=E/EL/ELLIOTJS
_perlmod=Perl-Critic-More
pkgname=perl-critic-more
pkgver=1.000
pkgrel=1
pkgdesc='Perl::Critic::More - Supplemental policies for Perl::Critic'
arch=('any')
url="http://search.cpan.org/~E/EL/ELLIOTJS/perl-critic-more/"
license=('GPL' 'PerlArtistic')
groups=()
depends=(
perl
)
makedepends=(
perl
)
optdepends=()
provides=(
perl-critic-policy-codelayout-requireascii
perl-critic-policy-editor-requireemacsfilevariables
perl-critic-policy-errorhandling-requireuseofexceptions
perl-critic-policy-modules-perlminimumversion
perl-critic-policy-modules-requireperlversion
perl-critic-policy-valuesandexpressions-restrictlongstrings
perl-critic-policy-miscellanea-requirercskeywords
)
conflicts=()
replaces=()
backup=()
options=(!emptydirs)
install=
source=("http://cpan.perl.org/modules/by-authors/id/$_author/$_perlmod-$pkgver.tar.gz"
http://cpansearch.perl.org/src/ELLIOTJS/Perl-Critic-1.116/lib/Perl/Critic/Policy/Miscellanea/RequireRcsKeywords.pm
)
noextract=()

build(){
  cd "$srcdir/$_perlmod-$pkgver"

  # Install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}
check(){
  cd "$srcdir/$_perlmod-$pkgver"
  make test
}
package(){
  cd "$srcdir/$_perlmod-$pkgver"
  make install DESTDIR="$pkgdir/"

  # Missing RequireRcsKeywords
  pod2man "$srcdir"/RequireRcsKeywords.pm \
    > "$pkgdir"/usr/share/man/man3/Perl::Critic::Policy::Miscellanea::RequireRcsKeywords.3pm
  install -Dm644 "$srcdir"/RequireRcsKeywords.pm \
    "$pkgdir"/usr/share/perl5/vendor_perl/Perl/Critic/Policy/Miscellanea/RequireRcsKeywords.pm

  # remove perllocal.pod and .packlist
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

md5sums=('b535e08346dfac2412e442f78b85c84a'
         '063f8fdceda40ec1a72276d404546035')
