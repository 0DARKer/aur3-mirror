_pkgname=sensorkinect
pkgname=sensorkinect-git
pkgver=20120103
pkgrel=1
pkgdesc="PrimeSensor Modules for OpenNI"
arch=('i686' 'x86_64')
url="https://github.com/avin2/SensorKinect"
license=('GPL')
depends=('openni-git')
makedepends=('git')
provides=($_pkgname)
conflicts=($_pkgname)
install=sensorkinect.install
source=()
noextract=()
md5sums=()

_gitroot="https://github.com/avin2/SensorKinect.git"
_gitname="sensor"
_gitbranch="unstable"
#_gitbranch="master"


if [ "${CARCH}" = "x86_64" ]; then
  _platform=x64
else
  _platform=x86
fi


build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone -b $_gitbranch $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd $srcdir/$_gitname-build/Platform/Linux/Build

  # BUILD
  make NI_CONF_DIR=/etc || return 1
}

package() {
  install -d -m755 ${pkgdir}/usr/{lib,bin}

  cd $srcdir/$_gitname-build/Platform/Linux/Bin/${_platform}-Release
  install XnSensorServer ${pkgdir}/usr/bin
  install libXnCore.so libXnDDK.so libXnDeviceFile.so libXnDeviceSensorV2KM.so libXnFormats.so ${pkgdir}/usr/lib
  chmod +s ${pkgdir}/usr/bin/XnSensorServer

  cd $srcdir/$_gitname-build/Platform/Linux/Install
  install -d -m755 ${pkgdir}/etc/udev/rules.d
  sed -i 's/SYSFS/ATTR/g' 55-primesense-usb.rules
  install 55-primesense-usb.rules ${pkgdir}/etc/udev/rules.d

  cd $srcdir/$_gitname-build/Data
  install -d -m755 ${pkgdir}/etc/primesense
  install GlobalDefaultsKinect.ini ${pkgdir}/etc/primesense

  install -d -m777 ${pkgdir}/var/log/primesense/XnSensorServer 
}