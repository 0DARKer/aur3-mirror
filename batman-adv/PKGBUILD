# Maintainer: Thomas Wei√üschuh <thomas t-8ch de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=batman-adv
pkgver=2014.1.0
pkgrel=2
pkgdesc='batman kernel module'
arch=('i686' 'x86_64')
url='http://www.open-mesh.net/'
license=('GPL')
install='batman-adv.install'
source=("http://downloads.open-mesh.org/batman/releases/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz"
        '0001-batman-adv-call-unregister_netdev-to-have-it-handle-.patch'
        'batman-adv.install')
depends=('linux')
makedepends=('linux-headers')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # http://www.open-mesh.org/issues/183
  patch < "${srcdir}/0001-batman-adv-call-unregister_netdev-to-have-it-handle-.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make KERNELPATH=/usr/lib/modules/$(uname -r)/build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -D -m644 batman-adv.ko "${pkgdir}/usr/lib/modules/$(uname -r)/updates/net/batman-adv/batman_adv.ko"
  install -D -m644 README "${pkgdir}/usr/share/doc/batman-adv/README"
}

sha256sums=('f2aa26158da12d95952be308d09d8338e0e771c1fcf23a7a2c7e1fa5a2dc2e6d'
            '940c5e16e9576fd489671a73f8597a4d2b88214e8193db1c65b79cf327f1b443'
            '347599c02426a905690002885c277f91b82da2b29d3372348e5f02d03c435c37')
