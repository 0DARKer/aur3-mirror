# Maintainer: boenki <boenki at gmx dot de>
# Contributor:Josip <josipponjavic at gmail dot com>

pkgname=breathe-icon-theme
pkgver=0.51
pkgrel=1
pkgdesc="A set of icons mixing KDEs "Oxygen" with Ubuntus "Human" set"
arch=('any')
url="https://wiki.ubuntu.com/Artwork/BreatheIconSet"
license=('CCPL:by-sa' 'custom:TRADEMARKS')
conflicts=('gnome-icons-breathe')
makedepends=('librsvg')
options=(!strip !zipman)
source=(http://launchpad.net/breathe-icon-set/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz
        archlinux-icon.svg
        TRADEMARKS)
md5sums=('1654eeb36246adf2556b84595bfb6e07'
         '34c9f7d5774e93f4a6cbf1ec818fb763'
         '05066419eb8239652a0467131485db4d')

package() {
  cd ${srcdir}
  install -d "${pkgdir}/usr/share/icons/breathe"
  cp -r ${pkgname}-${pkgver}/* "${pkgdir}/usr/share/icons/breathe"

  # Replace ubuntu logo with Arch Linux's
  for size in 16 22 24 32 48; do
   rsvg -w ${size} -h ${size} archlinux-icon.svg \
    "${pkgdir}/usr/share/icons/breathe/${size}x${size}/places/start-here.png"
  done
  install -D archlinux-icon.svg \
   "${pkgdir}/usr/share/icons/breathe/scalable/places/start-here.svg"

  install -D TRADEMARKS "${pkgdir}/usr/share/licenses/${pkgname}/TRADEMARKS"
  find ${pkgdir} ! -type d -exec chmod 644 {} +
}
