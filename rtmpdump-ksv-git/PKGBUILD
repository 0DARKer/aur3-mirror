pkgname=rtmpdump-ksv-git
_gitname=rtmpdump-ksv
pkgver=r497.550a0ca
pkgrel=1
pkgdesc="A version of rtmpdump-git including numerous patches released by KSV. Forum thread: http://stream-recorder.com/forum/-t16103.html"
arch=('i686' 'x86_64')
url="http://rtmpdump.mplayerhq.hu/"
license=('GPL2' 'LGPL2.1')
depends=('openssl')
makedepends=('git')
conflicts=('rtmpdump' 'rtmpdump-svn' 'rtmpdump-git' 'rtmpdump-ksv')
provides=('rtmpdump')
source=('git://github.com/BurntSushi/rtmpdump-ksv#branch=ksv')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_gitname"
  make
}

package() {
  cd "$srcdir/$_gitname"
  install -d -m755 "${pkgdir}/usr/lib"
  make prefix=/usr sbindir=/usr/bin \
    mandir=/usr/share/man DESTDIR="$pkgdir" \
    install
}
