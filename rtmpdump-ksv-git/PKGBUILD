# Maintainer: zepar <fezek666@gmail.com>
# Contributors: Army, tomegun, Christopher Rosell, UnCO, burntsushi
# KSV_patches: http://stream-recorder.com/forum/-t16103.html
_pkgname=rtmpdump
pkgname=$_pkgname-ksv-git
pkgver=0.499.a107cef
pkgrel=1
pkgdesc="A tool to download rtmp:// and rtmpe:// streams including patches released by KSV"
url="http://rtmpdump.mplayerhq.hu/"
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2.1')
depends=('openssl')
makedepends=('git' 'zlib')
provides=("$_pkgname")
conflicts=('rtmpdump' 'rtmpdump-svn' 'rtmpdump-git' 'rtmpdump-ksv')
source=("$_pkgname::git+git://git.ffmpeg.org/rtmpdump" Patch.diff)
md5sums=('SKIP' '3424259af28a27855afe5dfb82f104de')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo "0.$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
  cd "$srcdir/$_pkgname"
  patch -p0 -i $srcdir/Patch.diff
  make
}

package() {
  cd "$srcdir/$_pkgname"
  install -d -m755 "${pkgdir}/usr/lib"
  make prefix=/usr sbindir=/usr/bin \
    mandir=/usr/share/man DESTDIR="$pkgdir" \
    install
}
