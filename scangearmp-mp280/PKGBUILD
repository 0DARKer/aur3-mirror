pkgname=scangearmp-mp280
pkgver=1.60
pkgrel=1
_pkgver=1.60-1
pkgdesc="Canon Scanner Driver (MP280)"
url="http://support-my.canon-asia.com/contents/MY/EN/0100303302.html"
arch=('i686' 'x86_64')
license=('custom')
depends=('sane')
source=('http://gdlp01.c-wss.com/gds/3/0100003033/01/scangearmp-source-1.60-1.tar.gz'
	'scanfile.patch')
md5sums=('15782d670f9d5c5904e00610508114f3'
	 '9b66e8d9b12034eefba9e9eb19bce6e9')

build() {
  if [ "$CARCH" == "x86_64" ]; then  
    libdir=libs_bin64
  else
    libdir=libs_bin32
  fi

  cd ${srcdir}/scangearmp-source-${_pkgver}/scangearmp/src/
  patch -p0 -i $srcdir/scanfile.patch

  cd ${srcdir}/scangearmp-source-${_pkgver}/scangearmp/

  ./autogen.sh --prefix=/usr --enable-libpath=/usr/lib LDFLAGS="-lm -L`pwd`/../com/${libdir}"
  # Force the use of system's libtool
  rm -f libtool
  ln -s `which libtool` .
  # Build package
  make clean || return 1
  make || return 1
  # Install package
  install -d -m 0755 $pkgdir/usr/lib/bjlib
  make DESTDIR=${pkgdir} install || return 1

  # Install SANE configuration file
  install -d -m 0755 $pkgdir/etc/sane.d/
  install -m 0644 ${srcdir}/scangearmp-source-${_pkgver}/scangearmp/backend/canon_mfp.conf $pkgdir/etc/sane.d/canon_mfp.conf
 
  # Install common libraries
  install -m 0755 ${srcdir}/scangearmp-source-${_pkgver}/com/${libdir}/libcncpcmcm.so.8.0.1 ${pkgdir}/usr/lib/
  install -m 0755 ${srcdir}/scangearmp-source-${_pkgver}/com/${libdir}/libcncpmsimg.so.1.0.2 ${pkgdir}/usr/lib/
  install -m 0755 ${srcdir}/scangearmp-source-${_pkgver}/com/${libdir}/libcncpmslld.so.1.0.1 ${pkgdir}/usr/lib/
  install -m 0755 ${srcdir}/scangearmp-source-${_pkgver}/com/${libdir}/libcncpmsui.so.1.6.0 ${pkgdir}/usr/lib/
  install -m 0755 ${srcdir}/scangearmp-source-${_pkgver}/com/${libdir}/libcncpnet.so.1.2.0 ${pkgdir}/usr/lib/
  
  # Install MP280 series specific libraries
  install -m 0755 ${srcdir}/scangearmp-source-${_pkgver}/370/${libdir}/libcncpmsimg370.so.1.6.0 ${pkgdir}/usr/lib/
  install -m 0755 ${srcdir}/scangearmp-source-${_pkgver}/370/${libdir}/libcncpmslld370c.so.1.04.1 ${pkgdir}/usr/lib/
  install -m 0755 ${srcdir}/scangearmp-source-${_pkgver}/370/${libdir}/libcncpmslld370.so.1.6.0 ${pkgdir}/usr/lib/
  
  # Create symbolic links
  cd ${pkgdir}/usr/lib/
  ln -s libcncpcmcm.so.8.0.1 libcncpcmcm.so
  ln -s libcncpmsimg.so.1.0.2 libcncpmsimg.so
  ln -s libcncpmslld.so.1.0.1 libcncpmslld.so
  ln -s libcncpmsui.so.1.6.0 libcncpmsui.so
  ln -s libcncpnet.so.1.2.0 libcncpnet.so
  
  ln -s libcncpmsimg370.so.1.6.0 libcncpmsimg370.so
  ln -s libcncpmslld370c.so.1.04.1 libcncpmslld370c.so
  ln -s libcncpmslld370.so.1.6.0 libcncpmslld370.so
  
  # Make scangearmp usable from gimp
  install -d -m 0755 ${pkgdir}/usr/lib/gimp/2.0/plug-ins/
  ln -s /usr/bin/scangearmp ${pkgdir}/usr/lib/gimp/2.0/plug-ins/

  # Install .tbl and .dat files for MP280 series
  install -D -m 0755 ${srcdir}/scangearmp-source-${_pkgver}/370/cnc1746d.tbl ${pkgdir}/usr/lib/bjlib/
  install -D -m 0755 ${srcdir}/scangearmp-source-${_pkgver}/370/cnc_3700.tbl ${pkgdir}/usr/lib/bjlib/
  install -D -m 0755 ${srcdir}/scangearmp-source-${_pkgver}/370/CNC280.DAT ${pkgdir}/usr/lib/bjlib/
  install -D -m 0755 ${srcdir}/scangearmp-source-${_pkgver}/370/CNC280P.DAT ${pkgdir}/usr/lib/bjlib/

  # Install udev rules
  install -D -m 0644 ${srcdir}/scangearmp-source-${_pkgver}/scangearmp/etc/80-canon_mfp.rules ${pkgdir}/etc/udev/rules.d/80-canon_mfp.rules
  
  # Install .ini file
  install -D -m 0666 ${srcdir}/scangearmp-source-${_pkgver}/com/ini/canon_mfp_net.ini ${pkgdir}/usr/lib/bjlib/

  # Install license file
  cd ${srcdir}/scangearmp-source-${_pkgver}
  install -D LICENSE-scangearmp-${pkgver}EN.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-scangearmp-${pkgver}EN.txt

  # Remove unneeded files
  rm ${pkgdir}/usr/lib/libsane-canon_mfp.a
  rm ${pkgdir}/usr/lib/libsane-canon_mfp.la
}
