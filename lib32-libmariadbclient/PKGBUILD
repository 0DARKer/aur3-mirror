# $Id: PKGBUILD 186282 2013-05-23 19:15:33Z bpiotrowski $
# Maintainer: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>

_pkgbase=mariadb
pkgname='lib32-libmariadbclient'
pkgdesc='MariaDB client libraries (32 bit)'
pkgver=5.5.31
pkgrel=1
arch=('x86_64')
license=('GPL')
url='http://mariadb.org/'
makedepends=('cmake' 'lib32-libaio' 'lib32-libevent' 'lib32-openssl' 'lib32-zlib')
depends=('lib32-openssl')
conflicts=('lib32-libmysqlclient')
provides=("lib32-libmysqlclient=$pkgver")
options=('!libtool')
source=(http://mirrors.supportex.net/$_pkgbase/$_pkgbase-$pkgver/kvm-tarbake-jaunty-x86/$_pkgbase-$pkgver.tar.gz)
sha256sums=('70b28e5dca7e9213fd092a669f74a537e7c88e0f670e32628907ce3ebcef2d28')

build() {
  cd $srcdir
  mkdir -p build
  cd build

  cmake ../$_pkgbase-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc/mysql \
    -DMYSQL_DATADIR=/var/lib/mysql \
    -DMYSQL_UNIX_ADDR=/run/mysqld/mysqld.sock \
    -DDEFAULT_CHARSET=utf8 \
    -DDEFAULT_COLLATION=utf8_general_ci \
    -DENABLED_LOCAL_INFILE=ON \
    -DINSTALL_INFODIR=share/mysql/docs \
    -DINSTALL_MANDIR=share/man \
    -DINSTALL_LIBDIR=lib32 \
    -DINSTALL_PLUGINDIR=/usr/lib32/mysql/plugin \
    -DINSTALL_SCRIPTDIR=bin \
    -DINSTALL_INCLUDEDIR=include/mysql \
    -DINSTALL_DOCREADMEDIR=share/mysql \
    -DINSTALL_SUPPORTFILESDIR=share/mysql \
    -DINSTALL_MYSQLSHAREDIR=share/mysql \
    -DINSTALL_DOCDIR=share/mysql/docs \
    -DINSTALL_SHAREDIR=share/mysql \
    -DWITH_READLINE=ON \
    -DWITH_ZLIB=system \
    -DWITH_SSL=system \
    -DWITH_LIBWRAP=OFF \
    -DWITH_EXTRA_CHARSETS=complex \
    -DWITH_EMBEDDED_SERVER=ON \
    -DWITH_ARCHIVE_STORAGE_ENGINE=1 \
    -DWITH_BLACKHOLE_STORAGE_ENGINE=1 \
    -DWITH_INNOBASE_STORAGE_ENGINE=1 \
    -DWITH_PARTITION_STORAGE_ENGINE=1 \
    -DWITHOUT_EXAMPLE_STORAGE_ENGINE=1 \
    -DWITHOUT_FEDERATED_STORAGE_ENGINE=1 \
    -DWITHOUT_PBXT_STORAGE_ENGINE=1 \
    -DCMAKE_C_FLAGS="-m32 -fPIC ${CFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -fomit-frame-pointer" \
    -DCMAKE_CXX_FLAGS="-m32 -fPIC ${CXXFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -felide-constructors -fno-rtti" \
    -DWITH_MYSQLD_LDFLAGS="${LDFLAGS}"


  make
}

package() {
  cd "$srcdir"/build
  for dir in libmysql libmysqld libservices; do
    make -C $dir DESTDIR="$pkgdir" install
  done
}

