# $Id: PKGBUILD 58551 2009-11-08 22:37:12Z eric $
# Maintainer: dorphell <dorphell@archlinux.org>
# Maintainer: Travis Willard <travis@archlinux.org>
# Maintainer: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=libpng12
_realname=libpng
pkgver=1.2.45
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('i686' 'x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('zlib')
options=('!libtool')
source=("http://sourceforge.net/projects/libpng/files/libpng12/1.2.45/libpng-${pkgver}.tar.xz"
        "http://littlesvr.ca/apng/diff/libpng-1.2.44-apng.patch")

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # Add animated PNG (apng) support
  # see http://hp.vector.co.jp/authors/VA013651/freeSoftware/apng.html
  patch -Np0 -i "${srcdir}/libpng-1.2.44-apng.patch"

  libtoolize --force --copy
  aclocal
  autoconf
  automake --add-missing

  ./configure --prefix=/usr

  sed 's|$(srcdir)/png.h $(srcdir)/$@|$(srcdir)/png.h -o $(srcdir)/$@|' -i Makefile

  make ECHO=echo
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  make ECHO=echo DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/usr/share"
  rm -rf "${pkgdir}/usr/bin/libpng-config"
  rm -rf "${pkgdir}/usr/lib/"{libpng.so,libpng.a}
  rm -fr "${pkgdir}/usr/lib/pkgconfig/libpng.pc"
  rm -rf "${pkgdir}/usr/include/"{pngconf.h,png.h}
}
md5sums=('098ab673ab74015978db98667febc4a9'
         '8cb91087fd91000d2e35823587a2e7ef')
md5sums=('330fec55329b204c7e4b48faac442db2'
         '8cb91087fd91000d2e35823587a2e7ef')
