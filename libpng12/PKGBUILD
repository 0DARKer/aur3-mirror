# $Id: PKGBUILD 58551 2009-11-08 22:37:12Z eric $
# Maintainer: dorphell <dorphell@archlinux.org>
# Maintainer: Travis Willard <travis@archlinux.org>
# Maintainer: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=libpng12
_realname=libpng
pkgver=1.2.44
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('i686' 'x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('zlib')
options=('!libtool')
source=("ftp://ftp.simplesystems.org/pub/libpng/png/src/libpng-${pkgver}.tar.xz"
        "http://littlesvr.ca/apng/diff/libpng-${pkgver}-apng.patch")

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # Add animated PNG (apng) support
  # see http://hp.vector.co.jp/authors/VA013651/freeSoftware/apng.html
  patch -Np0 -i "${srcdir}/libpng-1.2.44-apng.patch"

  libtoolize --force --copy
  aclocal
  autoconf
  automake --add-missing

  ./configure --prefix=/usr
  make ECHO=echo
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  make ECHO=echo DESTDIR="${pkgdir}" install

  rm -rf ${pkgdir}/usr/lib/{libpng.so,libpng.a,pkgconfig}
  rm -rf ${pkgdir}/usr/{include,share,bin}
}
md5sums=('098ab673ab74015978db98667febc4a9'
         '8cb91087fd91000d2e35823587a2e7ef')
