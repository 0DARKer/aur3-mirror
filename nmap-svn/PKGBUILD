# Author: Ermenegildo Fiorito <fiorito.g@gmail.com>
# Maintainer: Rickey Visinski <rickeyvisinski at gmail.com>

pkgname=nmap-svn
pkgver=20130417.30732
pkgrel=1
pkgdesc="A network exploration tool and security/port scanner"
arch=('i686' 'x86_64')
url="http://nmap.org"
license=('custom:GPL')
depends=('pcre' 'openssl' 'libpcap>=1.0.0' 'lua' 'libnl')
# python is needed for zenmap
makedepends=('python2' 'subversion')
optdepends=('pygtk: gui for nmap')
options=('!makeflags')

_svntrunk='svn+https://guest:@svn.nmap.org/nmap/'
_svnmod=nmap
provides=("${_svnmod}")
conflicts=("${_svnmod}")
source=($_svntrunk)
md5sums=('SKIP')

pkgver() {
	cd "${SRCDEST}/${_svnmod}"
	echo $(date +%F | tr -d -).$(svnversion)
}

build() {
  cd $_svnmod
  echo "TryExec=/usr/bin/pygtk-demo" >> zenmap/install_scripts/unix/zenmap.desktop || return 1
  echo "TryExec=/usr/bin/pygtk-demo" >> zenmap/install_scripts/unix/zenmap-root.desktop || return 1
  sed -i 's# @LIBS@# @LIBS@ -lnl-3#' ncat/Makefile.in

  ./configure --prefix=/usr --mandir=/usr/share/man PYTHON="python2" \
    --libexecdir=/usr/lib --with-openssl=/usr
  make -j2
}

package() {
  cd $_svnmod
  make DESTDIR="${pkgdir}" install

  # remove zenmap uninstall script
  rm "${pkgdir}/usr/bin/uninstall_zenmap"

  # install custom GPL2 license
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

