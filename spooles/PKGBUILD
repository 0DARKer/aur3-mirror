# Maintainer: Giuseppe Borzi <gborzi___AT___ieee___DOT___org>
# Contributor: Alberto Penaforte <apenafor@gmail.com>

pkgname=spooles
pkgver=2.2
pkgrel=4
pkgdesc="SParse Object Oriented Linear Equations Solver"
arch=('i686' 'x86_64')
license=('custom')
url="http://netlib.sandia.gov/linalg/spooles"
license=('GPL')
depends=('glibc')
makedepends=('perl' 'ghostscript')
source=($url/${pkgname}.${pkgver}.tgz $url/ReferenceManual.ps.gz
        spooles-2.2_I2Ohash-64bit.patch spooles-2.2_makefiles.patch
        spooles-2.2_MT.patch spooles-2.2_shared-libs.patch license.txt)
md5sums=('5789ca60d1ae565a4eaef6d03ca837af' '9e5e32828f59c4cf066fdb34218705e7'
         '19ae3d1626961a2f857df3864b2f0efd' '2b1eece89be3793027f8ce489835d4b8'
         '656aa81e40ada53ae800be45fb7407e0' 'beffc31213f75b6b1aaa439158396bb8'
         '0f6276a9728bcf7ab0a31350d9a906c2')
noextract=("${pkgname}.${pkgver}.tgz" "ReferenceManual.ps.gz")

build() {
  mkdir ${srcdir}/$pkgname
  cd ${srcdir}/$pkgname
  tar -xzf ${srcdir}/${pkgname}.${pkgver}.tgz
  patch -Np1 -i ${srcdir}/spooles-2.2_makefiles.patch
  patch -Np1 -i ${srcdir}/spooles-2.2_shared-libs.patch
  patch -Np1 -i ${srcdir}/spooles-2.2_I2Ohash-64bit.patch
  patch -Np0 -i ${srcdir}/spooles-2.2_MT.patch
  sed -i -e "s|-lm|-lpthread -lm|" makefile
  make CFLAGS="$CFLAGS" lib
  zcat "$srcdir/ReferenceManual.ps.gz" |ps2pdf - ReferenceManual.pdf
}

package() {
  cd ${srcdir}/$pkgname
  mkdir -p ${pkgdir}/usr/lib
  mkdir -p ${pkgdir}/usr/include/spooles
  cp libspooles.a libspooles.so.2.2 ${pkgdir}/usr/lib/
  ln -s libspooles.so.2.2 ${pkgdir}/usr/lib/libspooles.so.2
  ln -s libspooles.so.2 ${pkgdir}/usr/lib/libspooles.so
  for h in *.h; do
    if [ $h != 'MPI.h' ]; then
       cp $h ${pkgdir}/usr/include/spooles
       d=`basename $h .h`
       if [ -d $d ]; then
          mkdir ${pkgdir}/usr/include/spooles/$d
          cp $d/*.h ${pkgdir}/usr/include/spooles/$d
       fi
    fi
  done

  # Fix permissions
  cd ${pkgdir}/usr/include/spooles
  chmod -R oug+r *
  cd ${pkgdir}/usr/lib
  chmod oug+r *
  install -Dm644 "$srcdir/$pkgname/ReferenceManual.pdf" $pkgdir/usr/share/doc/$pkgname/ReferenceManual.pdf
  install -Dm644 $srcdir/license.txt $pkgdir/usr/share/licenses/$pkgname/license.txt
}
