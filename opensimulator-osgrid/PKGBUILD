# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: LTSmash <lord.ltsmash@gmail.com>
# Contributor: Zauber Exonar <zauberexonar at gmail>
# Maintainer: GordonGR <gordongr@freemail.gr>

pkgname=opensimulator-osgrid
pkgver=04232012.v0.7.4.fd27988
pkgrel=1
pkgdesc="OSGrid's distribution of OpenSimulator, preconfigured to connect with OSGrid"
arch=('i686' 'x86_64')
url="http://www.osgrid.org"
license=('BSD')
depends=('mono' 'sqlite3' 'mysql')
makedepends=('unzip')
install=$pkgname.install
source=("http://osgrid.googlecode.com/files/osgrid.opensim-$pkgver.zip" \
	"osgrid.sh" \
	"OpenSim.ini")
md5sums=('155c339a1acfcd19c04f49d1e210107f'
         '8ad5aab5c434aef00d12b08343a5da5a'
         'c01cc56fe1ee5fa1aec92c0a66fadfce')
package() {
  cd $srcdir/bin

  cp ../OpenSim.ini OpenSim.ini
  cp OpenSim.ini OpenSim.ini.example

  #copying Mono.Posix.dll so that OpenSimulator can use sockets, and by extension MySQL
  cp /usr/lib/mono/2.0/Mono.Posix.dll Mono.Posix.dll
  # install
  install -d $pkgdir/opt/$pkgname/bin
  cp -r ./* $pkgdir/opt/$pkgname/bin/
  # set permissions
  find $pkgdir/opt/$pkgname/bin -type d -exec chmod 755 {} +
  find $pkgdir/opt/$pkgname/bin -type f -exec chmod 644 {} +
  find $pkgdir/opt/$pkgname/bin -name "*.exe" -exec chmod 755 {} +
  find $pkgdir/opt/$pkgname/bin -name "*.ini" -exec chmod 666 {} +
  find $pkgdir/opt/$pkgname/bin -name "*.xml" -exec chmod 666 {} +
  chmod 777 $pkgdir/opt/$pkgname/bin/{,*/}
  chmod 755 $pkgdir/opt/$pkgname/bin/opensim-ode.sh
  # chmod 666 "$pkgdir"/opt/$pkgname/bin/OpenSim.log
  # install launch script
  install -m755 -D ../osgrid.sh $pkgdir/usr/bin/osgrid
}
