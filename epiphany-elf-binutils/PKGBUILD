# Maintainer: Jonathan Scott Tinkham <sctincman@gmail.com>
# Adapted from arm-none-eabi-binutils by Anatol Pomozov <anatol.pomozov@gmail.com>

_target=epiphany-elf
pkgname=$_target-binutils
_pkgname=epiphany-binutils
pkgver=2.23
pkgrel=1
pkgdesc='A set of programs to assemble and manipulate binary and object files for the Adapteva Epiphany target'
arch=(i686 x86_64)
url='http://www.adapteva.com/'
license=(GPL)
depends=(zlib)
source=("https://github.com/adapteva/$_pkgname/archive/$_pkgname-$pkgver.zip")
md5sums=('a306b4bd7039315444921ddec2c34614')

build() {
  cd $_pkgname-$_pkgname-$pkgver

  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  ./configure --target=$_target \
              --prefix=/usr \
              --enable-multilib \
              --enable-interwork \
              --with-gnu-as \
              --with-gnu-ld \
              --disable-nls \
	      --disable-werror

  make configure-host
  make
}

check() {
  cd $_pkgname-$_pkgname-$pkgver
  
  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  make LDFLAGS="" -k check
}

package() {
  cd $_pkgname-$_pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  # Remove file conflicting with host binutils and manpages for MS Windows tools
  rm "$pkgdir"/usr/share/man/man1/${_target}-{dlltool,nlmconv,windres,windmc}*

  # Remove info documents that conflict with host version
  rm -rf "$pkgdir"/usr/share/info
}
