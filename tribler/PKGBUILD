# Maintainer: RunningDroid <runningdroid AT zoho.com>
# Contributor: Alessio Bolognino <themolok at gmail dot com>
# Contributor: Simone Esposito <webmaster at freebnc dot net>
# Contributor: Jeremiah Dodds <jeremiah dot dodds at gmail dot com>

pkgname=tribler
pkgver=6.3.4
_mootools_ver=1.5.1
pkgrel=2
pkgdesc="A 4th generation file sharing system bittorrent client"
arch=('any')
url="http://www.tribler.org"
license=('LGPL2.1')
install=install
depends=('python2' 'wxpython2.8'
        'python2-m2crypto' 'python2-apsw'
        'python2-netifaces' "libswift>=${pkgver}"
        'gconf' 'libtorrent-rasterbar'
        'python2-feedparser' 'python2-twisted'
        'python2-pyasn1' 'python2-requests'
        'python2-pillow')
optdepends=('vlc: watch videos in tribler'
            'python2-cherrypy: use the tribler webUI'
            'desktop-file-utils')
source=("https://github.com/Tribler/tribler/releases/download/v${pkgver}/tribler_${pkgver}_all.deb"
        "mootools.js::http://mootools.net/download/get/mootools-core-${_mootools_ver}-full-nocompat.js"
        "imgs.tar.gz")
sha1sums=('76b37c823e5db737736096f9d2de6aee60d82fe5'
          '8ea80dbde71d3fec342699994efcef15e0ed1619'
          '6fcf5fcbd69e87a2bcc28a17b7fd77a58cc6ea87')

package() {
  cd "$srcdir"

  msg2 "Extracting tribler"
  tar -C "$pkgdir" -xJf data.tar.xz

  cd "$pkgdir"

  # replace pngs that have incorrect sRGB profiles
  # upstream issue #180
  cp "$srcdir"/imgs/* "$pkgdir"/usr/share/tribler/Tribler/Main/vwxGUI/images/

  # change the path to swift because it's a separate package
  sed -i -e "s|swiftbinpath\ =\ os.path.join(self.sconfig.get_install_dir(),\"swift\")|swiftbinpath\ =\ \"\/usr\/bin\/swift\"|g" usr/share/tribler/Tribler/Main/tribler.py

  msg2 "Changing python to python2"
  sed -i -e "s|^exec\ python|exec\ python2|g" -e "s/Ubuntu/Arch/" usr/bin/$pkgname
  # get everything in usr/share/tribler as well, just to be sure
  find . -iname '*.py' -exec sed -i -e "s|^#!/usr/bin/python|#!/usr/bin/python2|" -e "s|^#!/usr/bin/env\ python|#!/usr/bin/python2|" '{}' +

  # this is a (minor) bugfix
  # line 49 should grab all the "#!/usr/bin/env" lines, but two have a typo
  sed -i -e "s|^#!\ /usr/bin/env\ python|#!/usr/bin/python2|" -e "s|^#!\ /usr/bin/python|#!/usr/bin/python2|" "usr/share/tribler/Tribler/Core/DecentralizedTracking/pymdht/run_pymdht_node.py" usr/share/tribler/Tribler/vlc.py

  msg2 "Making tribler work with wxpython2.8"
  # make Tribler use wxpython2.8
  find . -iname '*.py' -exec sed -i -e "s|^import wx|import wxversion\nwxversion.select('2.8')\nimport wx|" '{}' +

  # replace the symlink to the file (which doesn't exist on Arch) with the actual file
  rm usr/share/tribler/Tribler/Main/webUI/static/mootools.js
  cp "$srcdir"/mootools.js usr/share/tribler/Tribler/Main/webUI/static/

  # this file appears to be run by apt when python is updated, but we don't do that
  rm -rf usr/share/python

  # delete files for Debian/Ubuntu
  rm -r usr/share/doc/tribler/{README.Debian,copyright,readme.txt.gz}

  # make tribler use pillow instead of looking for PIL
  sed -i -re 's/^import Image/import PIL.Image/' usr/share/tribler/Tribler/Core/Video/VideoUtility.py

}
