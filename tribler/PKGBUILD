# Maintainer: RunningDroid <rningdrd at tormail dot com>
# Contributor: Alessio Bolognino <themolok at gmail dot com>
# Contributor: Simone Esposito <webmaster at freebnc dot net>
# Contributor: Jeremiah Dodds <jeremiah dot dodds at gmail dot com>

pkgname=tribler
pkgver=6.0.4
pkgrel=3
pkgdesc="A 4th generation file sharing system bittorrent client"
arch=('any')
url="http://www.tribler.org"
license=('LGPL2.1')
depends=('python2' 'wxpython' 'python2-m2crypto' 'python2-apsw' 'python2-netifaces' 'libswift' 'gconf')
optdepends=('vlc: watch videos in tribler'
            'python2-feedparser: download torrents listed in a rss or atom feed')
source=("http://dl.tribler.org/tribler_${pkgver}_all.deb")
md5sums=('abf7465d37dafc65e57816488d8d6770')

package() {
  cd "$srcdir"

  ar vx tribler_${pkgver}_all.deb
  tar -C "$pkgdir" -xvzf data.tar.gz
  rm -r "$pkgdir"/usr/share/tribler/Tribler/SwiftEngine

  cd "$pkgdir"

  # change the path to swift because it's a separate package
  sed -i -e "s|swiftbinpath\ =\ os.path.join(self.sconfig.get_install_dir(),\"swift\")|swiftbinpath\ =\ \"\/usr\/bin\/swift\"|g" usr/share/tribler/Tribler/Main/tribler.py

  sed -i -e "s|^exec\ python|exec\ python2|g" -e "s/Ubuntu/Arch/" usr/bin/$pkgname
  # get everything in usr/share/tribler as well, just to be sure
  sed -i -e "s|^#!/usr/bin/python|#!/usr/bin/python2|" -e "s|^#!/usr/bin/env\ python|#!/usr/bin/python2|" usr/share/tribler/Tribler/{Main,Tools,Debug,dispersy/tool}/*.py usr/share/tribler/Tribler/Main/Utility/Feeds/*.py
  # the previous sed line should grab all the "#!/usr/bin/env" lines, but one has a typo
  sed -i -e "s|^#!\ /usr/bin/env\ python|#!/usr/bin/python2|" "usr/share/tribler/Tribler/Core/DecentralizedTracking/pymdht/run_pymdht_node.py"
}
