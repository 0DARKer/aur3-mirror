# Maintainer: RunningDroid <rningdrd at tormail dot com>
# Contributor: Alessio Bolognino <themolok at gmail dot com>
# Contributor: Simone Esposito <webmaster at freebnc dot net>
# Contributor: Jeremiah Dodds <jeremiah dot dodds at gmail dot com>

pkgname=tribler
pkgver=6.1
_mootools_ver=1.4.5
pkgrel=2
pkgdesc="A 4th generation file sharing system bittorrent client"
arch=('any')
url="http://www.tribler.org"
license=('LGPL2.1')
install=install
depends=('python2' 'wxpython'
        'python2-m2crypto' 'python2-apsw'
        'python2-netifaces' 'libswift'
        'gconf' 'libtorrent-rasterbar'
        'python2-feedparser')
optdepends=('vlc: watch videos in tribler'
            'python2-cherrypy: use the tribler webUI'
            'desktop-file-utils')
source=("http://dl.tribler.org/tribler_${pkgver}ubuntu1_all.deb"
        "mootools.js::http://mootools.net/download/get/mootools-core-${_mootools_ver}-full-nocompat.js")
sha1sums=('bd99ed3a34789e266f6046fb0ac33b6ddb027545'
          'bffe9e66e971a1ab3ce53cf9cbddbfa6823187d2')

package() {
  cd "$srcdir"

  ar vx tribler_${pkgver}ubuntu1_all.deb
  tar -C "$pkgdir" -xvzf data.tar.gz

  cd "$pkgdir"

  # change the path to swift because it's a separate package
  sed -i -e "s|swiftbinpath\ =\ os.path.join(self.sconfig.get_install_dir(),\"swift\")|swiftbinpath\ =\ \"\/usr\/bin\/swift\"|g" usr/share/tribler/Tribler/Main/tribler.py

  sed -i -e "s|^exec\ python|exec\ python2|g" -e "s/Ubuntu/Arch/" usr/bin/$pkgname
  # get everything in usr/share/tribler as well, just to be sure
  sed -i -e "s|^#!/usr/bin/python|#!/usr/bin/python2|" -e "s|^#!/usr/bin/env\ python|#!/usr/bin/python2|" usr/share/tribler/Tribler/{Main,Main/Utility/Feeds,Tools,Debug,dispersy/tool,dispersy/tests}/*.py usr/share/tribler/Tribler/Transport/bgprocess/{swarmengined,bgprocessd}

  # replace the symlink to the file (which doesn't exist on Arch) with the actual file
  rm usr/share/tribler/Tribler/Main/webUI/static/mootools.js
  cp "$srcdir"/mootools.js usr/share/tribler/Tribler/Main/webUI/static/

  # this is a (minor) bugfix
  # the previous sed line should grab all the "#!/usr/bin/env" lines, but two have a typo
  sed -i -e "s|^#!\ /usr/bin/env\ python|#!/usr/bin/python2|" -e "s|^#!\ /usr/bin/python|#!/usr/bin/python2|" "usr/share/tribler/Tribler/Core/DecentralizedTracking/pymdht/run_pymdht_node.py" usr/share/tribler/Tribler/vlc.py

  # this file appears to be run by apt when python is updated, but we don't do that
  rm -rf usr/share/python
}
