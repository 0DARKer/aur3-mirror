# Maintainer: David Raymond <raymond at kestrel dot nmt dot edu>
# Contributor: Alex Suykov <axs at ukr dot net>
# Contributor: Frank Thieme <frank at fthieme dot net>
pkgname=lprng
pkgver=3.8.C
pkgrel=2
pkgdesc="An Enhanced Printer Spooler"
arch=('i686' 'x86_64')
url="http://www.lprng.com"
license=('custom:Artistic')
depends=(openssl bash)
optdepends=(
	'xpdf: pdf to ps conversion in filters'
	'enscript: text to ps conversion in filters'
	'ghostscript: convert ps to device language'
)
conflicts=(cups)
source=(http://sourceforge.net/projects/lprng/files/lprng/lprng-3.8.C.tar.gz lpd.service gsfilter psfilter printcap_remote printcap_server README lpd.conf.simple lpd.perms.simple lprng.install)
install=lprng.install
md5sums=('5901bed95e61d2bea3ba3056056af432'
         '990745083e4f627a714569dc20816b0a'
         'dfe47679d1f1289f8f54e2925f99dad7'
         '58a4bf1a740b67fea83f7f68336f20e7'
         '496dfbb160f2a0308b7847541b03c8db'
         'b03c00aeb5f92b7510343ad3d046aa98'
         '5e8f71feba4048818221352afbafff8d'
         '3ef9f91dc0de273da1f5f1b20d49cd17'
         '3f861c75c2d68c0e45b4095ab39ba1c8'
         '0656eb5410652b654cddbad672755d77')

build() {
  cd "$srcdir/lprng-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc/lprng --localstatedir=/var\
      --mandir=/usr/share/man --libexecdir=/usr/lib/lprng\
      --with-userid=daemon --with-groupid=lp\
      --enable-ssl
  make -j1 || return 1
}

package() {
  cd "$srcdir/lprng-$pkgver"

  make -j1 MAKEPACKAGE=YES DESTDIR="$pkgdir/" install
  mkdir $pkgdir/usr/lib/systemd
  mkdir $pkgdir/usr/lib/systemd/system/
  install -D -m 0644 ../lpd.service $pkgdir/usr/lib/systemd/system/
  install -D -m 0644 COPYRIGHT $pkgdir/usr/share/licenses/lprng/COPYRIGHT
  install -D -m 0755 ../gsfilter $pkgdir/etc/lprng/lpd/
  install -D -m 0755 ../psfilter $pkgdir/etc/lprng/lpd/
  install -D -m 0644 ../printcap_remote $pkgdir/etc/lprng/
  install -D -m 0644 ../printcap_server $pkgdir/etc/lprng/
  install -D -m 0644 ../README $pkgdir/etc/lprng/
  install -D -m 0644 ../lpd.conf.simple $pkgdir/etc/lprng/lpd/
  install -D -m 0644 ../lpd.perms.simple $pkgdir/etc/lprng/lpd/
}
