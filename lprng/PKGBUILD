# Contributor: Alex Suykov <axs@ukr.net>
# with updates from Frank Thieme <frank@fthieme.net>
pkgname=lprng
pkgver=3.8.35
pkgrel=1
pkgdesc="LPRng - An Enhanced Printer Spooler"
arch=('i686' 'x86_64')
url="http://www.lprng.com"
license=('custom:Artistic')
depends=()
conflicts=(cups)
source=(http://www.lprng.com/DISTRIB/LPRng/LPRng-$pkgver.tgz lpd.init make.diff gcc-4.4fix.diff ssl-const.diff)
md5sums=('aaf76e4a94300352514d23bdfa66f0e7'
         '298a0c3397853f8c03ec17d70ba9037b'
         '79600907f91aa25b9e98cd29b4d5cc8c'
         '40a35d7e8ec5ce1674ca0268412d2343'
         'a05119bb86e26a2c7b4c3132e0d73496')

build() {
  cd "$srcdir/LPRng-$pkgver"
  patch -p0 < "$startdir/make.diff"
  patch -p0 < "$startdir/gcc-4.4fix.diff"
  patch -p0 < "$startdir/ssl-const.diff"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var\
      --mandir=/usr/share/man --libexecdir=/usr/lib/lprng\
      --with-userid=daemon --with-groupid=lp\
      --enable-ssl
  make -j1 || return 1
  make -j1 MAKEPACKAGE=YES DESTDIR="$pkgdir/" install
  rm -f "$pkgdir/etc/printcap" "$pkgdir/etc/lpd/lpd.conf" "$pkgdir/etc/lpd/lpd.perms"
  rm -rf "$pkgdir/etc/rc.d/init.d"
  install -D -m 0755 ../lpd.init $pkgdir/etc/rc.d/lpd
  install -D -m 0644 COPYRIGHT $pkgdir/usr/share/licenses/lprng/COPYRIGHT
}

# vim:set ts=2 sw=2 et:
