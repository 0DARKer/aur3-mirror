# Maintainer: Ricardo Funke <ricardo [at] gmail [dot] com>
# Contributor: Attila Bukor <r1pp3rj4ck [at] w4it [dot] eu>
# Contributor: Iwan Timmer <irtimmer [at] gmail [dot] com>
# Contributor: Eric Engestrom <aur [at] engestrom [dot] ch>
# Contributor: Ricardo Band <me [at] xengi [dot] de>
# Contributor: Axilleas Pipinellis (aka axil42) <axilleas [at] archlinux [dot] info>
# Contributor: UshakovVasilii <UshakovVasilii [at] yahoo [dot] com>
# Contributor: Giulio Fidente <gfidente [at] gmail [dot] com>

pkgname=popcorntime-bin
pkgver=0.3.7.1
pkgrel=1
pkgdesc="Stream movies from torrents. Skip the downloads. Launch, click, watch."
arch=('x86_64' 'i686')
url="http://popcorntime.io/"
license=('GPL3')
depends=('ttf-liberation' 'gconf' 'nss')
optdepends=('net-tools: necessary for the new vpn feature')
provides=('popcorntime')
conflicts=('popcorntime')
options=('!strip')
install="popcorntime.install"
sha1sums=('6a4809b09ff68d634dedd52bd172a5309b6c6603'
          'db504aabcd038f90c329d0c8281463171c828022'
          '23ee4387bebeaec41fb5b2d0428dd0ff5d0cad73'
          'dab731709c070a26a5e964bced9efc8d36d8c184')

_platform=Linux64

if [ "$CARCH" = 'i686' ]; then
  _platform=Linux32
  sha1sums[0]='e8e4a1ae9f4dca2072e19fb4e7ca162fc540a4d9'
fi

source=("http://get.popcorntime.io/build/Popcorn-Time-${pkgver}-${_platform}.tar.xz"
        "icon.png"
        "popcorntime.install"
        "popcorntime.desktop")

package() {
  cd "${srcdir}"

  install -dm755 "${pkgdir}/opt/${pkgname}/"
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share"

  # Program
  echo "${pkgdir}/opt/${pkgname}/"
  install -Dm755 ${srcdir}/Popcorn-Time		"${pkgdir}/opt/${pkgname}/"
  install -Dm644 ${srcdir}/nw.pak		"${pkgdir}/opt/${pkgname}/"
  install -Dm644 ${srcdir}/libffmpegsumo.so	"${pkgdir}/opt/${pkgname}/"
  install -Dm644 ${srcdir}/package.nw		"${pkgdir}/opt/${pkgname}/"

  # Link to program
  ln -s "/opt/${pkgname}/Popcorn-Time" "${pkgdir}/usr/bin/popcorntime"

  # Desktop file
  install -Dm644 "${srcdir}/popcorntime.desktop" "${pkgdir}/usr/share/applications/popcorntime.desktop"

  # Icon
  install -Dm644 "${srcdir}/icon.png" "${pkgdir}/usr/share/pixmaps/popcorntime.png"
}
