# Maintainer: kalgasnik <pan.pav.7c5@gmail.com>

pkgname=lightdm-another-gtk-greeter
pkgver=1.0.1
pkgrel=1
pkgdesc="Yet another GTK+ greeter for LightDM"
arch=('i686'
      'x86_64')
url="https://github.com/kalgasnik/lightdm-another-gtk-greeter"
license=('GPL3')
depends=('lightdm'
         'gtk3')
makedepends=('git'
             'gnome-common'
             'gobject-introspection'
             'intltool')
optdepends=('onboard: On-screen keyboard')
backup=('etc/lightdm/lightdm-another-gtk-greeter.conf')
install='lightdm-another-gtk-greeter.install'

_gitroot='git://github.com/kalgasnik/lightdm-another-gtk-greeter.git'
_gitname='lightdm-another-gtk-greeter'

build() {
  msg "Connecting to GIT server...."

  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}" && git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"

  cd "${srcdir}/${_gitname}-build"

  mkdir build/ && cd build/

  ../autogen.sh --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/lightdm \
    --disable-static

  make
}

package() {
  cd "${srcdir}/${_gitname}-build/build"
  make DESTDIR="${pkgdir}/" install
}