# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributer: Pranay Kanwar <pranay.kanwar@gmail.com>

pkgname=nessus
pkgver=4.4.1
pkgrel=1
_bigver=Nessus-$pkgver-fc14
pkgdesc="Vulnerability scanner"
arch=('i686' 'x86_64')
depends=('openssl' 'gnupg')
makedepends=('rpmextract')
license=('custom')
url="http://www.nessus.org"
install=nessus.install
backup=(opt/nessus/etc/nessus/nessusd.conf
        opt/nessus/etc/nessus/nessusd.rules
        opt/nessus/etc/nessus/nessus-fetch.rc)

# Download the rpm from <http://tenable.com/products/nessus/nessus-download-agreement>
# 32-bit: Nessus-4.4.0-fc14.i386.rpm (12103 KB)
# 64-bit: Nessus-4.4.0-fc14.x86_64.rpm (12092 KB)
source=($_bigver.i386.rpm
        nessusd
        LICENSE.NESSUS
        nessus.install)
md5sums=('7f1f490407b267ca8b41130dfc73baa0'
         '389dab32c24f0f70bfab18c8f7df5892'
         '8ff98bc9488304fcb66753d3cfb5f30e'
         'd9d25d4075acc50c0050c7a4f244c7cb')

if [[ $CARCH = x86_64 ]]; then
  source[0]="$_bigver.$CARCH.rpm"
  md5sums[0]='5fad2a71cab0f1448c098d97e3c3a6c8'
fi

build() {
  cd "$srcdir"

  if [[ $CARCH = i686 ]]; then
    rpmextract.sh $_bigver.i386.rpm &>/dev/null
  else
    rpmextract.sh $_bigver.$CARCH.rpm &>/dev/null
  fi

  # binaries to patch (is this still needed?)
  local patch_files=(opt/nessus/bin/nasl
                     opt/nessus/bin/nessus-fetch
                     opt/nessus/bin/nessus
                     opt/nessus/bin/nessuscmd
                     opt/nessus/sbin/nessusd
                     opt/nessus/sbin/nessus-adduser
                     opt/nessus/sbin/nessus-admin
                     opt/nessus/sbin/nessus-chpasswd
                     opt/nessus/sbin/nessus-check-signature
                     opt/nessus/sbin/nessus-mkcert
                     opt/nessus/sbin/nessus-mkcert-client
                     opt/nessus/sbin/nessus-rmuser
                     opt/nessus/sbin/nessus-update-plugins)

  # patch binaries (is this still needed?)
  local file
  for file in ${patch_files[@]}; do
    sed -i 's/libcrypto.so.10/libcrypto.so\x00\x00\x00/g' $file
    sed -i 's/libssl.so.10/libssl.so\x00\x00\x00/g' $file
  done
}

package() {
  mkdir -p "$pkgdir"/{etc/ld.so.conf.d,usr/share,opt/nessus}

  # path to libraries
  echo /opt/nessus/lib > "$pkgdir/etc/ld.so.conf.d/nessus.conf"

  # man pages
  cp -a opt/nessus/man "$pkgdir/usr/share/man"

  # main files
  cp -a opt/nessus/{bin,com,etc,lib,sbin,var} "$pkgdir/opt/nessus"

  # daemon script
  install -Dm755 nessusd "$pkgdir/etc/rc.d/nessusd"

  # license
  install -Dm644 LICENSE.NESSUS "$pkgdir/usr/share/licenses/nessus/LICENSE.NESSUS"
}
