# Maintainer: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

_pkgbase=ctrulib
pkgname="$_pkgbase-git"
pkgver=r256.fab8bc2
pkgrel=2
pkgdesc="C library for writing user mode arm11 code for the 3DS (CTR)"
arch=('i686' 'x86_64')
url="https://github.com/smealum/ctrulib"
license="ZLIB"
depends=('devkitarm')
makedepends=('git')
optdepends=('doxygen: to generate HTML documentation, rebuild after installing')
options=('!strip' 'staticlibs')
source=("$_pkgbase::git+https://github.com/smealum/ctrulib"
        "http://mtheall.com/%7Efincs/3dsdkA/dkA-3dsx-patch-07112014.zip")
sha1sums=('SKIP'
          '6774e64c64eca063dbe19b2b5a9f6b93fe8f15a2')

pkgver() {
  cd "$srcdir/$_pkgbase"
  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_pkgbase/libctru"
  unset CFLAGS
  source /etc/profile.d/devkitarm.sh
  make

  # Don’t fail when doxygen isn’t present.
  make dox || true

  cd "$srcdir/3dstools-source"
  autoreconf -fi
  ./configure --prefix="$DEVKITARM"
  make
}

package() {
  source /etc/profile.d/devkitarm.sh
  export DEVKITPRO_="$pkgdir/$DEVKITPRO"
  export DEVKITARM_="$pkgdir/$DEVKITARM"

  cd "$srcdir/$_pkgbase"
  install -dm755 "$DEVKITPRO_/examples"
  cp -r examples "$DEVKITPRO_/examples/3ds"

  cd libctru
  install -dm755 "$DEVKITPRO_/$_pkgbase"
  cp -r include lib "$DEVKITPRO_/$_pkgbase"

  # Don’t fail when doxygen isn’t present.
  hash doxygen && (
    install -dm755 "$pkgdir/usr/share/doc"
    cp -r docs "$pkgdir/usr/share/doc/$_pkgbase"
  )

  cd "$srcdir/devkitARM"
  install -dm755 "$DEVKITARM_/arm-none-eabi/lib"
  install -Dm644 3ds_rules "$DEVKITARM_"
  install -Dm644 arm-none-eabi/lib/* "$DEVKITARM_/arm-none-eabi/lib"

  cd "$srcdir/3dstools-source"
  make DESTDIR="$pkgdir/" install

  # Make $CTRULIB available system-wide.
  install -dm755 "$pkgdir/etc/profile.d"
  echo "export CTRULIB='$DEVKITPRO/$_pkgbase'" > "$pkgdir/etc/profile.d/$_pkgbase.sh"
  chmod +x "$pkgdir/etc/profile.d/$_pkgbase.sh"
}
