# Maintainer: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

_pkgbase=libctru
pkgname="ctrulib-git"
pkgver=r293.550f690
pkgrel=1
pkgdesc="C library for writing user mode arm11 code for the 3DS (CTR)"
arch=('i686' 'x86_64')
url="https://github.com/smealum/ctrulib"
license="ZLIB"
depends=('devkitarm')
makedepends=('git')
optdepends=('doxygen: to generate HTML documentation, rebuild after installing')
options=('!strip' 'staticlibs')
source=("$_pkgbase::git+https://github.com/smealum/ctrulib")
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgbase"
  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_pkgbase/libctru"
  unset CFLAGS
  source /etc/profile.d/devkitarm.sh
  make

  # Don’t fail when doxygen isn’t present.
  make dox || true
}

package() {
  source /etc/profile.d/devkitarm.sh
  export DEVKITPRO_="$pkgdir/$DEVKITPRO"

  cd "$srcdir/$_pkgbase"
  install -dm755 "$DEVKITPRO_/examples"
  cp -r examples "$DEVKITPRO_/examples/3ds"

  cd libctru
  install -dm755 "$DEVKITPRO_/$_pkgbase"
  cp -r include lib "$DEVKITPRO_/$_pkgbase"

  # Don’t fail when doxygen isn’t present.
  hash doxygen && (
    install -dm755 "$pkgdir/usr/share/doc"
    cp -r docs "$pkgdir/usr/share/doc/$_pkgbase"
  )

  # Make $CTRULIB available system-wide.
  install -dm755 "$pkgdir/etc/profile.d"
  echo "export CTRULIB='$DEVKITPRO/$_pkgbase'" > "$pkgdir/etc/profile.d/$_pkgbase.sh"
  chmod +x "$pkgdir/etc/profile.d/$_pkgbase.sh"
}
