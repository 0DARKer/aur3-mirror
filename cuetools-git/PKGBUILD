# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=cuetools-git
_basename=cuetools
pkgver=20130708.236
pkgrel=1
pkgdesc="Set of utilities for working with cue files and toc files"
arch=('i686' 'x86_64')
url="http://developer.berlios.de/projects/cuetools/"
license=('GPL')
depends=('glibc')
makedepends=('bison' 'autoconf')
conflicts=('cuetools')
source=('git+https://github.com/svend/cuetools.git' \
        'automake.diff' \
        'metaflac-options.diff' \
        'unicode.diff')
sha1sums=('SKIP' \
          '976dd089edd36ebf72ed776f14ae2348a7ff15fe' \
          '58da7572e60af71963b2465d8a0190caf5903dbd' \  
          'e1fae16b519d481d2dc8e08743c6b27f723dda3f')

pkgver() {
  cd "$srcdir/$_basename"
  echo $(date --utc --date="@`git show -s --format='%ct'`" '+%Y%m%d').$(git rev-list --count HEAD)
}

build() {
  cd "$srcdir/$_basename"

  patch -Np1 < "$srcdir/automake.diff"
  patch -Np1 < "$srcdir/metaflac-options.diff"
  patch -Np1 < "$srcdir/unicode.diff"

  rm -fr m4
  mkdir -p m4
  aclocal
  autoheader
  autoconf
  automake -a -c

  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/$_basename"

  make DESTDIR="$pkgdir" install
  #install -m755 extras/cuetag.sh "$pkgdir"/usr/bin/cuetag.sh
}
