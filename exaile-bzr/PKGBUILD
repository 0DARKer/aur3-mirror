# Maintainer: Alessio Sergi <asergi at archlinux dot us>
# Contributor: Aren Olson <reacocard at gmail dot com>
# Contributor: Stefan Lohmaier <noneuss at gmail dot com>

pkgname=exaile-bzr
_pkgname=exaile
pkgver=3898
pkgrel=1
pkgdesc="A full featured music player written for GTK+"
arch=('any')
url="https://launchpad.net/exaile"
license=('GPL2' 'GPL3')
depends=('dbus-python' 'desktop-file-utils' 'gstreamer0.10-good-plugins' \
         'gstreamer0.10-python' 'mutagen' 'pygtk' 'python2-bsddb')
makedepends=('bzr' 'help2man')
optdepends=('exfalso: ex-falso tag editor'
            'gstreamer0.10-plugins: more codecs'
            'hal: device autodetection'
            'ipython: ipython plugin'
            'libgpod: iPod support'
            'moodbar: moodbar plugin'
            'notify-osd: notify-osd notifications'
            'pycddb: CD metadata retrieval'
            'python-notify: libnotify notifications'
            'python-imaging: contextinfo plugin'
            'python2-mmkeys: xkeys plugin'
            'pywebkitgtk: contextinfo plugin'
            'streamripper: streamripper plugin')
provides=(${_pkgname})
conflicts=(${_pkgname} ${_pkgname}-beta ${_pkgname}-old)
install=${pkgname}.install

_bzrtrunk="lp:${_pkgname}"
_bzrmod="${_pkgname}"

build() {
  cd "${srcdir}"

  msg "Connecting to the server...."

  if [ -d ${_bzrmod} ]; then
    bzr up ${_bzrmod}
    msg "The local files are updated."
  else
    bzr co ${_bzrtrunk} ${_bzrmod}
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${_bzrmod}-build"
  cp -r "${_bzrmod}" "${_bzrmod}-build"
  cd "${_bzrmod}-build"

  # python2 fix
  for file in plugins/*/*.py; do
    sed -i 's_python_&2_' "${file}"
  done

  make
}

package() {
  cd "${srcdir}/${_bzrmod}-build"

  make PREFIX=/usr DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
