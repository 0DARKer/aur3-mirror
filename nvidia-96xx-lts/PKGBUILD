#Contributor: Bjoern Franke <bjo@nord-west.org>

pkgname=nvidia-96xx-lts
pkgver=96.43.23
pkgrel=3
_kbr="3.0"
_kmin="3.0.36-2"
_kless="3.1"
_extramodules=extramodules-${_kbr}-lts
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
pkgdesc="NVIDIA drivers for linux-lts, 96xx branch."
arch=('i686' 'x86_64')
[ "$CARCH" = "i686"   ] && ARCH=x86
[ "$CARCH" = "x86_64" ] && ARCH=x86_64
url="http://www.nvidia.com/"
depends=("linux-lts>=${_kmin}" "linux-lts<${_kless}" 
"nvidia-96xx-utils")
makedepends=("linux-lts-headers>=${_kmin}" 
"linux-lts-headers<${_kless}")
conflicts=('nvidia-173xx' 'nvidia')
provides=('nvidia-96xx')
license=('custom')
install=nvidia.install
source=("http://download.nvidia.com/XFree86/Linux-${ARCH}/${pkgver}/NVIDIA-Linux-${ARCH}-${pkgver}-pkg0.run")
options=(!strip)
md5sums=('ca0bc6ae3b37cb259f3a906b4dc4670b')
[ "$CARCH" = "x86_64" ] && md5sums[0]='a043fe8dd639bd00b1792eea7a195677'

prepare() {
  test -e "$srcdir/NVIDIA-Linux-$ARCH-${pkgver}-pkg0" \
    || sh NVIDIA-Linux-$ARCH-${pkgver}-pkg0.run --extract-only

  ln -sf Makefile.kbuild "$srcdir/NVIDIA-Linux-$ARCH-${pkgver}-pkg0/usr/src/nv/Makefile"
}

build() {
  cd "$srcdir/NVIDIA-Linux-$ARCH-${pkgver}-pkg0/usr/src/nv/"
  make SYSSRC=/usr/lib/modules/${_kernver}/build module
}

package() {
  cd "$srcdir/NVIDIA-Linux-$ARCH-${pkgver}-pkg0/usr/src/nv/"
  # Don't listen umask - put mkdir/install with "-m" option 
  install -dm755 "$pkgdir/usr/lib/modules/${_extramodules}/"
  install -dm755 "$pkgdir/etc/modprobe.d"
  install -m 644 nvidia.ko "$pkgdir/usr/lib/modules/${_extramodules}/"
  gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"

  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "$startdir/nvidia.install"
}
