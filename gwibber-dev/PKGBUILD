# Maintainer: György Balló <ballogy@freestart.hu>
_pkgname=gwibber
pkgname=$_pkgname-dev
pkgver=3.1.90
pkgrel=1
pkgdesc="Microblogging client for GNOME, which supports Twitter, Identi.ca, StatusNet, Facebook, Flickr, Digg, FriendFeed and Qaiku (development release)"
arch=('i686' 'x86_64')
url="http://gwibber.com/"
license=('GPL')
depends=('libgee' 'libnotify>=0.7' 'dee>=0.5.18-2' 'json-glib' 'gtkspell3' 'desktop-file-utils' 'dbus-python' 'gnome-keyring' 'python-gnomekeyring' 'python-wnck' 'python-egenix-mx-base' 'python2-oauth' 'python-imaging' 'python-pycurl' 'python-simplejson' 'pywebkitgtk' 'pyxdg' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('vala>=0.9.2' 'intltool>=0.35.0' 'gobject-introspection>=0.10')
optdepends=('python2-gtkspell: spell-checking support'
            'python-notify: notification support')
provides=('gwibber')
conflicts=('gwibber')
options=(!libtool)
install=$_pkgname.install
source=(http://launchpad.net/$_pkgname/3.2/$pkgver/+download/$_pkgname-$pkgver.tar.gz)
md5sums=('c2bbc1709526ad876a6b50452e4a71a4')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-schemas-compile --disable-unity
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
