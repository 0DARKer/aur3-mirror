# Maintainer: György Balló <ballogy@freestart.hu>
_pkgname=gwibber
pkgname=$_pkgname-dev
pkgver=3.1.0
pkgrel=1
pkgdesc="Microblogging client for GNOME, which supports Twitter, Identi.ca, StatusNet, Facebook, Flickr, Digg, FriendFeed and Qaiku (development release)"
arch=('any')
url="http://gwibber.com/"
license=('GPL')
depends=('dbus-python>=0.80.2' 'python2-feedparser>=4.1' 'python2-gconf>=2.18.0' 'python-gnomekeyring' 'gnome-keyring' 'python-wnck' 'python-mako>=0.2.2' 'python-egenix-mx-base>=3.0.0' 'python2-oauth' 'python-imaging>=1.1.6' 'python-pycurl' 'python-simplejson>=1.9.1' 'pywebkitgtk>=1.0.1' 'pyxdg>=0.15')
makedepends=('python-distutils-extra')
optdepends=('python2-gtkspell: spell-checking support'
            'python-notify: notification support')
provides=('gwibber')
conflicts=('gwibber')
options=(!emptydirs)
source=(http://launchpad.net/$_pkgname/3.2/$pkgver/+download/$_pkgname-$pkgver.tar.gz
        launchpad-export.tar.gz)
md5sums=('e022ad230bd7ef0c735069ba6407df0d'
         '1980931d983a0842e47cd918cda1c4bf')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  rename $_pkgname- '' ../po/$_pkgname-*.po
  mv -f -t po ../po/*

  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  python2 setup.py build
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  python2 setup.py install --root=$pkgdir/ --optimize=1
}
