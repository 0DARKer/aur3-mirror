# Maintainer: György Balló <ballogy@freestart.hu>
_pkgname=gwibber
pkgname=$_pkgname-dev
pkgver=3.2.0.1
pkgrel=2
pkgdesc="Microblogging client for GNOME, which supports Twitter, Identi.ca, StatusNet, Facebook, Flickr, Digg, FriendFeed and Qaiku (development release)"
arch=(i686 x86_64)
url="http://gwibber.com/"
license=(GPL)
depends=(libgee 'dee>=0.5.18-2' json-glib gtkspell3 dbus-python gnome-keyring python-gnomekeyring python-notify python-wnck python-egenix-mx-base python2-oauth python-imaging python-pycurl python-simplejson pywebkitgtk pyxdg xdg-utils)
makedepends=(vala-012 'intltool>=0.35.0' 'gobject-introspection>=0.10')
provides=(gwibber)
conflicts=(gwibber)
options=(!libtool)
install=$_pkgname.install
source=(http://launchpad.net/$_pkgname/3.2/$pkgver/+download/$_pkgname-$pkgver.tar.gz
        launchpad-export.tar.gz)
md5sums=(4038c998791806c09189110b2fc06f48
         d0baea6c9f164247e837ab62f4ac6a8a)

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  # Install updated language files
  echo 'af an ar ast az be bg bn br bs ca ca@valencia cy cs da de dv el en_AU en_CA en_GB eo es et eu fa fi fil fr fy ga gl gu he hi hr hu hy ia id is it ja ka kk km kn ko ku lb lo lt lv mg mk ml mn mr ms nb nl nn oc pa pl pt pt_BR ro ru si sk sl sq sr sv ta te th tr tt ug uk vi yo zh_CN zh_HK zh_TW zza' >po/LINGUAS
  rename $_pkgname- '' ../po/$_pkgname-*.po
  mv -f -t po ../po/*

  # python2 fix
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  # Remove unused dbusmenu dependency
  sed -i 's/--pkg Dbusmenu-0.4//' client/Makefile.{am,in}

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-schemas-compile --disable-unity
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
