# Maintainer:  Andrzej Giniewicz <gginiu@gmail.com>

pkgname=input-wacom-cvs
pkgver=20110217
pkgrel=1
pkgdesc="Kernel driver for Wacom tablets - development version"
arch=('i686' 'x86_64')
url="http://linuxwacom.sourceforge.net/"
license=('GPL')
depends=('kernel26>=2.6.26')
makedepends=('cvs' 'kernel26-headers')
provides=('input-wacom')
conflicts=('input-wacom')
options=('!libtool')

_cvsroot=":pserver:anonymous@linuxwacom.cvs.sourceforge.net:/cvsroot/linuxwacom"
_cvsmod="input-wacom"

build() {
  cd ${srcdir}
  msg "Connecting to linuxwacom CVS server......."
  if [ -d ${_cvsmod}/CVS ]; then
    (cd ${_cvsmod} && cvs -z3 update -D ${pkgver} -d) || return 1
    rm -rf ${_cvsmod}-build
  else
    cvs -z3 -d ${_cvsroot} co -D ${pkgver} -f ${_cvsmod} || return 1
  fi
  cp -rf ${_cvsmod} ${_cvsmod}-build
  cd ${_cvsmod}-build

  ./autogen.sh
}

package() {
  cd "${srcdir}"/input-wacom-build
  _ver=`uname -r | sed 's/[0-9]*\.[0-9]*\.\([0-9]*\).*/\1/'`
  if test $_ver -ge 36
  then
    _ver=2.6.36
  else
    _ver=2.6.30
  fi
  install -D -m 644 $_ver/wacom.ko "${pkgdir}"/lib/modules/$(uname -r)/updates/wacom.ko
  install -D -m 644 $_ver/wacom_w8001.ko "${pkgdir}"/lib/modules/$(uname -r)/updates/wacom_w8001.ko
}

