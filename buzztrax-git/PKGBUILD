# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=buzztrax-git
pkgver=0.8.0.640ee65
pkgrel=1
pkgdesc="A modular, free, open source music studio that is conceptually based on Buzz (former Buzztard)"
arch=('i686' 'x86_64')
url="http://www.buzztrax.org/"
license=('LGPL')
depends=('gst-buzztrax-git' 'libgsf' 'libgnomecanvas' 'dconf'
         'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('git' 'intltool' 'gtk-doc')
optdepends=('buzzmachines-git: buzz machines collection')
provides=('buzztard' "buzztrax=$pkgver")
conflicts=('buzztard' 'buzztrax' 'buzztrax-svn' 'bsl')
replaces=('buzztard-git' 'buzztrax-svn' 'bsl')
options=('!libtool' '!emptydirs' '!strip')
install="$pkgname.install"
source=("$pkgname::git+https://github.com/Buzztrax/buzztrax.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  echo $(awk -F '[][]' '/AC_INIT/{print $4}' configure.ac).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/$pkgname"

  # deprecated glib
  sed -i '/g_type_init ();/d' `grep -rl 'g_type_init ();'`

  # do not fail on deprecated glib-2.0 API (g_value_array_*)
  export CFLAGS+=" -Wno-error=deprecated-declarations"

  ./autogen.sh
  ./configure --prefix=/usr \
              --enable-static=no \
              --enable-man \
              --enable-debug \
              --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir/" install

  # delete unneeded cache files
  rm -f "$pkgdir/usr/share/applications/mimeinfo.cache"
  find "$pkgdir/usr/share/mime" -maxdepth 1 -type f -exec rm {} \;
}
