# Maintainer: David Manouchehri <d@32t.ca>

pkgname=iverilog-git
_gitname=$(printf ${pkgname%%-git})
_gitbranch=v0_9-branch # I think this is the latest branch, please email me if I'm wrong
_gitauthor=steveicarus
pkgver=0.9.7.7.g9b38de5
pkgrel=2
pkgdesc="Icarus Verilog compiler and simulation tool"
arch=('i686' 'x86_64')
url="http://iverilog.icarus.com/"
license=('GPL')
depends=('bzip2' 'sh' 'gcc-libs' 'zlib')
provides=('iverilog')
conflicts=('iverilog-snapshot' 'iverilog-legacy' 'iverilog')
options=('staticlibs')
makedepends=('gperf' 'bison' 'flex' 'readline')
source=("git://github.com/$_gitauthor/$_gitname.git#branch=$_gitbranch")
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  git describe | sed 's/^.//;s/_/./g;s/-/./g'
}

build() {
  cd "$srcdir/$_gitname"
  sh autoconf.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_gitname"
  make -j1 prefix="$pkgdir/usr" install # Sometimes it fails for me without -j1 I think
}
