# Maintainer: David Manouchehri <d@32t.ca>

pkgname=iverilog-git
_gitname=iverilog
_gitbranch=v0_9-branch # I think this is the latest branch, please email me if I'm wrong
_gitauthor=steveicarus
pkgver=v0_9_7.r7.g9b38de5
pkgrel=1
pkgdesc="Icarus Verilog compiler and simulation tool"
arch=('i686' 'x86_64')
url="http://iverilog.icarus.com/"
license=('GPL')
depends=('bzip2' 'sh' 'gcc-libs' 'zlib')
conflicts=('iverilog-snapshot' 'iverilog-legacy' 'iverilog')
options=('staticlibs')
makedepends=('gperf' 'bison' 'flex' 'readline')
source=("git://github.com/$_gitauthor/$_gitname.git#branch=$_gitbranch")
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$_gitname"
  sh autoconf.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_gitname"
  make -j1 prefix="$pkgdir/usr" install
}

