pkgbase='gcc-x32-seed'
pkgname=('gcc-x32-seed')
pkgver=4.7.1
pkgrel=5
#_snapshot=4.7-20120505
_libstdcppmanver=20120605		# Note: check source directory name when updating this
pkgdesc="An incomplete GCC build for building Glibc with X32 support"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL' 'custom')
url="http://sourceware.org/glibc/wiki/x32"
makedepends=('binutils-multilib>=2.22' 'libmpc' 'cloog' 'ppl' 'gcc-ada-multilib'
             'lib32-glibc>=2.16')
checkdepends=('dejagnu')
options=('!libtool' '!emptydirs')
source=(
ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.bz2
	#ftp://gcc.gnu.org/pub/gcc/snapshots/${_snapshot}/gcc-${_snapshot}.tar.bz2
	ftp://gcc.gnu.org/pub/gcc/libstdc++/doxygen/libstdc++-api.${_libstdcppmanver}.man.tar.bz2
	https://sites.google.com/site/x32abi/documents/gcc-x32-seed.tar.bz2
	gcc_pure64-multilib.patch
	gcc-4.7.1-libada-pic.patch
	gcc-4.7.1-libgo-write.patch
        gcc-4.7.1-libgo-mksysinfo.patch)
md5sums=('933e6f15f51c031060af64a9e14149ff'
         '767c62f9a047c4434f2345decf1d0819'
         '1672d3283e6827b8b8ff634f85ddf523'
         '55818c34a79ec90913ef7778e17ea6b6'
         '2acbc9d35cc9d72329dc71d6b1f162ef'
         'df82dd175ac566c8a6d46b11ac21f14c'
         '8e847244dba042d0aa3297713edaf70c')

if [ -n "${_snapshot}" ]; then
  _basedir="${srcdir}/gcc-${_snapshot}"
else
  _basedir="${srcdir}/gcc-${pkgver}"
fi

build() {
  cd ${_basedir}

  # Do not install libiberty
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in

  # Do not run fixincludes
  sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  patch -p1 -i ${srcdir}/gcc_pure64-multilib.patch

  # http://gcc.gnu.org/bugzilla/show_bug.cgi?id=53679
  patch -p1 -i ${srcdir}/gcc-4.7.1-libgo-write.patch
  
  # bug to file...
  patch -p1 -i ${srcdir}/gcc-4.7.1-libada-pic.patch

  # http://gcc.gnu.org/ml/gcc-patches/2012-06/msg01946.html
  patch -p0 -i ${srcdir}/gcc-4.7.1-libgo-mksysinfo.patch

  echo ${pkgver} > gcc/BASE-VER

  cd ${srcdir}
  mkdir gcc-build && cd gcc-build

  ${_basedir}/configure --prefix=/opt/gcc-x32-seed \
      --libdir=/opt/gcc-x32-seed/lib --libexecdir=/opt/gcc-x32-seed/lib \
      --mandir=/opt/gcc-x32-seed/share/man --infodir=/opt/gcc-x32-seed/share/info \
      --with-bugurl=https://bugs.archlinux.org/ \
      --enable-languages=c,c++ \
      --enable-shared --enable-threads=posix \
      --with-system-zlib --enable-__cxa_atexit \
      --disable-libunwind-exceptions --enable-clocale=gnu \
      --disable-libstdcxx-pch --enable-libstdcxx-time \
      --enable-gnu-unique-object --enable-linker-build-id \
      --with-ppl --enable-cloog-backend=isl \
      --disable-ppl-version-check --disable-cloog-version-check \
      --enable-lto --enable-gold --enable-ld=default \
      --enable-plugin --with-plugin-ld=ld.gold \
      --with-linker-hash-style=gnu \
      --enable-multilib --with-multilib-list=m32,m64,mx32 \
      --disable-libssp \
      --disable-build-with-cxx --disable-build-poststage1-with-cxx \
      --enable-checking=release

  # do not abort on error, because libgcc will fail due to glibc
  make || true

  ln -s build-*/fixincludes || true
}

package_gcc-x32-seed()
{
  groups=('multilib-devel')
  install=gcc.install

  cd gcc-build

  make -j1 DESTDIR=${pkgdir} install || true

  #install -d $pkgdir/opt/gcc-x32-seed/share/gdb/auto-load/opt/gcc-x32-seed/lib
  #mv $pkgdir{,/opt/gcc-x32-seed/share/gdb/auto-load}/opt/gcc-x32-seed/lib/libstdc++.so.6.0.17-gdb.py

  # unfortunately it is much, much easier to install the lot and clean-up the mess...
  rm $pkgdir/opt/gcc-x32-seed/bin/{{$CHOST-,}gfortran,{$CHOST-,}gccgo,gnat*} || true
  rm $pkgdir/opt/gcc-x32-seed/lib{,32}/*.so* || true
  rm $pkgdir/opt/gcc-x32-seed/lib{,32}/lib{ffi,gfortran,go{,begin},objc,quadmath}.a || true
  rm $pkgdir/opt/gcc-x32-seed/lib{,32}/libgfortran.spec || true
  rm -r $pkgdir/opt/gcc-x32-seed/lib/gcc/$CHOST/${pkgver}/{{,32/}ada{include,lib},finclude,include/objc} || true
  rm $pkgdir/opt/gcc-x32-seed/lib/gcc/$CHOST/${pkgver}/include/{ffi{,target}.h,quadmath{,_weak}.h} || true
  rm $pkgdir/opt/gcc-x32-seed/lib/gcc/$CHOST/${pkgver}/{cc1obj{,plus},f951,gnat1,go1} || true
  rm $pkgdir/opt/gcc-x32-seed/lib/gcc/$CHOST/${pkgver}/{,32/}{libcaf_single,libgfortranbegin}.a || true
  rm -r $pkgdir/opt/gcc-x32-seed/lib{,32}/go || true
  rm $pkgdir/opt/gcc-x32-seed/share/info/{gccgo,gfortran,gnat*,libgomp,libquadmath,libitm}.info || true
  rm $pkgdir/opt/gcc-x32-seed/share/locale/{de,fr}/LC_MESSAGES/libstdc++.mo || true
  rm $pkgdir/opt/gcc-x32-seed/share/man/man1/{gccgo,gfortran}.1 || true
  rm $pkgdir/opt/gcc-x32-seed/share/man/man3/ffi* || true

  # many packages require these symlinks
  #install -dm755 ${pkgdir}/lib
  #ln -s /opt/gcc-x32-seed/bin/cpp ${pkgdir}/lib/cpp
  ln -s gcc ${pkgdir}/opt/gcc-x32-seed/bin/cc

  # POSIX conformance launcher scripts for c89 and c99
  cat > $pkgdir/opt/gcc-x32-seed/bin/c89 <<"EOF"
#!/bin/sh
fl="-std=c89"
for opt; do
  case "$opt" in
    -ansi|-std=c89|-std=iso9899:1990) fl="";;
    -std=*) echo "`basename $0` called with non ANSI/ISO C option $opt" >&2
	    exit 1;;
  esac
done
exec gcc $fl ${1+"$@"}
EOF

  cat > $pkgdir/opt/gcc-x32-seed/bin/c99 <<"EOF"
#!/bin/sh
fl="-std=c99"
for opt; do
  case "$opt" in
    -std=c99|-std=iso9899:1999) fl="";;
    -std=*) echo "`basename $0` called with non ISO C99 option $opt" >&2
	    exit 1;;
  esac
done
exec gcc $fl ${1+"$@"}
EOF

  chmod 755 $pkgdir/opt/gcc-x32-seed/bin/c{8,9}9

  # install the libstdc++ man pages
  install -dm755 ${pkgdir}/opt/gcc-x32-seed/share/man/man3
  install -m644 ${srcdir}/libstdc++-api.${_libstdcppmanver}.man/man3/* \
    ${pkgdir}/opt/gcc-x32-seed/share/man/man3/

  # Install Runtime Library Exception
  install -Dm644 ${_basedir}/COPYING.RUNTIME \
    ${pkgdir}/opt/gcc-x32-seed/share/licenses/gcc-multilib/RUNTIME.LIBRARY.EXCEPTION

  # Install x32 seed
  install -dm755 ${pkgdir}/opt/gcc-x32-seed/lib/gcc/$CHOST/${pkgver}/x32
  cp ${srcdir}/x32/* ${pkgdir}/opt/gcc-x32-seed/lib/gcc/$CHOST/${pkgver}/x32
}

