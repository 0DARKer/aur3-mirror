# Contributor: giniu <gginiu@gmail.com>
pkgname=python-ets-apptools-svn
pkgver=25953
pkgrel=1
pkgdesc="ETS Application Tools."
arch=('any')
url="http://code.enthought.com/projects/app_tools.php"
license=('BSD')
depends=('python2' 'python-numpy>=1.1' 'python-configobj'
         'python-ets-enthoughtbase-svn' 'python-ets-traits-svn'
         'python-ets-traitsgui-svn')
makedepends=('subversion' 'python2-distribute')
conflicts=('python-enthought-utils')
options=(!emptydirs)

_svnmod=AppTools
_svntrunk=https://svn.enthought.com/svn/enthought/"${_svnmod}"/trunk

build() {
  cd "$srcdir"

  if [ -d "$_svnmod"/.svn ]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$_svnmod-build"
  svn export "$_svnmod" "$_svnmod-build"
  cd "$_svnmod-build"

  python2 setup.py install --root="$pkgdir"/ --optimize=1 || return 1

  # fix ommited preferences.ini file
  _target="$pkgdir`python2 -c 'from distutils.sysconfig import get_python_lib; print get_python_lib()'`"
  cp enthought/help/help_plugin/preferences.ini \
    "${_target}"/enthought/help/help_plugin
}

