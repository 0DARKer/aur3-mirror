# Maintainer: trya <tryagainprod@gmail.com>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir.archlinux.org>

_pkgbasename=muparser
pkgname=lib32-muparser
pkgver=2.2.3
pkgrel=1
pkgdesc="A fast math parser library (32-bit)"
arch=('x86_64')
url="http://muparser.sourceforge.net/"
license=('custom')
depends=('lib32-glibc' 'gcc-libs-multilib' 'muparser')
makedepends=('gcc-multilib')
options=('!makeflags')
source=("http://downloads.sourceforge.net/$_pkgbasename/${_pkgbasename}_v${pkgver//./_}.zip")
md5sums=('9de40ec1dab5bd2787ee344fce5846ad')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  cd $srcdir/${_pkgbasename}_v${pkgver//./_}
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd $srcdir/${_pkgbasename}_v${pkgver//./_}
  make DESTDIR=${pkgdir} install

  rm -rf ${pkgdir}/usr/{include,share}

  # license
  install -D -m644 License.txt \
	${pkgdir}/usr/share/licenses/${pkgname}/License
}

