# Maintainer: ianux <ianux@free.fr>

pkgname=stellarium-bzr
pkgver=4897
pkgrel=1
pkgdesc="A 3D planetarium software which renders realistic skies in real time"
arch=('i686' 'x86_64')
url="http://stellarium.org/"
license=('GPL2')
conflicts=('stellarium' 'stellarium-svn')
provides=('stellarium')
replaces=('stellarium-svn')
depends=('gettext' 'qt>=4.6.0' 'libgl' 'mesa' 'zlib')
makedepends=('cmake>=2.8.0' 'bzr')
source=('stellarium.desktop' 'stellarium.png')
md5sums=('62523af1a8fb1c227f0356d4794fe48f' 'fa3ecce583a4301f292d1b6fb5822dc2')
sha1sums=('f2c76c4fd0d96358b5e21f8f331ab0ec95c57382' 'b3e48fc879388fe9b239f8483391c56776daa30a')
install=stellarium.install

# for launchpad repository, do NOT use lp:module URI. Always use the "http" protocol.
# For instance:
# _bzrtrunk="http://bazaar.launchpad.net/~user/project/branch"
# _bzrmod="project"
_bzrtrunk="http://bazaar.launchpad.net/~stellarium/stellarium/trunk"
_bzrmod=stellarium

build() {
  cd ${srcdir}

  msg "Connecting to Bazaar server..."
  if [ -d ${_bzrmod} ]; then
    cd ${_bzrmod} && bzr --no-plugins pull ${_bzrtrunk} -r ${pkgver}
    msg "The local files are updated."
  else
    bzr --no-plugins branch ${_bzrtrunk} ${_bzrmod} -q -r ${pkgver}
  fi

  msg "Baazar checkout done or server timeout"

  msg "Removing old build directory..."
  rm -rf ${srcdir}/${_bzrmod}-build

  msg "Creating build directory..."
  cp -r ${srcdir}/${_bzrmod} $srcdir/${_bzrmod}-build

  msg "Starting make..."
  cd ${srcdir}/${_bzrmod}-build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd ${srcdir}/${_bzrmod}-build
  make DESTDIR=${pkgdir} install
  install -Dm644 ${startdir}/stellarium.desktop ${pkgdir}/usr/share/applications/stellarium.desktop
  install -Dm644 ${startdir}/stellarium.png ${pkgdir}/usr/share/pixmaps/stellarium.png
}
