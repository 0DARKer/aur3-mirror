# Maintainer: Anthony Cornehl <accornehl@gmail.com>

_realpkgname=nova
pkgname=openstack-${_realpkgname}-git
pkgdesc="OpenStack cloud computing fabric controller"
pkgver=0.0.0
pkgrel=1
conflicts=("openstack-${_realpkgname}" "${_realpkgname}")
provides=("openstack-${_realpkgname}")
arch=("any")
license=("custom")
url="https://github.com/openstack/${_realpkgname}"
depends=(
    "glance-git"
    "python2-amqplib"
    "python2-babel"
    "python2-carrot"
    "python2-cheetah"
    "python2-daemon"
    "python2-feedparser"
    "python2-gflags"
    "python2-lockfile"
    "python2-netaddr"
    "python2-paramiko"
    "python2-suds"
)
makedepends=(
    "python2-setuptools"
    "git"
)
source=("$pkgname"::"git+https://github.com/openstack/${_realpkgname}")
md5sums=("SKIP")

pkgver() {
  cd "$pkgname"
  local ver="$(git describe --long)"
  printf "%s" "${ver//-/.}"
}

build() {
  cd "$pkgname"
  python2 setup.py build
}

package() {
  cd "$pkgname"
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -dm600 "$pkgdir/etc/$pkgname"
  install -Dm600 etc/* "$pkgdir/etc/$pkgname/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
