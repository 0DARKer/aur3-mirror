# Maintainer: RockrKnight <aquavicio[at]gmail[dot]com>
# Submitter: TDY <tdy[at]gmx[dot]com>

pkgname=qftp
pkgver=1.4.5
pkgrel=1
pkgdesc="QFtp is a user interface for Ftp file transfer."
url="http://www.phenix.bnl.gov/WWW/publish/hpereira/software/index.php?page=package&package_list=software_list_qt4&package=qftp&full=1"
arch=('x86_64' 'i686')
license=('GPLv2')
depends=('qt')
source=("http://www.phenix.bnl.gov/WWW/publish/hpereira/software/tgz/${pkgname}-${pkgver}.tar.gz"
	"qftp.desktop"
	"qftp.png")
md5sums=('15ab369a80e5e76952a5bb9e79ea9b61'
         '510935a999ca9fa177eae455508b6454'
         '1aa3d5bbd8ac849c355821f3282d2df0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export LDFLAGS="-lX11" # Fix linking error.
  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  
  install -d "${pkgdir}"/usr/share/{pixmaps,applications}
  install -m644 "${srcdir}"/qftp.desktop "${pkgdir}"/usr/share/applications
  install -m644 "${srcdir}"/qftp.png "${pkgdir}"/usr/share/pixmaps
}

# vim:set ts=2 sw=2 et: