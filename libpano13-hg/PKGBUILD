# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=libpano13-hg
pkgver=784.04c52cae988a
pkgrel=1
pkgdesc="Basic library to calculate panoramical pictures - core functions of the panotools"
url="http://panotools.sourceforge.net/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('libpng' 'libtiff')
makedepends=('mercurial')
provides=('libpano13=2.9.19')
conflicts=('libpano13')
options=(!libtool)
source=('hg+http://panotools.hg.sourceforge.net:8000/hgroot/panotools/libpano')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/libpano"
  echo $(hg identify -n).$(hg identify -i)
}

build() {
  cd "$srcdir/libpano"

  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd "$srcdir/libpano"
  make DESTDIR="$pkgdir" install
}
