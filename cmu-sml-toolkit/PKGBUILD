# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=CMU-Cam_Toolkit_v2
pkgname=cmu-sml-toolkit
pkgver=2.05
pkgrel=1
pkgdesc="The CMU-Cambridge Statistical Language Modeling toolkit is a suite of UNIX software tools to facilitate the construction and testing of statistical language models."
arch=(i686 x86_64)
url="http://www.speech.cs.cmu.edu/SLM/toolkit.html"
license=('GPL')
source=("http://www.speech.cs.cmu.edu/SLM/$_pkg.tar.gz")
md5sums=('ef64b894c252774fdd2e86261008198f')

build() {
  cd "$srcdir/$_pkg/src"

  # enable little endian build
  sed -i "/^#BYTESWAP_FLAG/s/#//" Makefile

  make
}

package() {
  cd "$srcdir/$_pkg"

  # bin
  install -d "$pkgdir"/usr/{lib,share/doc/$pkgname}
  cp -a bin "$pkgdir/usr"

  # header
  cp -a include "$pkgdir/usr"

  # lib
  install -Dm644 lib/SLM2.a "$pkgdir/usr/lib"

  # docs
  install -Dm644 doc/* "$pkgdir/usr/share/doc/$pkgname"
}

# vim:set ts=2 sw=2 et:
