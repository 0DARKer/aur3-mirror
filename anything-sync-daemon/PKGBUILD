# Maintainer: graysky <graysky AT archlinux DOT us>

pkgname='anything-sync-daemon'
pkgver=3.13
pkgrel=1
pkgdesc='Symlinks and syncs user specified dirs to RAM (tmpfs) thus reducing HDD/SDD calls and speeding-up the system.'
arch=('any')
url='https://github.com/graysky2/anything-sync-daemon'
license=('MIT')
depends=('rsync')
conflicts=('monitorix-sync-daemon' 'user-profile-sync-daemon' 'goanysync' 'goanysync-git' 'tmpfs-store' 'tmpfs-sync')
source=("http://repo-ck.com/source/$pkgname/$pkgname-$pkgver.tar.xz")
backup=('etc/asd.conf')
install=asd.install
sha256sums=('236ae6a45b5f7f739ab9f803645f8b18b4c048bcaf9ba729ac9964b471ae1ad7')

build() {
  cd "$pkgname-$pkgver"
  ln -s Makefile.archlinux Makefile
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
