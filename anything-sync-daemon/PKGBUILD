# Maintainer: graysky <graysky AT archlinux DOT us>

pkgname='anything-sync-daemon'
_pkgname_short='asd'
pkgver=3.11
pkgrel=1
pkgdesc='Symlinks and syncs user specified dirs to RAM thus reducing HDD/SDD calls and speeding-up the system.'
arch=('any')
url='https://wiki.archlinux.org/index.php/Anything-sync-daemon'
license=('GPL')
depends=('rsync')
conflicts=('monitorix-sync-daemon')
optdepends=('cron: allow hourly (default) or periodic syncs of tmpfs <--> HDD/SDD storage')
source=("http://repo-ck.com/source/$pkgname/$pkgname-$pkgver.tar.xz")
backup=("etc/$_pkgname_short.conf")
install=asd.install
sha256sums=('4a1d511e9510608f75bb63e246dec13a16d4d8e547c76cef847919c1b08d14e7')

package() {
	cd "$srcdir/$pkgname-$pkgver"
	install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
	install -Dm644 $_pkgname_short.conf "$pkgdir/etc/$_pkgname_short.conf"
	install -Dm755 $_pkgname_short.cron.hourly "$pkgdir/etc/cron.hourly/$_pkgname_short-update"
	install -Dm644 "$_pkgname_short.service" "$pkgdir/usr/lib/systemd/system/$_pkgname_short.service"
	
	gzip -9 $_pkgname_short.manpage
	install -Dm 0644 $_pkgname_short.manpage.gz "$pkgdir/usr/share/man/man1/$pkgname.1.gz"
	ln -s "$pkgname.1.gz" "$pkgdir/usr/share/man/man1/$_pkgname_short.1.gz"
}
