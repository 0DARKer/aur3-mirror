# Maintainer: Nicolas Avrutin  <nicolasavru@gmail.com>
pkgname=hidapi-git
pkgver=20120805
pkgrel=1
pkgdesc="A Simple library for communicating with USB and Bluetooth HID devices."
arch=(i686 x86_64)
url="http://www.signal11.us/oss/hidapi/"
license=('GPL3' 'BSD' 'custom')
depends=('systemd-tools' 'libusbx' 'fox')
makedepends=('git')
options=('!libtool')

_gitroot="git://github.com/signal11/hidapi.git"
_gitname="hidapi"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./bootstrap
  ./configure --enable-testgui --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
  install -D -m644 LICENSE-bsd.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-bsd.txt"
  install -D -m644 LICENSE-orig.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-orig.txt"
}
