# Contributor: Patrick Bartels <p4ddy.b@gmail.com>

pkgname='supertux-svn'
pkgver=6190
pkgrel=1
pkgdesc="A classic 2D jump'n run sidescroller game in a style similar to the original SuperMario game"
url='http://super-tux.sourceforge.net'
license=('GPL')
arch=('i686' 'x86_64')
depends=('sdl_image' 'physfs' 'openal' 'libvorbis' 'curl')
makedepends=('subversion' 'cmake' 'glew')
conflicts=('supertux')
provides=('supertux')

_svntrunk='http://supertux.lethargik.org/svn/supertux/trunk/supertux'
_svnmod='supertux'

build() {
  cd ${srcdir}

  if [ -d ${_svnmod}/.svn ]; then
    cd ${_svnmod} && svn up -r ${pkgver} && cd ..
  else
    svn co ${_svntrunk} --config-dir ./ -r ${pkgver} ${_svnmod}
  fi
  
  msg 'SVN checkout done or server timeout'
  msg 'Starting make...'

  rm -rf ${_svnmod}-build
  cp -r ${_svnmod} ${_svnmod}-build
  cd ${_svnmod}-build
  
  cmake -D CMAKE_INSTALL_PREFIX=/usr .
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # Fix path issue. If anyone knows a better way of handling this,
  # please enlighten me!
  mv ${pkgdir}/usr/games ${pkgdir}/usr/bin
}
