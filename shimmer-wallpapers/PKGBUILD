# Maintainer: flan_suse

# This wallpaper collection contains the following:
#        * Albatross wallpaper
#        * Bluebird wallpaper
#        * Greybird wallpaper

# This wallpaper collection is complementary to the
# Shimmer Project Xfce themes

pkgname=shimmer-wallpapers
_pkgname=Wallpapers
_pkgnamelong="Shimmer Wallpapers"
pkgver=1.0
_pkgver=ba134cbfa7130e88822f07b18c454a8791d504b8
pkgrel=1
_gitdir=Wallpapers
_gitbranch=master
_giturl="git://github.com/shimmerproject/$_gitdir.git"
pkgdesc="Complementary wallpaper collection for the Shimmer Project Xfce themes"
arch=('any')
url="http://shimmerproject.org"
license=('CCPL')
groups=('xfce-themes-shimmer-collection')
makedepends=('git')
optdepends=('xfce-theme-albatross: matching Shimmer Project Xfce theme'
            'xfce-theme-bluebird: matching Shimmer Project Xfce theme'
            'xfce-theme-greybird: matching Shimmer Project Xfce theme'
            'elementary-xfce-icons: matching icon set')

build() {

    # Change into source directory
    cd "${srcdir}"

    # Clone or pull from GIT server, and notify user of the status
    msg "Connecting to GIT server...."
    if [[ -d $_gitdir ]] ; then
      cd "$_gitdir" && git pull origin "$_gitbranch"
      msg "The local files are updated."
    else
      git clone "$_giturl" "$_gitdir"
      cd "$_gitdir"
    fi

    # Checkout current git-tag stable release
    # Comment out the next line if you wish to use the master branch instead; not recommended
    git checkout "$_pkgver"

    # Notify the user that the build() process is complete
    echo "$_pkgnamelong version $pkgver is ready to be packaged!"

}

package() {

    # Change into source directory
    cd "${srcdir}"

    # Create installation directories
    install -d -m755 "${pkgdir}/usr/share/backgrounds/"
    install -d -m755 "${pkgdir}/usr/share/xfce4/backdrops/"

    # Install the wallpapers
    cp -f "$_pkgname/Albatross/albatross-2009-10.png" "${pkgdir}/usr/share/backgrounds/Albatross.png"
    cp -f "$_pkgname/Bluebird/bluebird-2010-08-1920x1440-notext.png" "${pkgdir}/usr/share/backgrounds/Bluebird.png"
    cp -f "$_pkgname/Greybird/greybird-wall-1920x1200.png" "${pkgdir}/usr/share/backgrounds/Greybird.png"

    # Make symbolic links inside of the Xfce backdrops directory, for use with Xfce Desktop Settings
    cd "${pkgdir}/usr/share/xfce4/backdrops/"
    find "../../backgrounds/" -type f -exec ln -s "{}" "$PWD" \;

}
