pkgname=fpgui
pkgver=0.8
pkgrel=2
pkgdesc="Widget set to quickly develop cross-platform GUI software using the Free Pascal Compiler"
url="http://fpgui.sourceforge.net"
license=("LGPL2 with static linking exception")
arch=(i686 x86_64)
makedepends=(fpc)
depends=(fpc)
source=("http://downloads.sourceforge.net/fpgui/fpgui_src-$pkgver.tar.gz")
md5sums=('1aee981a2a1e73e8c79c9d260443704c')
_unittgt=`fpc -iSP`-`fpc -iSO`
_fpcver=`fpc -iV`

build() {
  cd "${srcdir}/fpgui-$pkgver/src"
  ./build.sh
  cd "../docs"
  ./build_html.sh
  ./build_ipf.sh
  ./build_rtf.sh
}

package() {
  cd "${srcdir}/fpgui-$pkgver/lib/$_unittgt"
  mkdir -p "$pkgdir/usr/share/doc/fpgui"
  find . -name '*.o' -o -name '*.ppu' -o -name '*.rst' -o -name '*.a' -o -name '*.compiled' |
    xargs -rtl1 -I {} install -Dm644 {} "$pkgdir/usr/lib/fpc/$_fpcver/units/$_unittgt/"{}
  cd "../../docs"
  install -Dm644 fpgui.ipf "$pkgdir/usr/share/doc/fpgui/fpgui.ipf"
  install -m644 fpgui.rtf "$pkgdir/usr/share/doc/fpgui/"
  cp -r html "$pkgdir/usr/share/doc/fpgui"
  cd ..
  install -Dm644 COPYING.modifiedLGPL "$pkgdir/usr/share/licenses/$pkgname/COPYING.modifiedLGPL"
  install -m644 COPYING.txt "$pkgdir/usr/share/licenses/$pkgname/"
}