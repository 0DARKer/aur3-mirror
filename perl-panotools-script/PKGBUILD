# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=perl-panotools-script
pkgver=0.27
pkgrel=1
pkgdesc="Perl/CPAN Module Panotools::Script : Panorama Tools scripting"
arch=('any')
url="http://search.cpan.org/dist/Panotools-Script/"
license=("GPL2")
depends=('perl')
checkdepends=('hugin')
optdepends=('zenity: GUI' 'enblend-enfuse: enblend-* enfuse-* scripts' \
            'imagemagick: entile' 'perl-image-size: erect2* and few other scripts' \
            'hugin: tools for generating panorama projects')
source=(http://search.cpan.org/CPAN/authors/id/B/BP/BPOSTLE/Panotools-Script-$pkgver.tar.gz)
md5sums=('393fc72c8118b41fa500638c405a1a7c')

build() {
  cd "$srcdir"/Panotools-Script-$pkgver
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  perl Makefile.PL INSTALLDIRS
  make
}

check() {
  cd "$srcdir"/Panotools-Script-$pkgver
  make -k test
}

package() {
  cd "$srcdir"/Panotools-Script-$pkgver
  make install DESTDIR="$pkgdir"  
}
