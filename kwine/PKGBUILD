# Contributor: Darwin Bautista <djclue917@gmail.com>

pkgname=kwine
pkgver=0.1
pkgrel=5
pkgdesc="A set of tools for Wine and KDE interoperability"
arch=(i686 x86_64)
url="http://kwine.sourceforge.net/"
license="GPL"
depends=('kdebase' 'kwine-base')
source=(http://dl.sourceforge.net/sourceforge/${pkgname}/kfile_wine-${pkgver}.tar.gz \
        http://dl.sourceforge.net/sourceforge/${pkgname}/kio_wine-${pkgver}.tar.gz \
        http://dl.sourceforge.net/sourceforge/${pkgname}/kwine-${pkgver}.tar.gz \
        http://dl.sourceforge.net/sourceforge/${pkgname}/kwine_startmenu-${pkgver}.tar.gz \
        kwine_startmenu-0.1-gcc41.patch \
        kwine-0.1-wine_cmd_exe.patch)
md5sums=('033e65ac0e837175abdad16a15811789' '61829cd325d8af869a40601f1e10e7ad'\
         'fc56cc387fde3f501e0f8b2fbdbc8b21' 'f7e59ce251effa4d210c58f23372c66d'\
         '0a67ab9b46ec09013b517375ea7c9b97' 'a453c909eb062cb4ae489421f2b425ef')

build() {
  # Apply patches
  patch -Np0 -i kwine_startmenu-${pkgver}-gcc41.patch || return 1
  patch -Np0 -i kwine-0.1-wine_cmd_exe.patch || return 1

  _packages="kfile_wine-${pkgver} kio_wine-${pkgver} kwine-${pkgver} kwine_startmenu-${pkgver}"

  # Configure packages
  for _pkg in ${_packages}; do
    cd ${startdir}/src/${_pkg}/
    ./configure --prefix=/opt/kde --disable-debug
  done

  # Build packages
  for _pkg in ${_packages}; do
    cd ${startdir}/src/${_pkg}/
    make || return 1
  done

  # Install packages
  for _pkg in ${_packages}; do
    cd ${startdir}/src/${_pkg}/
    make DESTDIR=${startdir}/pkg install
  done
}
