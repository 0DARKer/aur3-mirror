# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: snostorm <snostormjp@gmail.com>

_pkgname=illumination
pkgname=$_pkgname-software-creator
pkgver=5.0b3
pkgrel=2
pkgdesc="Illumination Software Creator: Graphical programming environment that generates source code for various targets (including ActionScript/Adobe Flex, Android, HTML/JS, and Python/GTK)"
arch=(i686 x86_64)
url="http://lunduke.com/?page_id=3454"
license=(custom:GLL)
depends=(gtk2 desktop-file-utils)
optdepends=(
    'pygtk: Needed for Python/GTK target'
    'eclipse-android: Needed for Android target'
    'android-sdk: Needed for Android target'
    'adobe-air-sdk: Needed for building Adobe Air/flex targets')
if [[ $CARCH == "x86_64" ]]; then
    depends[0]=lib32-gtk2
    optdepends+=(
        'lib32-libcanberra: To get rid of an annoying error'
        'lib32-gtk-engines: Needed to get a decent look & feel')
fi
provides=($_pkgname)
install=$_pkgname.install
source=(http://www.lunduke.com/isc/beta/isc-5-beta-3-linux.zip
    LICENSE
    $_pkgname.png
    $_pkgname.desktop
)
sha256sums=('cd48f0d69a6e0aae2a72a2dd31d4226ab1248b04676028a8871b1355f43e08dc'
    '72f4a64010d31d601c2780344b52c87ce08d5c604e1d58255578cd9cc3c8fc5a'
    'e97606f3482f0d4110aa8333c52faa7a77e417aff4f17ec87c149f96b7924e7e'
    '476cc13ed5d1250dca40a9ca1513c11684a9c85d74d11cdaadd762554e53258a')
sha512sums=('3c96a4020875e72abfe1faf4afb84599458a19735fd7e851e5fb8b26b0d4307e3bdea62b1f37282ae452b729fe61493ad41c204c8ab0d1c2ecb8dc7eeb6d67d2'
    'beba5402b92dd9e756a1afd4b5d2cbcb8b70b110bb0db5b1ca2bc24a010f4ade92bfdca8ef548cfbc52a17b565010421bb793410366c4c41fd0127fc0d6dec90'
    '606649a56c5d747b6b32e5a3389341183bee86fe34aa935ada0d40726413995b297ad8194ce8cee83c4290f0c8b040a5dfa9e80e9914f1ae5ec692d753a5e06d'
    '9c2442390a654e4aa7c396b7c5ae3f2a676f641a47dbae873dc0af880fbfe436e46cfd539e542a91edcccf13bf6b28acf63ff34220765e4c447d8a4a719a750f')

package() {
    cd "$srcdir"
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    install -Dm644 $_pkgname.png "$pkgdir"/usr/share/pixmaps/$_pkgname.png

    install -d "$pkgdir"/opt/
    cp -r $_pkgname/ "$pkgdir"/opt/$pkgname

    desktop-file-install $_pkgname.desktop --dir "$pkgdir"/usr/share/applications/

    install -d "$pkgdir"/usr/bin/
    ln -s /opt/$pkgname/$_pkgname "$pkgdir"/usr/bin/$_pkgname
}
