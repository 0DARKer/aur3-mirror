# Maintainer: Charles Bos <charlesbos1 AT gmail>

pkgname=gnome-flashback
pkgver=3.14.0
pkgrel=2
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc="GNOME Flashback session"
url="https://git.gnome.org/browse/gnome-flashback"
# Note: Dependencies from gnome-panel onwards are flashback session dependencies and are not required for building the flashback module
depends=('glib2>=2.39.91' 'gnome-desktop>=3.8.0' 'gtk3>=3.10.0' 'libcanberra>=0.13' 'libpulse' 'gnome-panel' 'gnome-screensaver' 'gnome-session' 'gnome-settings-daemon-compat' 'gnome-themes-standard' 'metacity' 'nautilus' 'notification-daemon' 'polkit-gnome')
makedepends=('gnome-common' 'intltool')
optdepends=('compiz: required for the GNOME Flashback (Compiz) session')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        session-components.patch)
sha256sums=('5e3d344e86f7785fad188d97fb3dd5aa2bdffa41c9f9d94026534fa5e4739f8d'
            'e09a7292f08d7c5fde757024f702e622e21762d43e5fc99515cb8c8e8bbd85f2')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Add extra components to the Flashback sessions
  patch -Np1 -i "$srcdir/session-components.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/"$pkgname"

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

