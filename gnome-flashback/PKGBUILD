# Maintainer: Charles Bos <charlesbos1 AT gmail>

pkgname=gnome-flashback
pkgver=3.16.0
pkgrel=3
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc="GNOME Flashback session"
url="https://wiki.gnome.org/Projects/GnomeFlashback"
# Note: Dependencies from gnome-panel onwards are flashback session dependencies and are not required for building the flashback module
depends=('glib2>=2.44.0' 'gnome-desktop>=3.12.0' 'gtk3>=3.15.2' 'libcanberra>=0.13' 'libpulse' 'gsettings-desktop-schemas>=3.12.0' 'gnome-panel' 'gnome-screensaver' 'gnome-session' 'gnome-settings-daemon-compat' 'gnome-themes-standard' 'metacity' 'nautilus' 'notification-daemon' 'polkit-gnome')
makedepends=('gnome-common' 'intltool')
optdepends=('compiz: required for the GNOME Flashback (Compiz) session')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        session-components.patch)
sha256sums=('55f4fd781009f25f6cdd38a2c706709a03618ed3b436bcb964e29faed6788b2d'
            '9e82664e782c49215295fb68585463e8460479515efb136aa9d13033de37e344')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Add extra components to the Flashback sessions
  patch -Np1 -i "$srcdir/session-components.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/"$pkgname"

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

