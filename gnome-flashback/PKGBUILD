# Maintainer: Charles Bos <charlesbos1 AT gmail>

pkgname=gnome-flashback
pkgver=3.10.0
pkgrel=4
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc="GNOME Flashback session"
url="https://git.gnome.org/browse/gnome-flashback"
# Note: Dependencies from gnome-panel onwards are flashback session dependencies and are not required for building the flashback module
depends=('glib2>=2.39.91' 'gnome-desktop>=3.8.0' 'gtk3>=3.10.0' 'libcanberra>=0.13' 'libpulse' 'gnome-panel' 'gnome-screensaver' 'gnome-session' 'gnome-settings-daemon-compat' 'gnome-themes-standard' 'metacity' 'nautilus' 'notification-daemon' 'polkit-gnome')
makedepends=('gnome-common' 'intltool')
optdepends=('compiz: required for the GNOME Flashback (Compiz) session')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        session-modification.patch)
sha256sums=('bfdda1b408b299d983e0ecc1386ab9e2bd58056433ac3f829a195a712db16782'
            '2b4ef78ba8ef2bb19aef7018911a2a6e8171f3c62e5fbeb1cd7e2c33b7434b0f')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Rename GNOME Flashback (Metacity) to GNOME Flashback and add extra required components
  patch -Np1 -i "$srcdir/session-modification.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/"$pkgname"

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

