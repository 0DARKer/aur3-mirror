# Maintainer: Lara Maia <lara@craft.net.br>

pkgname=redshift-git
pkgver=1.8.6.g3297f52
pkgrel=1
pkgdesc='Adjusts the color temperature of your screen according to your surroundings.'
arch=('i686' 'x86_64')
url='http://jonls.dk/redshift/'
license=('GPL3')
depends=('gconf' 'geoclue' 'libxxf86vm')
optdepends=('pygtk: for gtk-redshift'
            'python2-xdg: for gtk-redshift'
            'librsvg: for gtk-redshift')
makedepends=('python2' 'git')
install='redshift.install'
source=(git+https://github.com/jonls/redshift)
md5sums=('SKIP')

pkgver() {
  cd redshift
  git describe --tags | sed 's/^v//;s/-/./g'
}

prepare() {
  cd redshift
  
  sed -i 's/python/python2/' src/redshift-gtk/redshift-gtk
  ./bootstrap
}

build() {
  cd redshift

  PYTHON=/usr/bin/python2 ./configure --prefix=/usr
  make
}

package() {
  cd redshift
  
  make DESTDIR="$pkgdir" install
}
