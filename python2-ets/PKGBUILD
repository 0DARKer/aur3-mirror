# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
pkgname=python2-ets
pkgver=4.2.0
_githubtag=f895bee
pkgrel=1
pkgdesc="Enthought Tool Suite"
arch=('any')
url="https://github.com/enthought/ets"
license=('BSD')
depends=('python2-apptools' 'python2-blockcanvas' 'python2-chaco'
         'python2-codetools' 'python2-enable' 'python2-enaml' 'python2-encore'
         'python2-envisage' 'python2-etsdevtools' 'python2-etsproxy'
         'python2-graphcanvas' 'python2-mayavi' 'python2-pyface'
         'python2-scimath' 'python2-traits' 'python2-traitsui')
makedepends=('python2-distribute')
conflicts=('python2-ets-git')
options=(!emptydirs)

source=("https://github.com/enthought/ets/tarball/${pkgver}")
md5sums=('4636d086a1bf1acfabc0556fa2e16b46')

build() {
  cd "$srcdir/enthought-ets-${_githubtag}"

  python2 setup.py build
}

package() {
  cd "$srcdir/enthought-ets-${_githubtag}"

  python2 setup.py install --root="$pkgdir"/ --optimize=1

  sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    $(find "${pkgdir}" -name '*.py')

  install -D "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

