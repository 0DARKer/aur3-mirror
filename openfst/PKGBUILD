# Maintainer: Christoph Drexler <chrdr at gmx dot at>

pkgname=openfst
pkgver=1.2.6
pkgrel=1
pkgdesc="Library for constructing, combining, optimizing, and searching weighted finite-state transducers (FSTs)"
arch=('i686' 'x86_64')
url="http://www.openfst.org/"
license=('APACHE')
options=(!libtool)
source=("http://openfst.cs.nyu.edu/twiki/pub/FST/FstDownload/${pkgname}-${pkgver}.tar.gz"
        'far.patch')
md5sums=('cbb2b7421d4ba36b329242041057b98f'
         'bb6fbaa5350cdf8fd6b8a09a0f11d803')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	# apply patch according to http://openfst.cs.nyu.edu/twiki/bin/view/Forum/FstForum#far_weirdness
	if [[ "$CARCH" == 'i686' ]]; then
		patch -p0 <../far.patch
	fi

	# Options according to http://openfst.cs.nyu.edu/twiki/bin/view/FST/ReadMe
	# using "--enable-far" and "--with-icu" together produces an error
	OPTIONS="--prefix=/usr"
	OPTIONS+=" --enable-bin"            # Enable fst::script and command-line binaries; Default: yes
	OPTIONS+=" --enable-compact-fsts"   # Enable all CompactFst classes;                Default: no
	OPTIONS+=" --enable-const-fsts"     # Enable FAR (FST Archive) extension;           Default: no
#	OPTIONS+=" --enable-far"            # Enable LookAheadFst classes;                  Default: no
	OPTIONS+=" --enable-lookahead-fsts" # Enable all ConstFst classes;                  Default: no
	OPTIONS+=" --enable-pdt"            # Experimental push-down transducer extensions; Default: no
	OPTIONS+=" --with-icu"              # Use ICU to provide unicode support;           Default: no
	LIBS="-ldl" ./configure $OPTIONS
	make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
