# Maintainer: Ner0

pkgname=my-weather-indicator
pkgver=0.5.0.12
pkgrel=1
pkgdesc="A simple weather indicator"
arch=('any')
url="https://www.launchpad.net/my-weather-indicator"
license=('GPL3')
depends=('hicolor-icon-theme' 'python2' 'libchamplain' 'lsb-release' 'gtk3' 'gdk-pixbuf2' 'libappindicator3' 'python2-lxml' 'python2-dateutil' 'python-geoip' 'python2-geoclue' 'libnotify')
install=$pkgname.install
source=("https://launchpad.net/~atareao/+archive/test/+files/${pkgname}_$pkgver-1ubuntu1.tar.gz"
	'locale.patch')
md5sums=('4ec0118912fae4ff390dfb0d0b7c5bf4'
         '59adb4650872e39233ba0bee2387595a')

package() {
  cd $pkgname

  patch -Np0 -i "$srcdir/locale.patch"

  install -vDm755 bin/$pkgname "$pkgdir/usr/bin/$pkgname"
  install -vDm644 data/My-Weather-Indicator.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -vdm755 "$pkgdir"/usr/share/icons/hicolor/{24x24,48x48}/status
  install -vm644 data/icons/*.png "$pkgdir/usr/share/icons/hicolor/24x24/status/"
  install -vm644 data/images/*.png "$pkgdir/usr/share/icons/hicolor/48x48/status/"
  install -vdm755 "$pkgdir/usr/share/$pkgname/logos"
  install -vm644 src/*.py "$pkgdir/usr/share/$pkgname"
  install -vm755 data/logos/*.png "$pkgdir/usr/share/$pkgname/logos"
  install -vDm644 data/images/$pkgname.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -vm755 data/my-weather-indicator-autostart.desktop "$pkgdir/usr/share/$pkgname/"

  sed -i 's/python$/python2/;s/env\ python\ /env\ python2/' "$pkgdir"/usr/share/$pkgname/*.py
  sed -i 's/bash/sh/' "$pkgdir/usr/bin/$pkgname"

  cd template1

  for i in *.po; do 
    install -dm755 "$pkgdir"/usr/share/locale/${i%.*}/LC_MESSAGES
    msgfmt "$i" -o "$pkgdir"/usr/share/locale/${i%.*}/LC_MESSAGES/my-weather-indicator.mo
  done

  chmod +x "$pkgdir"/usr/share/$pkgname/*.py
}
