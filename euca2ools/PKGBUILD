# Contributors:
#	henning mueller <henning@orgizm.net>
#   Andrew Hamilton

pkgname=euca2ools
pkgver=1.3.1
pkgrel=5
pkgdesc='Command-line tools for interacting with Amazon EC2 and S3 API-compatible Web services using the REST/Query API.'
arch=(any)
url=http://open.eucalyptus.com
license=(BSD)
depends=(python2 python-boto python-m2crypto)
makedepends=(python2 help2man python-boto python-m2crypto)
source=(
	http://eucalyptussoftware.com/downloads/releases/$pkgname-$pkgver.tar.gz
)
md5sums=(
  a835e8fabd5875a5c8dbcba1bf89d402
)

package() {
	# Create needed directories in $pkgdir.
	cd $pkgdir
	mkdir -p \
		usr/lib/python2.7/site-packages/$pkgname \
		usr/bin \
		etc/bash_completion.d \
		usr/share/man/man1

	cd $srcdir/$pkgname-$pkgver

	# Change shebang lines for python2.
	find . -type f | xargs sed -i "s:/python:/python2:"

	# Edit the connections module name as it should be lowercase
	for action in upload delete download; do
		sed -i "s/import Connection/import connection/" \
			bin/euca-$action-bundle
	done

	# Edit the euca2ools module to correctly connect
	sed -i "s/return boto.s3.Connection(/return boto.s3.connection.S3Connection(/" \
		$pkgname/$pkgname/__init__.py

	# Copy python module over to python module dir.
	cp $pkgname/$pkgname/* \
		$pkgdir/usr/lib/python2.7/site-packages/$pkgname

	# Copy binaries.
	cp bin/* \
		$pkgdir/usr/bin

	# Copy bash completion script.
	cp util/* \
		$pkgdir/etc/bash_completion.d

	# Generate and copy man pages.
	make man
	cp man/* \
		$pkgdir/usr/share/man/man1
}
