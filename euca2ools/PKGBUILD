# Contributors:
#   henning mueller <henning@orgizm.net>
#   Andrew Hamilton
#   Tom T.<tomtryf@gmail.com>

pkgname=euca2ools
pkgver=3.0.2
pkgrel=1
pkgdesc='Command-line tools for interacting with Amazon EC2 and S3 API-compatible Web services using the REST/Query API.'
arch=(any)
url=http://www.eucalyptus.com
license=(custom)
depends=(python2-six python2-lxml python2-progressbar python2-requestbuilder python2-requests rsync)
makedepends=(python2-setuptools)
source=(
  http://downloads.eucalyptus.com/software/euca2ools/3.0/source/$pkgname-$pkgver.tar.gz
  http://downloads.eucalyptus.com/software/euca2ools/3.0/source/$pkgname-$pkgver.tar.gz.asc
)

build() {
  cd $srcdir/$pkgname-$pkgver
  python2 setup.py build
  gzip man/*.1
}

package() {
  cd $srcdir/$pkgname-$pkgver

  python2 setup.py install --root="${pkgdir}" --optimize=1

  mkdir -p "$pkgdir/usr/share/"{euca2ools/certs,man/man1}

  install -Dm644 conf/euca2ools.ini "$pkgdir/etc/euca2ools/euca2ools.ini"
  install -m644 certs/* "$pkgdir/usr/share/euca2ools/certs/"
  install -m644 man/*.1.gz "$pkgdir/usr/share/man/man1/"
  install -D COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

sha256sums=('8ce61695731e3682c3599274cc2b7aade4e408fd2635831e769143fe2f5159ec'
            'SKIP')
