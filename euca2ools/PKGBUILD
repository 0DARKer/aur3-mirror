# Contributors:
#   henning mueller <henning@orgizm.net>
#   Andrew Hamilton

pkgname=euca2ools
pkgver=2.1.1
pkgrel=2
pkgdesc='Command-line tools for interacting with Amazon EC2 and S3 API-compatible Web services using the REST/Query API.'
arch=(any)
url=http://www.eucalyptus.com
license=(custom)
depends=(python2 python2-boto python2-m2crypto)
makedepends=(python2 help2man python2-boto python2-m2crypto)
source=(
  https://github.com/eucalyptus/euca2ools/tarball/$pkgver
)
sha256sums=(
  a08b3d9e2f31aeb95e45445979a9d2cb9b892f9713d7425e48a614af2d8d483d
)

build() {
  cd $srcdir/eucalyptus-$pkgname-*

  python2 setup.py build

  echo generating manpages...
  ./generate-manpages.sh
  gzip man/*.1
}

package() {
  cd $srcdir/eucalyptus-$pkgname-*

  python2 setup.py install --prefix=$pkgdir/usr

  cd man
  for i in *.1.gz; do
    install -D $i $pkgdir/usr/share/man/man1/$i;
  done
  cd -

  install -D COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}
