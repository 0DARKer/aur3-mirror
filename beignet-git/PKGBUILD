# Maintainer: Boqun Feng <boqun.feng@gmail.com>

pkgname=beignet-git
_gitname=beignet
pkgver=9acc4ce
pkgrel=1
pkgdesc="OpenCL Implemenation for Intel IvyBridge"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/beignet/"
license=('LGPL')
groups=()
depends=('clang>=3.3' 'llvm>=3.3' 'ncurses')
makedepends=('git' 'cmake>=2.6' 'llvm>=3.3' 'python2')

#provides and conflicts with libcl since ICD loader is not support now
provides=('opencl-intel' 'opencl-headers' 'libcl')
conflicts=('opencl-intel' 'opencl-headers' 'libcl')
source=('beignet::git://anongit.freedesktop.org/beignet')
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_gitname}"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "${srcdir}/${_gitname}"

  cmake -DCMAKE_BUILD_TYPE=Release -DPYTHON_EXECUTABLE:FILEPATH=/usr/bin/python2 -DCMAKE_INSTALL_PREFIX=/usr .
  make -j4
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir/" install
  ln -s /usr/lib/beignet/libcl.so $pkgdir/usr/lib/libOpenCL.so.1
  ln -s /usr/lib/libOpenCL.so.1 $pkgdir/usr/lib/libOpenCL.so
}

# vim:set ts=2 sw=2 et:
