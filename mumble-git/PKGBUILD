# $Id$
# Maintainer:  Radioactiveman <thomas-lange2@gmx.de>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Lauri Niskanen <ape@ape3000.com>

# If you want support for your G15 Keyboard, please add 'g15daemon'
# to the depends and delete "no-g15" in the configure line below

pkgname=mumble-git
pkgver=2013.06.17
_pkgver=1.2.5
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='A voice chat application similar to TeamSpeak'
license=('BSD')
depends=('qt4' 'speex' 'celt' 'lsb-release' 'libxi' 'avahi' 'libsndfile' 'protobuf' 'libpulse')
makedepends=('boost' 'mesa' 'git')
#optdepends=('portaudio: for portaudio back-end' 'g15daemon: G15 Keyboard support')
options=('!libtool')
conflicts=('mumble')
provides=('mumble')
install=$pkgname.install
url='http://mumble.sourceforge.net/'
source=('git://github.com/mumble-voip/mumble.git')
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/mumble"
    git log -n 1 --pretty=format:"%cd" --date=short | sed "s/-/./g"
}

build() {
    cd "$srcdir/mumble"

    # Buildfix
    sed -i "s/CELT_SET_VBR_RATE/CELT_SET_BITRATE/g" src/mumble/AudioInput.cpp

    # Building mumble
    qmake-qt4 main.pro \
      CONFIG+="no-bundled-celt no-bundled-speex no-bundled-opus no-speechd \
               no-g15 no-server no-embed-qt-translations no-update packaged" \
      DEFINES+="PLUGIN_PATH=/usr/lib/mumble"

    make release
}

package() {
    cd "$srcdir/mumble"

    # bin stuff
    install -m755 -D ./release/mumble $pkgdir/usr/bin/mumble
    install -m755 -D ./scripts/mumble-overlay $pkgdir/usr/bin/mumble-overlay

    # lib stuff
    install -m755 -D ./release/libmumble.so.$_pkgver $pkgdir/usr/lib/mumble/libmumble.so.$_pkgver
    ln -s libmumble.so.$_pkgver $pkgdir/usr/lib/mumble/libmumble.so
    ln -s libmumble.so.$_pkgver $pkgdir/usr/lib/mumble/libmumble.so.1
    ln -s libmumble.so.$_pkgver $pkgdir/usr/lib/mumble/libmumble.so.1.2
    install -m755 -D ./release/plugins/liblink.so $pkgdir/usr/lib/mumble/liblink.so
    install -m755 -D ./release/plugins/libmanual.so $pkgdir/usr/lib/mumble/libmanual.so

    # other stuff
    install -m644 -D ./scripts/mumble.desktop $pkgdir/usr/share/applications/mumble.desktop
    install -m755 -d $pkgdir/usr/share/man/man1
    install -m644 -D ./man/mum* $pkgdir/usr/share/man/man1/
    install -m644 -D ./icons/mumble.svg $pkgdir/usr/share/icons/hicolor/scalable/apps/mumble.svg
}
