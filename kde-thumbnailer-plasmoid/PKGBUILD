# Maintainer: FadeMind <fademind@gmail.com>

pkgname=kde-thumbnailer-plasmoid
pkgver=1.1
pkgrel=1
pkgdesc="Preview image generator plugin for plasma plasmoid package."
arch=('i686' 'x86_64')
license=('GPL')
url="http://kde-apps.org/content/show.php/KDE+Plasmoid+Thumbnailer?content=146527"
depends=('kdebase-workspace')
makedepends=('automoc4' 'cmake' 'kdelibs')
source=("${pkgname}-${pkgver}.tar.bz2::http://kde-apps.org/CONTENT/content-files/146527-${pkgname}-${pkgver}.tar.bz2"
        "LICENSE::https://www.gnu.org/licenses/gpl.txt")
sha256sums=('1c49ed288c22811abb30ac6a616c8473b7e9f6dae70d9cb519f58bde95a9fbaf'
            '8ceb4b9ee5adedde47b31e975c1d90c73ad27b6b165a1dcd80c7c545eb65b903')

build() {
    install -d build/
    cd build/
    cmake ../${pkgname}/ \
        -DCMAKE_INSTALL_PREFIX=$(kde4-config --prefix) \
        -DCMAKE_BUILD_TYPE=Release \
        -DQT_QMAKE_EXECUTABLE=qmake-qt4
    make
}

package() {
    install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    make -C build DESTDIR="${pkgdir}" install
}
