pkgname=softethervpn-git
pkgver=r61.64fd19e
pkgrel=1
pkgdesc="Multi-protocol VPN Program from University of Tsukuba"
arch=('i686' 'x86_64')
source=(
  'git+https://github.com/SoftEtherVPN/SoftEtherVPN.git'
  'softethervpn-bridge.service'
  'softethervpn-client.service'
  'softethervpn-server.service'
)
md5sums=(
  'SKIP'
  'fd24a71e0e7ab42afb411b8e5dfee640'
  'f3370c9560a40e1fbf81947d28fb5950'
  '941e98017314d74dffd8a7074ab9200f'
)
license=('GPL')
url="http://www.softether.org/"

pkgver() {
  cd "SoftEtherVPN"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build(){
  cd "${srcdir}"/SoftEtherVPN
  if [ "${CARCH}" == "i686" ]; then
    cp src/makefiles/linux_32bit.mak Makefile
  elif [ "${CARCH}" == "x86_64" ]; then
    cp src/makefiles/linux_64bit.mak Makefile
  fi
  make
}

package(){
  cd "${srcdir}"/SoftEtherVPN
  install -d "${pkgdir}"/usr/{bin,vpnbridge,vpncmd,vpnclient,vpnserver}
  for inst in vpnclient vpnserver vpnbridge vpncmd
  do
    cp bin/${inst}/${inst} "${pkgdir}"/usr/${inst}/${inst}
    cp bin/${inst}/hamcore.se2 "${pkgdir}"/usr/${inst}/hamcore.se2
    echo "#!/bin/sh" > "${pkgdir}"/usr/bin/${inst}
    echo /usr/${inst}/${inst} '"$@"' >> "${pkgdir}"/usr/bin/${inst}
    echo 'exit $?' >> "${pkgdir}"/usr/bin/${inst}
    chmod 755 "${pkgdir}"/usr/bin/${inst}
  done
  install -d "${pkgdir}"/usr/lib/systemd/system
  cp "${srcdir}"/*.service "${pkgdir}"/usr/lib/systemd/system
}
