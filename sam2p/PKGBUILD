# Contributor: Jonathan Liu <net147@hotmail.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: menta

pkgname=sam2p
pkgver=0.49.1
pkgrel=1
pkgdesc="A bitmap-to-eps-or-pdf-converter that produces very small files"
arch=('i686' 'x86_64')
url="http://www.inf.bme.hu/~pts"
license=('GPL2')
# depends=('gcc-libs') is part of "base" group, which "is assumed to be installed on all Arch Linux systems"
optdepends=('ghostscript: PS, EPS, PDF support'
'libjpeg: JPEG support'
'tif22pnm: for tiff and png support')
# 'netpbm: PNG support'
source=("http://sam2p.googlecode.com/files/$pkgname-$pkgver.tar.gz")
md5sums=('fe518b3d69a95b7b4114dfd84b69badf')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix="$pkgdir"/usr --enable-lzw --enable-gif
  make 
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make install
  install -D -m644 README "$pkgdir"/usr/share/sam2p/docs/README
}
