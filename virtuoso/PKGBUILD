# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano DOT torromeo AT google mail service>

pkgname=virtuoso
pkgver=6.1.8
pkgrel=1
arch=('i686' 'x86_64')
url='http://virtuoso.openlinksw.com/wiki/main/Main/'
license=('GPL')
pkgdesc='A scalable cross-platform server of virtuoso (very minimal installation)'
depends=('libldap' 'libxml2' 'java-environment')
makedepends=('bison' 'flex' 'gperf' 'net-tools' 'optipng')
replaces=('virtuoso-base')
provides=('virtuoso-base')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-opensource-${pkgver}.tar.gz")
md5sums=('94ef4f93cad915f73bee547fe95db725')

prepare() {
  cd ${pkgname}-opensource-${pkgver}

  optipng -quiet -force -fix appsrc/ODS-Wiki/http/images/opts_16.png
}

build() {
  cd ${pkgname}-opensource-${pkgver}

  ./configure --with-layout=debian \
    --program-transform-name='s/isql$$/isql-vt/;s/isqlw/isqlw-vt/' \
    --with-readline
  make
}

package() {
  cd ${pkgname}-opensource-${pkgver}
  make DESTDIR="${pkgdir}" install

  # rename to avoid conflicts with unixodbc
  for bin in isql isqlw; do
    install -Dm755 "${srcdir}"/${pkgname}-opensource-${pkgver}/binsrc/tests/$bin \
      "${pkgdir}"/usr/bin/$bin-vt
  done

  # remove conflicts
  rm "${pkgdir}"/usr/bin/isql{,w-vt}
  rm "${pkgdir}"/usr/bin/virtuoso-t
  rm "${pkgdir}"/usr/lib/virtodbc.so
  rm "${pkgdir}"/usr/lib/virtodbc_r.so
  rm "${pkgdir}"/usr/lib/virtodbcu.so
  rm "${pkgdir}"/usr/lib/virtodbcu_r.so
}
