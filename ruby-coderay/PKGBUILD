# Maintainer: TDY <tdy@gmx.com>

pkgname=ruby-coderay
pkgver=0.9.7
pkgrel=1
pkgdesc="A fast syntax highlighting library for Ruby"
arch=('any')
url="http://coderay.rubychan.de/"
license=('LGPL')
depends=('ruby')
makedepends=('rake')
source=(http://rubyforge.org/frs/download.php/74005/${pkgname/*-}-$pkgver.tar.gz
        gem.rake)
md5sums=('e758591cfd75e6541fc60b8b02a042d4'
         '134ab6e0ff049c630dd5e5de84b1b76a')

build() {
  cd "$srcdir"
  install -dm755 rake_tasks
  ln -sf ../gem.rake rake_tasks
  rake gem
}

package() {
  cd "$srcdir/pkg"
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"
  gem install --ignore-dependencies -i "$pkgdir$_gemdir" -n "$pkgdir/usr/bin" \
    ${pkgname/*-}-$pkgver.gem
}
