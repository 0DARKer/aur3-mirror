# Maintainer: Andreas Wagner <AndreasBWagner@pointfree.net>
# Contributor: Thomas Jost <schnouki@schnouki.net>
pkgname=tmsu
pkgver=0.5.1
pkgrel=0
pkgdesc="An application that allows you to tag your files to organize them."
arch=('i686' 'x86_64')
url="http://tmsu.org"
license=('GPL3')
depends=('bash')
makedepends=('git' 'go-fuse' 'go-sqlite3')
source=("https://github.com/oniony/TMSU/archive/v${pkgver}.tar.gz")
md5sums=('280a22651123e0809c68fa4f905c29a7')
sha256sums=('e5446b362026e09681ee3117912f26a2420df4bfd8fd8bfcb825ac88113850d3')

build() {
  cd "$srcdir/TMSU-${pkgver}"
  make compile GOPATH="/usr/lib/go/site:$srcdir/TMSU-${pkgver}"
}

package() {
  cd "$srcdir/TMSU-${pkgver}"
  mkdir -p "$pkgdir"/usr/{bin,share/tmsu,share/zsh/site-functions}

  make install \
       INSTALL_DIR="$pkgdir"/usr/bin \
       MOUNT_INSTALL_DIR="$pkgdir"/usr/bin \
       MAN_INSTALL_DIR="$pkgdir"/usr/share/man/man1 \
       ZSH_COMP_INSTALL_DIR="$pkgdir"/usr/share/zsh/site-functions

  for f in misc/db-upgrade/*; do
    install -Dm644 "$f" "$pkgdir"/usr/share/tmsu/
  done
}
# vim:set ts=2 sw=2 et:

