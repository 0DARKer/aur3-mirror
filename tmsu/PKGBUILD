# Maintainer: Andreas Wagner <AndreasBWagner@pointfree.net>
# Contributor: Thomas Jost <schnouki@schnouki.net>
pkgname=tmsu
pkgver=0.5.2
pkgrel=1
pkgdesc="An application that allows you to tag your files to organize them."
arch=('i686' 'x86_64')
url="http://tmsu.org"
license=('GPL3')
depends=('bash')
makedepends=('git' 'go-fuse' 'go-sqlite3')
source=("https://github.com/oniony/TMSU/archive/v${pkgver}.tar.gz")
install=${pkgname}.install
md5sums=('1d7591aa723e77fb663388a6d3865ffd')
sha256sums=('c6b749ee20774d39b36ab1182d80e22697bd7a6a9c5af2acbb090db0d867619f')

build() {
  cd "$srcdir/TMSU-${pkgver}"
  make compile GOPATH="/usr/lib/go/site:$srcdir/TMSU-${pkgver}"
}

package() {
  cd "$srcdir/TMSU-${pkgver}"
  mkdir -p "$pkgdir"/usr/{bin,share/tmsu,share/zsh/site-functions}

  make install \
       INSTALL_DIR="$pkgdir"/usr/bin \
       MOUNT_INSTALL_DIR="$pkgdir"/usr/bin \
       MAN_INSTALL_DIR="$pkgdir"/usr/share/man/man1 \
       ZSH_COMP_INSTALL_DIR="$pkgdir"/usr/share/zsh/site-functions

  for f in misc/db-upgrade/*; do
    install -Dm644 "$f" "$pkgdir"/usr/share/tmsu/
  done
}
# vim:set ts=2 sw=2 et:


