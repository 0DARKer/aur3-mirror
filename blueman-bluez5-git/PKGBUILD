# Maintainer: Martin Wimpress <code@flexion.org>

_ver=1.2.3
_pkgname=blueman
pkgname=${_pkgname}-bluez5-git
pkgver=1.2.3.20140501.7813dde
pkgrel=1
pkgdesc="A GTK+ Bluetooth Manager (BlueZ 5)"
arch=('i686' 'x86_64')
license=('GPL')
url="https://github.com/blueman-project/blueman"
depends=('bluez' 'dbus-glib' 'gconf' 'gtk3' 'notification-daemon'
         'libnotify' 'polkit' 'python2-dbus' 'python2-gobject'
         'python2-pybluez' 'startup-notification')
optdepends=('dnsmasq: DHCP server for Network Access Point'
            'pulseaudio: for some audio support')
makedepends=('git' 'intltool' 'libtool' 'pyrex')
groups=('mate-unstable-extra')
source=("${pkgname}"::"git+https://github.com/blueman-project/${_pkgname}.git"
        "use-python2.diff")
sha1sums=('SKIP'
          '73bcad1a5e42474573cc565e3a250f560ca78e8d')
install=${_pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo ${_ver}.$(date +%Y%m%d).$(git rev-parse --short master)
}

prepare() {
    cd "${srcdir}/${pkgname}"
    # Coerce the build system to use python2
    patch -Np1 -i "${srcdir}/use-python2.diff"
    sed -i 's|AM_CHECK_PYTHON_HEADERS|#AM_CHECK_PATH_HEADERS|' configure.ac
    sed -i 's|@PYTHON_INCLUDES@|-I\/usr\/include\/python2.7|' module/Makefile.am
    sed -i 's|@PYTHON_INCLUDES@|-I\/usr\/include\/python2.7|' sendto/Makefile.am

    # Replace the python interpreter with python2
    for PY in apps/blueman-*
    do
        sed -i 's/python/python2/' "${PY}"
    done
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgname}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${_pkgname} \
        --with-gtk=3.0 \
        --disable-hal \
        --disable-sendto \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
