# Contributor: Jesus Jerez <jerezmoreno@gmail.com>

pkgname=eclipse-jdt
pkgver=3.6
_pkgbuild=R-3.6-201006080911
pkgrel=2
pkgdesc="Eclipse Java development tools"
url="http://www.eclipse.org/jdt/"
arch=('any')
license=('EPL')
depends=('eclipse-platform')
makedepends=('unzip')
changelog=$pkgname.changelog
source=("http://www.mirrorservice.org/sites/download.eclipse.org/eclipseMirror/eclipse/downloads/drops/${_pkgbuild}/org.eclipse.jdt-${pkgver}.zip")
md5sums=('1273090c43a8b948847899f3a442aab8')
sha256sums=('b946098ec74f0699ad4fe146c2135b43a1a9e4a74f270e6df6e617a2e7b02c0a')

build() {

  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}

  # Features
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
    else
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done

}
