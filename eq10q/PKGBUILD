# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=eq10q
pkgver=1.2
pkgrel=1
pkgdesc="An LV2 parametric equalizer audio plugin"
arch=('i686' 'x86_64')
url="http://eq10q.sourceforge.net/"
license=('GPL3')
groups=('lv2-plugins')
depends=('lv2' 'plotmm')
makedepends=('lv2-c++-tools' 'pstreams')
source=("http://download.sourceforge.net/project/eq10q/EQ10Q-LV2Plugin-source-$pkgver.tar.gz")
md5sums=('b1b248537d7977cbd48acf9e28672590')

build() {
  cd "$srcdir/EQ10Q"

  # fix resource path
  sed "s|/local||g" -i main_window.cpp pixmapcombo.h

  make
}

package() {
  cd "$srcdir/EQ10Q"
  make INSTALL_DIR="$pkgdir/usr/lib/lv2" install
}
