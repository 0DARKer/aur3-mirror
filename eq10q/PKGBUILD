# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=eq10q
pkgver=2_beta5.1
pkgrel=1
pkgdesc="An LV2 parametric equalizer audio plugin"
arch=('i686' 'x86_64')
url="http://eq10q.sourceforge.net/"
license=('GPL3')
groups=('lv2-plugins')
depends=('lv2' 'gtkmm')
makedepends=('cmake')
source=("http://download.sourceforge.net/project/eq10q/$pkgname-${pkgver//_/-}.tar.gz"
        "$pkgname-ui.patch::http://kxstudio.sourceforge.net/Paste/repo/vbr0g")
md5sums=('45be6754c7503cc5af2441c461911a92'
         '58c7d07403407d744bd690f50e96f8b8')

prepare() {
  cd $pkgname-${pkgver//_/-}
  [ -d b ] || mkdir b

  # strip local path
  sed -i "s|/local||" CMakeLists.txt

  # fix http://sourceforge.net/p/eq10q/bugs/9/
  patch -p1 -i ../${source[1]%::*}
}

build() {
  cd $pkgname-${pkgver//_/-}/b
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr/lib/lv2
  make
}

package() {
  cd $pkgname-${pkgver//_/-}/b
  make DESTDIR="$pkgdir/" install
}
