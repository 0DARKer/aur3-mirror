# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=eq10q
pkgver=2_beta4_1
pkgrel=1
pkgdesc="An LV2 parametric equalizer audio plugin"
arch=('i686' 'x86_64')
url="http://eq10q.sourceforge.net/"
license=('GPL3')
groups=('lv2-plugins')
depends=('lv2' 'gtkmm')
makedepends=('cmake')
source=("http://download.sourceforge.net/project/eq10q/$pkgname-${pkgver//_/-}.tar.gz")
md5sums=('81b47ac71d27bdce8f55eb2c1cc4dfd3')

prepare() {
  cd "$srcdir/$pkgname-${pkgver//_/-}"
  sed -i "s|/local||" CMakeLists.txt # strip local path
}

build() {
  cd "$srcdir/$pkgname-${pkgver//_/-}"
  [ -d bld ] || mkdir bld && cd bld
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr/lib/lv2
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver//_/-}/bld"
  make DESTDIR="$pkgdir" install
}
