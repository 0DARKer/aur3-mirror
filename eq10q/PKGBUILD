# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=eq10q
pkgver=2_beta2
pkgrel=1
pkgdesc="An LV2 parametric equalizer audio plugin"
arch=('i686' 'x86_64')
url="http://eq10q.sourceforge.net/"
license=('GPL3')
groups=('lv2-plugins')
depends=('lv2' 'plotmm')
makedepends=('lv2-c++-tools' 'pstreams' 'cmake')
source=("http://download.sourceforge.net/project/eq10q/$pkgname-${pkgver/_/-}.tar.gz")
md5sums=('a5a6eca6b360201c67e9240673079f4f')

build() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"

  # strip /local
  sed -i "s|/local||" CMakeLists.txt

  [ -d bld ] || mkdir bld && cd bld
  cmake .. && make
}

package() {
  cd "$srcdir/$pkgname-${pkgver/_/-}/bld"
  make DESTDIR="$pkgdir" install

  # remove .svn folders
  rm -rf `find "$pkgdir" -type d -name ".svn"`
}
