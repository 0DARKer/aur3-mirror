# Maintainer: Hilton Medeiros <medeiros.hilton@gmail.com>

pkgname=angelscript
pkgver=2.30.0
pkgrel=2
pkgdesc="An extremely flexible cross-platform scripting library."
arch=('i686' 'x86_64')
url="http://www.angelcode.com/angelscript/"
license=('custom:ZLIB')
depends=('gcc-libs')
source=("http://www.angelcode.com/$pkgname/sdk/files/${pkgname}_${pkgver}.zip"
        LICENSE)
md5sums=('1b0ec7ead145cbcb252d9e1ce14f490d'
         '2c3eb74c82a0a8d5bb505a5988b71aec')

build() {
  cd "$srcdir/sdk/$pkgname/projects/gnuc"

  sed -i -e "s/)_s/)/g" Makefile
  sed -i -e "s/^\t\$(MAKE) install_shared$//g" Makefile

  #we can't use absolute paths on linking because we like moving the compiled libraries around
  sed -i -e "/soname/s/\$(LIBRARYDEST)//" Makefile

  make SHLIB=lib$pkgname-$pkgver.so PREFIX=/usr
}

package() {
  cd "$srcdir/sdk/$pkgname/projects/gnuc"

  install -d "$pkgdir"/usr/{lib,include} \
             "$pkgdir"/usr/share/{licenses,doc}/$pkgname \
             "$pkgdir"/usr/share/$pkgname \
             "$pkgdir"/usr/share/doc/$pkgname/{articles,manual}

  make install PREFIX="$pkgdir"/usr SHLIB=lib$pkgname-$pkgver.so
  #using this because the makefile creates symbolic links with full paths
  cd "$pkgdir/usr/lib/"
  cp "$srcdir/sdk/angelscript/lib/lib$pkgname-$pkgver.so" .
  ln -fs lib$pkgname-$pkgver.so lib$pkgname.so
  ln -fs lib$pkgname-$pkgver.so lib$pkgname.so.$pkgver

  chmod 644 "$pkgdir"/usr/include/*

  msg "Installing add-ons..."
  cd "$srcdir/sdk"
  cp -rf add_on "$pkgdir/usr/share/$pkgname/"

  msg "Installing documentation and license..."
  cd "$srcdir/sdk/docs"
  install -m644 articles/* "$pkgdir/usr/share/doc/$pkgname/articles"
  install -m644 manual/* "$pkgdir/usr/share/doc/$pkgname/manual"
  install -m644 index.html "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
