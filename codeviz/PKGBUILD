# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=codeviz
pkgver=1.0.11
_gccver=3.4.6
pkgrel=1
pkgdesc="A call graph generation utility for C/C++"
arch=('i686' 'x86_64')
url="http://www.csn.ul.ie/~mel/projects/codeviz/"
license=('GPL2')
depends=('perl' 'graphviz')
source=(http://www.csn.ul.ie/~mel/projects/codeviz/codeviz-$pkgver.tar.gz \
        ftp://ftp.gnu.org/pub/gnu/gcc/gcc-$_gccver/gcc-$_gccver.tar.gz \
        gcc_build_fixes.diff \
        gcc_pure64.diff)
install=codeviz.install
md5sums=('9de2506f8d87eebea86a93197ee58316'                                                                                                                                                                                                  
         'b0c915e554aaf475dde0571677c15a9c'                                                                                                                                                                                                  
         '6796b290d7f734ee9d92e1e38f363bd7'                                                                                                                                                                                                  
         'eb834abd7620a5f11492ee2c243b8346')

_gccprefix=/opt/gccgraph

build() {
  #####################
  # build patched gcc #
  #####################
  # unset the C(XX)FLAGS because they would break the compilation if they contained
  # options not supported in $_gccver
  unset CFLAGS
  unset CXXFLAGS
  cd "$srcdir"/gcc-$_gccver
  # apply codeviz patches
  patch -Np1 < "$srcdir"/$pkgname-$pkgver/compilers/gcc-patches/gcc-$_gccver-cdepn.diff || true
  # apply fixes needed for build
  patch -Np1 < "$srcdir"/gcc_build_fixes.diff || true
  # apply pure64 patch
  if [ "${CARCH}" = "x86_64" ]; then
    patch -Np1 < "$srcdir"/gcc_pure64.diff || true
  fi

  if [ ! -d "$srcdir"/gcc-build ]; then
    mkdir "$srcdir"/gcc-build
  fi
  cd "$srcdir"/gcc-build
  ../gcc-$_gccver/configure --prefix=$_gccprefix --enable-shared --enable-languages=c,c++ \
                            --enable-threads=posix --enable-__cxa_atexit --disable-multilib \
                            --enable-clocale=gnu --libdir=$_gccprefix/lib \
                            --libexecdir=$_gccprefix/lib
  make || true
  # now the build most likely failed
  # we have to fix some problems and try to build gcc again
  sed 's/.*undef HAVE_LIMITS_H.*/\#define HAVE_LIMITS_H 1/' -i $CHOST/libiberty/config.h
  sed 's/.*undef HAVE_STDLIB_H.*/\#define HAVE_STDLIB_H 1/' -i $CHOST/libiberty/config.h
  sed 's/.*undef HAVE_UNISTD_H.*/\#define HAVE_UNISTD_H 1/' -i $CHOST/libiberty/config.h
  echo "#undef HAVE_SYS_STAT_H" >> $CHOST/libiberty/config.h
  echo "#define HAVE_SYS_STAT_H 1" >> $CHOST/libiberty/config.h
  # repeat the build
  make
}

package() {
  # install gcc
  cd "$srcdir"/gcc-build
  make DESTDIR="$pkgdir" install
  
  # install codeviz
  cd "$srcdir/$pkgname-$pkgver"
  # install perl libs
  install -d -m755 "$pkgdir"/usr/lib/perl5/vendor_perl/CodeViz
  install -m444 lib/CodeViz/* "$pkgdir"/usr/lib/perl5/vendor_perl/CodeViz/
  # install codeviz binaries
  install -d -m755 "$pkgdir"/usr/bin
  install -m755 bin/* "$pkgdir"/usr/bin/
}

# vim:set ts=2 sw=2 et:
