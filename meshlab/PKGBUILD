# Maintainer: Jonathan Liu <net147@gmail.com>
pkgname=meshlab
pkgver=1.3.0a
pkgrel=3
pkgdesc="System for processing and editing triangular meshes"
arch=('i686' 'x86_64')
url="http://meshlab.sourceforge.net/"
license=('GPL2')
depends=('bzip2' 'desktop-file-utils' 'lapack' 'openssl' 'qt')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/meshlab/meshlab/MeshLab%20v${pkgver//[a-z]/}/MeshLabSrc_AllInc_v${pkgver//./}.tgz"
        "import_ply.h.patch"
        "lapack.patch"
        "rpath.patch"
        "qt-4.8.patch"
        "meshlab.1"
        "meshlabserver.1"
        "meshlab_32x32.xpm"
        "meshlab_48x48.xpm"
        "meshlab.desktop")
md5sums=('46c14a0fce7bed03113d54807f3c19bc'
         '430e19576d894273b4ede74924fc271f'
         '4139d3217f1540c67306545213126391'
         '017c0defde5488ef15c179515e7bb527'
         '958d6ad60dc64fbe77d50dc3f977133d'
         '32a52b0a8dab1b4816b028b463e7fd9c'
         '0af205a93961dbfcb6b003765f79c4cf'
         'fee344c5e6f45a075f98a7fb9c6bd0a4'
         '9e6a653ea2040e37e71dc67e681ba84e'
         'd6c143a12a3703f053cf8d7c85cfe34d')

build() {
  cd "$srcdir"
  # fix Qt 4.8 compatibility
  patch -Np0 -i "$srcdir/qt-4.8.patch"

  cd "$srcdir/vcglib"
  # add missing include
  patch -Np1 -i "$srcdir/import_ply.h.patch"

  cd "$srcdir/meshlab/src"
  # build levmar with lapack
  patch -Np2 -i "$srcdir/lapack.patch"
  # fix rpath
  patch -Np2 -i "$srcdir/rpath.patch"

  # build external libraries
  cd external
  qmake -recursive external.pro
  make

  # build meshlab
  cd ..
  qmake -recursive meshlab_full.pro
  make
}

package() {
  cd "$srcdir/meshlab/src"

  # install meshlab
  install -d -m755 "$pkgdir/opt"
  cp -a distrib "$pkgdir/opt/meshlab"

  # add symbolic links for executables
  install -d -m755 "$pkgdir/usr/bin"
  ln -s ../../opt/meshlab/meshlab "$pkgdir/usr/bin/meshlab"
  ln -s ../../opt/meshlab/meshlabserver "$pkgdir/usr/bin/meshlabserver"

  # install man pages
  install -d -m755 "$pkgdir/usr/share/man/man1"
  install -m644 "$srcdir/meshlab.1" "$pkgdir"/usr/share/man/man1
  install -m644 "$srcdir/meshlabserver.1" "$pkgdir"/usr/share/man/man1

  # install icons
  install -d -m755 "$pkgdir/usr/share/pixmaps"
  install -m644 "$srcdir/meshlab_32x32.xpm" "$pkgdir/usr/share/pixmaps"
  install -m644 "$srcdir/meshlab_48x48.xpm" "$pkgdir/usr/share/pixmaps"

  # install desktop entry
  install -d -m755 "$pkgdir/usr/share/applications"
  install -m644 "$srcdir/meshlab.desktop" "$pkgdir/usr/share/applications"
}

# vim:set ts=2 sw=2 et:
