# Contributor: Jonathan Liu <net147@gmail.com>
pkgname=meshlab
pkgver=1.2.3a
pkgrel=2
pkgdesc="System for processing and editing triangular meshes"
arch=('i686' 'x86_64')
url="http://meshlab.sourceforge.net/"
license=('GPL2')
depends=('bzip2' 'desktop-file-utils' 'lapack' 'openssl' 'qt')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/meshlab/meshlab/MeshLab%20v${pkgver//[a-z]/}/MeshLabSrc_AllInc_v${pkgver//./}.tgz"
        "lapack.patch"
        "meshlab.qrc.patch"
        "rpath.patch"
        "meshlab.1"
        "meshlabserver.1"
        "meshlab_32x32.xpm"
        "meshlab_48x48.xpm"
        "meshlab.desktop")
md5sums=('f1d7abbe92ac1a64913b505058399eec'
         '4139d3217f1540c67306545213126391'
         '83f4b5f00a1f7ba2c9a484070d6e6bb9'
         '017c0defde5488ef15c179515e7bb527'
         '97a1ad63d32ea4071d2a9c86620f5423'
         '1a4602257dc5664c1b654ac88fc73935'
         'fee344c5e6f45a075f98a7fb9c6bd0a4'
         '9e6a653ea2040e37e71dc67e681ba84e'
         'd6c143a12a3703f053cf8d7c85cfe34d')

build() {
  cd "$srcdir/MeshLabSrc_AllInc_v${pkgver//./}/meshlab/src"

  # build levmar with lapack
  patch -Np2 -i "$srcdir/lapack.patch" || return 1
  # remove unused missing resource
  patch -Np2 -i "$srcdir/meshlab.qrc.patch" || return 1
  # fix rpath
  patch -Np2 -i "$srcdir/rpath.patch" || return 1

  # build external libraries
  cd external
  qmake -recursive external.pro
  make || return 1

  # build meshlab
  cd ..
  qmake -recursive meshlabv12.pro
  make || return 1

  # install meshlab
  install -d -m755 "$pkgdir/opt"
  cp -a distrib "$pkgdir/opt/meshlab"

  # add symbolic links for executables
  install -d -m755 "$pkgdir/usr/bin"
  ln -s ../../opt/meshlab/meshlab "$pkgdir/usr/bin/meshlab"
  ln -s ../../opt/meshlab/meshlabserver "$pkgdir/usr/bin/meshlabserver"

  # install man pages
  install -d -m755 "$pkgdir/usr/share/man/man1"
  install -m644 "$srcdir/meshlab.1" "$pkgdir"/usr/share/man/man1
  install -m644 "$srcdir/meshlabserver.1" "$pkgdir"/usr/share/man/man1

  # install icons
  install -d -m755 "$pkgdir/usr/share/pixmaps"
  install -m644 "$srcdir/meshlab_32x32.xpm" "$pkgdir/usr/share/pixmaps"
  install -m644 "$srcdir/meshlab_48x48.xpm" "$pkgdir/usr/share/pixmaps"

  # install desktop entry
  install -d -m755 "$pkgdir/usr/share/applications"
  install -m644 "$srcdir/meshlab.desktop" "$pkgdir/usr/share/applications"
}

# vim:set ts=2 sw=2 et:
