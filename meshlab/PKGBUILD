# Maintainer: Jonathan Liu <net147@gmail.com>
pkgname=meshlab
pkgver=1.3.2
pkgrel=4
pkgdesc="System for processing and editing triangular meshes"
arch=('i686' 'x86_64')
url="http://meshlab.sourceforge.net/"
license=('GPL2')
depends=('bzip2' 'desktop-file-utils' 'glu' 'lapack' 'openssl' 'qt4')
install="${pkgname}.install"
source=("http://downloads.sourceforge.net/project/meshlab/meshlab/MeshLab%20v${pkgver//[a-z]/}/MeshLabSrc_AllInc_v${pkgver//./}.tgz"
        "gcc-4.7.patch"
        "lapack.patch"
        "rpath.patch"
        "qt-4.8.patch"
        "meshlab.1"
        "meshlabserver.1"
        "meshlab.desktop")
noextract=("MeshLabSrc_AllInc_v${pkgver//./}.tgz")
md5sums=('3cba61f6d34559f98129d9d0a3126f81'
         '8d0d8397902d53bd27556c78d676e793'
         '4139d3217f1540c67306545213126391'
         '8e6f41263aef2214e46288c9d779c60d'
         '958d6ad60dc64fbe77d50dc3f977133d'
         '32a52b0a8dab1b4816b028b463e7fd9c'
         '0af205a93961dbfcb6b003765f79c4cf'
         'bf7ad5ae4a408ba2b66ad9a06ca24346')

build() {
  cd "${srcdir}"
  tar -zxf "MeshLabSrc_AllInc_v${pkgver//./}.tgz"

  # fix compile errors with GCC 4.7
  patch -Np0 -i "${srcdir}/qt-4.8.patch"
  # fix Qt 4.8 compatibility
  patch -Np1 -i "${srcdir}/gcc-4.7.patch"

  cd "${srcdir}/meshlab/src"
  # build levmar with lapack
  patch -Np2 -i "${srcdir}/lapack.patch"
  # fix rpath
  patch -Np2 -i "${srcdir}/rpath.patch"

  # build external libraries
  cd external
  qmake-qt4 -recursive external.pro
  make

  # build meshlab
  cd ..
  qmake-qt4 -recursive meshlab_full.pro
  make
}

package() {
  cd "${srcdir}/meshlab/src"

  # install meshlab
  install -d -m755 "${pkgdir}/opt"
  cp -a distrib "${pkgdir}/opt/meshlab"

  # add symbolic links for executables
  install -d -m755 "${pkgdir}/usr/bin"
  ln -s ../../opt/meshlab/meshlab "${pkgdir}/usr/bin/meshlab"
  ln -s ../../opt/meshlab/meshlabserver "${pkgdir}/usr/bin/meshlabserver"

  # install man pages
  install -d -m755 "${pkgdir}/usr/share/man/man1"
  install -m644 "${srcdir}/meshlab.1" "${pkgdir}"/usr/share/man/man1
  install -m644 "${srcdir}/meshlabserver.1" "${pkgdir}"/usr/share/man/man1

  # install icons
  install -d -m755 "${pkgdir}/usr/share/pixmaps"
  install -m644 "${srcdir}/meshlab/src/meshlab/images/eye_cropped.png" "${pkgdir}/usr/share/pixmaps/meshlab.png"

  # install desktop entry
  install -d -m755 "${pkgdir}/usr/share/applications"
  install -m644 "${srcdir}/meshlab.desktop" "${pkgdir}/usr/share/applications"
  sed -i "s#Version=.*#Version=$pkgver#" "${pkgdir}/usr/share/applications/meshlab.desktop"
}

# vim:set ts=2 sw=2 et:
