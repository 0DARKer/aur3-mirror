# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Based off of dibblethewrecker's PKGBUILD for grass in community.

pkgname=grass-svn
pkgver=45039
pkgrel=1
pkgdesc='Geographic Information System (GIS) used for geospatial data management and analysis, image processing, graphics/maps production, spatial modeling, and visualization.'
arch=('i686' 'x86_64')
url='http://grass.itc.it/index.php'
license=('GPL')
conflicts=('grass')
provides=('grass')
depends=('gdal' 'tk' 'sqlite3' 'xorg-server' 'python' 'mesa' 'swig' 'proj' 'libjpeg' 'libpng' 'libtiff')
makedepends=('mysql' 'postgresql' 'r' 'fftw' 'freetype2' 'subversion')
optdepends=('fftw: required for i.fft and i.ifft modules'
            'postgresql: PostgreSQL database interface'
            'r: R language interface'
            'lapack: required for GMATH library'
            'blas: required for GMATH library'
            'xorg-server: required for the graphical interface')
options=('!libtool' '!makeflags')
install=grass.install
source=('grass.desktop'
        'grass.sh'
        'grass.png')
md5sums=('c0d669c77f60f8544a34a55c5e9e087c'
         'db93730dd330f1539bd31b4373e1c00e'
         'ae778ed261a40b078a8465994a0eb25e')

_svntrunk='https://svn.osgeo.org/grass/grass/branches/releasebranch_6_4'
_svnmod='grass64_release'

build() {
  if [ -d ${_svnmod} ]; then
    cd ${_svnmod}
    svn up
  else
    svn co ${_svntrunk} ${_svnmod}
    cd ${_svnmod}
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."


  # python2 fix
  sed -i 's_python $< $(GISBASE) > $@_python2 $< $(GISBASE) > $@_' gui/wxpython/Makefile
  for file in $(find . -name '*.py' -print); do
    #sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    #sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
    sed -i '1 i#!/usr/bin/python2' $file
  done

  find -type f -exec sed -i 's/$(PYTHON)/&2/g' {} \;

  export PYTHON=python2

  PYTHON=python2 ./configure \
    --prefix=/opt \
    --with-mysql-includes=/usr/include/mysql \
    --with-mysql \
    --with-sqlite \
    --with-postgres \
    --with-fftw \
    --with-gdal=/usr/bin/gdal-config \
    --with-python=/usr/bin/python2-config \
    --with-blas \
    --with-lapack \
    --with-proj-libs=/usr/lib \
    --with-proj-includes=/usr/include \
    --with-proj-share=/usr/share/proj \
    --with-fftw-includes=/usr/include \
    --with-fftw-libs=/usr/lib

  make
}

package() {
  cd ${_svnmod}

  make \
    INST_DIR=${pkgdir}/opt/grass-${pkgver} \
    BINDIR=${pkgdir}/usr/bin \
    install

  # fix $GISBASE path
  sed -i "s|GISBASE=${pkgdir}/opt/grass-${pkgver}|GISBASE=/opt/grass-${pkgver}|g" \
    ${pkgdir}/usr/bin/grass64

  # strip html docs
  #rm -r ${pkgdir}/opt/grass-${pkgver}/docs/html

  # install profile.d file
  install -D ${srcdir}/grass.sh \
    ${pkgdir}/etc/profile.d/grass.sh

  # install some freedesktop.org compatibility
  install -D -m644 ${srcdir}/grass.desktop \
    ${pkgdir}/usr/share/applications/grass.desktop

  install -D -m644 ${srcdir}/grass.png \
    ${pkgdir}/usr/share/pixmaps/grass.png

  # create a symlink for version work around
  ln -sf /opt/grass-${pkgver} ${pkgdir}/opt/grass

  install -d ${pkgdir}/etc/ld.so.conf.d/
  echo '/opt/grass/lib' > ${pkgdir}/etc/ld.so.conf.d/grass.conf
}
