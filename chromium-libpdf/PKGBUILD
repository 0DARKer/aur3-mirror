# Maintainer: Andrew Webley <unsuspectinghero@gmail.com>
# Contributor: Dmitriy Morozov <archlinux@foxcub.org>


pkgname="chromium-libpdf"
#The pkgver is the same as for google chrome dev. The library isn't meant to be distributed on its own, and no version information exists.
pkgver="28.0.1500.20"
pkgrel="1"
pkgdesc="A PDF Viewer for Chromium and Chromium-based browsers"
url="http://wiki.archlinux.org/index.php/Chromium#libpdf.so"
arch=("i686" "x86_64")
license=("custom:chrome")
optdepends=("chromium: default browser to use plugin in"
			"chromium-dev: development version of default browser, requires modifying the PKGBUILD"
			"iron: alternate google-chrome based browser, requires modifying the PKGBUILD")
sha512sums=("2d09d2ca3c960f38463ed742a2f038e919363fe2cb8d65d86cd4672559b44369037d94f9a09ede478fadd20b4a6dc9271e8d1af1f2cc2bc92ed90d0a4810ac67")
if [ "$CARCH" = "i686" ]; then
	_arch="i386"
    sha512sums+=("5762dab6f8a660e51f57a9ac5e430c5144b3f0210c9fc5f97fa0853378f6b7228ffe0bd88f04656db0a5f67860a56dc0de8243f218d10dea63c469044deb9828")
elif [ "$CARCH" = "x86_64" ]; then
	_arch="amd64"
	sha512sums+=("33a58c2e328c397634cb8060e4da13e7ae2eff7eb47e4182abb26da42ebe77ef3bc48632a6235234b49839c24265e0c67fb7bfdfc6b3c7c3d6f6a7f120001282")
fi
source=(LICENSE
		"https://dl.google.com/linux/direct/google-chrome-unstable_current_${_arch}.deb")
build() {
	cd "$srcdir"
	bsdtar xvf "google-chrome-unstable_current_$_arch.deb" data.tar.lzma
	bsdtar xvf data.tar.lzma opt/google/chrome/libpdf.so
}
package(){
	#Uncomment the relevant line below to install the library in the correct place if you're running something other than chromium.
	#chromium (Default)
	install -Dm755 opt/google/chrome/libpdf.so "$pkgdir/usr/lib/chromium/libpdf.so"
    #chromium-dev
    #install -Dm755 opt/google/chrome/libpdf.so "$pkgdir/usr/lib/chromium-dev/libpdf.so"
	#iron-browser
    #install -Dm755 opt/google/chrome/libpdf.so "$pkgdir/opt/iron/libpdf.so"
	install -Dm755 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
