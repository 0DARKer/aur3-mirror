# Maintainer: Andrew Webley <unsuspectinghero@gmail.com>
# Contributor: Dmitriy Morozov <archlinux@foxcub.org>


pkgname="chromium-libpdf"
#The pkgver is the same as for google chrome unstable, because this library isn't meant to be distributed on its own, and I cannot find any proper version information.
pkgver="27.0.1453.12"
pkgrel=1
pkgdesc="PDF Viewer for Chromium"
url="http://wiki.archlinux.org/index.php/Chromium#libpdf.so"
arch=('i686' 'x86_64')
license=('custom')
optdepends=('chromium: default browser to use plugin in'
'chromium-dev: development version of default browser, requires modifying the PKGBUILD'
'iron: alternate google-chrome based browser, requires modifying the PKGBUILD')
sha512sums=('2d09d2ca3c960f38463ed742a2f038e919363fe2cb8d65d86cd4672559b44369037d94f9a09ede478fadd20b4a6dc9271e8d1af1f2cc2bc92ed90d0a4810ac67')
if [ "$CARCH" = "i686" ]; then
	_arch="i386"
    sha512sums+=('71c3a4b2b481e3a8928f88942658a3dd23f6b110bad2e4200091e7b15942ac967b86b47416872e1b2bb9225782ae7cd3645704c021abe5ab4e6dcd0bbeede50b')
elif [ "$CARCH" = "x86_64" ]; then
	_arch="amd64"
	sha512sums+=('214b9b31e154267ed0fd58db12f4087498cdb0df126903982e3f6c2adb5901188cfa496368cea814cef7f87f4f8182ba6bbdddd122ac9b587612c2b40bdaaf7f')
fi
source=(LICENSE
        "https://dl.google.com/linux/direct/google-chrome-unstable_current_${_arch}.deb"
)


build() {
	cd "$srcdir"
	#This extracts the .deb file to only get the relevant tarball, and then extracts that tarball to get the relevant library file.
	bsdtar xvf "google-chrome-unstable_current_${_arch}.deb" data.tar.lzma
	bsdtar xvf data.tar.lzma opt/google/chrome/libpdf.so
}
package(){
	#These lines install the library to the correct directory and with the correct permissions.
    #Uncomment relevant lines if you're using something other than chromium, and comment out line 36.
	install -Dm755 opt/google/chrome/libpdf.so "${pkgdir}/usr/lib/chromium/libpdf.so"
    #chromium-dev
    #install -Dm755 opt/google/chrome/libpdf.so "${pkgdir}/usr/lib/chromium-dev/libpdf.so"
	#iron-browser
    #install -Dm755 opt/google/chrome/libpdf.so "${pkgdir}/opt/iron/libpdf.so"
    install -Dm755 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
