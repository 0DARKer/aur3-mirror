# Maintainer: Andrew Webley <unsuspectinghero@gmail.com>
# Contributor: Dmitriy Morozov <archlinux@foxcub.org>


pkgname="chromium-libpdf"
#The pkgver is the same as for google chrome dev. The library isn't meant to be distributed on its own, and no version information exists.
pkgver="29.0.1516.3"
pkgrel="1"
pkgdesc="A PDF Viewer for Chromium and Chromium-based browsers"
url="http://wiki.archlinux.org/index.php/Chromium#libpdf.so"
arch=("i686" "x86_64")
license=("custom:chrome")
depends=("glib2")
optdepends=("chromium: default browser to use plugin in"
			"chromium-dev: development version of default browser, requires modifying the PKGBUILD"
			"iron: alternate google-chrome based browser, requires modifying the PKGBUILD")
sha512sums=("2d09d2ca3c960f38463ed742a2f038e919363fe2cb8d65d86cd4672559b44369037d94f9a09ede478fadd20b4a6dc9271e8d1af1f2cc2bc92ed90d0a4810ac67")
if [ "$CARCH" = "i686" ]; then
	_arch="i386"
    sha512sums+=("f16a9979ff6210a1142aee82bb9f867b65f08ea3d44bbc4b75eb9c35de0bca66b7e7e7f9a43ba20c1d993c2eab21466a8a373701aff770b1e715ca945c43a0c7")
elif [ "$CARCH" = "x86_64" ]; then
	_arch="amd64"
	sha512sums+=("8118e3068489664b6d39090469fdb5a0b3ee132b1404e514c91915312d4603216b3673834d701fca5406303396f3f3fbe9ef7d16cc195b198168f6dedd442fbf")
fi
source=(LICENSE
		"https://dl.google.com/linux/direct/google-chrome-unstable_current_${_arch}.deb")
build() {
	cd "$srcdir"
	bsdtar xvf "google-chrome-unstable_current_$_arch.deb" data.tar.lzma
	bsdtar xvf data.tar.lzma opt/google/chrome/libpdf.so
}
package(){
	#Uncomment the relevant line below to install the library in the correct place if you're running something other than chromium.
	#chromium (Default)
	install -Dm755 opt/google/chrome/libpdf.so "$pkgdir/usr/lib/chromium/libpdf.so"
    #chromium-dev
    #install -Dm755 opt/google/chrome/libpdf.so "$pkgdir/usr/lib/chromium-dev/libpdf.so"
	#iron-browser
    #install -Dm755 opt/google/chrome/libpdf.so "$pkgdir/opt/iron/libpdf.so"
	install -Dm755 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
