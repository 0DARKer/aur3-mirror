# Maintainer: Andrew Webley <unsuspectinghero@gmail.com>
# Contributor: Dmitriy Morozov <archlinux@foxcub.org>


pkgname="chromium-libpdf"
#The pkgver is the same as for google chrome dev. The library isn't meant to be distributed on its own, and no version information exists.
pkgver="28.0.1500.11"
pkgrel="1"
pkgdesc="A PDF Viewer for Chromium and Chromium-based browsers"
url="http://wiki.archlinux.org/index.php/Chromium#libpdf.so"
arch=("i686" "x86_64")
license=("custom:chrome")
optdepends=("chromium: default browser to use plugin in"
			"chromium-dev: development version of default browser, requires modifying the PKGBUILD"
			"iron: alternate google-chrome based browser, requires modifying the PKGBUILD")
sha512sums=("2d09d2ca3c960f38463ed742a2f038e919363fe2cb8d65d86cd4672559b44369037d94f9a09ede478fadd20b4a6dc9271e8d1af1f2cc2bc92ed90d0a4810ac67")
if [ "$CARCH" = "i686" ]; then
	_arch="i386"
    sha512sums+=("59855e71c2eab096530640641a7fa7bc5c9f04a25be4be27a9e5c0f83cf905b86ac722fbd62369737d868e05423df47a7f8514ddaa9fdc79a33f4b2bab4987f3")
elif [ "$CARCH" = "x86_64" ]; then
	_arch="amd64"
	sha512sums+=("f41bee390e0f251b99875c382f9329411847f93717330064f5308dfbbb14f033c5fee28862c761e55ed6b8ea029d61ffef1ebd052d7e44ad193349ce4c6bd806")
fi
source=(LICENSE
		"https://dl.google.com/linux/direct/google-chrome-unstable_current_${_arch}.deb")
build() {
	cd "$srcdir"
	bsdtar xvf "google-chrome-unstable_current_$_arch.deb" data.tar.lzma
	bsdtar xvf data.tar.lzma opt/google/chrome/libpdf.so
}
package(){
	#Uncomment the relevant line below to install the library in the correct place if you're running something other than chromium.
	#chromium (Default)
	install -Dm755 opt/google/chrome/libpdf.so "$pkgdir/usr/lib/chromium/libpdf.so"
    #chromium-dev
    #install -Dm755 opt/google/chrome/libpdf.so "$pkgdir/usr/lib/chromium-dev/libpdf.so"
	#iron-browser
    #install -Dm755 opt/google/chrome/libpdf.so "$pkgdir/opt/iron/libpdf.so"
	install -Dm755 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
