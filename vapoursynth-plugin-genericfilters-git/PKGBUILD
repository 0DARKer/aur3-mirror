# Maintainer:  Gustavo Alvarez <sl1pkn07@gmail.com>

_plug=genericfilters
pkgname=vapoursynth-plugin-${_plug}-git
pkgver=20131205.95bc18a
pkgrel=1
pkgdesc="Plugin for Vapoursynth: ${_plug} (GIT version)"
arch=('i686' 'x86_64')
url="https://github.com/myrsloik/${_plug}"
license=('LGPL2.1')
depends=('vapoursynth')
makedepends=('git')
provides=("vapoursynth-plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}")
source=("${_plug}::git+https://github.com/myrsloik/GenericFilters.git")
md5sums=('SKIP')
_gitname="${_plug}"

pkgver() {
  cd "${_gitname}"
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

build() {
  cd "${_gitname}/src"
  ./configure --install="${pkgdir}/usr/lib/vapoursynth"
  make
}

package(){
  cd "${_gitname}/src"
  make install
  install -Dm644 ../readme.rst "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/README"
}
