# Contributor: graysky <graysky AT archlinux dot us>

pkgname=monitorix-git
_pkgname=monitorix
pkgver=2c44fa1
pkgrel=1
pkgdesc='A lightweight system monitoring tool that uses rrd databases. Git version.'
arch=('any')
url='http://www.monitorix.org'
license=('GPL')
depends=('perl' 'perl-cgi' 'perl-mailtools' 'perl-mime-lite' 'perl-libwww'
'perl-dbi' 'perl-xml-simple' 'perl-config-simple' 'perl-config-general'
'rrdtool' 'perl-http-server-simple')
optdepends=(
	'anything-sync-daemon: offload your databases to tmpfs to save i/o to your disk.'
	'hddtemp: enable support for hdd temp monitoring.'
	'lm_sensors: enable support for system temp monitoring.'
	'nvidia: enable support for nVidia card temp and usage monitoring.'
	'smartmontools: enable support for hdd bad sector monitoring.'
	'terminus-font: if graphs do not contain characters, you may need this font package.')
conflicts=$_pkgname
replaces=$_pkgname
backup=(etc/$_pkgname/$_pkgname.conf etc/$_pkgname.conf)
install=readme.install
source=("git://github.com/mikaku/$_pkgname.git")
sha256sums=('SKIP')
_basedir="srv/http/monitorix"
_libdir="var/lib/$_pkgname"


pkgver() {
	cd ${_pkgname}
	git describe --always | sed 's|-|.|g'
}

prepare() {
	cd "$_pkgname"

	# use Arch default location for http servers and disable built-in httpd
	sed -e '/^base_dir/ s,var\/lib\/monitorix\/www,srv\/http\/monitorix,' \
		-e '/^<httpd_builtin>/{$!N; s/y/n/}' <$_pkgname.conf >$_pkgname.conf.Arch_modified
}

package() {
	cd "$_pkgname"

	install -Dm755 $_pkgname "$pkgdir/usr/bin/$_pkgname"
	install -Dm755 $_pkgname.cgi "$pkgdir/$_basedir/cgi/$_pkgname.cgi"
	install -Dm644 $_pkgname.conf.Arch_modified "$pkgdir/etc/$_pkgname/$_pkgname.conf"
	
	for i in lib/*.pm; do
		install -Dm644 "$i" "$pkgdir/usr/lib/$_pkgname/${i##lib/}"
	done

	for i in Changes COPYING README*; do
		install -Dm644 "$i" "$pkgdir/usr/share/doc/$_pkgname/$i"
	done
	
	install -Dm644 logo_bot.png "$pkgdir/$_basedir/logo_bot.png"
	install -m644 logo_top.png "$pkgdir/$_basedir/logo_top.png"
	install -m644 monitorixico.png "$pkgdir/$_basedir/monitorixico.png"
	install -Dm644 docs/$_pkgname.service "$pkgdir/usr/lib/systemd/system/$_pkgname.service"
	sed -i 's/EnvironmentFiles/EnvironmentFile/' "$pkgdir/usr/lib/systemd/system/$_pkgname.service"
	install -Dm644 docs/$_pkgname.logrotate "$pkgdir/etc/logrotate.d/$_pkgname"
	install -m644 docs/$_pkgname-alert.sh "$pkgdir/usr/share/doc/$_pkgname/$_pkgname-alert.sh"
	install -m644 docs/$_pkgname-apache.conf "$pkgdir/usr/share/doc/$_pkgname/$_pkgname-apache.conf"
	install -Dm644 man/man5/$_pkgname.conf.5 "$pkgdir/usr/share/man/man5/$_pkgname.conf.5"
	install -Dm644 man/man8/$_pkgname.8 "$pkgdir/usr/share/man/man8/$_pkgname.8"
	install -dm777 "$pkgdir/$_basedir/imgs"

	for i in reports/*.html; do
		install -Dm644 "$i" "$pkgdir/$_libdir/reports/${i##reports/}"
	done
}
