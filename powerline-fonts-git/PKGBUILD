# Maintainer: Devin Christensen <quixoten at gmail dot com>
pkgname=powerline-fonts-git
_gitname=powerline-fonts
pkgver=a6c24b7
pkgrel=1
pkgdesc="Powerline fonts for X11 and the console"
arch=('any')
url=('https://github.com/Lokaltog/powerline-fonts')
license=('CPL')
depends=('fontconfig' 'xorg-font-utils')
makedepends=('git')
conflicts=('powerline-fonts')
install=$pkgname.install
source=('git://github.com/Lokaltog/powerline-fonts.git'
        'Monaco_Linux-Powerline.ttf')
md5sums=('SKIP' '0ee621b836e612113cf497d810301e2f')

pkgver() {
  cd $_gitname
  # Use the tag of the last commit
  git describe --always | sed 's|-|.|g'
}

package() {
  cp="cp -dpr --no-preserve=ownership"
  install -d "$pkgdir/usr/share/fonts/TTF"
  install -d "$pkgdir/usr/share/fonts/misc"
  install -d "$pkgdir/usr/share/kbd/consolefonts"
  find $srcdir/$_gitname -iname "*.[ot]tf" -exec $cp {} $pkgdir/usr/share/fonts/TTF \;
  find $srcdir/$_gitname -iname "*.pcf.gz" -exec $cp {} $pkgdir/usr/share/fonts/misc \;
  find $srcdir/$_gitname -iname "*.psf.gz" -exec $cp {} $pkgdir/usr/share/kbd/consolefonts \;
}
