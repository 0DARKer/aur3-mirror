# $Id: PKGBUILD 44620 2009-07-02 21:38:57Z jgc $
# Maintainer: MaÃ«l Lavault <moimael@neuf.fr>

pkgname=glade-git
pkgver=20100612
pkgrel=2
pkgdesc="user interface builder for GTK+ and GNOME."
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('gtk2>=2.20' 'libxml2>=2.6.32' 'desktop-file-utils')
makedepends=('intltool' 'pkgconfig' 'python>=2.6' 'gtk-doc')
optdepends=('python: Python widgets support')
options=('!libtool')
url="http://glade.gnome.org/"
install=glade.install
source=()
md5sums=()

_gitroot="git://git.gnome.org/glade3"
_gitname="glade3"

build() {
cd ${srcdir}
  msg "Connecting to the GIT server...."
  
  if [[ -d $srcdir/$_gitname ]] ; then
    cd $_gitname
    git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi

  msg "GIT checkout done"
  msg "Starting make..."
  
  rm -rf $srcdir/$_gitname-build
  git clone $srcdir/$_gitname $srcdir/$_gitname-build
  
  cd $srcdir/$_gitname-build

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
