# Maintainer: William A. Kennington III <william@wkennington.com>
pkgname=jailkit
pkgver=2.16
pkgrel=3
pkgdesc="Chroot jailing scripts"
arch=('i686' 'x86_64')
url="http://olivier.sessink.nl/jailkit/"
license=('GPL')
depends=('glibc' 'python2')
backup=(etc/jailkit/jk_{check,chrootsh,init,lsh,socketd,update}.ini)
install="$pkgname.install"
source=("http://olivier.sessink.nl/jailkit/$pkgname-$pkgver.tar.gz")
sha512sums=('beb47ab23ede55fd1e677bdf0cdd331f2ddfb134f53caa7c7c2c593fa030ae27ece2f7bc0dd0984db05623bec29d28941513b00afdb32d77f9f3bd73b0f26290')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/jailkit \
    PYTHONINTERPRETER=/usr/bin/python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
