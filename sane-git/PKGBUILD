#Maintainer: Jesse Jaara <jesse.jaara@gmail.com>

pkgname=sane-git
pkgver=8002.f67f37e
pkgrel=1
pkgdesc="Scanner Access Now Easy"
url="http://www.sane-project.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libgphoto2' 'libusbx' 'libcups'
         'libieee1284' 'v4l-utils' 'net-snmp')
makedepends=('texlive-latexextra')
install=sane.install

backup=(etc/sane.d/{abaton.conf,agfafocus.conf,apple.conf,artec.conf,artec_eplus48u.conf,avision.conf,bh.conf,canon.conf,canon630u.conf,canon_dr.conf,canon_pp.conf,cardscan.conf,coolscan2.conf,coolscan3.conf,coolscan.conf,dc25.conf,dc210.conf,dc240.conf,dell1600n_net.conf,dll.conf,dmc.conf,epjitsu.conf,epson.conf,epson2.conf,fujitsu.conf,genesys.conf,gphoto2.conf,gt68xx.conf,hp.conf,hp3900.conf,hp4200.conf,hp5400.conf,hpsj5s.conf,hs2p.conf,ibm.conf,kodak.conf,kodakaio.conf,leo.conf,lexmark.conf,ma1509.conf,magicolor.conf,matsushita.conf,microtek.conf,microtek2.conf,mustek.conf,mustek_pp.conf,mustek_usb.conf,nec.conf,net.conf,p5.conf,pie.conf,pixma.conf,plustek.conf,plustek_pp.conf,qcam.conf,ricoh.conf,rts8891.conf,s9036.conf,saned.conf,sceptre.conf,sharp.conf,sm3840.conf,snapscan.conf,sp15c.conf,st400.conf,stv680.conf,tamarack.conf,teco1.conf,teco2.conf,teco3.conf,test.conf,u12.conf,umax.conf,umax1220u.conf,umax_pp.conf,xerox_mfp.conf,v4l.conf} etc/xinetd.d/sane)

options=(!libtool)
conflicts=('sane')
provides=('sane')
source=("sane::git://git.debian.org/sane/sane-backends.git"
'network.patch'
'saned.service'
'saned.socket'
'sane.xinetd')

pkgver() {
  cd "${srcdir}/sane"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "${srcdir}/sane"

   # fix http://vasks.debian.org/tracker/?func=detail&atid=410366&aid=313760&group_id=30186
   patch -Np1 -i ${srcdir}/network.patch
}

build() {
  cd "${srcdir}/sane"

  ./configure --prefix=/usr \
              --sbindir=/usr/bin \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --with-docdir=/usr/share/doc/sane \
              --enable-avahi \
              --enable-pthread \
              --disable-rpath \
              --enable-libusb_1_0 \
              --disable-locking

  make
}

package () {
  cd "${srcdir}/sane"

  make DESTDIR="${pkgdir}" install
  # fix hp officejets
  echo "#hpaio" >> "${pkgdir}/etc/sane.d/dll.conf"
  # install udev files
  install -D -m0644 tools/udev/libsane.rules \
    "${pkgdir}/usr/lib/udev/rules.d/49-sane.rules"

  # install xinetd file
  install -D -m644 "${srcdir}/sane.xinetd" "${pkgdir}/etc/xinetd.d/sane"

  # Install the pkg-config file
  install -D -m644 ${srcdir}/saned.socket \
      "${pkgdir}/usr/lib/systemd/system/saned.socket"
  install -D -m644 ${srcdir}/saned.service \
      "${pkgdir}/usr/lib/systemd/system/saned@.service"
}

md5sums=('SKIP'
         '44e77692c52db15b0f530a1d9a8e1296'
         '3f9fe36b0c4ff0b01b53b1cffa5d9700'
         'e57e9e15528f47f5f1b3f1411135ed5d'
         '203af92f59d7b2443ae1a7604ed95b55')
