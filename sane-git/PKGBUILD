#Maintainer: Jesse Jaara <jesse.jaara@gmail.com>

pkgname=sane-git
pkgver=20120602
pkgrel=1
pkgdesc="Scanner Access Now Easy"
url="http://www.sane-project.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libtiff>=4.0.0' 'libgphoto2>=2.4.7-2' 'libieee1284' 'v4l-utils' 'avahi' 'net-snmp')
install=sane.install
backup=(etc/sane.d/{abaton.conf,agfafocus.conf,apple.conf,artec.conf,artec_eplus48u.conf,avision.conf,bh.conf,canon.conf,canon630u.conf,canon_dr.conf,canon_pp.conf,cardscan.conf,coolscan2.conf,coolscan3.conf,coolscan.conf,dc25.conf,dc210.conf,dc240.conf,dell1600n_net.conf,dll.conf,dmc.conf,epjitsu.conf,epson.conf,epson2.conf,fujitsu.conf,genesys.conf,gphoto2.conf,gt68xx.conf,hp.conf,hp3900.conf,hp4200.conf,hp5400.conf,hpsj5s.conf,hs2p.conf,ibm.conf,kodak.conf,leo.conf,lexmark.conf,ma1509.conf,magicolor.conf,matsushita.conf,microtek.conf,microtek2.conf,mustek.conf,mustek_pp.conf,mustek_usb.conf,nec.conf,net.conf,p5.conf,pie.conf,pixma.conf,plustek.conf,plustek_pp.conf,qcam.conf,ricoh.conf,rts8891.conf,s9036.conf,saned.conf,sceptre.conf,sharp.conf,sm3840.conf,snapscan.conf,sp15c.conf,st400.conf,stv680.conf,tamarack.conf,teco1.conf,teco2.conf,teco3.conf,test.conf,u12.conf,umax.conf,umax1220u.conf,umax_pp.conf,xerox_mfp.conf,v4l.conf} etc/xinetd.d/sane)

source=(sane.xinetd)
options=(!libtool)
conflicts=('sane')
provides=('sane')


_gitroot="git://git.debian.org/sane/sane-backends.git"
_gitname="sane-backends"

build() {
#-------------------Checkout source--------------------#

  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname} --depth=1
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_gitname}-build"
  mkdir "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

#--------------------BUILD-----------------------------#

  ../${_gitname}/configure --prefix=/usr \
                           --sysconfdir=/etc \
                           --localstatedir=/var \
                           --with-docdir=/usr/share/doc/sane \
                           --enable-avahi \
                           --disable-locking
  make
}

package() {
 cd "${srcdir}/${_gitname}-build"

  make DESTDIR="${pkgdir}" install
  # fix hp officejets
  echo "#hpaio" >> "${pkgdir}/etc/sane.d/dll.conf"
  # install udev files
  install -D -m0644 tools/udev/libsane.rules \
    "${pkgdir}/usr/lib/udev/rules.d/53-sane.rules"
  # fix udev rules
  sed -i 's|NAME="%k", ||g' "${pkgdir}/usr/lib/udev/rules.d/53-sane.rules"
  # install xinetd file
  install -D -m644 "${srcdir}/sane.xinetd" "${pkgdir}/etc/xinetd.d/sane"

  # Install the pkg-config file
  install -D -m644 tools/sane-backends.pc \
     "${pkgdir}/usr/lib/pkgconfig/sane-backends.pc"
}

md5sums=('203af92f59d7b2443ae1a7604ed95b55')
