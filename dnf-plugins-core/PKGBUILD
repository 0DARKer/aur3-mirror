pkgname=dnf-plugins-core
pkgver=0.1.8
_gittag=${pkgname}-${pkgver}-1
pkgrel=1
pkgdesc="Core DNF Plugins"
arch=('any')
url="https://github.com/rpm-software-management/${pkgname}"
license=('GPL2')
depends=('dnf' 'python')
makedepends=('cmake')
checkdepends=('python-nose')
source=("${url}/archive/${_gittag}.tar.gz")
md5sums=('1d2b27bdc7f787229f0535a12901bb26')

prepare() {
	cd "$pkgname-$_gittag"
	rm -rf build
	mkdir build
}

build() {
	cd "$pkgname-$_gittag"
	cd build
	cmake -DCMAKE_BUILD_TYPE='Release'  \
	      -DCMAKE_INSTALL_PREFIX='/usr' \
	      -DPYTHON_DESIRED='3'          \
	      ..
	make
}

check() {
	cd "$pkgname-$_gittag"
	PYTHONPATH=./plugins nosetests -s tests
}

package() {
	cd "$pkgname-$_gittag"
	cd build
	make DESTDIR="$pkgdir/" install
}
