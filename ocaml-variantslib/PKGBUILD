# Maintainer: Aaron Chen <nextAaron at gmail.com>
# Contributor: Serge Zirukin <ftrvxmtrx@gmail.com>

_pkgname=variantslib
pkgname=ocaml-$_pkgname
pkgver=109.15.00
pkgrel=1
pkgdesc="OCaml variant types as first class values"
arch=('i686' 'x86_64')
url="http://janestreet.github.com"
license=('LGPL')
depends=('ocaml>=3.12' 'ocaml-type_conv>=109.15.00')
makedepends=('ocaml-findlib')
options=(!strip !makeflags)
source=("https://ocaml.janestreet.com/ocaml-core/$pkgver/individual/$_pkgname-$pkgver.tar.gz")
md5sums=('aaa391cb621fb944fb362d3c881e04ca')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --disable-debug --prefix /usr --destdir "$pkgdir"
  make all
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  install -dm 755 "$OCAMLFIND_DESTDIR/stublibs"
  make install
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
