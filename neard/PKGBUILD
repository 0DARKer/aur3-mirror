# Maintainer: Dmytro Bagrii <dimich@softerra.com>

pkgname=neard
pkgver=0.10
pkgrel=1
pkgdesc="Intel's Near Field Communication manager"
arch=('i686' 'x86_64')
url="https://01.org/linux-nfc/"
license=('GPL2')
depends=('glibc' 'dbus>=1.2' 'libnl' 'glib2>=2.28')
makedepends=('automake' 'autoconf>=2.60')
backup=(etc/neard/main.conf)
source=(http://git.kernel.org/cgit/network/nfc/neard.git/snapshot/$pkgname-$pkgver.tar.gz
        sbindir.patch
        config.patch
        neard
        neard.service)
md5sums=('440a242fd994b9cd688382f1fd64c3a2'
         '88e0b5c7ad0c5d892c9d3fb42b808919'
         '81bb7ce7ed11e10eb640700a5a5f2307'
         '278da29e03060c827cf1551782ea7b6d'
         '102ed535b4ed07b81637e8ab08fbcd69')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 -i $srcdir/sbindir.patch

  patch -p1 -i $srcdir/config.patch

  ./bootstrap && \
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-debug \
    --enable-tools

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -D -m755 "${srcdir}/neard" \
    "${pkgdir}/etc/rc.d/neard"

  install -D -m644 "${srcdir}/neard.service" \
    "${pkgdir}/usr/lib/systemd/system/neard.service"

  install -D -m644 "${srcdir}/$pkgname-$pkgver/src/main.conf" \
    "${pkgdir}/etc/neard/main.conf"
}
