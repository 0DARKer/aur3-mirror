# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=android-qt5-qtjsbackend
pkgver=5.1.0_rc2
pkgrel=1
pkgdesc="Qt 5 for Android: qtjsbackend module (includes QtV8)"
arch=('i686' 'x86_64')
url="http://qt-project.org/wiki/Qt5ForAndroid"
license=('LGPL')
groups=('qt-android' 'qt5-android')
makedepends=('python' "android-qt5-qtbase=${pkgver}")
options=(!libtool !strip)
depends=('android-qt5-qtbase')
_pkgver="${pkgver/_/-}"
_pkgfqn="qtjsbackend-opensource-src-${_pkgver}"
source=("http://download.qt-project.org/development_releases/qt/5.1/${_pkgver}/submodules/${_pkgfqn}.tar.xz")
md5sums=('71a86b8b144773178e11c699f87d6277')

build() {
  cd ${srcdir}/${_pkgfqn}
  
  /opt/android-qt5/${_pkgver}/bin/qmake
  make
}

package() {
  cd ${srcdir}/${_pkgfqn}
  
  make INSTALL_ROOT="${pkgdir}" install
    
  # Strip by hand, system strip doesn't work on arm binaries

  # arm dynamic libs in lib
  /opt/android-ndk/toolchains/arm-linux-androideabi-4.7/prebuilt/linux-x86*/bin/arm-linux-androideabi-strip ${STRIP_SHARED} \
    ${pkgdir}/opt/android-qt5/${_pkgver}/lib/*.so*
}
