# Maintainer: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>
# Contributor: Ondrej Martinak <omartinak@gmail.com>

pkgname=sfml-git
pkgver=20120531
pkgrel=1
pkgdesc='A simple, fast, cross-platform, and object-oriented multimedia API.'
arch=('i686' 'x86_64')
url='http://www.sfml-dev.org/'
license=('ZLIB')
depends=('libsndfile' 'libxrandr' 'libjpeg' 'openal' 'glew' 'freetype2')
makedepends=('git' 'mesa' 'cmake' 'doxygen')
conflicts=('sfml')
provides=('sfml=2.0')

_gitroot='https://github.com/LaurentGomila/SFML.git'
_gitname='SFML'

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
    cd $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  mkdir "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  cmake -DCMAKE_INSTALL_PREFIX=/usr "../$_gitname" \
        -DSFML_BUILD_DOC=true \
        -DSFML_BUILD_EXAMPLES=true
  make
  make doc
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir/" install

  install -Dm644 "$pkgdir/usr/share/SFML/cmake/Modules/FindSFML.cmake" "$pkgdir/usr/share/cmake-2.8/Modules/FindSFML.cmake"

  install -Dm644 "../$_gitname/license.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sts=2 sw=2 et:
