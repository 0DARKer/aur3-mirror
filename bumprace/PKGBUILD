# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=bumprace
pkgver=1.5.3
pkgrel=3
pkgdesc="A racing game through a multi-level maze for 1 or 2 players"
arch=('i686' 'x86_64')
url="http://www.linux-games.com/bumprace/index.html"
license=('GPL')
depends=('sdl_image' 'sdl_mixer')
source=("http://ftp.debian.org/debian/pool/main/b/${pkgname}/${pkgname}_${pkgver}.orig.tar.gz"
        "${pkgname}.png"
        "${pkgname}.desktop")
md5sums=('a72733718ee6eed8cd1657db89bf8e0d'
         'ba9ed35c7797d112d6c7c21c5fa58a81'
         '80cc86c984b50b7276df332fee59b51d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Install pixmap and .desktop file
  install -Dm644 "../${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 "../${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install readme
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
