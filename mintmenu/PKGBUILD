# Maintainer: twa022 <twa022 at gmail dot com>
# Contributor: Giuseppe Borzi <gborzi _AT_ ieee _DOT_ org>
# Contributor: ryooichi <ryooichi+arch AT gmail DOT com>
# Real thanks goes to jelly, mike_93 and the original creators of mintmenu, usp, slab...
# See also:
#   http://bbs.archlinux.org/viewtopic.php?id=66987
#   http://bbs.archlinux.org/viewtopic.php?id=68633
#   http://github.com/jelly/archmenu

pkgname=mintmenu
pkgver=5.3.7
pkgrel=5
pkgdesc="Linux Mint Menu for MATE"
arch=('i686' 'x86_64')
url="http://packages.linuxmint.com/pool/main/m/mintmenu"
license=('GPL')
depends=('pygtk' 'python-mate' 'python-mate-desktop' 'python-corba' 'archlinux-artwork' 'gksu' 'python2-xdg' 'xdg-utils' 'python-keybinder')
optdepends=('mate-menu-editor: for editing the gnome menu'
            'mint-translations: translations files'
            'gnome-packagekit: package manager button')
source=("${url}/${pkgname}_${pkgver}.tar.gz" 
        'arch-patch.diff'
        'execute_fix.diff'
        'keybinder.diff'
        'removescript')

build() {
  cd "$srcdir/${pkgname}"
  patch -uNp2 -r- -i "$srcdir/arch-patch.diff"
  patch -uNp2 -r- -i "$srcdir/execute_fix.diff"
  patch -uNp2 -r- -i "$srcdir/keybinder.diff"
  rm -f usr/lib/linuxmint/mintMenu/*.pyc usr/lib/linuxmint/mintMenu/plugins/*pyc
  sed -i -e "s/__version__/$pkgver/" usr/lib/linuxmint/mintMenu/mintMenu.py
  cp -R usr "$pkgdir/"
  chmod 644 "$pkgdir/usr/lib/matecomponent/servers/mintMenu.server"
  install -m755 "$srcdir/removescript" "$pkgdir/usr/lib/linuxmint/mintMenu/"
  sed -i 's:#!/usr/bin/python:&2:' ${pkgdir}/usr/bin/mintmenu ${pkgdir}/usr/lib/linuxmint/mintMenu/{./,plugins/}*py
  sed -i 's:#!/usr/bin/env python:&2:' ${pkgdir}/usr/bin/mintmenu ${pkgdir}/usr/lib/linuxmint/mintMenu/{./,plugins/}*py
  cd "$pkgdir"/usr/lib/linuxmint/mintMenu
  ./compile.py
}


sha256sums=('9ace38b5b8289e6bd2cc668149c7c6c3b2e49b7c86ed7af148ae0e269581f0d4'
            '7bd49dd5e971003c39455a4db311826cf4da32e31fbc354ab97e0254736b2f81'
            '2115ec0202745e415daefadc9d3096f32b20ab3d6b782342880a6e3877fdf0e8'
            'cac0340f20ec39f34c64de069740e47b97a6630f3c772eb9e4630435e74c27cf'
            '9533d0f5416af1f2b8ef5097f06e39135278eef856994d859c4ee7ba57d6fcaa')
