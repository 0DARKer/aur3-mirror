# Maintainer: David Scholl <djscholl at gmail dot com>
pkgname=leo
pkgver=4.10
pkgrel=4
pkgdesc="Literate programmer's editor, outliner, and project manager"
arch=('any')
url="http://leoeditor.com/"
license=('custom')
depends=('python2-pyqt' 'shared-mime-info')
makedepends=('unzip')
backup=('opt/leo/leo/config/leoSettings.leo')
install="leo.install"
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/Leo-$pkgver-final.zip
        leo.desktop
        leo.xml)
md5sums=('5cf70fff15da57957d84b25842159467'
         'fe65422281f7fcda8d1f11d457445496'
         'b4c0250152aa6393d4a705fcccbad26b')

package() {
  install -d $pkgdir/opt/$pkgname
  cd $srcdir/Leo-$pkgver-final
  sed 's/python/python2/' -i launchLeo.py
  chmod a+x launchLeo.py
  cp -R * $pkgdir/opt/$pkgname
  install -D LICENSE.TXT $pkgdir/usr/share/licenses/$pkgname/LICENSE.TXT
  install -d $pkgdir/usr/bin
  install -D $srcdir/leo.desktop $pkgdir/usr/share/applications/leo.desktop
  install -D $srcdir/leo.xml $pkgdir/usr/share/mime/packages/leo.xml
  install -D leo/Icons/application-x-leo-outline.png $pkgdir/usr/share/pixmaps/leo.png
  cd $pkgdir/usr/bin
  ln -s ../../opt/leo/launchLeo.py leo
}
