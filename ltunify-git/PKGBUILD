# Maintainer: Adrien Sohier <adrien.sohier@art-software.fr>
pkgname=ltunify-git
pkgver=0.1.30.g1e9b877
pkgrel=1
pkgdesc="A command-line tool to pair Logitech Unifying devices"
arch=('i686' 'x86_64' 'ARM')
url="http://lekensteyn.nl"
license=('GPL')
depends=('glibc')
makedepends=('gcc' 'make' 'git')
source=('git+https://git.lekensteyn.nl/ltunify.git')
md5sums=('SKIP')
_gitname='ltunify'

pkgver() {
	cd "$_gitname"
	git describe --always | sed 's|^v||;s|-|.|g'
}

build() {
	cd "$_gitname"
	make ltunify
}

package() {
	cd "$_gitname"
	install -Dm755 "ltunify" "$pkgdir/usr/bin/ltunify"
	install -Dm644 "udev/42-logitech-unify-permissions.rules" "$pkgdir/etc/udev/rules.d/42-logitech-unify-permissions.rules"
}
