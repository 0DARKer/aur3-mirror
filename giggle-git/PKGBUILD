#Maintainer: Christian Hesse <mail@eworm.de>
#Contributor: Gergely Imreh <imrehg(at)gmail(dot)com>

pkgname=giggle-git
pkgver=0.5.286.g9999a7f
pkgrel=1
pkgdesc="A graphical frontend for the git directory tracker - git checkout"
arch=('i686' 'x86_64')
url="http://live.gnome.org/giggle"
license=('GPL')
depends=('git' 'gtksourceview3' 'vte3')
options=(!libtool)
install=giggle.install
provides=('giggle')
conflicts=('giggle')
source=('git://git.gnome.org/giggle')

pkgver() {
	cd giggle/
	git describe --tags --long | sed 's|^[^0-9]*||;s|-|.|g'
}

build() {
	cd giggle/

	./autogen.sh
	./configure --prefix=/usr
	make
}

package() {
	cd giggle/
	
	make prefix=${pkgdir}/usr install
	# this file shouldn't be here
	rm ${pkgdir}/usr/share/icons/hicolor/icon-theme.cache
}

sha256sums=('SKIP')
