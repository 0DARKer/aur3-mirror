pkgname=audacious-gtk2
_pkgalias=audacious
pkgver=3.0.1
pkgrel=2
pkgdesc='Lightweight, GTK-based advanced audio player focused on audio quality'
arch=('i686' 'x86_64')
url='http://audacious-media-player.org/'
license=('GPL3')
depends=('gtk2' 'libmcs' 'dbus-glib' 'libguess' 'libsm' 'audacious-plugins' 'desktop-file-utils' 'hicolor-icon-theme')
optdepends=('unzip: zipped skins support')
conflicts=('audacious')
install=${_pkgalias}.install
source=("http://distfiles.atheme.org/${_pkgalias}-${pkgver}.tar.gz")
md5sums=('a988c9dcf7999051657a9f2859c1b168')

build()
{
    cd "${srcdir}/${_pkgalias}-${pkgver}"
    [[ ${CARCH} == x86_64 ]] && extraopts='' || extraopts='--disable-sse2'
    ./configure --prefix=/usr ${extraopts} --disable-gtk3
    make
}

package()
{
    cd "${srcdir}/${_pkgalias}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
