# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=samplv1
pkgver=0.0.8svn760
pkgrel=1
pkgdesc="Polyphonic sampler synthesizer with stereo fx"
arch=(i686 x86_64)
url="http://www.rncbc.org/drupal/node/473"
license=('GPL')
groups=('lv2-plugins')
depends=('lv2' 'jack' 'qt')
install="$pkgname.install"
source=("http://www.rncbc.org/snapshots/$pkgname-$pkgver.tar.gz")
md5sums=('6dbb862b4d17c3c6c6827365a34fa95c')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # x86_64 lib path fix
  sed -i "s/lib64/lib/" src/src_lv2.pro

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
