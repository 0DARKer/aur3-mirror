# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=samplv1
pkgver=0.2.0
pkgrel=1
pkgdesc="Polyphonic sampler synthesizer with stereo fx"
arch=(i686 x86_64)
url="http://$pkgname.sourceforge.net/$pkgname-index.html"
license=('GPL')
groups=('lv2-plugins')
depends=('lv2' 'jack' 'qt')
install="$pkgname.install"
source=("http://download.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('7dbff3ca0afa27bf3f2173ef365226b4')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # x86_64 lib path fix
  sed -i "s/lib64/lib/" src/src_lv2.pro

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
