# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkgbasename=gramps
pkgname=$_pkgbasename-light
pkgver=3.2.5
pkgrel=1
pkgdesc="A genealogy program for Linux. Light version."
arch=('any')
url="http://gramps-project.org"
license=('GPL')
depends=('python-gtkspell' 'gconf' 'pywebkitgtk' 'shared-mime-info')
makedepends=('gettext' 'gnome-doc-utils' 'perlxml')
optdepends=('python-pyenchant: spellchecking support')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/gramps/$_pkgbasename-$pkgver.tar.gz")
md5sums=('6f6636e548dbf9f833b83bb8331d2762')

build() {
  cd "$srcdir/$_pkgbasename-$pkgver"

  # This Gramps PKGBUILD just replace the big gnome-python-extras dependency
  # with the just needed python-gtkspell (in AUR)

  export PYTHON="python2"

  sed \
	-e "s_\(env python\).*_\12_" \
	-e "s_\(bin/python\).*_\12_" \
	-i `grep -Erl "(env python|bin/python)" .`

  ./configure \
	--prefix=/usr \
	--disable-mime-install
  make
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"

  make DESTDIR="$pkgdir/" install
}
