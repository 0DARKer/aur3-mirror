# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=gramps
pkgname=$_pkg-light
pkgver=3.3.0
pkgrel=1
pkgdesc="A genealogy program for Linux. Light version."
arch=('any')
url="http://gramps-project.org"
license=('GPL')
depends=('pygtk' 'librsvg' 'desktop-file-utils' 'shared-mime-info' 'gnome-icon-theme' 'xdg-utils')
makedepends=('intltool')
optdepends=('python2-gnomevfs: mime functions support'
            'python2-gtkspell: spellchecking support'
            'python-osmgpsmap: openstreetmap support'
            'pywebkitgtk: html rendering support'
            'graphviz: graph exporting support'
            'ttf-freefont: more font support'
            'pyexiv2: exif support')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$_pkg/Stable/$pkgver/$_pkg-$pkgver-1.tar.gz")
md5sums=('c28d387a5c50eac6813d91ac684028d3')

build() {
  cd "$srcdir/$_pkg-$pkgver"

  export PYTHON="python2"

  sed -e "s_\(env python\).*_\12_;s_\(bin/python\).*_\12_" \
      -i `grep -Erl "(env python|bin/python)" .`

  ./configure --prefix=/usr \
              --disable-mime-install
  make
}

package() {
  cd "$srcdir/$_pkg-$pkgver"

  make DESTDIR="$pkgdir/" install
}
