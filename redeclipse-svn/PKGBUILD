# Contributor: J.W. Birdsong <jwbirdsongATgmailDOTcom>
# Maintainer: Martin Erik Werner ("arand") <martinerikwerner@mail.com>
# Based on the AUR package for version 1.0 by Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
pkgname=redeclipse-svn
# svn automagic seems to override the $pkgver anyways..
pkgver=3515
pkgrel=1
pkgdesc="A single-player and multi-player first-person ego-shooter, built as a total conversion of Cube Engine 2, "
arch=('i686' 'x86_64')
url="http://redeclipse.net"
license=('custom' 'ZLIB' 'MIT' 'CC-BY-SA')
depends=('sdl_image' 'libgl' 'sdl_mixer')
makedepends=('subversion' 'imagemagick' 'gzip' 'mesa' 'sdl_image' 'libgl' 'sdl_mixer')

_svntrunk=https://redeclipse.svn.sourceforge.net/svnroot/redeclipse
_svnmod=redeclipse

build() {
	cd "$srcdir"

	# update/checkout svn
	if [ -d "$_svnmod"/.svn ]; then
		msg "Updating existing SVN checkout"
		(cd "$_svnmod" && svn up -r "$pkgver")
	else
		msg "New checkout from SVN"
		svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
	fi

	msg "SVN export to clean build directory"

	# export to get rid of .svn metadata
	rm -rf "$srcdir/$pkgname"
	svn export "$srcdir/$_svnmod" "$srcdir/$pkgname"

	msg "Starting make"

	# build in exported directory
	cd "$srcdir/$pkgname/src"
	make client server
}

package() {
	msg "Starting install phase"

	cd "$srcdir/$pkgname"
	make -C src DESTDIR="$pkgdir" prefix=/usr redeclipse=redeclipse-svn \
		system-install

	msg "Adding \"SVN\" to manpages and desktop file"
	sed -e 's/\(^Name.*$\)/\1\ SVN/' \
		-i "$pkgdir/usr/share/applications/redeclipse-svn.desktop"
	zcat "$pkgdir/usr/share/man/man6/redeclipse-svn.6.gz" | \
		sed -e 's/REDECLIPSE/REDECLIPSE-SVN/' | \
		gzip -9 -c \
		> "$pkgdir/usr/share/man/man6/redeclipse-svn.6.gz.tmp" && \
		mv "$pkgdir/usr/share/man/man6/redeclipse-svn.6.gz.tmp" \
		"$pkgdir/usr/share/man/man6/redeclipse-svn.6.gz"

	msg "Installing license and trademark info"
	install -Dm644 license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -Dm644 trademark.txt "$pkgdir/usr/share/licenses/$pkgname/TRADEMARK"
}
