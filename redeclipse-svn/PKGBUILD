# Maintainer: Martin Erik Werner ("arand") <martinerikwerner@mail.com>

pkgname=redeclipse-svn
_svnmod=redeclipse
pkgver=5593
pkgrel=1
pkgdesc="Red Eclipse is a fun-filled new take on the casual first person arena shooter, built as a total conversion of Cube Engine 2"
arch=('i686' 'x86_64')
url="http://redeclipse.net"
license=('ZLIB' 'MIT' 'CCPL:by-sa' 'CCPL:by' 'custom')
depends=('mesa' 'sdl_image' 'sdl_mixer' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('subversion' 'gzip' 'ed')
source=("$_svnmod::svn+https://redeclipse.svn.sourceforge.net/svnroot/redeclipse")
md5sums=('SKIP')
install="$pkgname.install"

pkgver() {
	pwd
	cd "$SRCDEST/$_svnmod"
	svnversion | tr -d [A-z]
}

build() {
	pwd
	cd "$srcdir/$_svnmod"

	make -C src/ client server icons
}

package() {
	pwd
	cd "$srcdir/$_svnmod"

	make -C src/ DESTDIR="$pkgdir" prefix=/usr \
		appname=redeclipse-svn cappname=REDECLIPSE-SVN \
		appsrcname=redeclipse \
		system-install

	msg "Adapting scripts, manpages and desktop file to SVN package"

	ed -s "$pkgdir/usr/bin/redeclipse-svn" <<'EOF'
,s/exec "\.\/redeclipse-svn"/& -h"$HOME\/.redeclipse-svn\/"/
w
EOF
	ed -s "$pkgdir/usr/bin/redeclipse-svn-server" <<'EOF'
,s/exec "\.\/redeclipse-svn-server"/& -h"$HOME\/.redeclipse-svn\/"/
w
EOF
	ed -s "$pkgdir/usr/share/applications/redeclipse-svn.desktop" <<'EOF'
,s/^Name.*$/& SVN/
w
EOF

	msg "Installing full license info"
	install -Dm644 doc/all-licenses.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
