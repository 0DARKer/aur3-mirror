# Maintainer: Martin Erik Werner ("arand") <martinerikwerner@mail.com>

pkgname=redeclipse-svn
pkgver=3745
pkgrel=1
pkgdesc="A single-player and multi-player first-person ego-shooter, built as a total conversion of Cube Engine 2, "
arch=('i686' 'x86_64')
url="http://redeclipse.net"
license=('custom' 'ZLIB' 'MIT' 'CC-BY-SA')
depends=('hicolor-icon-theme' 'sdl_image' 'libgl' 'sdl_mixer')
makedepends=('subversion' 'imagemagick' 'gzip' 'mesa')

_svntrunk=https://redeclipse.svn.sourceforge.net/svnroot/redeclipse
_svnmod=redeclipse

build() {
	cd "$srcdir"

	# update/checkout svn
	if [ -d "$_svnmod"/.svn ]; then
		msg "Updating existing SVN checkout"
		(cd "$_svnmod" && svn up -r "$pkgver")
	else
		msg "New checkout from SVN"
		svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
	fi

	msg "SVN export to clean build directory"

	rm -rf "$srcdir/$pkgname"
	svn export "$srcdir/$_svnmod" "$srcdir/$pkgname"

	msg "Starting make"

	# build in exported directory
	cd "$srcdir/$pkgname/src"
	make client server icons
}

package() {
	cd "$srcdir/$pkgname"
	make -C src DESTDIR="$pkgdir" prefix=/usr \
		appname=redeclipse-svn cappname=REDECLIPSE-SVN \
		appsrcname=redeclipse \
		system-install

	msg "Adapting scripts, manpages and desktop file"

	sed -e 's#\(RE_OPTIONS=".*\)-r\(.*"\)#\1-h$HOME/.redeclipse-svn/\2#' \
		-i "$pkgdir/usr/bin/redeclipse-svn" \
		-i "$pkgdir/usr/bin/redeclipse-svn-server"

	sed -e 's#\(^Name.*$\)#\1\ SVN#' \
		-i "$pkgdir/usr/share/applications/redeclipse-svn.desktop"

	msg "Installing license and trademark info"
	install -Dm644 license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -Dm644 trademark.txt \
		"$pkgdir/usr/share/licenses/$pkgname/TRADEMARK"
}
