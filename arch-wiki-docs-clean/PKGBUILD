# Maintainer: Jakub Klinkovsk√Ω <j.l.k@gmx.com>

_pkgname=arch-wiki-docs
pkgname=arch-wiki-docs-clean
pkgdesc="Pages from Arch Wiki optimized for offline browsing"
pkgver=2014.03.21
pkgrel=1
arch=('any')
url="https://github.com/lahwaacz/arch-wiki-docs"
license=('FDL')
options=('!strip')
makedepends=('git' 'python' 'python-simplemediawiki-git' 'python-lxml' 'python-cssselect')
source=('git://github.com/lahwaacz/arch-wiki-docs.git')
md5sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  date "+%Y.%m.%d"
}

build() {
  cd "$_pkgname"
  python arch-wiki-docs.py --output-directory "$srcdir/build_wiki"
}

package() {
  install -dm755 "$pkgdir/usr/share/doc/arch-wiki/html-clean"
  cp -r "$srcdir"/build_wiki/* "$pkgdir/usr/share/doc/arch-wiki/html-clean/"
}

# vim:set ts=2 sw=2 et:
