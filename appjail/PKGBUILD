# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
pkgname=appjail
pkgver=1
pkgrel=1
pkgdesc="Sandboxing tool to protect private data from untrusted applications"
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
url="https://github.com/brain0/appjail"
license=('custom')
depends=('glib2' 'libcap' 'libutil-linux' 'libnl')
makedepends=('util-linux' 'git')
install=${pkgname}.install
backup=('etc/appjail.conf')
source=("${pkgname}::git+https://github.com/brain0/appjail#tag=v${pkgver}")
sha1sums=('SKIP')

prepare() {
  cd "${srcdir}"/${pkgname}
  ./autogen.sh
}

build() {
  cd "${srcdir}"/${pkgname}
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}"/${pkgname}
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
