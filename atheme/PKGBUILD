# Maintainer: Vincent Post <vincent.post@w1r3.net>
# Github: https://github.com/w1r3/archlinux-packages/

pkgname=atheme
pkgver=7.0.7
pkgrel=2
pkgdesc='A portable and secure set of open-source and modular IRC services'
url='http://atheme.net/'
license=('ISC')
arch=('x86_64' 'i686')
depends=('libmowgli'
         'openssl')
makedepends=('perl'
             'cracklib')
optdepends=('libldap: auth/ldap module'
            'cracklib: nickserv/cracklib module'
            'perl: scripting/perl')
install=atheme.install

source=("http://atheme.net/downloads/atheme-services-$pkgver.tar.bz2"
        'atheme.service'
        'atheme.tmpfiles')
sha1sums=('17497276467d0ca27171756d6c81d78f51f69690'
          'b67062fd3d9f5254c122b5b319fdfe97a0155234'
          'ca271a9bd901cb7d1f44a545af487cf6a7763837')

build() {
  cd "${srcdir}/atheme-services-${pkgver}"
  ./configure --enable-fhs-paths \
    --prefix=/usr \
    --sysconfdir=/etc/atheme \
    --localstatedir=/var \
    --docdir=/usr/share/doc/atheme \
    --with-cracklib \
    --with-perl \
    --enable-contrib
  make
}

package() {
  install -Dm644 "${srcdir}/atheme.service" "${pkgdir}/usr/lib/systemd/system/atheme.service"
  install -Dm644 "${srcdir}/atheme.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/atheme.conf"
  cd "${srcdir}/atheme-services-${pkgver}"
  make DESTDIR="${pkgdir}" install
  chown 142:142 "${pkgdir}/var/lib/atheme" -R
  chown 142:142 "${pkgdir}/var/run/atheme" -R
  chown 142:142 "${pkgdir}/etc/atheme" -R
  install -Dm 644 "${pkgdir}/usr/share/doc/atheme/LICENSE" "${pkgdir}/usr/share/licenses/atheme/LICENSE"
}
