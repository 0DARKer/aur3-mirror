# Committer: Piotr Beling <qwak@w8.pl>
# Contributor: Vianney le Clément <vleclement AT gmail · com>
pkgname=unittestpp
pkgver=1.4
pkgrel=2
pkgdesc="UnitTest++ is a lightweight unit testing framework for C++."
arch=('i686' 'x86_64')
url="http://unittest-cpp.sourceforge.net/"
depends=('gcc-libs')
source=("http://downloads.sourceforge.net/project/unittest-cpp/UnitTest%2B%2B/$pkgver/unittest-cpp-${pkgver}.zip"
        UnitTest++.h)
license=('MIT')
md5sums=('bd373a53403ed51ea1bbb60b1952d7e3'
         '212556c29e2555dba70af5ca6e952c76')
sha1sums=('dad944159e2e135aea74039987490eaaee00f2ad'
          '55596fd4141e624b67842591dfac9f5031a687e2')

build() {
  cd "$srcdir/UnitTest++"
  make libUnitTest++.a
}

package() {
  cd "$srcdir/UnitTest++"
  
  install -dm755 "$pkgdir/usr/include/UnitTest++/Posix"
  install -m644 src/*.h "$pkgdir/usr/include/UnitTest++"
  install -m644 src/Posix/*.h "$pkgdir/usr/include/UnitTest++/Posix"
  install -m644 "$srcdir/UnitTest++.h" "$pkgdir/usr/include"
  
  install -dm755 "$pkgdir/usr/lib"
  install -m644 libUnitTest++.a "$pkgdir/usr/lib"
}

# vim:set ts=2 sw=2 et:
