# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=vis-git
pkgver=0.r161.g88d1e3f
pkgrel=1
pkgdesc='suckless vim like editor - git checkout'
arch=('i686' 'x86_64')
url="http://repo.or.cz/vis.git"
conflicts=('vis')
provides=('vis')
makedepends=('git')
license=('AS-IS')
source=('git://repo.or.cz/vis.git')

pkgver() {
	cd vis/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

build() {
	cd vis/

	make
}

package() {
	cd vis/

	# this does not yet work as files are missing
	#make DESTDIR="${pkgdir}" install

	install -D -m0755 vis ${pkgdir}/usr/bin/vis
}

sha256sums=('SKIP')
