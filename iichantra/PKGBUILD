# Maintainer: archtux <antonio.arias99999 at gmail dot com>

pkgname=iichantra
pkgver=1.2
pkgrel=1
pkgdesc="Cross-platform 2D platformer arcade"
arch=('i686' 'x86_64')
url="http://iichantra.ru/"
license=('MIT')
depends=('devil' 'lua' 'sdl_net' 'zziplib')
makedepends=('cmake')
options=('!strip')
install=$pkgname.install
source=(http://iichantra.ru/files/pear-dso-src$pkgver.zip
        iichantra
        iichantra.desktop
        iichantra.install
        iichantra.png)
md5sums=('05811dcf2d72c64380037a35c073843c'
         '71cde5a4e0f051c5ec23d894c36ae5e1'
         '378c2f809b4422a84034cf0eedd86a8f'
         '330bfe2786687ffe16ecdc6edeb3d72c'
         '4a5682a730e590ae408a0ebdd93182c5')


build() {
   cd $srcdir

   # Fix cmake(lua)
   sed -i 's_lua5.1_lua_' CMakeLists.txt

   cmake . -DCMAKE_INSTALL_PREFIX=/usr
   make 
}

package() {
   mkdir -p $pkgdir/usr/share/iichantra
   cp -r bin/ lib/ $pkgdir/usr/share/iichantra

   # Change language from russian -> english (comment next line if you want russian)
   sed -i 's_russian_english_' $pkgdir/usr/share/iichantra/bin/config/default.lua

   # Start file
   cd $startdir
   install -Dm755 iichantra $pkgdir/usr/bin/iichantra

   # Desktop icon
   install -Dm644 $pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
   install -Dm644 $pkgname.png $pkgdir/usr/share/pixmaps/$pkgname.png

   # Remove unneeded libraries
   cd $pkgdir/usr/share/iichantra/lib
   if [ "${CARCH}" = 'x86_64' ]; then
   rm libbass.so
     elif  [ "${CARCH}" = 'i686' ]; then 
   rm libbass_x64.so
   fi
   rm bass.lib zziplib.lib
}
