# Maintainer: Reventlov <contact+aur@volcanis.me>
pkgname=searx-git
pkgver=r659.8167d3c
pkgrel=1
pkgdesc="A privacy-respecting, hackable metasearch engine"
arch=('any')
url="https://searx.0x2a.tk/"
license=('AGPL')
depends=('python2-flask' 'python2-flask-babel' 'python2-grequests' 'python2-lxml' 'python2-yaml' 'python2-dateutil')
makedepends=('git')
backup=('usr/lib/python2.7/site-packages/searx/settings.yml')
install=searx.install
source=("$pkgname"::'git+https://github.com/asciimoo/searx.git' 'searx.install' 'searx.service')
noextract=()
md5sums=('SKIP'
         '708a59417394024510bbd8dc0444ef4c'
         '591625748fdd1b1ad1fe11c8ae8931b5')

pkgver() {
  cd "$srcdir/$pkgname"
  printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git describe --always)"
}

package() {
   msg "Starting build..."
   cd "$srcdir/$pkgname"
   python2 setup.py install --root="$pkgdir/" --optimize=1
   install -Dm644 ../searx.service "${pkgdir}"/usr/lib/systemd/system/searx.service
   rm  "$pkgdir/usr/lib/python2.7/site-packages/README.rst"
}

# vim:set ts=2 sw=2 et:
