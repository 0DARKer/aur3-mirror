# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
pkgname=ceph-git
pkgver=20101217
pkgrel=2
pkgdesc="Massively distributed fault-tolerant file system. Git development version."
arch=('x86_64' 'i686')
url="http://ceph.newdream.net/"
license=('GPL')
# Use kernel26-git or 2.6.37+ to get the latest BTRFS features
depends=('btrfs-progs-git' 'libedit' 'fuse' 'libsigc++' 'gtkmm' 'kernel26-git')
makedepends=('boost')
optdepends=('fcgi: radosgw - Amazon S3 compatibility'
						'expat: radosgw - Amazon S3 compatibility')
provides=('ceph')
conflicts=('ceph')
_gitroot="git://ceph.newdream.net/git/ceph.git"
_gitname="ceph"

build() {
  cd "$srcdir"

  msg "Connecting to the ceph git repository..."
  if [ -d "$srcdir/$_gitname" ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi
  msg "GIT checkout done or server timeout"

  cd "$srcdir"
  rm -rf $_gitname-build
  git clone $_gitname $_gitname-build

  msg "Configuring..."
  cd $_gitname-build

  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc || return 1
  make || return 1
  make DESTDIR="$pkgdir" install || return 1

  mkdir -p "$pkgdir/var/run/ceph" "$pkgdir/var/log/ceph" "$pkgdir/etc/rc.d" "$pkgdir/etc/ceph"
  install -Dm755 "src/init-ceph" "$pkgdir/etc/rc.d/ceph" || return 1

  # Install the sample configuration
  install -Dm644 "$pkgdir/usr/share/doc/ceph/sample.ceph.conf" \
    "$pkgdir/etc/ceph/ceph.conf.sample" || return 1

  # Move /usr/sbin to /sbin, which is expected by the mount command
  mv "$pkgdir/usr/sbin" "$pkgdir/"

  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING" || return 1
}
# vim:set ts=2 sw=2 et:
