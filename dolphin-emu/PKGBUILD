# Copied straight from the SVN in AUR;
#I take no create for creating this, only modifying to work with 3.0
# Thanks to all that contributed! :)
#
# Maintainer of dolphin-emu-svn: Joe Amenta <amentajo@msu.edu>
# Original Maintainer:  Devin Cofer <ranguvar@archlinux.us>
# Contributor: Hans-Kristian Arntzen <maister@archlinux.us>
# Contributor: Zephyr
# Contributor: Jeremy Newton (Mystro256)
# Special thanks to Zhou Xin <soul916@hotmail.com> for rewriting this to use cmake

pkgname=dolphin-emu
pkgver=3.0
pkgrel=1
pkgdesc="A GameCube and Wii emulator (stable)"
arch=('i686' 'x86_64')
url="http://www.dolphin-emu.com/"
license=('GPL2')

makedepends=('subversion' 'cmake' 'glproto' 'opencl-headers')
depends=('sdl' 'libxxf86vm' 'libao' 'wxgtk' 'wiiuse' 'nvidia-cg-toolkit' 'glew' 'mesa' 'libgl')
provides=("dolphin-emu=${pkgver}-${pkgrel}")
# Incompabatible with current version of sfml:
conflicts=('dolphin-emu-svn' 'dolphin-emu-svn-bin' 'dolphin-emu-svn-bin' 'sfml>=1.99.git20110719-1')

source=('dolphin-emu.desktop'
	'Dolphin_Logo.png')

md5sums=('feed4580c2e6bfbc7f6c67dad861daae'
	    'd15c51f547b4bd47e510faac40bcc9d6')

_svntrunk="http://dolphin-emu.googlecode.com/svn/tags/3.0/"
_svnmod="dolphin-emu-svn"


build() {
	cd "${srcdir}"

	if [ -d "${_svnmod}/.svn" ]; then
		(cd $_svnmod && svn up -r $pkgver)
	else
		svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
	fi

	msg "SVN checkout done or server timeout"
	msg "Starting make..."

	rm -rf "$srcdir/$_svnmod-build"
	cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
	mkdir -p "${srcdir}/${_svnmod}-build/build"
	cd "${_svnmod}-build/build"
	
	cmake .. -DCMAKE_INSTALL_PREFIX=/usr
	make
}
package() {
	cd "${srcdir}/${_svnmod}-build/build"

	make DESTDIR=${pkgdir} install

	install -Dm644 "${srcdir}/dolphin-emu.desktop" "${pkgdir}/usr/share/applications/dolphin-emu.desktop"
	install -Dm644 "${srcdir}/Dolphin_Logo.png" "${pkgdir}/usr/share/pixmaps/dolphin-emu.png"
}
