# Maintainer: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>

pkgname=ruby-reek
_gemname="${pkgname#ruby-}"
pkgver=1.3.1
pkgrel=1
pkgdesc="A static analysis tool for finding code smells."
url="https://github.com/troessner/reek"
license=("MIT")
arch=("any")
depends=("ruby" "ruby-ruby_parser" "ruby-sexp_processor" "ruby-ruby2ruby")
source=("http://rubygems.org/downloads/${_gemname}-${pkgver}.gem")
noextract=("${_gemname}-${pkgver}.gem")
md5sums=('c6934d20db5d5cfd47fa88a2fc6fb8d1')
sha1sums=('2d8dd4eb55902ee40a351e8820c09f29c703951c')

package() {
  # setup
  cd "${srcdir}"
  export HOME=/tmp
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"

  # isntall gem
  gem install \
    --no-user-install \
    --ignore-dependencies \
    --install-dir "${pkgdir}${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # install license
  mkdir -p "${pkgdir}/usr/share/licenses/ruby-${_gemname}/"
  cp `find "${pkgdir}/usr/lib/ruby/gems/" -type f -name License.txt` \
    "${pkgdir}/usr/share/licenses/ruby-${_gemname}/"
}

# vim:set ts=2 sw=2 et:
