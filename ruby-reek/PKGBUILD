# Maintainer: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
#
# Note: namcap warns that 'ruby-ruby_parser', 'ruby-sexp_processor' and
# 'ruby-ruby2ruby' (in the `depends` array) are unnecessary. This is untrue. The
# README.md file states that they are dependencies.

pkgname=ruby-reek
_gemname="${pkgname#ruby-}"
pkgver=1.3.6
pkgrel=1
pkgdesc="A static analysis tool for finding code smells."
url="https://github.com/troessner/reek"
license=("MIT")
arch=("any")
depends=("ruby-ruby_parser" "ruby-sexp_processor" "ruby-ruby2ruby")
source=("http://rubygems.org/downloads/${_gemname}-${pkgver}.gem")
noextract=("${_gemname}-${pkgver}.gem")
sha256sums=('37a276c0f9bd60143d1d0590256913f9b62fd4d4d7cc0bc84a2a905504ad57ee')

package() {
  # setup
  cd "${srcdir}"
  export HOME=/tmp
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"

  # install gem
  gem install \
    --no-user-install \
    --ignore-dependencies \
    --install-dir "${pkgdir}${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # install license
  install -Dm 644 \
    "$pkgdir"/usr/lib/ruby/gems/*/gems/"${_gemname}-${pkgver}"/License.txt \
    "${pkgdir}/usr/share/licenses/${pkgname}/License.txt"
}

# vim:set ts=2 sw=2 et:
