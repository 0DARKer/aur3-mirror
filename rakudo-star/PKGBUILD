# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
pkgname=rakudo-star
pkgver=2014.03
pkgrel=1
pkgdesc="A useful, usable, “early adopter” distribution of Perl 6"
arch=('i686' 'x86_64')
url='http://rakudo.org/'
license=(PerlArtistic)
depends=('nqp>=2014.03')
makedepends=('perl>=5.10')
provides=('rakudo')
conflicts=('rakudo')
options=('!makeflags')
source=(http://rakudo.org/downloads/star/$pkgname-$pkgver.tar.gz only-parrot-backend.patch)
sha512sums=('279cc82254a28156116584b9692950630a422290657ae0cf47dce87bea87444545c8da96d816695e3f318aa6f93e3989b5f58aaa4374cdebbb182325e2eec04f'
            'b32c8b60af28433be38858a4eb977bd67f74e16a59d528f17f691ce2dc144bd77a92ed810478ca07c255f3f0654998a05a278f2dbfd46c6f0f16b45e613a7b5b')

prepare() {
  cd "$pkgname-$pkgver"
  msg2 "$(patch -p1 < ../only-parrot-backend.patch)"
}

build() {
  cd "$pkgname-$pkgver"
  perl Configure.pl --prefix=/usr --backends=parrot
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # remove references to $pkgdir
  find "$pkgdir" -iname '*.pir' -exec env strip="$pkgdir" perl -pe 's/\Q$ENV{strip}//g' -i {} +

  # ufo appears to need this
  install --directory "$pkgdir"/usr/lib/parrot/"$(parrot_config VERSION)"/languages/perl6/site/
}
