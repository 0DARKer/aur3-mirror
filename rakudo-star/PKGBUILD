# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
pkgname=rakudo-star
pkgver=2014.01
pkgrel=2
pkgdesc="A useful, usable, “early adopter” distribution of Perl 6"
arch=('i686' 'x86_64')
url='http://rakudo.org/'
license=(PerlArtistic)
depends=('nqp>=2014.01')
makedepends=('perl>=5.8')
provides=('rakudo=2014.01')
conflicts=('rakudo')
options=('!makeflags')
source=(http://rakudo.org/downloads/star/$pkgname-$pkgver.tar.gz)
sha512sums=('4f34849ea1be2c576a5788d48d599fdf4a099946947dc3b1b4db26bf151be8f89bcdb872b5d675eab3ecaa1162937873f0e1954dbb6757b38226ca1798612592')

build() {
  cd "$pkgname-$pkgver"
  perl Configure.pl --prefix=/usr --backends=parrot
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # remove references to $pkgdir
  find "$pkgdir" -iname '*.pir' -exec env strip="$pkgdir" perl -pe 's/\Q$ENV{strip}//g' -i {} +

  # ufo appears to need this
  install --directory "$pkgdir"/usr/lib/parrot/"$(parrot_config VERSION)"/languages/perl6/site/
}
