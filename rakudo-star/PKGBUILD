# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
pkgname=rakudo-star
pkgver=2015.02
pkgrel=1
pkgdesc="A useful, usable, “early adopter” distribution of Perl 6"
arch=('i686' 'x86_64')
url='http://rakudo.org/'
license=(PerlArtistic)
depends=("nqp>=$pkgver" "nqp-moarvm>=$pkgver")
makedepends=('perl>=5.10')
provides=('rakudo' 'rakudo-moarvm')
conflicts=('rakudo' 'rakudo-moarvm')
options=('!makeflags')
source=(http://rakudo.org/downloads/star/$pkgname-$pkgver.tar.gz)
sha512sums=('8de5728ca3617846f5a0246e4f8b650da5aed62219c0f9e09cde3a6ef0e3dc171627861e7635711889a4fe2c323b3dfb1eac5958ab94562fb93f008718819c09')

build() {
  cd "$pkgname-$pkgver"
  perl Configure.pl --prefix=/usr --backends=moar,parrot
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # remove references to $pkgdir
  find "$pkgdir" -iname '*.pir' -o -wholename '*/bin/*' -exec env strip="$pkgdir" perl -pe 's/\Q$ENV{strip}//g' -i {} +

  # ufo appears to need this
  install --directory \
    "$pkgdir"/usr/lib/parrot/"$(parrot_config VERSION)"/languages/perl6/site/ \
    "$pkgdir"/usr/languages/perl6/site/

  cp "$pkgdir"/usr/bin/perl6{-m,}
}
