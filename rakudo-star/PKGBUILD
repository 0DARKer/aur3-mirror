# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
pkgname=rakudo-star
pkgver=2015.03
pkgrel=1
pkgdesc="A useful, usable, “early adopter” distribution of Perl 6"
arch=('i686' 'x86_64')
url='http://rakudo.org/'
license=(PerlArtistic)
depends=("nqp>=$pkgver")
makedepends=('perl>=5.10')
provides=('rakudo' 'rakudo-moarvm')
conflicts=('rakudo' 'rakudo-moarvm')
options=('!makeflags')
source=(http://rakudo.org/downloads/star/$pkgname-$pkgver.tar.gz)
sha512sums=('1f491ef985230ecb1caa9e90b6a76ba0265a0cfa9aff88bb5c46d68b8876ea184faf6d21e72ccba3f592702ce760b64c82e85001aed95f7c00ba7ceca17ee424')

build() {
  cd "$pkgname-$pkgver"
  perl Configure.pl --prefix=/usr --backends=moar
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # remove references to $pkgdir
  find "$pkgdir"/usr/bin/ -type f -exec env strip="$pkgdir" perl -pe 's/\Q$ENV{strip}//g' -i {} +

  # ufo appears to need this
  install --directory "$pkgdir"/usr/languages/perl6/site/
}
