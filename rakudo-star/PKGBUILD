# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
pkgname=rakudo-star
pkgver=2015.01
pkgrel=1
pkgdesc="A useful, usable, “early adopter” distribution of Perl 6"
arch=('i686' 'x86_64')
url='http://rakudo.org/'
license=(PerlArtistic)
depends=("nqp>=$pkgver" "nqp-moarvm>=$pkgver")
makedepends=('perl>=5.10')
provides=('rakudo' 'rakudo-moarvm')
conflicts=('rakudo' 'rakudo-moarvm')
options=('!makeflags')
source=(http://rakudo.org/downloads/star/$pkgname-$pkgver.tar.gz)
sha512sums=('d693792cd315b66dbb6b77cb788b46778f950131fba98e9903272f846fc65847c04c41aa8dfde9e8336e3b4abd0a6033215a4cfd161c7bc730b876ef080a3a83')

build() {
  cd "$pkgname-$pkgver"
  perl Configure.pl --prefix=/usr --backends=moar,parrot
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # remove references to $pkgdir
  find "$pkgdir" -iname '*.pir' -o -wholename '*/bin/*' -exec env strip="$pkgdir" perl -pe 's/\Q$ENV{strip}//g' -i {} +

  # ufo appears to need this
  install --directory \
    "$pkgdir"/usr/lib/parrot/"$(parrot_config VERSION)"/languages/perl6/site/ \
    "$pkgdir"/usr/languages/perl6/site/
}
