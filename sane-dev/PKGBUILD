# SANE (Dev): Installer: Arch
# Maintainer: Jeremy M. <jskier (at) gmail (dot) com>
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributed by Sarah Hay <sarahhay@mb.sympatico.ca>, Tobias Powalowski <t.powa@gmx.de>, Simo L. <neotuli@yahoo.com>, eric <eric@archlinux.org>

filename=sane
pkgname=${filename}-dev
pkgver=1.0.21
pkgdate=20101102
pkgrel=11
pkgdesc="Scanner Access Now Easy.  This is the latest development snapshot."
arch=("i686" "x86_64")
url="http://www.${filename}-project.org"
license=("GPL")
provides=("${filename}=${pkgver}")
depends=("libtiff" "libgphoto2" "libjpeg" "libieee1284" "libusb" "libv4l" "avahi" "bash")
conflicts=("${filename}")
backup=(etc/${filename}.d/{abaton.conf,agfafocus.conf,apple.conf,artec.conf,artec_eplus48u.conf,avision.conf,bh.conf,canon.conf,canon630u.conf,canon_dr.conf,canon_pp.conf,cardscan.conf,coolscan2.conf,coolscan3.conf,coolscan.conf,dc25.conf,dc210.conf,dc240.conf,dell1600n_net.conf,dll.conf,dmc.conf,epjitsu.conf,epson.conf,epson2.conf,fujitsu.conf,genesys.conf,gphoto2.conf,gt68xx.conf,hp.conf,hp3900.conf,hp4200.conf,hp5400.conf,hpsj5s.conf,hs2p.conf,ibm.conf,leo.conf,lexmark.conf,ma1509.conf,matsushita.conf,microtek.conf,microtek2.conf,mustek.conf,mustek_pp.conf,mustek_usb.conf,nec.conf,net.conf,pie.conf,pixma.conf,plustek.conf,plustek_pp.conf,qcam.conf,ricoh.conf,rts8891.conf,s9036.conf,${filename}d.conf,sceptre.conf,sharp.conf,sm3840.conf,snapscan.conf,sp15c.conf,st400.conf,stv680.conf,tamarack.conf,teco1.conf,teco2.conf,teco3.conf,test.conf,u12.conf,umax.conf,umax1220u.conf,umax_pp.conf,xerox_mfp,v4l.conf} etc/xinetd.d/${filename})
options=(!libtool)
install="${filename}.install"
source=(http://www.sane-project.org/snapshots/${filename}-backends-git${pkgdate}.tar.gz)
md5sums=('1c797e104e69aa6dd0e128ca32b473dd')

build() {
	cd "${srcdir}/${filename}-backends-git${pkgdate}"

	# Build
	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --with-docdir=/usr/share/doc/${filename} --enable-avahi || return 1
	make || return 1
	make DESTDIR="${pkgdir}" install || return 1

	# fix hp officejets
	echo "#hpaio" >> "${pkgdir}/etc/${filename}.d/dll.conf" || return 1

	# install udev files
	install -D -m0644 tools/udev/lib${filename}.rules \
	"${pkgdir}/lib/udev/rules.d/53-${filename}.rules" || return 1

	# fix group on /var/lock
	mkdir -p "${pkgdir}/var/lock/${filename}"
	chgrp 0 "${pkgdir}/var"
	chmod 755 "${pkgdir}/var"
	chmod 1777 "${pkgdir}/var/lock"
	chgrp 0 "${pkgdir}/var/lock"
	chmod 775 "${pkgdir}/var/lock/${filename}"
	chgrp 96 "${pkgdir}/var/lock/${filename}"

	# install xinetd file
	install -D -m644 "${startdir}/extra/${filename}.xinetd" "${pkgdir}/etc/xinetd.d/${filename}"
}

