# $Id$
# Maintainer:  Eric BÃ©langer <eric@archlinux.org>
# Contributor: Smith Dhumbumroong <zodmaner@gmail.com>

pkgname=lcs
pkgver=4.04.0
pkgrel=1
pkgdesc="Liberal Crime Squad: A satirical console-based political role-playing/strategy game in which you recruit a team of Elite Liberal radicals and try to save the United States from a descent into Arch-Conservatism."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/lcsgame/"
license=('GPL2')
depends=('ncurses')
makedepends=('autoconf')
install='install'
source=('http://downloads.sourceforge.net/project/lcsgame/Liberal%20Crime%20Squad/4.04/lcs_4.04.0_src.zip'
	'lcs-4.04.0-datafiles-locations.patch')

build() {
  cd "${srcdir}/lcs_${pkgver}_src"
  chmod +x bootstrap

  patch -p1 -i "${srcdir}/lcs-4.04.0-datafiles-locations.patch"

  ./bootstrap
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/lcs_${pkgver}_src"

  mkdir -p "${pkgdir}/usr/share/lcs"
  cp -R art "${pkgdir}/usr/share/lcs/"
  chmod 644 "${pkgdir}/usr/share/lcs/art/"*

  install -D -m 755 "${srcdir}/lcs_${pkgver}_src/src/crimesquad" "${pkgdir}/usr/bin/crimesquad"
}

sha256sums=('a9d861e7207c53fdeef1d8d4220056574e2f47f262caa8e06ea8d5a3c407de02'
            '653a4e7b652c59d1661cd57d9dc8bc1bdc61f9c2d780c53cab220280aac7393a')
