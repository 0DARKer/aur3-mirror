# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# 

pkgname=squid-beta
pkgver=3.3.4
pkgrel=1
pkgdesc='Full-featured Web proxy cache server'
arch=('x86_64' 'i686')
url='http://www.squid-cache.org'
depends=('openssl' 'pam' 'cron' 'perl' 'libltdl' 'libnetfilter_conntrack')
makedepends=('libcap')
license=('GPL')
options=('emptydirs')
conflicts=('squid')
backup=('etc/squid/squid.conf'
        'etc/squid/mime.conf'
        'etc/conf.d/squid')
install=$pkgname.install
source=("http://www.squid-cache.org/Versions/v3/3.3/squid-$pkgver.tar.bz2"
        'squid'
        'squid.conf.d'
        'squid.pam'
        'squid.cron'
        'squid.service')
md5sums=('0ef8e63a980389c62130fa639079fb54'
         'e90895ce22c0b618c89a46a9a181fe6c'
         '2383772ef94efddc7b920628bc7ac5b0'
         '270977cdd9b47ef44c0c427ab9034777'
         'b499c2b725aefd7bd60bec2f1a9de392'
         '20e00e1aa1198786795f3da32db3c1d8')

build() {
  cd "$srcdir/squid-$pkgver"

  # fix cache_dir, cache_dir size, and effective group.
  sed '/^DEFAULT_SWAP_DIR/ s@/cache@/cache/squid@' -i src/Makefile.in
  sed '/^#cache_dir/ s/100/256/
       /^NAME: cache_effective_group/ {n;n;s/none/proxy/}' -i src/cf.data.pre
  sed -i -e 's:/usr/local/squid/etc:/etc/squid:' \
		INSTALL QUICKSTART \
		helpers/basic_auth/MSNT/README.html \
		helpers/basic_auth/MSNT/confload.cc \
		helpers/basic_auth/MSNT/msntauth.conf.default \
		scripts/fileno-to-pathname.pl \
		scripts/check_cache.pl \
		tools/cachemgr.cgi.8 \
		tools/purge/conffile.hh \
		tools/purge/README  || die

  sed -i -e 's:/usr/local/squid/var/logs:/var/log/squid:' \
		QUICKSTART \
		src/log/access_log.cc || die
  sed -i -e 's:/usr/local/squid/logs:/var/log/squid:' \
		src/log/access_log.cc || die

  sed -i -e 's:/usr/local/squid/libexec:/usr/lib/squid:' \
		helpers/external_acl/unix_group/ext_unix_group_acl.8 \
		helpers/external_acl/session/ext_session_acl.8 \
		src/ssl/ssl_crtd.8 || die
  sed -i -e 's:/usr/local/squid/cache:/var/cache/squid:' \
		scripts/check_cache.pl || die
  sed -i -e 's:/usr/local/squid/ssl_cert:/etc/ssl/squid:' \
		src/ssl/ssl_crtd.8 || die
  sed -i -e 's:/usr/local/squid/var/lib/ssl_db:/var/cache/squid/ssl_cert_cache/ssl_db:' \
		src/ssl/ssl_crtd.8 || die
  sed -i -e 's:/var/lib/ssl_db:/var/lib/squid/ssl_db:' \
		src/ssl/ssl_crtd.8 || die

  ./configure \
    --prefix=/usr \
    --datadir=/usr/share/squid \
    --sysconfdir=/etc/squid \
    --libexecdir=/usr/lib/squid \
    --localstatedir=/var \
    --with-logdir=/var/log/squid \
    --with-pidfile=/run/squid.pid \
    --with-openssl \
    --enable-auth \
    --enable-auth-basic \
    --enable-auth-ntlm \
    --enable-auth-digest \
    --enable-auth-negotiate \
    --enable-removal-policies="lru,heap" \
    --enable-storeio="aufs,ufs,rock,diskd" \
    --enable-disk-io \
    --enable-external-acl-helpers \
    --enable-log-daemon-helpers \
    --enable-delay-pools \
    --enable-ssl \
    --enable-eui \
    --enable-icmp \
    --enable-esi \
    --enable-linux-netfilter \
    --enable-ident-lookups \
    --enable-cache-digests \
    --enable-htcp \
    --enable-snmp \
    --enable-ipv6 \
    --enable-epoll \
    --with-large-files \
    --enable-url-rewrite-helpers \
    --with-default-user=proxy \
    --enable-icap-client \
    --enable-ssl-crtd \
    --enable-zph-qos \
    --enable-follow-x-forwarded-for \
    --with-netfilter-conntrack \
    --enable-ecap
  make
}

package() {
  cd "$srcdir"

  make -C "squid-$pkgver" DESTDIR="$pkgdir" install
  install -Dm755 "$srcdir/squid" "$pkgdir/etc/rc.d/squid"
  install -Dm755 "$srcdir/squid.cron" "$pkgdir/etc/cron.weekly/squid"
  install -Dm755 "$srcdir/squid.conf.d" "$pkgdir/etc/conf.d/squid"
  install -Dm644 "$srcdir/squid.pam" "$pkgdir/etc/pam.d/squid"
  install -Dm644 "$srcdir/squid.service" \
    "$pkgdir/usr/lib/systemd/system/squid.service"
  rm -rf "$pkgdir/run" "$pkgdir/var/run"
}

# vim: ts=2 sw=2 et ft=sh
