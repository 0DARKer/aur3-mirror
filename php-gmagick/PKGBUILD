# Contributor: Pavol Hluchy (Lopo) <lopo@losys.eu>

pkgname=php-gmagick
pkgver=1.1.6RC3
pkgrel=1
pkgdesc="PHP extension for GMagick"
license=('PHP')
arch=('i686' 'x86_64')
url="http://pecl.php.net/package/gmagick"
depends=(
	'php>=5.1.3'
	'graphicsmagick>=1.2.6'
	)
backup=('etc/php/conf.d/gmagick.ini')
install=php-gmagick.install
source=("http://pecl.php.net/get/gmagick-$pkgver.tgz")
sha512sums=('55215584417383715eff2eed671cbdb9fb324a094ea6da455196f9088fc1cf660a2d158273489d1cebd60408af1181b4ddd48e12a4b46a9dc62689f3747f4c3e')

build() {
	cd "$srcdir/gmagick-$pkgver"

	phpize || return 1
	./configure --prefix=/usr
	make || return 1
	}

package() {
	cd "$srcdir/gmagick-$pkgver"

	make INSTALL_ROOT="$pkgdir" install
	echo ';extension=gmagick.so' > gmagick.ini
	install -D -m644 gmagick.ini "$pkgdir/etc/php/conf.d/gmagick.ini"
	}
