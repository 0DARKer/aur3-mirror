# Contributor: Alessio Biancalana <dottorblaster@gmail.com>

pkgname=gnome-power-manager-notify-osd
realname=gnome-power-manager
pkgver=2.28.3
pkgrel=1
pkgdesc="Session daemon that makes it easy to manage your laptop or desktop system."
arch=(i686 x86_64)
url="http://www.gnome.org/projects/gnome-power-manager/"
license=('GPL')
depends=('libnotify>=0.4.5' 'libunique>=1.1.2' 'libcanberra>=0.21' 'hicolor-icon-theme' 'devicekit-power>=012' 'gnome-keyring>=2.28.1')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils>=0.18.0' 'gnome-panel>=2.28.0')
optdepends=('gnome-panel')
options=(!emptydirs)
install=gnome-power-manager.install
groups=(gnome-extra)
provides=gnome-power-manager
replaces=gnome-power-manager
conflicts=gnome-power-manager
source=(http://ftp.gnome.org/pub/GNOME/sources/gnome-power-manager/2.28/${realname}-${pkgver}.tar.bz2 \
notifications.patch)
md5sums=('1fb329b8a6e9c63f46df1ea4bf3bb275'
         '2b4047347ab60100be85a3321541c03b')

build() {
  cd "${srcdir}/${realname}-${pkgver}"
  export LDFLAGS="${LDFLAGS} -Wl,--as-needed"
  patch -Np1 -i ${srcdir}/notifications.patch
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var \
	      --libexecdir=/usr/lib/gnome-power-manager \
	      --with-dpms-ext \
              --disable-scrollkeeper \
              --with-dbus-services=/usr/share/dbus-1/services \
              --enable-applets || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${realname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
