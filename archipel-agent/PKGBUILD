# Maintainer: Sebastien Leduc <sebastien@sleduc.fr>
# Contributor: Shanto <shanto@hotmail.com>

pkgname=archipel-agent
_pkgver=beta5
pkgver=0.5.0.beta
pkgrel=2
pkgdesc="A solution to manage and supervise virtual machines. (Agent)"
arch=(any)
url="http://archipelproject.org/"
license=('AGPL')
depends=(
  python2 python2-{apscheduler,sqlalchemy,numpy,magic,ctypes}
  libvirt qemu-kvm python-imaging xmpppy
)
options=(!strip !emptydirs)
install=$pkgname.install
source=(
  "https://github.com/ArchipelProject/Archipel/zipball/@$_pkgver"
  "initscript.patch"
  "archipel.service"
)
md5sums=('4362673aad9c29c99d24dba51d0a67d6'
         'd274be437e8b32413201277db12eb52f'
         'f6f40368da9bde8a41ab2c5a8f856b80')

package() {
  cd `find "$srcdir" -type d -name ArchipelAgent -print -quit`
  patch -p1 --input "$srcdir"/initscript.patch
  for mod in archipel-core archipel-agent-* archipel-agent; do
    pushd $mod 1>/dev/null
    python2 setup.py install --root="$pkgdir/" --optimize=1
    popd 1>/dev/null
  done;

  _datadir="$pkgdir"/usr/share/archipel
  mkdir -p "$_datadir"
  mv "$pkgdir/usr/install" "$_datadir"/

  cd "$srcdir"
  install -D -m644 archipel.service "$pkgdir/usr/lib/systemd/system/archipel.service"
}

# vim:set ts=2 sw=2 et:
