# Maintainer: Yushin Huang <hyslion@gmail.com>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgbase=lib32-gcin
pkgname=lib32-gcin
_pkgname=gcin
pkgver=2.8.0
pkgrel=1
true && pkgname=('lib32-gcin-im-client' 'lib32-gcin-qt4' 'lib32-gcin-gtk2')
url='http://hyperrate.com/dir.php?eid=67'
license=('LGPL')
arch=('x86_64')
depends=("gcin=${pkgver}")
makedepends=('lib32-gtk2' 'lib32-qt4' 'gcc-multilib')
source=("http://www.csie.nctu.edu.tw/~cp76/gcin/download/${_pkgname}-${pkgver}.tar.xz"
        "qconfig.h"
        "configure.patch"
        "qt4-im.patch")
sha1sums=('69fe604939bc013f043ff0b4baa63b7951b26c95'
          '85dba460dc77e0cd07d07a33388a345cc91c98d3'
          'a120289575debcc6e49fd77df50791df40805761'
          '35a288dd74f28fd730ac093878680f74630ea57b')

build() {
    cd "${srcdir}/"
    mkdir -p QtCore
    cp qconfig.h QtCore

    export CC="gcc -m32 -I${srcdir} -I${srcdir}/QtCore"
    export CXX="g++ -m32 -I${srcdir} -I${srcdir}/QtCore"

	cd "${srcdir}/${_pkgname}-${pkgver}"

    patch -p1 < ${srcdir}/configure.patch
    patch -p1 < ${srcdir}/qt4-im.patch

	sed '/include suffixes-rule/a \
		CFLAGS+='"${CFLAGS}"' \
		LDFLAGS+='"${LDFLAGS}"' \
		OPTFLAGS=' \
		-i Makefile
    

	./configure           \
        --prefix=/usr     \
        --use_i18n=Y

    make -C im-client # build gtk-im as dependency too
    make -C qt4-im
}

package_lib32-gcin-im-client() {
    pkgdesc='gcin 32bit im-client'
    depends=("gcin=${pkgver}")

	cd "${srcdir}/${_pkgname}-${pkgver}/im-client"
	make DESTDIR="${pkgdir}" install
}

package_lib32-gcin-gtk2() {
    pkgdesc='Gtk2 IM module for gcin - 32bit'
    depends=("lib32-gcin-im-client=${pkgver}")
    install=lib32-gcin-gtk2.install

	cd "${srcdir}/${_pkgname}-${pkgver}/gtk-im"
	make DESTDIR="${pkgdir}" install
}

package_lib32-gcin-qt4() {
    pkgdesc='Qt4 IM module for gcin - 32bit'
    depends=("lib32-gcin-im-client=${pkgver}")

	cd "${srcdir}/${_pkgname}-${pkgver}/qt4-im"
	make DESTDIR="${pkgdir}" install
}

pkgdesc="Gcin 32bit IM modules"
