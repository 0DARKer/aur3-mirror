# Maintainer: Hanspeter Portner <agenthp@users.sf.net>
pkgname=glmixer-svn
pkgver=560
pkgrel=2
pkgdesc="A real-time graphics mixing software for video performance"
arch=(i686 x86_64)
url="http://code.google.com/p/glmixer/"
license=('GPL3')
depends=(qt glew opencv)
makedepends=('subversion')
source=()
md5sums=()

_svntrunk=http://glmixer.googlecode.com/svn/trunk
_svnmod=glmixer

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  # build
  cmake -DUSE_OPENCV=ON -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir/" install
}
