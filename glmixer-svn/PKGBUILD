# Maintainer: Hanspeter Portner <agenthp@users.sf.net>
pkgname=glmixer-svn
pkgver=479
pkgrel=2
pkgdesc="A real-time graphics mixing software for video performance"
arch=(i686 x86_64)
url="http://code.google.com/p/glmixer/"
license=('GPL3')
depends=(ffmpeg qt glew)
makedepends=('subversion')
source=('01_compile_fix_on_arch_linux.patch')
md5sums=('1c4caddd62fff753df454685defcedcb')

_svntrunk=http://glmixer.googlecode.com/svn/trunk
_svnmod=glmixer

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  # apply patch
  patch -p0 < "$srcdir/01_compile_fix_on_arch_linux.patch"

  # build
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir/" install
}
