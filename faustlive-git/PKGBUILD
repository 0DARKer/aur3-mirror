# Maintainer: Albert Graef <aggraef at gmail.com>

pkgname=faustlive-git
pkgver=505.6fa5100
pkgrel=1
pkgdesc="An advanced self-contained prototyping environment for the Faust programming language with an ultra-short edit-compile-run cycle."
arch=('i686' 'x86_64')
url="http://faust.grame.fr/"
license=('GPL')
depends=('faust2-git' 'qt4' 'jack2' 'qrencode'
	 'libmicrohttpd' 'openssl' 'liblo' 'curl')
makedepends=('gcc' 'git')
provides=('faustlive')
conflicts=('faustlive')
source=("$pkgname::git+git://git.code.sf.net/p/faudiostream/faustlive")
md5sums=('SKIP')

pkgver() {
  cd $srcdir/$pkgname
  # Make sure that we get the dev branch.
  git checkout dev > /dev/null 2>&1
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $srcdir/$pkgname
  make arch=Linux PREFIX=/usr
}

package() {
  cd $srcdir/$pkgname
  make install arch=Linux PREFIX=/usr DESTDIR="$pkgdir"
  # docs
  install -d "$pkgdir/usr/share/doc/faustlive"
  cp -R Documentation/* "$pkgdir/usr/share/doc/faustlive"
}
