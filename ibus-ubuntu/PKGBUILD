# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars at gmail dot com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus-ubuntu
_ubuntu_rel=3ubuntu1
pkgver=1.4.1.${_ubuntu_rel}
pkgrel=1
pkgdesc='Next Generation Input Bus for Linux.'
arch=('i686' 'x86_64')
license=('LGPL')
url='http://ibus.googlecode.com'
depends=('dbus-python>=0.84.0' 'gconf' 'dconf' 'python2' 'pygtk' 'pyxdg' 'iso-codes' 'librsvg' 'python-notify' 'hicolor-icon-theme' 'gtk3')
optdepends=('notification-daemon')
makedepends=('intltool')
provides=("ibus=${pkgver}")
conflicts=('ibus')
options=('!libtool')
install=ibus.install
source=("http://ibus.googlecode.com/files/${pkgname%-*}-${pkgver%.*}.tar.gz"
        "http://archive.ubuntu.com/ubuntu/pool/main/i/${pkgname%-*}/${pkgname%-*}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('465f5d099ca60612e9d28c77da6a8da2435cc50c3f2f9b93c4c9258d18bb963d85b84ed9d7f9c58e90dc07a4898dc9668a6e2a3484b69074a1777761389ba468'
            '7792f9c12a78a9b1cdece576a8a787c68cf44e9d643e10df40efa2fe024f672c01f25a65cb83be97a2e6d83ab99230791140f05b8b71ec71a6a8ed14026ca3fb')

build() {
  cd "${pkgname%-*}-${pkgver%.*}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  # Cannot find gmodule-2.0
  CFLAGS="${CFLAGS} $(pkg-config --libs gmodule-2.0)"

  export PYTHON=python2

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/${pkgname%-*} \
    --sysconfdir=/etc \
    --with-gconf-schema-file-dir=/usr/share/gconf/schemas \
    --enable-gtk3 \
    --enable-surrounding-text # From debian/rules

  # Python2 fix
  for file in setup/ibus-setup.in ui/gtk/ibus-ui-gtk.in; do
    sed -i 's_exec python_exec python2_' $file
  done

  # Arch Linux's paths are different from Ubuntu's
  sed -i -e 's/\(lib\).*\(gtk-2.0\)/\1\/\2/g' -e 's/qt4/qt/g' "${srcdir}/debian/xinput/ibus"

  make ${MAKEFLAGS}
}

package() {
  cd "${pkgname%-*}-${pkgver%.*}"

  export PYTHON=python2
  make DESTDIR="${pkgdir}/" install

  find "${pkgdir}" -type f -exec sed -i 's_exec /usr/bin/python_exec /usr/bin/python2_' {} \;

  #Files for im-switch
  install -dm755 "${pkgdir}/etc/X11/xinit/xinput.d/"
  install -m644 "${srcdir}/debian/xinput/ibus" "${pkgdir}/etc/X11/xinit/xinput.d/"

  #Fix ibus not showing up in GTK2 programs
  #  Problem: Causes Unity shell to segfault at login
  #echo "export GTK_IM_MODULE_FILE=/etc/gtk-2.0/gtk.immodules" >> "${pkgdir}/etc/X11/xinit/xinput.d/ibus"
}

# vim:set ts=2 sw=2 et:
