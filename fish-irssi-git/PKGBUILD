# Maintainer: aksr <aksr at t-com dot me>
pkgname=fish-irssi-git
_pkgname=irssi
pkgver=143.76dcfd0
_pkgver=0.8.16
pkgrel=2
pkgdesc="irssi FiSH plugin using GMP"
arch=('i686' 'x86_64')
url="https://github.com/falsovsky/FiSH-irssi"
license=('GPL')
groups=()
depends=('irssi' 'openssl' 'gmp')
makedepends=('git')
provides=()
conflicts=('fish-irssi')
replaces=()
backup=()
options=()
install=
source=(http://irssi.org/files/$_pkgname-$_pkgver.tar.bz2
        irssi-fish::git://github.com/falsovsky/FiSH-irssi)
noextract=()
md5sums=('4346119c4c000d0198cda17666ff1f06'
         'SKIP')
sha1sums=('631dd70b6d3872c5f81c1a46a6872fef5bd65ffb'
          'SKIP')
sha256sums=('3f7afed2a2754f1f5bf5cf2c87476f3c4d4aa5d71f9f4f68d05f927220d19f97'
            'SKIP')

_gitname=irssi-fish

pkgver() {
  cd $_gitname
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd $srcdir/$_gitname
  ./autogen.sh
  ./configure --prefix=/usr --with-irssi="$srcdir/$_pkgname-$_pkgver"
  make
}

package() {
  cd $srcdir/$_gitname

  install -Dm755 src/.libs/libfish.so $pkgdir/usr/lib/irssi/modules/libfish.so
  install -Dm644 README               $pkgdir/usr/share/doc/$pkgname/help/FiSH-README
  install -Dm644 FiSH-irssi.txt       $pkgdir/usr/share/doc/$pkgname/help/FiSH-irssi.txt
}

