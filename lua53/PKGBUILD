# Maintainer: Philipp Gesang <phg42.2a@gmail.com>
# Derived from the Lua 5.2 and 5.1 PKGBUILDs in extra which lists
# these guys as contributors:
# Contributor: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=lua53
pkgver=5.3.0_work1
pkgrel=2
pkgdesc='Powerful light-weight programming language designed for extending applications, version 5.3 working draft'
arch=('i686' 'x86_64')
url='http://www.lua.org/'
depends=('readline')
license=('MIT')
options=('!makeflags' '!emptydirs')
source=('http://www.lua.org/work/lua-5.3.0-work1.tar.gz'
        'liblua.so.patch'
        'LICENSE'
        'lua.pc')
md5sums=('d4053ee55741eab5ecd7061326577586'
         '42ae5d526cc73695ee05ca8db6b19754'
         '5d236d27128ef36bbc8f228faaf76f7f'
         '3369fab35575e718eae71884674ba387')

build() {
  cd "lua-${pkgver/_/-}"
  patch -p1 -i "$srcdir/liblua.so.patch"
  export CFLAGS="$CFLAGS -fPIC"
  sed "s/%VER%/${pkgver%.*}/g;s/%REL%/$pkgver/g" ../lua.pc > lua.pc
  sed -r -e '/^LUA_(SO|A|T)=/ s/lua/lua5.3/' -e '/^LUAC_T=/ s/luac/luac5.3/' -i src/Makefile
  make MYCFLAGS="$CFLAGS" MYLDFLAGS="$LDFLAGS" linux
}

package() {
  cd "lua-${pkgver/_/-}"
  make \
    TO_BIN="lua5.3 luac5.3" \
    TO_LIB="liblua5.3.a liblua5.3.so liblua5.3.so.5.3 liblua5.3.so.5.3.0" \
    INSTALL_DATA="cp -d" \
    INSTALL_TOP="$pkgdir/usr" \
    INSTALL_INC="$pkgdir/usr/include/lua5.3" \
    INSTALL_MAN="$pkgdir/usr/share/man/man1" \
    install
  install -Dm644 lua.pc "$pkgdir/usr/lib/pkgconfig/lua5.3.pc"
  install -d "$pkgdir/usr/share/doc/lua5.3"
  install -m644 doc/*.{gif,png,css,html} "$pkgdir/usr/share/doc/lua5.3"
  install -D -m644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  ln -s liblua5.3.so "$pkgdir/usr/lib/liblua.so.5.3"
  ln -s liblua5.3.so "$pkgdir/usr/lib/liblua.so.$pkgver"
  cd "$pkgdir/usr/share/man/man1"
  mv lua.1 lua5.3.1
  mv luac.1 luac5.3.1
}

# vim:set ts=2 sw=2 et:
