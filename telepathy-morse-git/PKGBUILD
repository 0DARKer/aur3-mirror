# Maintainer: Antonio Rojas

pkgname=telepathy-morse-git
pkgver=r44.754c1c1
pkgrel=1
pkgdesc="Telepathy Connection Manager for the Telegram network"
arch=('i686' 'x86_64')
url="https://github.com/Kaffeine/telepathy-morse/"
license=('GPL')
depends=('telepathy-qt4')
makedepends=('cmake' 'git' 'telegram-qt4')
source=("git+https://github.com/Kaffeine/telepathy-morse/")
md5sums=('SKIP')

pkgver() {
  cd telepathy-morse
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build

  sed -i 's|CTelegramCore.hpp|TelegramQt/CTelegramCore.hpp|' telepathy-morse/textchannel.hpp
}

build() {
  cd build
  cmake ../telepathy-morse \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib/telepathy \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
