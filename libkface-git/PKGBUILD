# Contributor: Flamelab <panosfilip@gmail.com> 
# PKGBUILD based on libkface-svn. Contributors:
# Chris Heien <chris.h.heien@gmail.com>
# Dany Martineau <dany.luc.martineau at gmail.com>

pkgname=libkface-git
pkgver=r937.baf426f
pkgrel=1
pkgdesc='A Qt/C++ wrapper around LibFace library to perform face recognition and detection over pictures'
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs' 'opencv')
makedepends=('git' 'automoc4' 'cmake')
conflicts=('libkface')
provides=('libkface')
groups=('digikam-git')
source=('git://anongit.kde.org/libkface')
md5sums=('SKIP')

pkgver() {
  cd libkface
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake  ../libkface -DCMAKE_BUILD_TYPE=Release \
		-DKDE4_BUILD_TESTS=OFF \
		-DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}