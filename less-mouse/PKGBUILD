# This package is based on the original less package from ABS
# Patch by ararus from http://www.linuxquestions.org
# (view post http://www.linuxquestions.org/questions/linux-software-2/less-command-output-i-cannot-scroll-through-it-627821/page2.html?s=3b5e6292d15fca5f10cd901a9bf6c9f2#post3094148)
# edited by lxyd <lobster@hotmail.ru> and harryNID
# changes made to the original patch:
# - no environment vars needed
# - default scroll is _3_ lines
# - Ctrl+scroll scrolls half a page

pkgname=less-mouse
pkgver=451
pkgrel=1
pkgdesc="A terminal based program for viewing text files with mouse scroll"
license=('GPL3')
arch=('i686' 'x86_64')
url="http://www.greenwoodsoftware.com/less"
depends=('ncurses' 'pcre')
makedepends=('patch')
provides=('less')
conflicts=('less')
source=(http://www.greenwoodsoftware.com/less/less-$pkgver.tar.gz less-mouse.patch)
sha256sums=('9fe8287c647afeafb4149c5dedaeacfd20971ed7c26c7553794bb750536b5f57'
            '055b0b070e3fc8587f550e9b7e8eddf9c84455471cf873eaf97902aab2ab4e6a')

build() {
  cd ${srcdir}/less-${pkgver}
  patch -p1 -i ${srcdir}/less-mouse.patch
  ./configure --prefix=/usr --sysconfdir=/etc --with-regex=pcre
  make 
}
package() {
  cd "${srcdir}/less-${pkgver}"
  make prefix="${pkgdir}"/usr install
}
