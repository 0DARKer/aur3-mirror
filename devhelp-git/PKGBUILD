# Contributor: Yosef Or Boczko <yosefor3@walla.com>

_pkgname=devhelp
pkgname=$_pkgname-git
pkgver=3.8.0.26.g5948a88
pkgrel=1
pkgdesc="API documentation browser for GNOME"
arch=(i686 x86_64)
license=('GPL')
url="http://live.gnome.org/devhelp/"
depends=('webkitgtk3' 'hicolor-icon-theme' "gtk3>=3.9.2")
makedepends=('pkgconfig' 'intltool' 'python')
options=('!libtool' '!emptydirs')
install=devhelp.install
source=('git://git.gnome.org/devhelp'
        '0001-Port-to-GtkHeaderBar.patch'
		'0002-Make-headerbar-a-titlebar.patch'
		'0003-Use-GtkButton-subclasses-instead-of-GdHeaderButton-o.patch'
		'0004-Remove-libgd-submodule.patch'
    '0005-Bump-required-GTK.patch')
sha256sums=('SKIP'
            'f4c22a90520a7c71d2631e342ff5660d50553ee8060d5244009d510d9c6f8174'
            '5b866cd37d1ffa4387eb3e22d466514f3ad6eb91eeb26ff5c14046199c4cbe6a'
            'd8ef8f27840208e077fc8e050eeefe7b6de0b42e9381926490ba3396d0ec96bd'
            '53432a2620066058bdade136676a26f828608f81ad29d9c834a46c08d3ba8626'
            '475f1cdaa74089b6ec6165344164b56e7c9fa2060db101760b38ce662b2ff9b1')
conflicts=('devhelp')
replaces=('devhelp')
provides=('devhelp' 'devhelp=3.9.1')

pkgver() {
    cd "$srcdir/$_pkgname"
    git describe --always | sed 's|-|.|g'
}

prepare() {
  cd "$srcdir/$_pkgname"
  
  patch -Np1 -i "$srcdir/0001-Port-to-GtkHeaderBar.patch"
  patch -Np1 -i "$srcdir/0002-Make-headerbar-a-titlebar.patch"
  patch -Np1 -i "$srcdir/0003-Use-GtkButton-subclasses-instead-of-GdHeaderButton-o.patch"
  patch -Np1 -i "$srcdir/0004-Remove-libgd-submodule.patch"
  patch -Np1 -i "$srcdir/0005-Bump-required-GTK.patch"
}
build() {
  cd "$srcdir/$_pkgname"
    
  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="${pkgdir}" install
}
