# Maintainer: Yosef Or Boczko <yoseforb@gmail.com>

_pkgname=devhelp
pkgname=$_pkgname-git
pkgver=3.11.4.7.g631b8cc
pkgrel=1
pkgdesc="API documentation browser for GNOME"
arch=(i686 x86_64)
license=('GPL')
url="http://live.gnome.org/devhelp/"
depends=('webkitgtk3' 'hicolor-icon-theme' "gtk3" "glib2")
makedepends=('pkgconfig' 'intltool' 'python')
options=('!libtool' '!emptydirs')
install=devhelp.install
conflicts=('devhelp')
replaces=('devhelp')
provides=('devhelp=3.11.5')
source=('git://git.gnome.org/devhelp')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd "$srcdir/$_pkgname"
}

build() {
  cd "$srcdir/$_pkgname"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="${pkgdir}" install
}
