# Maintainer: Francois Boulogne <fboulogne at april dot org>
pkgname=subsurface-git
pkgver=v4.0.1.r8.g04aa563
pkgrel=1
pkgdesc="A divelog software written by Linus"
arch=('i686' 'x86_64')
url="http://github.com/torvalds/subsurface/"
license=('GPL2')
depends=('osm-gps-map' 'libdivecomputer-git' 'libxml2' 'gtk2' 'glib2' 'gconf' 'hicolor-icon-theme' 'libzip' 'kdeedu-marble')
optdepends=()
makedepends=('git' 'make' 'asciidoc')
install=subsurface-git.install
provides=()
conflicts=()
source=("git://subsurface.hohndel.org/subsurface.git")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/subsurface"
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/subsurface"
  qmake-qt4

  #Prevent pacman/namcap warning
  sed -i -e "s|\$(INSTALL) -d -m 644 \$(MANDIR)|\$(INSTALL) -d -m 755 \$(MANDIR)|" Makefile
  #gtk_update is handle by the PKGBUILD
  sed -i -e "s|\$(gtk_update_icon_cache)| |" Makefile

  make
}

package() {
  cd "${srcdir}/subsurface"
  make install INSTALL_ROOT="${pkgdir}"
}

# vim:set ts=2 sw=2 et:
