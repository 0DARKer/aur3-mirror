# Contributor: Francois Boulogne <fboulogne at april dot org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=subsurface-git
_pkgname=subsurface
pkgver=20150220.fc0d2a3
pkgrel=1
pkgdesc='Divelog program'
url='http://subsurface-divelog.org/'
license=('GPL2')
arch=('i686' 'x86_64')
makedepends=('git' 'asciidoc')
depends=('libzip' 'libxml2' 'libxslt' 'sqlite' 'libusbx' 'libgit2'
         'subsurface-libdc' 'subsurface-marble')
source=('git://git.subsurface-divelog.org/subsurface')
sha1sums=('SKIP')

install=install

provides=('subsurface')
conflicts=('subsurface')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	git log -1 --format='%cd.%h' --date=short | tr -d -
}

build() {
	cd "${srcdir}/${_pkgname}"
	qmake-qt5 \
		-config release \
		SPECIAL_MARBLE_PREFIX=1 \
		INCLUDEPATH+='/usr/include/subsurface' \

	make
}

package() {
	cd "${srcdir}/${_pkgname}"
	make INSTALL_ROOT="${pkgdir}" install
}
