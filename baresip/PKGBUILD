# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Luka Perkov <archlinux <at> lukaperkov <dOt> net>

pkgname=baresip
pkgver=0.4.0
pkgrel=1
pkgdesc="A portable and modular SIP User-Agent with audio and video support" 
arch=('i686' 'x86_64')
url="http://www.creytiv.com/"
license=('BSD')
depends=('libre>=0.4.0' 'librem>=0.4.0' 'celt' 'ffmpeg'
         'gstreamer0.10' 'portaudio' 'util-linux' 'v4l-utils')
source=("http://www.creytiv.com/pub/$pkgname-$pkgver.tar.gz")
md5sums=('770a13543866e801f6544e194097e911')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # x86_64 fPIC fix
  [ "$CARCH" = x86_64 ] && _fpic=-fPIC

  make CFLAGS="$_fpic \
               -I/usr/include/gsm \
               -I/usr/include/rem \
               -I/usr/include/re \
               `pkg-config --cflags gstreamer-0.10` \
               -Iinclude -DHAVE_PWD_H"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # license
  install -Dm644 docs/COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
