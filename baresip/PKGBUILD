# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Luka Perkov <archlinux <at> lukaperkov <dOt> net>

_ver=0.4.2
pkgname=baresip
pkgver=0.4.3
pkgrel=1
pkgdesc="A portable and modular SIP User-Agent with audio and video support" 
arch=('i686' 'x86_64')
url="http://www.creytiv.com/"
license=('custom:BSD')
depends=("librem>=$_ver" 'celt' 'ffmpeg' 'mpg123' 'cairo'
         'gstreamer0.10' 'portaudio' 'spandsp')
source=("http://www.creytiv.com/pub/$pkgname-$pkgver.tar.gz")
md5sums=('36224d4f60ee382832cc1fbdc0f85bd2')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # missing sys/types.h in alsa module
  sed -i '8i#include <sys/types.h>' modules/alsa/alsa*.c

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # license
  install -Dm644 docs/COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
