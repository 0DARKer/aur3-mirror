# Maintainer: Jameson Pugh <imntreal@gmail.com>
# Contributor: J0k3r <moebius282 e4at gmail D0_T com>

pkgname=lib32-sdl2
pkgver=2.0.0
pkgrel=1
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Version 2.0). (Mercurial Version) (32 bit)"
arch=('x86_64')
url="http://www.libsdl.org"
license=('MIT')
depends=('lib32-glibc' 'bash')
options=('!libtool')
_local_repo="sdl2"
source=("http://www.libsdl.org/tmp/release/SDL2-${pkgver}.tar.gz")
sha256sums=('b415230b8f2f42d2d1a37df3ace220199e5b035e381f5aaff350ece8d8d9b4cd')

prepare()
{
  mkdir -p "$srcdir/SDL2-${pkgver}/build/"
}

build() 
{
  export CC='gcc -m32'
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd "$srcdir/SDL2-${pkgver}/build/"
  ../configure --prefix=/usr --libdir=/usr/lib32
  make
}

package()
{
  cd "$srcdir/SDL2-${pkgver}/build/"
  make DESTDIR="$pkgdir" install

  #already included in sdl2-hg
  rm -rf "$pkgdir/usr/share/aclocal/" "$pkgdir/usr/include/" "${pkgdir}/usr/bin/"

  install -Dm644 ../COPYING.txt "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim:set ts=2 sw=2 et:
