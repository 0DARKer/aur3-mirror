# Maintainer: Alfredo Ramos <alfredo dot ramos at yandex dot com>
# Contributor: Martin C. Doege <mdoege at compuserve dot com>
# Contributor: kusakata <shohei atmark kusakata period com>

_pkgname=freeminer
pkgname=${_pkgname}-git
pkgver=0.4.12.6.r260.gab04c24
pkgrel=1
pkgdesc="An open source sandbox game inspired by Minecraft. Development version."
arch=(
	"${CARCH}"
)
url="http://freeminer.org/"
license=(
	"GPL3"
	"CCPL:cc-by-sa-3.0"
)

depends=(
	"irrlicht"
	"leveldb"
	"libvorbis"
	"openal"
	"sqlite"
	"curl"
	"luajit"
	"msgpack-c"
	"xdg-utils"
	"hicolor-icon-theme"
)
optdepends=()
makedepends=(
	"cmake"
	"git"
)
provides=(
	"${_pkgname}=${pkgver}"
)
conflicts=(
	"${_pkgname}"
)
replaces=(
	"${_pkgname}"
)

install=${pkgname}.install

source=(
	"git+https://github.com/${_pkgname}/${_pkgname}.git"
	"${pkgname}.install"
)
sha512sums=(
	"SKIP"
	"d590345e9b87e4350b7420eebf8f69e4ad65a53415257573b569ed1c85568a40b4f65fc8df5925f7c03af75d340c52a10a4d1389e5d868816aef9711102ebef1"
)

pkgver() {
	# Updating package version
	cd ${srcdir}/${_pkgname}
	(
		set -o pipefail
		git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g' ||
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

prepare() {
	# Cloning with submodules
	cd ${srcdir}/${_pkgname}
	git submodule update --init --recursive
	
	# Build directory
	mkdir -p ${srcdir}/build
}

build() {
	# Number of jobs
	declare -i njobs=$(nproc)
	
	if [[ ${njobs} -ge 8 ]]; then
		njobs=$(( ${njobs} - 2 ))
	fi

	# Building package
	cd ${srcdir}/build
	cmake ../${_pkgname} \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DRUN_IN_PLACE=0
	make -j${njobs}
}

package() {
	# Installing package
	cd ${srcdir}/build
	make DESTDIR=${pkgdir} install
}