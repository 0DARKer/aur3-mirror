# Maintainer: Alfredo Ramos <alfredo dot ramos at yandex dot com>
# Contributor: Martin C. Doege <mdoege at compuserve dot com>
# Contributor: kusakata <shohei atmark kusakata period com>

_pkgname=freeminer
pkgname=${_pkgname}-git
pkgver=0.4.10.4.r541.gb413a4b
pkgrel=1
pkgdesc="An open source sandbox game inspired by Minecraft. Development version."
arch=(
	"$CARCH"
)
url="http://freeminer.org/"
license=(
	"GPL3"
	"CCPL:cc-by-sa-3.0"
)

depends=(
	"irrlicht"
	"leveldb"
	"libvorbis"
	"openal"
	"sqlite"
	"curl"
	"luajit"
	"gtk-update-icon-cache"
	"msgpack"
)
optdepends=()
makedepends=(
	"cmake"
	"git"
)
provides=(
	"${_pkgname}=${pkgver}"
)
conflicts=(
	"${_pkgname}"
)
replaces=(
	"${_pkgname}"
)

install=${pkgname}.install

source=(
	"git+https://github.com/${_pkgname}/${_pkgname}.git"
	"${pkgname}.install"
)
sha384sums=(
	"SKIP"
	"5a0e3c9aa4ef5028f424000e3dd089e0db7921349fe71b840cb9372c6a95742cd6c315264eb726020cec55bc7aaf57f4"
)
sha512sums=(
	"SKIP"
	"dbb54802291438a256147765325be970379ee761f20b85ec31d878d145dcccd209ae7e521ae1ee5981adff0c7f62a50b582c0d47db59d491db07c9df842e635c"
)

pkgver() {
	# Updating package version
	cd ${srcdir}/${_pkgname}
	(
		set -o pipefail
		git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g' ||
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

prepare() {
	# Cloning with submodules
	cd ${srcdir}/${_pkgname}
	git submodule update --init --recursive
}

build() {
	# Number of jobs
	declare -i njobs=$(nproc)
	
	if [[ $(nproc) -ge 8 ]]; then
		njobs=$(( $njobs - 2 ))
	fi

	# Building package
	cd ${srcdir}/${_pkgname}
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DRUN_IN_PLACE=0
	make -j${njobs}
}

package() {
	# Installing package
	cd ${srcdir}/${_pkgname}
	make DESTDIR=${pkgdir} install
}