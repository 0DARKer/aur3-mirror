# Maintainer: Alfredo Ramos <alfredo dot ramos at yandex dot com>
# Contributor: Martin C. Doege <mdoege at compuserve dot com>
# Contributor: kusakata <shohei atmark kusakata period com>

_pkgname=freeminer
pkgname=${_pkgname}-git
pkgver=r5471.0fd8d93
pkgrel=1
pkgdesc="An open source sandbox game inspired by Minecraft. Development version."
arch=(
	"$CARCH"
)
url="http://freeminer.org/"
license=(
	"GPL3"
	"CCPL:cc-by-sa-3.0"
)

depends=(
	"irrlicht"
	"leveldb"
	"libvorbis"
	"openal"
	"sqlite"
	"curl"
	"luajit"
	"freetype2"
	"hicolor-icon-theme"
)
optdepends=()
makedepends=(
	"cmake"
	"make"
)
provides=(
	"${_pkgname}=${pkgver}"
)
conflicts=(
	"${_pkgname}"
)
replaces=(
	"${_pkgname}"
)

install=${pkgname}.install

source=(
	"git+https://github.com/${_pkgname}/${_pkgname}.git"
	"${pkgname}.install"
)
md5sums=(
	"SKIP"
	"fddfcb7efb5aad784ef04db97bba69e8"
)
sha512sums=(
	"SKIP"
	"dbb54802291438a256147765325be970379ee761f20b85ec31d878d145dcccd209ae7e521ae1ee5981adff0c7f62a50b582c0d47db59d491db07c9df842e635c"
)

pkgver() {
	# Updating package version
	cd ${srcdir}/${_pkgname}
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	# Cloning with submodules
	cd ${srcdir}/${_pkgname}
	git submodule update --init --recursive
}

build() {
	# Number of jobs
	declare -i njobs=$(nproc)
	
	if [[ $(nproc) -ge 8 ]]; then
		njobs=$(( $njobs/2 ))
	fi

	# Building package
	cd ${srcdir}/${_pkgname}
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DRUN_IN_PLACE=0
	make -j${njobs}
}

package() {
	# Installing package
	cd ${srcdir}/${_pkgname}
	make install DESTDIR=${pkgdir}
}