# Maintainer:  Martin C. Doege <mdoege at compuserve dot com>
# Contributor: kusakata <shohei atmark kusakata period com>

pkgname=freeminer-git
_pkgname=freeminer
pkgver=20140430.64d5b5d
pkgrel=1
pkgdesc='Minecraft-inspired sandbox game based on Minetest (includes default and dwarves games)'
arch=('i686' 'x86_64')
url="http://freeminer.org/"
license=('LGPL/CC-BY-SA')
depends=('curl' 'freetype2' 'hicolor-icon-theme' 'irrlicht' 'luajit' 'leveldb' 'libvorbis' 'openal' 'sqlite')
makedepends=('cmake' 'git')
install=freeminer.install
source=("git://github.com/freeminer/freeminer.git"
	"git://github.com/freeminer/default.git"
	"git://github.com/mito551/dwarves_game.git"
)

pkgver() {
	cd "${srcdir}/${_pkgname}"
	git log -1 --format='%cd.%h' --date=short | tr -d -
}

prepare() {
	cd "${srcdir}"
	cp -a default/* freeminer/games/default
	cp -a dwarves_game freeminer/games/
}

build() {
	cd "${srcdir}/freeminer"
	cmake -D CMAKE_INSTALL_PREFIX=/usr . -DRUN_IN_PLACE=0
	make
}

package() {
	cd "${srcdir}/freeminer"
	make DESTDIR="$pkgdir" install
	cp -a games/dwarves_game "$pkgdir/usr/share/freeminer/games"
}

md5sums=('SKIP' 'SKIP' 'SKIP')
