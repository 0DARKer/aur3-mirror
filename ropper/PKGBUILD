# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>

pkgname=ropper
_pkgname=Ropper
pkgver=1.3.2
pkgrel=1
pkgdesc="Show information about binary files and find gadgets to build rop chains for different architectures"
arch=('any')
url="https://github.com/sashs/Ropper"
license=('GPL2')
depends=('python2-capstone')
makedepends=('python2-setuptools')
source=(https://github.com/sashs/${_pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('d1ba4b3bd1652740f165c404688553fc5e277bd03f721509e07f68be20eb431ffcb3bbc2b2370e3e6d9ff9bb103e8ae5b4bb2d2ab1c6e2cac51ce1650c938855')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed -ri 's/(ropper)\.py/\1/g' ropperapp/options.py
}

build() {
  cd ${_pkgname}-${pkgver}
  python2 setup.py build
}

package() {
  cd ${_pkgname}-${pkgver}
  python2 setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim:set ts=2 sw=2 et:
