# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>

pkgname=ropper
_pkgname=Ropper
pkgver=1.5.0
pkgrel=1
pkgdesc="Show information about binary files and find gadgets to build rop chains for different architectures"
arch=('any')
url="https://github.com/sashs/Ropper"
license=('GPL2')
depends=('python-capstone')
makedepends=('python-setuptools')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/sashs/${_pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('fc474465dacbb047e0f19bf785cf5e80b75b4d77ae44980bd5c581272f2615d7c50b770dc0014e2b6b8d700d068be61fb6617bac5833ab3fe1f0f3f1bbfd210a')

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim:set ts=2 sw=2 et:
