# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>

pkgname=ropper
_pkgname=Ropper
pkgver=1.4.2
pkgrel=1
pkgdesc="Show information about binary files and find gadgets to build rop chains for different architectures"
arch=('any')
url="https://github.com/sashs/Ropper"
license=('GPL2')
depends=('python2-capstone')
makedepends=('python2-setuptools')
source=(https://github.com/sashs/${_pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('c1387e9538faab92706c6e9b9daa0dfab04a7e4db060827d022fe72dcbd077a94a041c869f15c34599dacc561d391ba4db59ccc82fbb6be92594874c1793f567')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed -ri 's/(ropper)\.py/\1/g' ropperapp/options.py
  sed -ri 's/env (python)/\12/g' ropperapp/printer/rawprinter.py ropperapp/loaders/raw.py ropperapp/console.py
}

build() {
  cd ${_pkgname}-${pkgver}
  python2 setup.py build
}

package() {
  cd ${_pkgname}-${pkgver}
  python2 setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim:set ts=2 sw=2 et:
