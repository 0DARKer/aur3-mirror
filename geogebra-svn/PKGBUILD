# Maintainer: ifaigios <ifaigios_at_gmail_dot_com>
# Contributor: moostik <mooostik_at_gmail_dot_com>

pkgname=geogebra-svn
pkgver=9649
pkgrel=1
pkgdesc="Dynamic mathematics software with interactive graphics, algebra and spreadsheet - SVN version"
arch=('any')
url='http://www.geogebra.org/'
license=('GPL' 'CCPL:by-sa') # Application and source code under GPL / Language files under CCPL
depends=('jre' 'jogl' 'hicolor-icon-theme' 'shared-mime-info')
makedepends=('subversion' 'jdk' 'apache-ant')
optdepends=('maxima: can be used as CAS in geogebra')
install=('geogebra.install')
source=(geogebra.install
	geogebra.xml
	geogebra.png
	geogebra.desktop
	geogebra.sh
	geogebra-maxima.sh
	'http://www.geogebra.org/download/GeoGebra_hicolor_icons.tar.gz')
provides=('geogebra' 'geogebra-beta' 'geogebra5-beta')
conflicts=('geogebra' 'geogebra-beta' 'geogebra5-beta')
md5sums=('7a82a9f42d2e5ae9bdd8a51a515b3c21'
         'a58d76de044c06ce30c5a6830246290f'
         'e324ee3a2bb438cee625e3f29770a315'
         '3c4d928e34cb9dab15436bbceb972490'
         'b2e82d9b21159fbc02e10e2986a39a6a'
         '623d543a7e25f08c6f3f3fe5c2e89be7'
         '2ccd08d308b229435d99b28277524ab6')

_withmaxima=false # Change it to "true" to use Maxima as CAS in GeoGebra
_svntrunk='http://www.geogebra.org/svn/trunk/geogebra/'
_svnmod=geogebra

build() {
    # SVN checkout
  cd "$srcdir"
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

    # Actual build
  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  source /etc/profile
  /usr/share/java/apache-ant/bin/ant geogebra-cl
}

package() {
    # Installing application
  cd "$srcdir/$_svnmod-build/build"
  install -d -m755 $pkgdir/usr/share/java/geogebra
  install -D -m644 *.jar $pkgdir/usr/share/java/geogebra

    # Installing launcher
  if [ $_withmaxima = "true" ]; then
    install -D -m755 $srcdir/geogebra-maxima.sh $pkgdir/usr/bin/geogebra
  else
    install -D -m755 $srcdir/geogebra.sh $pkgdir/usr/bin/geogebra
  fi

    # Adding icons
  mkdir $pkgdir/usr/share/icons
  cp -r $srcdir/hicolor $pkgdir/usr/share/icons/hicolor

    # Adding menu items
  install -D -m644 $srcdir/geogebra.desktop $pkgdir/usr/share/applications/geogebra.desktop
  install -D -m644 $srcdir/geogebra.png $pkgdir/usr/share/pixmaps/geogebra.png

    # Adding MIME types
  install -D -m644 $srcdir/geogebra.xml $pkgdir/usr/share/mime/packages/geogebra.xml
}
