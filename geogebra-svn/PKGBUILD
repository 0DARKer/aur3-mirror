# Maintainer: ifaigios <ifaigios_at_gmail_dot_com>
# Contributor: moostik <mooostik_at_gmail_dot_com>

pkgname=geogebra-svn
_realname=geogebra
pkgver=12427
_realver=4.1.4.0
pkgrel=1
pkgdesc="Dynamic mathematics software with interactive graphics, algebra and spreadsheet - SVN version"
arch=('i686' 'x86_64')
url='http://www.geogebra.org/'
license=('custom') # Application and source code under GPLv3 / Language files and documentation under CC-BY-SA 3.0
depends=('jre' 'hicolor-icon-theme' 'gtk-update-icon-cache')
makedepends=('subversion' 'jdk' 'apache-ant')
optdepends=('geogebra-thumbnail-kde: KDE thumbnailer for GeoGebra')
install="$_realname.install"
provides=('geogebra' 'geogebra-beta' 'geogebra42-beta')
conflicts=('geogebra' 'geogebra-beta' 'geogebra42-beta')

_svntrunk="http://www.geogebra.org/svn/trunk/$_realname/"
_svnmod="$_realname"

build() {
    # SVN checkout
  cd $srcdir
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  # Actual build
  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  sh $srcdir/$_svnmod-build/scripts/autobuild/make-vanilla-jars -j /opt/java
}

package() {
    # Installing application
  cd $srcdir/$_svnmod-build/build
  install -d -m755 $pkgdir/usr/share/java/$_realname
  install -D -m644 *.jar $pkgdir/usr/share/java/$_realname

    # Installing launcher
  install -D -m755 $srcdir/$_svnmod-build/scripts/autobuild/linux/$_realname $pkgdir/usr/bin/$_realname

    # Installing icons
  cd $srcdir/$_svnmod-build/icons/hicolor
  for i in *; do
      install -d -m755 $pkgdir/usr/share/icons/hicolor/$i/{apps,mimetypes}
      install -D -m644 $i/apps/* $pkgdir/usr/share/icons/hicolor/$i/apps
      install -D -m644 $i/mimetypes/* $pkgdir/usr/share/icons/hicolor/$i/mimetypes
  done

    # Installing menu item
  install -D -m644 $srcdir/$_svnmod-build/scripts/autobuild/linux/generic/$_realname.desktop $pkgdir/usr/share/applications/$_realname.desktop

    # Installing pixmap
  install -d -m755 $pkgdir/usr/share/pixmaps
  ln -s /usr/share/icons/hicolor/64x64/apps/$_realname.png $pkgdir/usr/share/pixmaps/$_realname.png

    # Installing mimetype
  install -D -m644 $srcdir/$_svnmod-build/scripts/autobuild/linux/generic/$_realname.xml $pkgdir/usr/share/mime/packages/$_realname.xml

    # Installing license
  install -D -m644 $srcdir/$_svnmod-build/_LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
