# Current i686 Maintainer: ifaigios <ifaigios_at_gmail_dot_com>
# Contributor: moostik <mooostik_at_gmail_dot_com>

pkgname=geogebra-svn
_realname=geogebra
pkgver=23296
_realver=4.9.97.0
pkgrel=1
pkgdesc="Dynamic mathematics software with interactive graphics, algebra and spreadsheet - SVN version"
arch=('i686' 'x86_64')
url="http://www.geogebra.org/"
license=('custom')
        # Application and source code under GPLv3
        # Language files and documentation under CC-BY-SA 3.0
        # Installer scripts under CC-BY-NC 3.0
depends=('java-runtime' 'shared-mime-info' 'xdg-utils' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('java-environment' 'subversion' 'apache-ant')
optdepends=('geogebra-thumbnail-kde: KDE thumbnailer for GeoGebra')
install="${_realname}.install"
provides=('geogebra' 'geogebra-beta' 'geogebra42-beta' 'geogebra5-beta')
conflicts=('geogebra' 'geogebra-beta' 'geogebra42-beta' 'geogebra5-beta')
source=("geogebra.sh")
md5sums=('2f87f90d8047eeda6886bcb6a8ba3d9b')

_svntrunk="http://dev.geogebra.org/svn/trunk"
_svnmod="${_realname}"

build() {
    # SVN checkout
  cd $srcdir
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"

    # Creating working directory
  rm -rf $srcdir/$_svnmod-build
  cp -r $srcdir/$_svnmod $srcdir/$_svnmod-build

  msg "Starting make..."

  cd $srcdir/$_svnmod-build/tools/build/platforms/desktop/jogl2
  ./1-compile
}

package() {
    # Installing application
  cd $srcdir/$_svnmod-build/geogebra/desktop/build
  install -dm755 $pkgdir/usr/share/java/$_realname
  install -Dm644 * $pkgdir/usr/share/java/$_realname

    # Installing launcher
  install -Dm755 $srcdir/$_realname.sh $pkgdir/usr/bin/$_realname

    # Installing icons
  cd $srcdir/$_svnmod-build/geogebra/desktop/icons/hicolor
  for i in *; do
    install -dm755 $pkgdir/usr/share/icons/hicolor/$i/{apps,mimetypes}
    install -Dm644 $i/apps/* $pkgdir/usr/share/icons/hicolor/$i/apps
    install -Dm644 $i/mimetypes/* $pkgdir/usr/share/icons/hicolor/$i/mimetypes
  done

    # Installing menu item
  install -Dm644 $srcdir/$_svnmod-build/geogebra/desktop/scripts/autobuild/linux/generic/$_realname.desktop $pkgdir/usr/share/applications/$_realname.desktop

    # Installing mimetype
  install -Dm644 $srcdir/$_svnmod-build/geogebra/desktop/scripts/autobuild/linux/generic/$_realname.xml $pkgdir/usr/share/mime/packages/$_realname.xml

    # Installing license
  install -Dm644 $srcdir/$_svnmod-build/geogebra/desktop/installer/LICENSE.txt $pkgdir/usr/share/licenses/$_realname/LICENSE
}
