# Maintainer: ifaigios <ifaigios_at_gmail_dot_com>
# Contributor: moostik <mooostik_at_gmail_dot_com>

pkgname=geogebra-svn
pkgver=9646
pkgrel=1
pkgdesc="Dynamic mathematics software with interactive graphics, algebra and spreadsheet - SVN version"
arch=('any')
url='http://www.geogebra.org/'
license=('GPL' 'CCPL:by-sa') # Application and source code under GPL / Language files under CCPL
depends=('jre')
makedepends=('subversion' 'jdk' 'apache-ant')
optdepends=('maxima: can be used as CAS in geogebra')
source=(geogebra.png
	geogebra.desktop
	geogebra.sh
	geogebra-maxima.sh)
provides=('geogebra' 'geogebra-beta')
conflicts=('geogebra' 'geogebra-beta')
md5sums=('e324ee3a2bb438cee625e3f29770a315'
         '0efb7a5d52871039aac935674061f899'
         '6c7d1bdb56fce09bd788fe32daf3f26e'
         '7be6164e564d8fe0ff97a051731a3722')

_withmaxima=false # Change it to "true" to use Maxima as CAS in GeoGebra
_svntrunk='http://www.geogebra.org/svn/trunk/geogebra/'
_svnmod=geogebra

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

    # Actual build
  source /etc/profile
  /usr/share/java/apache-ant/bin/ant geogebra-cl

    # Installing application
  cd "$srcdir/$_svnmod-build/build"
  install -d -m755 $pkgdir/usr/share/java/geogebra
  install -D -m644 *.jar $pkgdir/usr/share/java/geogebra
  if [ $_withmaxima = "true" ]; then
    install -D -m755 $srcdir/geogebra-maxima.sh $pkgdir/usr/bin/geogebra
  else
    install -D -m755 $srcdir/geogebra.sh $pkgdir/usr/bin/geogebra
  fi

  # Creating menu item
  install -D -m644 $srcdir/geogebra.png $pkgdir/usr/share/pixmaps/geogebra.png
  install -D -m644 $srcdir/geogebra.desktop $pkgdir/usr/share/applications/geogebra.desktop
}
