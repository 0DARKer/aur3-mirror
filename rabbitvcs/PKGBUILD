# Contributor: Thomas Oster <thomas.oster@rwth-aachen.de>
# Maintainer: Zeph <zeph33@gmail.com>

pkgname=rabbitvcs
pkgver=0.15.3
pkgrel=4
pkgdesc="A project with the goal of developing a collection of utilities to allow for better client integration with some of the popular version control systems (core)"
arch=('any')
url="http://rabbitvcs.org/"
install=${pkgname}.install
changelog=${pkgname}.changelog
depends=('git' 'pygtk' 'python2-pysvn' 'python2-dulwich' 'python2-gobject2' 'python2-configobj' 'python2-simplejson')
optdepends=('meld: graphical diff viewer')
makedepends=('python2-setuptools')
license=('GPL')
source=("http://rabbitvcs.googlecode.com/files/$pkgname-$pkgver.tar.bz2"
		"git.patch")
md5sums=('2b381ed4f824cc18a032e4c7d3ea5e02' '34de7099bedf609619722e7ae56ac507')


build() {
	cd "$srcdir/$pkgname-$pkgver"
    patch -Np0 -i "${srcdir}/git.patch"
	sed -i "s#env python#env python2#" setup.py
	find . -name "*.py" | xargs grep -l sys.executable | xargs sed -i 's|sys\.executable|"/usr/bin/python2"|g'
	python2 setup.py build 
}
	
package(){
	cd "$srcdir/$pkgname-$pkgver"
	python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}
