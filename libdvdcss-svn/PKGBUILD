# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libdvdcss-svn
pkgver=256
pkgrel=1
pkgdesc="A portable abstraction library for DVD decryption. (SVN version)"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.videolan.org/libdvdcss"
depends=('glibc')
makedepends=('svn' 'texlive-latexextra')
options=('!libtool')
provides=('libdvdcss')
conflicts=('libdvdcss')

_svntrunk="svn://svn.videolan.org/libdvdcss/trunk"
_svnmod="libdvdcss"

build() {
  cd "${srcdir}"

  msg "Connecting to SVN server...."

  if [ -d "${srcdir}/${_svnmod}" ] ; then
      cd "${_svnmod}" && svn update
  else
      svn co "${_svntrunk}" "${_svnmod}"
  fi

  rm -fr "${srcdir}/${_svnmod}-build"
  cp -R "${srcdir}/${_svnmod}" "${srcdir}/${_svnmod}-build"

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  cd  "${srcdir}"/"${_svnmod}-build"

  # Fix for automake 1.13
  if [ "$(LANG=C pacman -Qi automake |grep Version | cut -d ' ' -f10 | sed 's|.[0-9]-[0-9]||g')" = "1.13" ]; then
    sed 's|for v in 12 11 10 9 8 7 6 5; do|for v in 13 12 11 10 9 8 7 6 5; do|g' -i bootstrap
    sed 's|AM_CONFIG_HEADER(config.h)|AC_CONFIG_HEADERS([config.h])|g' -i configure.ac
    sed '5a AC_CONFIG_MACRO_DIR([m4])' -i configure.ac
    sed '2a ACLOCAL_AMFLAGS = -I m4' -i Makefile.am
  fi
  # Fix include patch in libdvdcss.pc
  sed 's|-I${includedir}/@PACKAGE@||g' -i src/libdvdcss.pc.in
  ./bootstrap
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_svnmod}-build"
  make DESTDIR="${pkgdir}" install
  install -d "${pkgdir}/usr/share/doc/libdvdcss"
  cp -R doc/html/* "${pkgdir}/usr/share/doc/libdvdcss/"
  chmod 664 "${pkgdir}/usr/share/doc/libdvdcss/"*
}
