# Maintainer: Carl George <carl@carlgeorge.us>
# Contributor:  Ryan Peters <sloshy45@sbcglobal.net>
# Special thanks to the Linux Mint project, without them this would not exist.

pkgname=mint-x-icons
pkgver=1.1.6
pkgrel=1
pkgdesc="Mint-X Icon themes.  A mint/metal theme based on mintified versions of Clearlooks Revamp, Elementary and Faenza."
arch=('any')
url="http://packages.linuxmint.com/pool/main/m/${pkgname}"
license=('GPL3')
source=("${url}/${pkgname}_${pkgver}.tar.gz")
install=${pkgname}.install
sha256sums=('383f483b394e952678cb47ba45300a4ba0974e0e06a39d238720020eab233900')

prepare() {
    # https://bugs.launchpad.net/linuxmint/+bug/1245735
    mv ${srcdir}/${pkgname}/usr/share/icons/Mint-X/apps/scalable/cairo-dock{\ ,}-c.svg
    mv ${srcdir}/${pkgname}/usr/share/icons/Mint-X/apps/scalable/cairo-dock{\ ,}-o.svg
    find ${srcdir} -type d ! -perm 755 -exec chmod 755 {} +
    find ${srcdir} -type f ! -perm 644 -exec chmod 644 {} +
}

package() {
    cp -dr --no-preserve=ownership ${srcdir}/${pkgname}/usr ${pkgdir}/
}

