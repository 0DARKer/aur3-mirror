# Maintainer: Serge Zirukin <ftrvxmtrx@gmail.com>
# Contributor: Sergei Lebedev <superbobry@gmail.com>

pkgname=ocaml-core-extended
_pkgname=core_extended
pkgver=108.00.02
pkgrel=2
pkgdesc="Jane Street's extended standard library for OCaml"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('ocaml>=3.12'
         'ocaml-findlib'
         'ocaml-res'
         'ocaml-sexplib>=5.1.2'
         'ocaml-bin-prot>=1.3.1'
         'ocaml-fieldslib>=0.1.2'
         'ocaml-core>=0.7.0')
options=(!strip !makeflags)
url="http://bitbucket.org/yminsky/ocaml-core/wiki/Home"
source=("http://ocaml.janestreet.com/ocaml-core/$pkgver/individual/$_pkgname-$pkgver.tar.gz")
md5sums=('abbf538eed3dc860d4d77daa9eafcf37')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --disable-docs --disable-debug --prefix /usr --destdir "$pkgdir"
  make all
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  export OCAMLFIND_COMMANDS="ocamldoc=ocamldoc -thread"
  install -dm 755 "$OCAMLFIND_DESTDIR/stublibs"
  make install
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
