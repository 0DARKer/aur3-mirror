# Maintainer: Sergei Lebedev <superbobry@gmail.com>
pkgname=ocaml-core-extended
pkgver=0.7.0
pkgrel=1
pkgdesc="Jane Street's extended standard library for OCaml"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('ocaml>=3.12' 'ocaml-findlib' 'ocaml-res' 'ocaml-sexplib>=5.1.2'
         'ocaml-bin-prot>=1.3.1' 'ocaml-fieldslib>=0.1.2' 'ocaml-core>=0.7.0')
options=(!strip !makeflags)
url="http://ocaml.janestcapital.com/?q=node/13"
source=(http://www.janestreet.com/ocaml/core_extended-$pkgver.tar.gz)
md5sums=('43cc3e891a367cfb152d7e4b90a1714e')
noextract=()
install=ocaml-core-extended.install

build() {
  cd "$srcdir/core_extended-$pkgver"

  ocaml setup.ml -configure --prefix /usr --destdir ${pkgdir} --disable-tests || return 1
  ocaml setup.ml -build || return 1
}

package() {
  cd "$srcdir/core_extended-$pkgver"

  export OCAMLFIND_DESTDIR="${pkgdir}$(ocamlfind printconf destdir)"
  export OCAMLFIND_LDCONFG="ignore"

  mkdir -p "${OCAMLFIND_DESTDIR}/stublibs"

  ocaml setup.ml -install
  install -Dm 644 MLton-license $pkgdir/usr/share/licenses/$pkgname/MLton-license
}
