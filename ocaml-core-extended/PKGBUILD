# Maintainer: Sergei Lebedev <superbobry@gmail.com>

pkgname=ocaml-core-extended
pkgver=107.01
pkgrel=3
pkgdesc="Jane Street's extended standard library for OCaml"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('ocaml>=3.12'
         'ocaml-findlib'
         'ocaml-res'
         'ocaml-sexplib>=5.1.2'
         'ocaml-bin-prot>=1.3.1'
         'ocaml-fieldslib>=0.1.2'
         'ocaml-core>=0.7.0')
options=(!strip !makeflags)
url="http://ocaml.janestcapital.com/?q=node/13"
source=("http://www.janestreet.com/ocaml/core_extended-$pkgver.tar.gz")
md5sums=('c8a7e7fc48347a37b6ee5126bd7272a9')

build() {
  cd "$srcdir/core_extended-$pkgver"

  make CONFIGUREFLAGS="--prefix /usr --destdir $pkgdir --disable-tests" build
}

package() {
  cd "$srcdir/core_extended-$pkgver"

  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  install -dm 755 "$OCAMLFIND_DESTDIR/stublibs"
  make install
  install -Dm 644 MLton-license "$pkgdir/usr/share/licenses/$pkgname/MLton-license"
}
