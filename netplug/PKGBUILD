# vim:set ts=2 sw=2 et:

pkgname=netplug
pkgver=1.2.9.2
pkgrel=2
pkgdesc='A Linux daemon that manages network interfaces in response to network cables being plugged in and out.'
arch=('i686' 'x86_64')
url='http://www.red-bean.com/~bos/'
license=('GPL2')
depends=('iproute2>=2.4.7')
backup=('etc/netplug/netplug' 'etc/netplug/netplugd.conf')
options=('!purge')
install="${pkgname}d.install"
source=("http://www.red-bean.com/~bos/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'Makefile.patch'
        'man.patch'
        "rc.${pkgname}d"
        "${pkgname}d.conf.d"
        "${pkgname}d.install")
md5sums=('1d6db99536bdf875ce441f2c0e45ebf2'
         '6c7ca374f549b05c01cc3f8b30bb5f44'
         'fae6c1659c98c80871a38e2d65b91064'
         '1f7a33ca800f04b2cdb01555292a55a7'
         'ddda834021991b23d4c4fdf161523387'
         '50c01189419b0fc26c0fd8d3efd7e1f6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  /usr/bin/patch -Np1 < "${srcdir}/man.patch"
  /usr/bin/patch -N < "${srcdir}/Makefile.patch"
  /usr/bin/make scriptdir=/etc/netplug
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  /usr/bin/make DESTDIR="${pkgdir}/" install \
    bindir=/usr/sbin/ \
    initdir=/etc/rc.d/ \
    scriptdir=/etc/netplug
  /bin/rm "${pkgdir}/etc/rc.d/${pkgname}d"
  /bin/install -d -m755 "${pkgdir}/etc/conf.d/"
  /bin/install -D -m644 "${srcdir}/${pkgname}d.conf.d" "${pkgdir}/etc/conf.d/${pkgname}d"
  /bin/install -m755 -o root -g root "${srcdir}/rc.${pkgname}d" "${pkgdir}/etc/rc.d/${pkgname}d"
} 
