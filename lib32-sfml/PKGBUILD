pkgname=lib32-sfml
pkgver=1.6
pkgrel=2
pkgdesc="A simple, fast, cross-platform and object-oriented multimedia API."
arch=('x86_64')
url="http://www.sfml-dev.org"
license=('custom:zlib')
depends=('sfml' 'lib32-libxrandr' 'lib32-libsndfile' 'lib32-openal' 'lib32-glew' 'lib32-libjpeg' 'lib32-libpng' 'lib32-soil' 'lib32-zlib' 'lib32-freetype2')
source=(http://downloads.sourceforge.net/sfml/SFML-${pkgver}-sdk-linux-32.tar.gz
        use-system-libs.patch)
md5sums=('acc678933c19558587aad8332ea6f459'
         '505ea908fb6e4b9359061d8d55373963')

build() {
  cd "${srcdir}/SFML-${pkgver}"

  # apply patch to use system libs in favor of included ones (fixes many problems)
  patch -Np1 < ../use-system-libs.patch
  sed -i 's/g++/g++ -m32/;s/gcc/gcc -m32/' samples/Makefile src/SFML/Makefile

  # compile sfml
  make

  # prepare samples
  sed -e '/export LDFLAGS/d' -i samples/Makefile

  #check optional dependencies
  if [ ! -e "/usr/bin/wx-config" ]; then
    sed -e 's/wxwidgets-sample //' -i samples/Makefile
  fi
  if [ ! -e "/usr/include/QtGui" ]; then
    sed -e 's/qt-sample //' -i samples/Makefile
  fi

  # fix some samples
  sed -e 's|qt4/||g' -i samples/qt/Makefile
  sed -e '/#include <iostream>/a\#include <stdlib.h>' -i \
    samples/sockets/Sockets.cpp \
    samples/voip/VoIP.cpp

  # fix the library softlinks for samples
  cd lib
  for lib in *; do
      ln -sf $lib ${lib/.${pkgver}/}
  done
  cd ..
}

package() {
  cd "${srcdir}/SFML-${pkgver}"

  # prepare some dirs
  mkdir -p ${pkgdir}/usr/lib \
           ${pkgdir}/usr/include \
           ${pkgdir}/usr/share/lib32-sfml \
           ${pkgdir}/usr/share/doc

  # install it
  sed '/export DESTDIR/d' -i src/SFML/Makefile
  make DESTDIR="${pkgdir}/usr" install

  # fix the library softlinks - again
  cd ${pkgdir}/usr/lib
  rm *.so
  for lib in *; do
      ln -s $lib ${lib/.${pkgver}/}
  done
  cd "${srcdir}/SFML-${pkgver}"

  # install docs
  cp -r doc ${pkgdir}/usr/share/doc/lib32-sfml

  # handy symlinks
  cd ${pkgdir}/usr/share/lib32-sfml
  ln -s ../doc/lib32-sfml docs

  # remove includes
  rm -r ${pkgdir}/usr/include

  # move libraries dir
  mv ${pkgdir}/usr/lib ${pkgdir}/usr/lib32

  # install license
  install -Dm 644 ${srcdir}/SFML-${pkgver}/license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
