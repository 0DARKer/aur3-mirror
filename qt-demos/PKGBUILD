# Maintainer: Vojtech Kral <kral.vojtech at gmail dot com>

pkgname=qt-demos
pkgver=4.7.1
pkgrel=1
pkgdesc='Qt ships with lots of example programs that teach you how to implement various tasks with Qt'
arch=('i686' 'x86_64')
url='http://qt.nokia.com/'
license=('GPL3' 'LGPL')
depends=('qt>=4.7.1')
makedepends=('qt>=4.7.1')
optdepends=('qt-doc: Examples fully documented')
options=('!libtool')

_pkgfqn="qt-everywhere-opensource-src-${pkgver}"
source=("ftp://ftp.qt.nokia.com/qt/source/${_pkgfqn}.tar.gz"
        'shadow-build-plugandpaint.patch'
        'qtdemo.desktop')
    
md5sums=('6f88d96507c84e9fea5bf3a71ebeb6d7'
         'efcec5ad80da872876b56c19fc503cb4'
         '43b3f3172713f5a7b1ece6c8ddafc03b')

build()
{
  cd $srcdir/$_pkgfqn

  cp -r include/QtGui/private demos/shared/

  #This patch fixes linking error during shadow build
  patch -p0 < $srcdir/shadow-build-plugandpaint.patch

  cd $srcdir/$_pkgfqn/demos
  qmake
  make

  cd $srcdir/$_pkgfqn/examples
  qmake
  make

  cd $srcdir/$_pkgfqn/demos/qtdemo
  qmake
  make
}

package()
{
  cd $srcdir/$_pkgfqn

  find demos/* -not -name "*.*" -type f -executable -exec install -p -D -m755 '{}' ${pkgdir}'/usr/share/doc/qt/{}' \;
  find examples/* -not -name "*.*" -type f -executable -exec install -p -D -m755 '{}' ${pkgdir}'/usr/share/doc/qt/{}' \;
  install -p -D -m755 bin/qtdemo ${pkgdir}/usr/bin/qtdemo
  install -p -D -m644 ${srcdir}/qtdemo.desktop ${pkgdir}/usr/share/applications/qtdemo.desktop
}
