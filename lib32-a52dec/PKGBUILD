# derived from a52dec package in extra:
#   Contributor: Thomas Baechler <thomas@archlinux.org>
#   Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

_pkgbasename=a52dec
pkgname=lib32-a52dec
pkgver=0.7.4
pkgrel=6
pkgdesc="A free library for decoding ATSC A/52 streams"
url="http://liba52.sourceforge.net/"
arch=('x86_64')
license=('GPL2')
depends=('lib32-glibc' $_pkgbasename)
makedepends=('gcc-multilib')
options=(!libtool)
source=("http://liba52.sourceforge.net/files/${_pkgbasename}-${pkgver}.tar.gz"
        'a52dec-0.7.4-build.patch')
md5sums=('caa9f5bc44232dc8aeea773fea56be80'
         'fa16f224a7dceb7613824380abef0052')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  patch -Np1 -i "${srcdir}/a52dec-0.7.4-build.patch"
  ./bootstrap
  ./configure --prefix=/usr --libdir=/usr/lib32 --enable-shared
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,bin,share}
}
