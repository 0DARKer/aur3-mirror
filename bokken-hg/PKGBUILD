# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>

pkgname=bokken-hg
_hgname=bokken
pkgver=1.6.64.caaa65c431a8
pkgrel=1
pkgdesc="GUI for radare2 and pyew"
url="http://inguma.eu/projects/bokken"
arch=('any')
license=('GPL2')
depends=('pygtksourceview2'
         'graphviz')
optdepends=('pyew: pyew backend support'
            'r2-bindings: radare2 backend support')
makedepends=('mercurial')
source=(hg+https://inguma.eu/repos/bokken)
sha512sums=('SKIP')

pkgver() {
  cd ${_hgname}
  hg log -r . --template '{latesttag}.{latesttagdistance}.{node|short}\n'
}

prepare() {
  cd ${_hgname}
  sed -e 's/env python$/env python2/g' -i ui/xdot.py plugins/pdfid_PL.py \
    lib/web/__init__.py lib/web/utils.py
  sed -e 's|/usr/bin/python|/usr/bin/env python2|g' -i bokken.py
}

package() {
  cd ${_hgname}

  install -d "${pkgdir}/usr/share/bokken"
  install -Dm 755 bokken bokken.py "${pkgdir}/usr/share/bokken"
  cp -r lib plugins static ui "${pkgdir}/usr/share/bokken"

  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README "${pkgdir}/usr/share/doc/${pkgname}/README.md"

  install -d "${pkgdir}/usr/bin/"
  cat > "${pkgdir}/usr/bin/bokken" << EOF
#!/bin/bash
cd /usr/share/bokken/
exec ./bokken "$@"
EOF
  chmod +x "${pkgdir}/usr/bin/bokken"
}

# vim: ts=2 sw=2 et:
