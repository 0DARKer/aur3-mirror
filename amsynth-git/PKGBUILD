# Maintainer: Nick Lanham <nick@afternight.org>
pkgname=amsynth-git
_gitname=amsynth
pkgver=20130330.592
pkgrel=1
pkgdesc="software synth that provides a classic subtractive synthesizer topology"
arch=('i686' 'x86_64')
url="https://code.google.com/p/amsynth/"
license=('GPL2')
depends=('gtkmm' 'jack')
makedepends=('git')
provides=('amsynth')
conflicts=('amsynth')
source=('git+https://code.google.com/p/amsynth/')
md5sums=('SKIP')

pkgver() {
  cd "$_gitname"
  echo $(git log -1 --format="%cd" --date=short | tr -d '-').$(git rev-list --count HEAD)
}

build() {
  cd "$_gitname"
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$_gitname"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
