# Contributor:	Thomas Mudrunka <harvie@@email..cz>
# Maintainer:	Thomas Mudrunka <harvie@@email..cz>
# You can also contact me on http://blog.harvie.cz/
# Maintainer: Max Fierke <max@maxfierke.com>

pkgbase=apparmor
pkgname=apparmor
true && pkgname=($pkgbase apparmor-parser apparmor-libapparmor apparmor-utils apparmor-profiles apparmor-pam apparmor-vim)
pkgver=2.7.2
pkgrel=3
arch=('i686' 'x86_64')
license=('GPL')
url='http://wiki.apparmor.net/index.php/Main_Page'
groups=('apparmor')
makedepends=('swig' 'perl' 'python2' 'ruby' 'perl-locale-gettext' 'perl-rpc-xml' 'wxgtk' 'audit')
optdepends=('linux-hardened: kernel with AppArmor, SELinux, Grsecurity/PAX, and TOMOYO support')

test -n "$BASH_VERSION" && [[ "$pkgver" =~ ^([0-9]*\.[0-9]*) ]] && bigver="${BASH_REMATCH[1]}"
test -n "$bigver" || bigver="$(echo ${pkgver} | cut -d . -f -2)"

source=("http://launchpad.net/apparmor/${bigver}/${pkgver}/+download/apparmor-${pkgver}.tar.gz"
        "apparmor")
md5sums=('2863e85bdfdf9ee35b83db6721fed1f1'
         '3c56820370318f6f195ea88b723ef628')
sha1sums=('0645cc2352f89c41825c59a07dfca9035599d85d'
          '02951f448d7a3d222472304d3135c324f9909923')
sha256sums=('42deb8cbf4937fac07a48ec8427b90131e92ed2f83b606beee092bdb4fc2a41f'
            '6dc84e8c3cdc6009ae128930a04fd8d3bf36afc2fd8f4283c234f235753fa8ca')
sha384sums=('2892812f8542ba0acb2697d049318550d9d74e460d2e08169f22452ff3c722674d52682c357a1b0db2c6c8ba97724896'
            
'fa032ddb7d66050f017c4f8297ceb8affbfeaf7b7d447eb6408a23d205f495579b1eec4f127d4611c4d382ef06ae0a49')
sha512sums=('52a6e48ddaefe15d2a150958f6025d1ac6ad07caf3077ccb23ce66f719330b9f94acfe3e464f84301d4ac44831ce521128d7f70e18e43df0c547bfa143af22f8'
            
'2b4f811f923dfcf424f45d3c91c662193fab45c4a1e3f2f9bb039bd76865e93139c87df838ad57eb9fc618b0f07bcaca917f34eff008bc330d0129b79017825f')



#Configuration
core_perl_dir='/usr/bin/core_perl'
export MAKEFLAGS+=" POD2MAN=${core_perl_dir}/pod2man"
export MAKEFLAGS+=" POD2HTML=${core_perl_dir}/pod2html"
export MAKEFLAGS+=" PROVE=${core_perl_dir}/prove"
export PYTHON='/usr/bin/python2'

build() {
  msg2 "Building: apparmor-parser"
	cd "${srcdir}/${pkgbase}-${pkgver}/parser"
  msg2 'Patching: apparmor-parser'
  #Patch (maybe we can avoid patching by ./configuring things better)
  patch=Makefile; { rm "$patch"
    sed -e 's/pdflatex/true/g' > "$patch" #just workaround until we'll get pdflatex package
  } < "$patch"
  echo '#!/bin/true' > tst/caching.sh #Can't pass this test with current kernel
	make

  msg2 "Building: apparmor-libapparmor"
  cd "${srcdir}/${pkgbase}-${pkgver}/libraries/libapparmor"
  ./autogen.sh
  ./configure --prefix=/usr --with-perl --with-python --with-ruby
  make

  msg2 "Building: apparmor-utils"
  cd "${srcdir}/${pkgbase}-${pkgver}/utils"
  make

  msg2 "Building: apparmor-profiles"
  cd "${srcdir}/${pkgbase}-${pkgver}/profiles"
  make

  msg2 "Building: apparmor-pam"
  cd "${srcdir}/${pkgbase}-${pkgver}/changehat/pam_apparmor"
  make

  msg2 "Building: apparmor-vim"
  cd "${srcdir}/${pkgbase}-${pkgver}/utils/vim/"
  make || make || true #FIXME: ??????
}

package_apparmor() {
  pkgdesc='Linux application security framework - mandatory access control for programs (metapackage for AUR)'
  depends=(${pkgname[@]})
  install='apparmor.install'
}

package_apparmor-parser() {
	pkgdesc='AppArmor parser - loads AA profiles to kernel module'
  depends=("apparmor-libapparmor")

	cd "${srcdir}/${pkgbase}-${pkgver}/parser"
	make install DESTDIR=${pkgdir}
}

package_apparmor-libapparmor() {
  pkgdesc='AppArmor library'
  makedepends=('swig' 'perl' 'python2' 'ruby')
  provides=('apparmor-lib' 'libapparmor' 'apparmor-perl' 'apparmor-python' 'apparmor-ruby')

  cd "${srcdir}/${pkgbase}-${pkgver}/libraries/libapparmor"
  make install DESTDIR=${pkgdir}
  #FIXME: this file should install automatically:
  install -m644 "swig/perl/LibAppArmor.pm" "${pkgdir}/usr/lib/perl5/vendor_perl/"
}

package_apparmor-utils() {
  pkgdesc='AppArmor userspace utilities'
  depends=('perl' 'perl-locale-gettext' 'perl-term-readkey' 'perl-file-tail' 'perl-rpc-xml')
  provides=('apparmor-notify');
  install='apparmor-utils.install'

  cd "${srcdir}/${pkgbase}-${pkgver}/utils"
  make install DESTDIR=${pkgdir}
  install -dm755 "${pkgdir}/etc/rc.d"
  install -m755 "../../apparmor" "${pkgdir}/etc/rc.d/"
}

package_apparmor-profiles() {
  pkgdesc='AppArmor sample pre-made profiles'
  depends=("apparmor-libapparmor")

  cd "${srcdir}/${pkgbase}-${pkgver}/profiles"
  make install DESTDIR=${pkgdir}
}

package_apparmor-pam() {
  pkgdesc='AppArmor PAM library'
  depends=('apparmor-vim')

  cd "${srcdir}/${pkgbase}-${pkgver}/changehat/pam_apparmor"
  make install DESTDIR=${pkgdir}
}
package_apparmor-vim() {
  pkgdesc='AppArmor VIM support'
  depends=('vim')

  install -dm755 "${pkgdir}/usr/share/vim/vimfiles/"
  install -m644 \
    "${srcdir}/${pkgbase}-${pkgver}/utils/vim/apparmor.vim" \
    "${pkgdir}/usr/share/vim/vimfiles/apparmor.vim"
}
#AUR hack:
pkgdesc='Linux application security framework - mandatory access control for programs'
