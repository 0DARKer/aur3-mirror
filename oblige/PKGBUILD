# Contributor: Jan Cholasta <grubber@grubber.cz>
# Maintainer: Jan Cholasta <grubber@grubber.cz>

pkgname=oblige
pkgver=3.57
pkgrel=3
pkgdesc="A random level maker for games based on the Doom engine"
arch=('i686' 'x86_64')
url="http://oblige.sourceforge.net/"
license=('GPL')
depends=('fltk>=1.1.9' 'lua>=5.1.2')
makedepends=('glbsp>=2.24')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-357-src.zip
        ${pkgname}-${pkgver}-fix.patch)
md5sums=('e07ab38282ebcb8cd9e531c2a6d14a55'
         '1475b78722dfa45b258bc5fa0a25e898')

build() {
  cd "$srcdir"/${pkgname}_${pkgver//./}_src

  patch -p1 -i ../${pkgname}-${pkgver}-fix.patch || return 1
  make -C gui -f Makefile.unixy CFLAGS="${CFLAGS} `fltk-config --use-images --cflags` `pkg-config --cflags lua` -DUNIX -DFHS_INSTALL" CXXFLAGS="${CXXFLAGS} `fltk-config --use-images --cxxflags` `pkg-config --cflags lua` -DUNIX -DFHS_INSTALL" LDFLAGS="${LDFLAGS} `fltk-config --use-images --ldflags` `pkg-config --libs lua` /usr/lib/libglbsp.a" LIBS= || return 1
}

package() {
  cd "$srcdir"/${pkgname}_${pkgver//./}_src

  mkdir -p "$pkgdir"/usr/{bin,share/oblige} || return 1
  make -C gui -f Makefile.unixy INSTALL_PREFIX="$pkgdir"/usr install || return 1
}
