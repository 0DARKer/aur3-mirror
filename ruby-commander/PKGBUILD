# Maintainer: Peter Richard Lewis <plewis@aur.archlinux.org>
_gemname=commander
pkgname=ruby-$_gemname
pkgver=4.2.0
pkgrel=1
pkgdesc="The complete solution for Ruby command-line executables."
arch=(any)
url="http://visionmedia.github.com/commander/"
license=('MIT')
depends=('ruby' 'ruby-highline')
makedepends=('rubygems')
source=("http://gems.rubyforge.org/gems/$_gemname-$pkgver.gem"
 "https://raw.githubusercontent.com/visionmedia/commander/master/LICENSE")
noextract=($_gemname-$pkgver.gem)
sha256sums=('cc67a24f7eec5688182190cd64a6c26e6561891244e242adb043888ae9f45aba'
            'd1c0118f15b4e197c63ce1894a1f72f786020adad84a0930157dc0f74c38e0c0')

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" -n "$pkgdir/usr/bin" \
    "$_gemname-$pkgver.gem"

  install -m 644 -D "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/ruby-commander/LICENSE"
}

# vim:set ts=2 sw=2 et:
