# Maintainer: Peter Richard Lewis <plewis@aur.archlinux.org>
_gemname=commander
pkgname=ruby-$_gemname
pkgver=4.0.6
pkgrel=2
pkgdesc="The complete solution for Ruby command-line executables."
arch=(any)
url="http://visionmedia.github.com/commander/"
license=('MIT')
depends=('ruby' 'ruby-highline1.5')
makedepends=('rubygems')
source=("http://gems.rubyforge.org/gems/$_gemname-$pkgver.gem")
noextract=($_gemname-$pkgver.gem)
sha256sums=('05449fbdb3e6beddca3b86d08709c3198f73df1194ed06f7a7be01defd94ce09')

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --ignore-dependencies -i "$pkgdir$_gemdir" -n "$pkgdir/usr/bin" \
    "$_gemname-$pkgver.gem"
}

# vim:set ts=2 sw=2 et:
