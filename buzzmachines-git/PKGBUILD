# Maintainer: speps <speps at aur dot archlinux dot org>

_name=buzzmachines
pkgname=buzzmachines-git
pkgver=165.8031c38
pkgrel=1
pkgdesc="Collection of freely available buzz machine sources for Buzztrax (former Buzztard)"
arch=('i686' 'x86_64')
url="http://www.buzztrax.org"
license=('LGPL')
makedepends=('git')
provides=('buzzmachines')
conflicts=('buzzmachines' 'buzzmachines-svn')
replaces=('buzzmachines-svn')
options=('!libtool')
source=("$pkgname::git+https://github.com/Buzztrax/buzzmachines.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/$pkgname"
  ./autogen.sh
  ./configure --prefix=/usr \
              --enable-static=no
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir/" install
}
