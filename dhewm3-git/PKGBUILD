# Maintainer: Mike Swanson <mikeonthecomputer@gmail.com>
pkgname=dhewm3-git
pkgver=20120609
pkgrel=1
pkgdesc="Doom 3 engine with native 64-bit support, SDL, and OpenAL"
arch=('i686' 'x86_64')
url="https://github.com/dhewm/dhewm3"
license=('GPL3')
depends=('doom3-data' 'libjpeg' 'libogg' 'libvorbis' 'openal' 'sdl')
makedepends=('cmake' 'git')
optdepends=('curl: download support')
source=('dhewm3.desktop'
        'dhewm3.sh'
        '0001-game_data_location.patch')
sha256sums=('7c9ae892c6cf0453fcd57731689ccedac8f8ce10f33043f7dd5fb66bd73d1287'
            'e4eadde4a9303b2b98fb7455de42b9cbdbae97c17440d17c7ce450f2c5ebb3ac'
            '4da23b26c740a5b53b90905ac9fb6d7e80ff3d50282ad2130018d82906fa9c24')

_gitroot=git://github.com/dhewm/dhewm3.git
_gitname=dhewm3

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  cd "$srcdir/$_gitname"
  patch -p1 < "$srcdir"/0001-game_data_location.patch

  cd neo
  cmake .
  make
}

package() {
  cd "$srcdir/$_gitname/neo"

  install -Dm 755 dhewm3 "$pkgdir"/usr/lib/dhewm3/dhewm3

  if [ "$CARCH" = "i686" ]; then
    install -Dm 755 gamex86.so "$pkgdir"/usr/lib/dhewm3/gamex86.so
  fi
  if [ "$CARCH" = "x86_64" ]; then
    install -Dm 755 gamex86_64.so "$pkgdir"/usr/lib/dhewm3/gamex86_64.so
  fi

  install -Dm 755 "$srcdir"/dhewm3.sh "$pkgdir"/usr/bin/dhewm3
  install -Dm 644 "$srcdir"/dhewm3.desktop "$pkgdir"/usr/share/applications/dhewm3.desktop
}
