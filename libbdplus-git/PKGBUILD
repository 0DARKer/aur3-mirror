# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libbdplus-git
pkgver=0.1.0.0.g4f4dabd
pkgrel=1
pkgdesc="Open implementation of BD+ protocol. (GIT version)"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.videolan.org/developers/libbdplus.html"
depends=('libaacs')
makedepends=('git')
provides=('libbdplus')
conflicts=('libbdplus')
options=('!libtool')
source=('git://git.videolan.org/libbdplus.git')
md5sums=('SKIP')
_gitname="libbdplus"

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - .)"
}

build() {
  cd "${_gitname}"
  ./bootstrap
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
}
