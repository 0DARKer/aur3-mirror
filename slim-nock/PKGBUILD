# $Id: PKGBUILD 159123 2012-05-16 10:08:44Z tpowa $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Hugo Ideler <hugoideler@dse.nl>

pkgname=slim-nock
pkgver=1.3.3
pkgrel=4
pkgdesc='Desktop-independent graphical login manager for X11. Without ConsoleKit'
arch=('i686' 'x86_64')
url='http://slim.berlios.de/'
license=('GPL2')
depends=('pam' 'libxmu' 'libpng' 'libjpeg' 'libxft')
makedepends=('cmake' 'freeglut')
provides=('slim=1.3.3')
conflicts=('slim')
backup=('etc/slim.conf' 'etc/logrotate.d/slim' 'etc/pam.d/slim')
source=("http://download.berlios.de/slim/slim-${pkgver}.tar.gz"
        'rc.d'
        'pam.d'
        'logrotate'
        'slim.service'
        'ptr_pam.patch'
        'session-name.patch'
        'fix-numlock-1.3.3.patch')

install=install

build() {
        cd "${srcdir}/slim-${pkgver}"
	patch -p1 -i ../ptr_pam.patch # FS#23995: pointer mishandling confuses PAM
	patch -p1 -i ../session-name.patch # FS#26693: fix default session name
        patch -p1 -i ../fix-numlock-1.3.3.patch # FS#29818: fix numlock handling
        cd ${srcdir}
        mkdir build
        cd build
        cmake ../slim-${pkgver} \
                -DCMAKE_BUILD_TYPE=Release \
                -DCMAKE_SKIP_RPATH=ON \
                -DCMAKE_INSTALL_PREFIX=/usr \
                -DUSE_PAM=yes -DUSE_CONSOLEKIT=no
	make
}

package() {
        cd  ${srcdir}/build/
	make DESTDIR="${pkgdir}" install

	install -D -m755 ../rc.d "${pkgdir}"/etc/rc.d/slim
	install -D -m644 ../pam.d "${pkgdir}"/etc/pam.d/slim
	install -D -m644 ../logrotate "${pkgdir}"/etc/logrotate.d/slim

	# Provide sane defaults
	sed -i 's|#xserver_arguments.*|xserver_arguments -nolisten tcp vt07|' "${pkgdir}"/etc/slim.conf
	sed -i 's|/var/run/slim.lock|/var/lock/slim.lock|' "${pkgdir}"/etc/slim.conf
        # install systemd files
        install -D -m644 ${srcdir}/slim.service ${pkgdir}/usr/lib/systemd/system/slim.service
}
md5sums=('ce53e44c1e4a2eacf5bb7688ee2a5de8'
         'd8ea9c4dee2811524b67f4f666311a1f'
         'ebfff3417058619a1328284ae0e2f48e'
         '43da096480bf72c3ccec8ad8400f34f0'
         '472b102efd2f215026d23d6e68f8824f'
         'ae4b237d472ec5d373e30080aa0ff804'
         'ebcb6829028615686de7b64ceeaaf8ed'
         '6c29c93370e44ed310ff30132424b619')
