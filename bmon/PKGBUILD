# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=bmon
pkgver=3.1
pkgrel=1
pkgdesc="Portable bandwidth monitor and rate estimator"
arch=('i686' 'x86_64')
url="http://www.carisma.slowglass.com/~tgr/bmon/"
license=('MIT')
depends=('ncurses' 'libnl' 'confuse')
source=(http://www.carisma.slowglass.com/~tgr/$pkgname/files/$pkgname-$pkgver.tar.gz)
sha256sums=('7ea2d0d5a82cab6856978b388dd0f5a5f5a91c34f469de32509b8d37d859b100')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR=${pkgdir} install

# License
  install -d ${pkgdir}/usr/share/licenses/$pkgname
  cat ${srcdir}/$pkgname-$pkgver/configure.ac | tail -n+4 | head -n19 > \
      ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
