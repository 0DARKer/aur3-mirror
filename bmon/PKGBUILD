# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=bmon
pkgver=2.1.0
pkgrel=5
pkgdesc="Portable bandwidth monitor and rate estimator"
arch=('i686' 'x86_64')
url="http://people.suug.ch/~tgr/bmon/"
license=('MIT')
depends=('ncurses')
makedepends=('patch')
source=(ftp://ftp.freebsd.org/pub/FreeBSD/ports/distfiles/$pkgname-$pkgver.tar.gz \
        $pkgname-2.1.0-gcc4_libnl.patch)
sha256sums=('36a5772fc0241298b15db3dc4fb2552dcbb43edeffd6fcea4cd8818e97ec99fe'
            'dcb43da09304b89a2e43a510cbfb412096f72d0e92f4734321d76d581e6d03c5')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  patch -Np1 -i ${srcdir}/$pkgname-2.1.0-gcc4_libnl.patch

  ./configure --prefix=/usr --mandir=/usr/share/man --disable-asound
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR=${pkgdir} install

# License
  install -d ${pkgdir}/usr/share/licenses/$pkgname
  cat ${srcdir}/$pkgname-$pkgver/configure.ac | tail -n+4 | head -n19 > \
      ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
