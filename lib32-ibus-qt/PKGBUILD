# Contributor: Yichao Yu <yuyichao@gmail.com>
# Maintainer: Yichao Yu <yuyichao@gmail.com>

_pkgbasename=ibus-qt
pkgname=lib32-${_pkgbasename}
pkgver=1.3.1
pkgrel=5
pkgdesc='IBus qt library and IBus qt input method plugin'
arch=('x86_64')
license=('LGPL')
url='http://ibus.googlecode.com'
depends=('lib32-qt' 'lib32-icu' $_pkgbasename)
makedepends=('gcc-multilib' 'cmake' 'automoc4' 'doxygen')
source=("http://ibus.googlecode.com/files/${_pkgbasename}-${pkgver}-Source.tar.gz")
md5sums=('769e8872ca8a59327b2073ce2f142589')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cmake "../${_pkgbasename}-${pkgver}-Source" -DCMAKE_INSTALL_PREFIX=/usr -DLIBDIR=lib32 -DQT_PLUGINS_DIR=/usr/lib32/qt/plugins/
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
