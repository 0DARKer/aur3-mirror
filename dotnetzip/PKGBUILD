pkgname=dotnetzip
pkgdesc="Zip and Unzip in any .NET language"
pkgver=1.9.1.8
pkgrel=2
arch=(i686 x86_64)
license=("MS-PL")
options=(!emptydirs)
url="http://dotnetzip.codeplex.com"
source=("dotnetzip.zip::http://download-codeplex.sec.s-msft.com/Download/SourceControlFileDownload.ashx?ProjectName=dotnetzip&changeSetId=81674"
"Ionic.snk"
"ionic.zlib.pc.in"
"ionic.bzip2.pc.in")
depends=(mono)
makedepends=(dos2unix)
sha1sums=('e8e056cff2248a12abd798e7b36fbd82d3c23b5e'
          'e65a567bc5ece06d4941fe2c58ea4efecf4829bf'
          'd638210dbfa5f643ea4bb0a53d90129386e66bc5'
          'd9526874bc71dd2cdedfb2574202a927c181c0f4')

prepare() {
	cd "${srcdir}"
	find . -type f -exec dos2unix {} \;
	sed -i "s,Properties,properties,g" "Zlib/Zlib DLL.csproj"
}
          
build() {
	cd "${srcdir}/BZip2"
	xbuild "BZip2 DLL.csproj"
	cd ../Zlib
	xbuild "Zlib DLL.csproj"
}

package() {
	cd "${srcdir}"
	install -Dm644 License.BZip2.txt "$pkgdir/usr/share/licenses/$pkgname/License.BZip2.txt"
	install -m644 License.Combined.rtf "$pkgdir/usr/share/licenses/$pkgname/"
	install -m644 License.rtf "$pkgdir/usr/share/licenses/$pkgname/"
	install -m644 License.txt "$pkgdir/usr/share/licenses/$pkgname/"
	install -m644 License.zlib.txt "$pkgdir/usr/share/licenses/$pkgname/"
	cd BZip2/bin/Debug
	find . -type f -exec install -Dm644 {} "$pkgdir/usr/lib/dotnetzip/"{} \;
	cd ../../../Zlib/bin/Debug
	find . -type f -exec install -m644 {} "$pkgdir/usr/lib/dotnetzip/" \;
	find "$pkgdir" -name '*.dll' -exec gacutil -i {} -root "$pkgdir/usr/lib" \;
	find "$pkgdir" -name '*.dll' -exec mono --aot {} \;
	install -Dm644 "$srcdir/ionic.bzip2.pc.in" "$pkgdir/usr/lib/pkgconfig/ionic.bzip2.pc"
	install -m644 "$srcdir/ionic.zlib.pc.in" "$pkgdir/usr/lib/pkgconfig/ionic.zlib.pc"
	find "$pkgdir/usr/lib/pkgconfig" -type f -exec sed -i "s,@VERSION@,$pkgver," {} \;
}
