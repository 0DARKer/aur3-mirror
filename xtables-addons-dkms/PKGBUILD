# Maintainer: Lubomir Krajcovic <lubomir.krajcovic(AT)gmail(DOT)com>
# Contributor: Vladimir Kutyavin <vlkut(AT)bk(DOT)ru>
pkgname=xtables-addons-dkms
pkgver=2.3
pkgrel=1
pkgdesc="Successor to patch-o-matic(-ng). Contains extensions that were not accepted in the main Xtables. DKMS flavor for kernels >= 3.7."
arch=('i686' 'x86_64')
license=('GPL2')
url="http://xtables-addons.sourceforge.net/"
depends=('iptables>=1.4.5' 'dkms' 'gcc' 'make' 'automake' 'autoconf')
makedepends=()
conflicts=(xtables-addons xtables-addons-git xtables-addons-multikernel)
replaces=(xtables-addons xtables-addons-git xtables-addons-multikernel)
source=(dkms.conf
        make.sh
        http://download.sourceforge.net/project/xtables-addons/Xtables-addons/$pkgver/xtables-addons-$pkgver.tar.xz)
sha512sums=('3f38f5dc87f5e7cf1a78bf04899349b2d4d0f507747478d46a9b3344e972ebed7b2adec71f5e5aedb7da6365a5a9017a23c901c44c5dd1ec5b125341c4acc156'
            'd1e917ac3c15ea8a533686781f6989ef648786f7a6666d06739c96d37debdc44bd2449c332db6e30af0f655540d1df49d4f5b702da4731aa7d550204ac908333'
            '08d529f0a2fa96ba715d2142934d6568a3c4f0ddb49f06c3c4d4ac200de0a4d2b59a4007302b557ca21014cbacda104e7781df0d5158e5313a673a928453abcc')
install=$pkgname.install

package() {
	# go to builddir
	cd "${srcdir}/xtables-addons-${pkgver}"
	# disable install-exec-hook (avoids useless calling of depmod -a at 'make install' stage)
	sed -i 's/^install-exec-hook:$/dont-run:/' Makefile.am
	# prepare dkms build tree
	dkmsDst="${pkgdir}/usr/src/xtables-addons-${pkgver}-${pkgrel}/"
	mkdir -p "${dkmsDst}"
	cp -R . "${dkmsDst}"
	# prepare dkms config
	cp "${srcdir}/make.sh" "${dkmsDst}"
	cp "${srcdir}/dkms.conf" "${dkmsDst}"
	sed -i -e "s/@VERSION@/${pkgver}-${pkgrel}/" "${dkmsDst}dkms.conf"
	# build userspace parts
	./autogen.sh
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib/iptables \
		--sysconfdir=/etc \
		--with-xtlibdir=/usr/lib/iptables \
		--mandir=/usr/share/man \
		--without-kbuild
	make || return 1
	make DESTDIR="${pkgdir}" install || return 1
}
