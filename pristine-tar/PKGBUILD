# Maintainer:  Patrick McCarty <pnorcks at gmail dot com>
# Contributor: Jimmy Tang <jtang@tchpc.tcd.ie>

pkgname=pristine-tar
pkgver=1.28
pkgrel=1
pkgdesc="Tool to regenerate a pristine upstream tarball using only a small binary delta file and a copy of the source which can be a revision control checkout."
arch=('i686' 'x86_64')
url="http://joeyh.name/code/pristine-tar/"
license=('GPL')
depends=('perl' 'xdelta')
conflicts=('pristine-tar-git')
source=(http://ftp.debian.org/debian/pool/main/p/${pkgname}/${pkgname}_${pkgver}.tar.gz)
sha256sums=('67c48238bc39173bd0ee50d35e468c0d556eedc2ee61fd0271c4f9a1d12f2238')

build() {
  cd "$srcdir/$pkgname"

  export PATH=/usr/bin/core_perl:$PATH

  perl Makefile.PL
  make
}

package() {
  cd "$srcdir/$pkgname"

  make install DESTDIR=$pkgdir PREFIX=/usr INSTALLSITESCRIPT=/usr/bin
  install -d $pkgdir/usr/share/doc/pristine-tar
  install -D GPL TODO delta-format.txt $pkgdir/usr/share/doc/pristine-tar
}

# vim:set ts=2 sw=2 et:
