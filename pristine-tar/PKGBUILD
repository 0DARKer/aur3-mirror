# Maintainer:  Patrick McCarty <pnorcks at gmail dot com>
# Contributor: Jimmy Tang <jtang@tchpc.tcd.ie>

pkgname=pristine-tar
pkgver=1.26
pkgrel=1
pkgdesc="Tool to regenerate a pristine upstream tarball using only a small binary delta file and a copy of the source which can be a revision control checkout."
arch=('i686' 'x86_64')
url="http://joeyh.name/code/pristine-tar/"
license=('GPL')
depends=('perl' 'xdelta')
conflicts=('pristine-tar-git')
source=(http://ftp.debian.org/debian/pool/main/p/${pkgname}/${pkgname}_${pkgver}.tar.gz)
sha256sums=('90c699569e877c20ba8c2d94e06ed9e6dc0d0f4a2e306dcbd1322f98f97667e7')

build() {
  cd "$srcdir/$pkgname"

  export PATH=/usr/bin/core_perl:$PATH

  perl Makefile.PL
  make
}

package() {
  cd "$srcdir/$pkgname"

  make install DESTDIR=$pkgdir PREFIX=/usr INSTALLSITESCRIPT=/usr/bin
  install -d $pkgdir/usr/share/doc/pristine-tar
  install -D GPL TODO delta-format.txt $pkgdir/usr/share/doc/pristine-tar
}

# vim:set ts=2 sw=2 et:
