# Contributor: gadget3000 <gadget3000 at msn dot com>
# Contributor: mikers <mikeonthecomputer at gmail dot com>
# Maintainer: Eric Anderson <ejona86@gmail.com>

pkgname=vvvvvv
pkgver=2.1beta
pkgrel=1
pkgdesc='A retro styled 2D platformer (game sold separately)'
arch=('i686' 'x86_64')
url='http://thelettervsixtim.es/'
groups=('humblebundle3' 'humblebundle4' 'humblebundles')
license=('custom')
depends=('sdl_image' 'sdl_mixer' 'gcc-libs')
source=("hib://VVVVVV_2.01_Linux.tar.gz" "${pkgname}.desktop"
    'http://www.machinestudios.co.uk/VVVVVV/VVVVVV_Linux_Patch_2_2.zip'
    'http://www.machinestudios.co.uk/VVVVVV/levels.zip')
md5sums=(
    '54d3679da69d92adb65fe9633f3c67d7'
    'f3f06f16bf7f3280279e2d3da425a5d2'
    '7409fc06024a4ecaea2ad098f4fc944a'
    'cbad860d2163b77d6f8f2f5e0a4503fc')
noextract=('levels.zip')
# You can download the Humble Indie Bundle file manually, or you can configure
# DLAGENTS in makepkg.conf to auto-download.
#
# For example, to use hib-dlagent to download files set something like this in
# your makepkg.conf (change/add -k and add -u/-p to your needs):
# DLAGENTS=('hib::/usr/bin/hib-dlagent -k 1a2b3c -o %o $(echo %u | cut -c 7-)')
#
# To auto-search through a directory containing Humble Bundle downloads, you
# could set:
# DLAGENTS=('hib::/usr/bin/find /path/to/downloads -name $(echo %u | cut -c 7-) -exec ln -s \{\} %o \; -quit')
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Download manually to \"$(pwd)\" or setup hib:// DLAGENT in /etc/makepkg.conf."; echo "Read this PKGBUILD for more info."; exit 1')
PKGEXT='.pkg.tar'

build() {
  cd "${srcdir}"

  mkdir levels
  bsdtar -x -C levels -f levels.zip
}

package() {
  cd "${srcdir}/VVVVVV"

  install -d "${pkgdir}/opt/${pkgname}"
  install -m755 VVVVVV "${pkgdir}/opt/${pkgname}/VVVVVV"
  if [ "${CARCH}" = "x86_64" ]; then
    install -m755 VVVVVV_64 "${pkgdir}/opt/${pkgname}/VVVVVV_64"
  else
    install -m755 VVVVVV_32 "${pkgdir}/opt/${pkgname}/VVVVVV_32"
  fi
  install -d "${pkgdir}/opt/${pkgname}/data"
  install -m644 -t "${pkgdir}/opt/${pkgname}/data" data/vvvvvvmusic.vvv
  install -d "${pkgdir}/opt/${pkgname}/data/graphics"
  install -m644 -t "${pkgdir}/opt/${pkgname}/data/graphics" data/graphics/*.png
  install -d "${pkgdir}/opt/${pkgname}/data/icons"
  install -m644 -t "${pkgdir}/opt/${pkgname}/data/icons" data/icons/*.png
  install -d "${pkgdir}/opt/${pkgname}/data/levels"
  install -m644 -t "${pkgdir}/opt/${pkgname}/data/levels" data/levels/*.vvvvvv
  install -d "${pkgdir}/opt/${pkgname}/data/sounds"
  install -m644 -t "${pkgdir}/opt/${pkgname}/data/sounds" data/sounds/*.wav

  install -d "${pkgdir}/usr/bin"
  ln -s "/opt/${pkgname}/VVVVVV" "${pkgdir}/usr/bin/${pkgname}"

  install -D -m644 "data/icons/32_2.png" \
      "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -D -m644 "${srcdir}/${pkgname}.desktop" \
      "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Patch for 2.2
  install -m644 -t "${pkgdir}/opt/${pkgname}/data/levels" ${srcdir}/levels/*
  if [ "${CARCH}" = "x86_64" ]; then
    install -m755 ${srcdir}/VVVVVV_64 "${pkgdir}/opt/${pkgname}/VVVVVV_64"
  else
    install -m755 ${srcdir}/VVVVVV_32 "${pkgdir}/opt/${pkgname}/VVVVVV_32"
  fi
}
