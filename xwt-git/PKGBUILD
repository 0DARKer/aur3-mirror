pkgname=xwt-git
pkgver=537d273
pkgrel=2
pkgdesc="Xwt is a new .NET framework for creating desktop applications that run on multiple platforms from the same codebase."
arch=(i686 x86_64)
license=("BSD")
depends=(gtk-sharp-2)
makedepends=(mono git)
url="https://github.com/mono/xwt"
source=("${pkgname%-*}::git+https://github.com/mono/xwt.git"
"Xwt.sln.diff")
md5sums=(SKIP '0351606cb45e8415932acb1b0566de5b')

pkgver () {
	cd "$srcdir/${pkgname%-*}"
	echo "$(git describe --always | sed s/${_pkgname}-// | sed 's/-/./g')"
}

build() {
	cd "${srcdir}/${pkgname%-*}"
	patch -Np0 Xwt.sln < $srcdir/Xwt.sln.diff
	xbuild Xwt.sln /p:Configuration=Linux-Debug
}

package() {
	cd "${srcdir}/${pkgname%-*}/Xwt.Gtk/bin/Debug"
	find . -name '*.dll' -o -name '*.mdb' -o -name '*.config' |
		xargs -rtl1 -I {} install -Dm644 {} "$pkgdir/usr/lib/xwt/"{}
	find "$pkgdir/usr/lib/xwt" -name '*.dll' | xargs -rtl1 -I {} gacutil -i {} -root "$pkgdir/usr/lib"
	find "$pkgdir/usr/lib/xwt" -name '*.dll' -o -name '*.exe' | xargs -rtl1 mono --aot
}
