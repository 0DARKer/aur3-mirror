pkgname=xwt-git
pkgver=1335
pkgrel=1
pkgdesc="Xwt is a new .NET framework for creating desktop applications that run on multiple platforms from the same codebase."
arch=(i686 x86_64)
license=("BSD")
depends=(gtk-sharp-2)
makedepends=(mono git)
url="https://github.com/mono/xwt"
source=("${pkgname%-*}::git+https://github.com/mono/xwt.git"
"Xwt.sln.diff"
"xwt-gtk.pc.in")
md5sums=('SKIP'
         '0351606cb45e8415932acb1b0566de5b'
         'eee3cbc2387649754666387f05e1084d')

pkgver () {
	cd "$srcdir/${pkgname%-*}"
	echo $(git rev-list --count master)
}

build() {
	cd "${srcdir}/${pkgname%-*}"
	patch -Np0 Xwt.sln < $srcdir/Xwt.sln.diff
	xbuild Xwt.sln /p:Configuration=Linux-Debug
}

package() {
	cd "${srcdir}/${pkgname%-*}/Xwt.Gtk/bin/Debug"
	find . -name '*.dll' -o -name '*.mdb' -o -name '*.config' |
		xargs -rtl1 -I {} install -Dm644 {} "$pkgdir/usr/lib/xwt/"{}
	find "$pkgdir/usr/lib/xwt" -name '*.dll' | xargs -rtl1 -I {} gacutil -i {} -root "$pkgdir/usr/lib"
	find "$pkgdir/usr/lib/xwt" -name '*.dll' -o -name '*.exe' | xargs -rtl1 mono --aot
	install -Dm644 "$srcdir/xwt-gtk.pc.in" "$pkgdir/usr/lib/pkgconfig/xwt-gtk.pc"
}
