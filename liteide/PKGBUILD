# Maintainer: Harley Laue <losinggeneration@gmail.com>
pkgname=liteide
pkgver=14.1
pkgrel=1
pkgdesc="A simple, open source, cross-platform IDE"
arch=('x86_64')
url="http://code.google.com/p/liteide"
license=('LGPL')
depends=('qt' 'qjson')
makedepends=('mercurial' 'go')
provides=(liteide)
conflicts=(liteide-hg)
source=("qjson.patch")
md5sums=('2ba8ef67fbe5583c3ae67e85d45dec3f')

__hgroot=https://code.google.com/p/liteide/
__hgrepo=liteide

build() {
  cd "$srcdir"
  msg "Connecting to Mercurial server...."

  if [ ! -d "${__hgrepo}" ]; then
    hg clone "${__hgroot}" "${__hgrepo}"
  fi

  cd "${__hgrepo}"
  hg up -r "x$pkgver"

  msg "Mercurial checkout done or server timeout"
  msg "Starting build..."

  rm -fr "$srcdir/${__hgrepo}-build"
  cp -r "$srcdir/${__hgrepo}" "$srcdir/${__hgrepo}-build"
  cd "$srcdir/${__hgrepo}-build"
  patch -p1 < "$srcdir/qjson.patch"

  cd "build"
  QTDIR=/usr ./build_linux.sh
}

package() {
  cd "$srcdir/${__hgrepo}-build/build/liteide"
  mkdir -p "$pkgdir/usr"
  cp -r bin lib share "$pkgdir/usr"
  cp "LICENSE.LGPL" "LGPL_EXCEPTION.TXT" "README.TXT" "$pkgdir/usr/share/$pkgname"
}

# vim:set ts=2 sw=2 et:
