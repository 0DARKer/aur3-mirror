# Maintainer: Harley Laue <losinggeneration@gmail.com>
pkgname=liteide
pkgver=x15.1
pkgrel=2
pkgdesc="A simple, open source, cross-platform IDE"
arch=('x86_64')
url="http://code.google.com/p/liteide"
license=('LGPL')
depends=('qt' 'qjson')
optdepends=('qtwebkit: html render used if installed')
makedepends=('go' 'qtwebkit')
provides=(liteide)
conflicts=(liteide-hg)
source=(http://${pkgname}.googlecode.com/archive/${pkgver}.zip
        qjson.patch)
md5sums=('29957eb1789e79bf9ddbf0783b268bd5'
         '2ba8ef67fbe5583c3ae67e85d45dec3f')

__hgrev=ff5536dba42c

build() {
  cd "$srcdir/${pkgname}-${__hgrev}"

  patch -p1 < "$srcdir/qjson.patch"

  cd build
  QTDIR=/usr sh ./build_linux.sh
}

package() {
  cd "$srcdir/${pkgname}-${__hgrev}/build/liteide"

  mv bin/*.so* lib
  rm -rf lib/liteide/*.so*

  mkdir -p "$pkgdir/usr"
  cp -r bin lib share "$pkgdir/usr"
  cp "LICENSE.LGPL" "LGPL_EXCEPTION.TXT" "../../README.md" "$pkgdir/usr/share/$pkgname"
}

# vim:set ts=2 sw=2 et:
