# Maintainer: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

_pkgname=biboumi
pkgname="$_pkgname-git"
pkgver=r229.fa07130
pkgrel=1
pkgdesc="XMPP gateway to IRC"
arch=('i686' 'x86_64' 'armv7h')
url="http://dev.louiz.org/projects/biboumi"
license=('ZLIB')
depends=('expat' 'libidn')
makedepends=('git' 'cmake')
provides=("$_pkgname=0.1")
conflicts=("$_pkgname")
source=("$_pkgname::git://git.louiz.org/biboumi")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_pkgname"

  mkdir build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make biboumi
}

check() {
  cd "$srcdir/$_pkgname/build"
  make test
  ./test
}

package() {
  cd "$srcdir/$_pkgname/build"
  make DESTDIR="$pkgdir/" install

  cd "$srcdir/$_pkgname"
  #install -Dm644 doc/biboumi.1 "$pkgdir/usr/share/man/man1/$_pkgname.1"
  install -Dm644 doc/biboumi.1.md "$pkgdir/usr/share/doc/$_pkgname/$_pkgname.md"
}
