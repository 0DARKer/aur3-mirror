# Maintainer: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

_pkgname=biboumi
pkgname="$_pkgname-git"
pkgver=r98.de7b350
pkgrel=1
pkgdesc="XMPP gateway to IRC"
arch=('i686' 'x86_64')
url="http://dev.louiz.org/projects/biboumi"
license=('ZLIB')
depends=('crypto++' 'expat' 'libidn')
makedepends=('git' 'cmake')
provides=("$_pkgname=0.1")
conflicts=("$_pkgname")
source=("$_pkgname::git://git.louiz.org/biboumi")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_pkgname"

  mkdir build
  cd build
  cmake ..
  make
}

package() {
  cd "$srcdir/$_pkgname/build"
  install -Dm755 biboumi "$pkgdir/usr/bin/$_pkgname"

  cd "$srcdir/$_pkgname"
  install -Dm644 doc/biboumi.1 "$pkgdir/usr/share/man/man1/$_pkgname.1"
  install -Dm644 doc/biboumi.md "$pkgdir/usr/share/doc/$_pkgname/$_pkgname.md"
}
