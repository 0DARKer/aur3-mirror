# Maintainer: Tom Moore <tmoore01[at]gmail.com>
# Contributors: Anthony Ruhier <a.ruhier[at]laposte.net>

pkgname=subsonic-kang
pkgver=5.1
pkgrel=1

pkgdesc="Fork of the Subsonic project, an open-source web-based media streamer, with the licensing code removed"
url="https://github.com/EugeneKay/subsonic"
arch=('any')
license=('GPL')
depends=('bash' 'java-environment')
conflicts=('subsonic' 'subsonic-kang-git' 'subsonic-beta')
backup=('etc/conf.d/subsonic-kang.conf' 'opt/subsonic-kang/subsonic.properties' 'opt/subsonic-kang/subsonic.sh')
noextract=('subsonic-v${pkgver}-kang.war')
install='subsonic-kang.install'
source=("http://prdownloads.sourceforge.net/subsonic/subsonic-${pkgver}-standalone.tar.gz"
        "https://github.com/KHresearch/subsonic/releases/download/v${pkgver}-kang/subsonic-v${pkgver}-kang.war"
        'subsonic-kang.service')

package() {
  war_name="subsonic-v${pkgver}-kang.war"
  install -dm 755 "${pkgdir}"/{opt/subsonic-kang,etc/conf.d,usr/lib/systemd/system,/var/lib/subsonic-kang/playlists}
  cp -dr --no-preserve='ownership' * "${pkgdir}"/opt/subsonic-kang/
  cp --no-preserve='ownership' "${war_name}" "${pkgdir}"/opt/subsonic-kang/subsonic.war
  sed -i 's/SUBSONIC_HOME=\/var\/subsonic/SUBSONIC_HOME=\/opt\/subsonic-kang/' "${pkgdir}"/opt/subsonic-kang/subsonic.sh
  sed -i 's/\/var/\/var\/lib\/subsonic-kang/' "${pkgdir}"/opt/subsonic-kang/subsonic.sh
  touch "${pkgdir}"/opt/subsonic-kang/subsonic.properties
  ln -fs /opt/subsonic-kang/subsonic.sh "${pkgdir}"/etc/conf.d/subsonic-kang.conf
  install -m 644 subsonic-kang.service "${pkgdir}"/usr/lib/systemd/system/
  rm -f "${pkgdir}"/opt/subsonic-kang/{subsonic.bat,subsonic-"${pkgver}"-standalone.tar.gz,"${war_name}",subsonic-kang.service}
}

sha256sums=('ae7685393d40a49224126ec677d081385f104316b0bc8f4c20901dc0e5ec8840'
            '7dda918b18f202f12241998095ed73c1d42a5599af7276f2b22bb1812fd1b8c4'
            'a984953632162ad6197ef8d644042498d06bccbf30887f614cf5e41da6020ff4')

# vim: ts=2 sw=2 et:
