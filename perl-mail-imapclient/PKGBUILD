# Maintainer: DonVla <donvla@users.sourceforge.net>

pkgname=perl-mail-imapclient
_realname=Mail-IMAPClient
pkgver=3.25
pkgrel=1
pkgdesc="Perl/CPAN Module Mail::IMAPClient : An IMAP Client API"
arch=('any')
url="http://search.cpan.org/dist/Mail-IMAPClient"
license=('GPL' 'Artistic')
depends=('perl>=5.10.0' 'perl-parse-recdescent' 'perl-digest-hmac')
optdepends=("perl-authen-sasl: for Authmechanism 'DIGEST-MD5'")
options=(!emptydirs)
source=(http://search.cpan.org/CPAN/authors/id/P/PL/PLOBBES/${_realname}-${pkgver}.tar.gz)
md5sums=('1035ad9328fef03f72a65d7afed6fa26')
sha256sums=('aa38c4bf35c0bce280e7822cd218631123dbc94107905c1eeaf43a6b89244273')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # Install module into the vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
