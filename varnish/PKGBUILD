# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Douglas Soares de Andrade
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=varnish
pkgver=2.1.4
pkgrel=1
pkgdesc="High-performance  HTTP accelerator"
arch=('i686' 'x86_64')
url="http://www.varnish-cache.org/"
license=('BSD')
depends=('ncurses' 'pcre')
backup=('etc/varnish.conf')
install=$pkgname.install
options=('!libtool')
source=(http://www.varnish-software.com/sites/default/files/$pkgname-$pkgver.tar.gz \
        $pkgname.conf \
        $pkgname.init \
        $pkgname.runit \
        $pkgname.log.runit)
md5sums=('e794a37b6fbb786a083c0946103ae103'
         '2816759b6185d08c98309e33948599c6'
         'c9a4856f6ec6f3cb01ab33e28d77d3a9'
         '34681f7ed6fef8deb7b9a0e291f22ddb'
         '2ee74b4a9166dc2ad260dc8885bd7fd3')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR=${pkgdir} install

  install -d ${pkgdir}/var/$pkgname/$(hostname)
  install -D -m755 ${srcdir}/$pkgname.init ${pkgdir}/etc/rc.d/$pkgname
  install -D -m755 ${srcdir}/$pkgname.runit ${pkgdir}/etc/sv/$pkgname/run
  install -D -m755 ${srcdir}/$pkgname.log.runit ${pkgdir}/etc/sv/$pkgname/log/run
  install -D -m755 ${srcdir}/$pkgname.conf ${pkgdir}/etc/$pkgname.conf

#license
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
