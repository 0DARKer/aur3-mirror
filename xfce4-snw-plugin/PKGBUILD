# Maintainer: equeim <equeim@gmail.com>

pkgname=xfce4-snw-plugin
pkgver=0.3.1
pkgrel=1
pkgdesc="Plugin for xfce4-panel to show StatusNotifierItems (also known as AppIndicators)"
url="https://github.com/equeim/xfce4-snw-plugin"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('libdbusmenu-gtk2' 'xfce4-panel')
makedepends=('cmake' 'vala')
source=("https://github.com/equeim/xfce4-snw-plugin/archive/${pkgver}.tar.gz")
sha256sums=('e16b04b9f56c6f02fa412a7d8022b0f188dfd760026f17887d5f7d1e2b0971c3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}


