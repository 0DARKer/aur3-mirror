# Maintainer: mathieui <mathieui[at]mathieui.net>
# Contributor: Baptiste Jonglez <zerstorer at free dot fr>

pkgname=poezio-git
_pkgname=poezio
pkgrel=3
pkgver=20140728.fccf7f5
pkgdesc="A full-featured command-line irc-like (muc-centered) jabber client (git)"
arch=('i686' 'x86_64')
url="http://poez.io"
replaces=('poezio-hg')
conflicts=('poezio' 'poezio-hg')
license=('zlib')
depends=('python' 'python-sleekxmpp-devel' 'python-dnspython')
makedepends=('git')
optdepends=('pure-python-otr-git: OTR plugin'
            'python-pyinotify: Autoaway w/ screen plugin'
            'gnupg: GPG plugin'
            'figlet: ASCII art plugin'
            'python-sphinx: Generating documentation')

source=("git://git.poez.io/poezio/")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git log -1 --format='%cd.%h' --date=short | tr -d - | sed 's/-/./'
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir/" prefix=/usr install
}
