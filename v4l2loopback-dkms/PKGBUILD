# Maintainer: rtfreedman  <rob<d0t>til<d0t>freedman< T>googlemail<d0t>com>
# Based on PKGBUILD.v4l2loopback

pkgname=v4l2loopback-dkms
_pkgname=v4l2loopback
pkgver=0.8.0
pkgrel=2
pkgdesc="A kernel module and utils to create V4L2 loopback devices - using DKMS"
arch=(i686 x86_64)
url="https://github.com/umlaeute/v4l2loopback"
license=('GPL')
depends=('glibc')
provides=('v4l2loopback')
conflicts=('v4l2loopback')
makedepends=('help2man')
optdepends=('v4l-utils: for v4l2loopback-ctl and utils'
            'gstreamer0.10: for v4l2loopback-ctl and utils')
install="$pkgname.install"
source=("v4l2loopback-${pkgver}.tar.gz::https://github.com/umlaeute/v4l2loopback/archive/v${pkgver}.tar.gz"
		'make_parm_settable.patch' 'support-linux318.patch')
md5sums=('33af9825c54d858d5c963504cc00f5c4'
         '323d087f05d01514149106526015d052'
         'c101232b55313334699490d03d479eec')

prepare() {
	cd "$_pkgname-${pkgver}/"
	# make MAX_DEVICES/TIMEOUT/BUFFERS settable during build-process 
	# https://github.com/umlaeute/v4l2loopback/issues/55
	patch -p1 -i ../make_parm_settable.patch
	patch -p1 -i ../support-linux318.patch
}

build() {
  cd "$_pkgname-${pkgver}/examples"
  
  # path fix
  sed -i "s/\.\/utils\///" restarting-writer.sh

  # yuv4mpeg_to_v4l2 mod for flash
  sed "s/YUV420/YVU420/" yuv4mpeg_to_v4l2.c > yuv4mpeg_to_v4l2_flash.c
  sed -i "1s/$/ yuv4mpeg_to_v4l2_flash/" Makefile
  make
}

package() {
  cd "$_pkgname-${pkgver}"

  make DESTDIR="$pkgdir/" PREFIX=/usr install-{man,utils}

  # destination of kernel module
  sed -i 's/\/extra/\/extramodules/' dkms.conf
  
  # dkms module source
  install -dm755 "$pkgdir"/usr/src/v4l2loopback-$pkgver
  install -m644 dkms.conf Makefile v4l2loopback.c "$pkgdir"/usr/src/v4l2loopback-$pkgver

  # docs
  install -Dm644 doc/v4l2.html \
    "$pkgdir/usr/share/doc/$pkgname/v4l2.html"
  install -Dm644 README NEWS \
    "$pkgdir/usr/share/doc/$pkgname"

  # examples
  cd examples
  install -d "$pkgdir/usr/share/doc/$pkgname/examples"
  install -Dm644 {*.c,README,Makefile} "$pkgdir/usr/share/doc/$pkgname/examples"
  install -Dm755 {yuv4mpeg_to_v4l2{,_flash},restarting-writer.sh}  "$pkgdir/usr/bin"
  install -Dm755 test "$pkgdir/usr/bin/$_pkgname-test"
}

# vim:set ts=2 sw=2 et:
