# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=k3b-frameworks-git
pkgver=2.9.90.20141018.429127b
pkgrel=1
pkgdesc='Feature-rich and easy to handle CD burning application. KF5 Frameworks branch (Git version)'
arch=('i686' 'x86_64')
license=('GPL')
url='http://k3b.sourceforge.net'
depends=('qt5-webkit' 'kfilemetadata5' 'knotifyconfig' 'kcmutils' 'libkcddb-frameworks-git' 'libmpcdec' 'libsamplerate' 'libmad' 'libdvdread' 'musicbrainz')
makedepends=('git' 'cmake')
install="k3b.install"
optdepends=('cdrdao: for CD DAO mode burning support'
            'cdrkit: for CD burning support'
            'cdrtools: for CD burning support'
            'dvd+rw-tools: for DVD burning support'
            'normalize: for WAV normalization'
            'vcdimager: for VCD burning support'
            'transcode: for advanced MPEG conversion support'
            'emovix: for bootable multimedia CD/DVD support')
options=('!libtool')
provides=('k3b')
conflicts=('k3b')

source=('git://anongit.kde.org/k3b.git#branch=kf5')
sha1sums=('SKIP')
_gitname="k3b"

pkgver() {
  cd "${_gitname}"
  _major_v="$(grep -e "K3B_VERSION_MAJOR" CMakeLists.txt -num1 | tr -d ')' | cut -d ' ' -f4)"
  _minor_v="$(grep -e "K3B_VERSION_MINOR" CMakeLists.txt -num1 | tr -d ')' | cut -d ' ' -f4)"
  _patch_v="$(grep -e "K3B_VERSION_RELEASE" CMakeLists.txt -num1 | tr -d ')' | cut -d ' ' -f2)"
  echo "$(echo -n ${_major_v}.${_minor_v}.${_patch_v}).$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  rm -fr build
  mkdir -p build
}

build() {
  cd build
  cmake "../${_gitname}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
