# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=colord
pkgname=lib32-${_pkgbase}
pkgver=0.1.17
pkgrel=1
pkgdesc="Color daemon (32 bit)"
arch=('x86_64')
url="http://www.freedesktop.org/software/colord"
license=('GPL2')
depends=('lib32-glib2' 'lib32-libgusb' "${_pkgbase}") # 'lib32-lcms2' 'sqlite3' 'lib32-polkit' 'lib32-udev' 'lib32-sane'
makedepends=('intltool' 'vala' 'docbook2x' 'lib32-lcms2' 'lib32-sqlite3' 'lib32-polkit' 'lib32-udev' 'lib32-sane') # 'gobject-introspection'
options=('!libtool' '!emptydirs')
source=(${url}/releases/${_pkgbase}-${pkgver}.tar.xz
		fix-compilation.patch)
sha1sums=('7716c58a7ab85029637c7e88bdb77e48b6cd5ce9'
          '4ac0bacfdb72817eb8f4fcc985c1608a009a058a')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  patch -Np1 -i "${srcdir}/fix-compilation.patch"
  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --disable-static \
    --with-systemdsystemunitdir=/lib/systemd/system \
    --libexecdir=/usr/lib32/colord \
    --libdir=/usr/lib32 --enable-introspection=no
    
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,lib,var,usr/{bin,include,share}}
  rm -f "${pkgdir}/usr/lib32/colord/colord" # not a library
}
