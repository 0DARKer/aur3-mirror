# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=colord
pkgname=lib32-$_pkgbase
pkgver=0.1.13
pkgrel=1
pkgdesc="Color daemon (32 bit)"
arch=('x86_64')
url="http://www.freedesktop.org/software/colord"
license=('GPL2')
depends=('lib32-glib2' "$_pkgbase") # 'lib32-lcms2' 'sqlite3' 'lib32-polkit' 'lib32-udev' 'lib32-sane'
makedepends=('intltool' 'vala' 'docbook2x' 'lib32-lcms2' 'sqlite3' 'lib32-polkit' 'lib32-udev' 'lib32-sane') # 'gobject-introspection'
source=($url/releases/$_pkgbase-$pkgver.tar.xz)
options=('!libtool' '!emptydirs')
sha256sums=('eb3d00653d4a028f4e97f4e61ffa29940c65b42c20d4439e067cb183b725c292')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "$srcdir/$_pkgbase-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --disable-static \
    --libexecdir=/usr/lib32/colord \
    --libdir=/usr/lib32 --enable-introspection=no
    
  make
}

package() {
  cd "$srcdir/$_pkgbase-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/{etc,lib,var,usr/{bin,include,share}}
  rm -f "$pkgdir/usr/lib32/colord/colord" # not a library
}
