# Maintainer: Renato Garcia <fgar.renato@gmail.com>
# Contributor: Yannick Poirier <contact@yannickpoirier.fr>

pkgname=player
pkgver=3.0.2
pkgrel=3
pkgdesc='Robotic systems environment'
arch=('i686' 'x86_64')
url='http://playerstage.sourceforge.net'
license=('GPL')
depends=('libgnomecanvas' 'libtool' 'boost-libs' 'gsl' 'opencv' 'python2')
makedepends=('cmake' 'swig' 'boost')
source=("http://downloads.sourceforge.net/project/playerstage/Player/${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('b92b5ea028e6bfc49351849f420167db')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i s/lib64/lib/ cmake/internal/SetupDirectories.cmake
  sed -i '1i #include <cstddef>' server/drivers/blobfinder/shapetracker/shapetracker.cc
  sed -i '1i #include <cstddef>' server/drivers/blobfinder/upcbarcode/upcbarcode.cc
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_SKIP_BUILD_RPATH=TRUE
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}

