# Aur Mantainer: Aleksei Lissitsin <aleksei.lissitsin@gmail.com>
# This is just a copy from a previous version of opensc in community.
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: kevku <kevku@msn.com>

pkgname=opensc012
pkgver=0.12.2
pkgrel=1
pkgdesc='Access smart cards that support cryptographic operations (version 0.12)'
arch=('x86_64' 'i686')
url='http://www.opensc-project.org/opensc/'
license=('LGPL')
backup=('etc/opensc.conf')
makedepends=('docbook-xsl')
depends=('openssl' 'pcsclite' 'libltdl')
options=('!libtool' '!emptydirs')
conflicts=('opensc')
provides=('opensc=0.12.2')
source=("http://www.opensc-project.org/files/opensc/opensc-$pkgver.tar.gz")
md5sums=('5116adea5f2f9f22fb9896965789144b')
 
build() {
  cd opensc-$pkgver
  export LIBS=-lltdl
  _sheetdir=(/usr/share/xml/docbook/xsl-stylesheets-*)
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-man \
    --enable-doc \
    --enable-readline \
    --enable-openssl \
    --enable-pcsc \
    --enable-zlib \
    --with-xsl-stylesheetsdir="$_sheetdir"
  make
}
 
package(){
  cd opensc-$pkgver
  make DESTDIR="$pkgdir" install
  install -D -m644 etc/opensc.conf "$pkgdir/etc/opensc.conf"
} 
