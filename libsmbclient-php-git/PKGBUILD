pkgname=libsmbclient-php-git
pkgver=086409c
pkgrel=1
pkgdesc="libsmbclient bindings for php"
arch=('i686' 'x86_64')
url="https://github.com/eduardok/libsmbclient-php"
license=('PHP')
depends=('smbclient' 'php')
makedepends=('git')
source=('libsmbclient_location.patch' 'git://github.com/eduardok/libsmbclient-php')
md5sums=('d0c9137ed3c83b5f4bb501d86e335d34' 'SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --always | sed 's|-|.|g'
}

build() {
	cd "$srcdir/${pkgname%-git}"
	git apply ../libsmbclient_location.patch
	phpize
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/${pkgname%-git}"
	make INSTALL_ROOT="$pkgdir/" install
}
