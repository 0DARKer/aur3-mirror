# Maintainer: Kurt Marasco <celilo _at lavabit _dot com> 

pkgname=hiawatha
pkgver=7.8.1
pkgrel=1
pkgdesc="Secure and advanced webserver"
url="http://www.hiawatha-webserver.org/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('openssl' 'libxslt')
optdepends=('php-cgi: for php-fcgi support')
backup=(
	etc/rc.d/hiawatha
	etc/rc.d/php-fcgi
	etc/hiawatha/hiawatha.conf
	etc/hiawatha/hiawatha.conf.sample
	etc/hiawatha/php-fcgi.conf
	etc/hiawatha/cgi-wrapper.conf
	etc/hiawatha/mimetype.conf
	srv/http/hiawatha/cdcatalog.xml
	srv/http/hiawatha/cdcatalog.xslt
)

source=(
	"http://hiawatha-webserver.org/files/${pkgname}-${pkgver}.tar.gz" 
	'hiawatha' 
	'php-fcgi' 
	'hiawatha.conf.sample'
	'cdcatalog.xml'
	'cdcatalog.xslt'
)
md5sums=(
	'1fb9987aafd62d747faaa6058bd3ce44'
	'351d1098f29df91362ffab3ce2a2fb0e'
	'e23a8bc0690ba618ae737c7670683562'
	'e5e50c0fdce0aea076b7401af5168c6f'
	'74237747cbda94044a69382504e49685'
	'6af9cdecf8e221be8aae3d5a20dc099f'
)

build() {
  cd "$srcdir/${pkgname}-${pkgver}"

  ./configure webrootdir="/srv/http/hiawatha" \
	--prefix=/ \
        --exec-prefix=/usr \
	--mandir=/usr/share/man
  make
  make DESTDIR="$pkgdir" install

# The default capabilities are installed plus xslt support
# The following build options are available to customize your installation:
#option				description				required libraries when feature enabled
#--disable-dependency-tracking  speeds up one-time build
#--enable-dependency-tracking   do not reject slow dependency extractors
#--disable-largefile		disable support for large files	
#--disable-cache		disable file caching	
#--enable-chroot         	enable chroot support
#--enable-command		enable CommandChannel	
#--enable-debug			enable debug info (for development only)	
#--disable-ipv6			disable IPv6 support
#--disable-monitor       	disable monitor
#--disable-ssl			disable SSL support			libssl-dev
#--disable-toolkit		disable URL toolkit	
#--disable-xslt			enable XSLT support			libxml2-dev, libxslt1-dev 
# modify the above ./configure command to customize



# Fix hiawatha.conf (formerly httpd.conf)
  sed -e 's|#ServerId = www-data|ServerId = http|' \
      -e 's|/var/www/|/srv/http/|g' \
      -e 's|//|/|g' \
      -i "$pkgdir/etc/hiawatha/hiawatha.conf"

# Fix php-fcgi.conf
  sed -e 's|www-data|http|'\
      -e 's|/usr/bin/php5-cgi|/usr/bin/php-cgi|'\
      -e 's|/etc/php5/cgi/php.ini|/etc/php/cgi/php.ini|'\
      -i "$pkgdir/etc/hiawatha/php-fcgi.conf"
  echo "#unix socket example" >> "$pkgdir/etc/hiawatha/php-fcgi.conf"
  echo "#Server = /usr/bin/php-cgi ; /tmp/hiawatha.sock ; http:http ; /etc/php/php.ini" >> "$pkgdir/etc/hiawatha/php-fcgi.conf"
  echo "Server = /usr/bin/php-cgi ; 127.0.0.1:2005 ; http:http ; /etc/php/php.ini" >> "$pkgdir/etc/hiawatha/php-fcgi.conf"

# Fix hiawatha logrotate file (/etc/logrotate.d/hiawatha)
  sed -e 's|www-data|http|g' -i "$srcdir/$pkgname-$pkgver/etc/logrotate.d/hiawatha"
      mkdir "$pkgdir/etc/logrotate.d"
      install -m644 "$srcdir/$pkgname-$pkgver/etc/logrotate.d/hiawatha" "$pkgdir/etc/logrotate.d/hiawatha"
      
# Add startup scripts
  install -D -m755 "$srcdir/hiawatha" "$pkgdir/etc/rc.d/hiawatha"
  install -D -m755 "$srcdir/php-fcgi" "$pkgdir/etc/rc.d/php-fcgi"
  
# Install sample configuration implementing a virtual server for phpmyadmin, php-fastcgi, and a default website on localhost
  install -m644 "$srcdir/hiawatha.conf.sample" "$pkgdir/etc/hiawatha/"

# Install sample XSLT transformation on default website on localhost
  install -m644 "$srcdir/cdcatalog.xml" "$pkgdir/srv/http/hiawatha/"
  install -m644 "$srcdir/cdcatalog.xslt" "$pkgdir/srv/http/hiawatha/"

}
