# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=dkms-vhba
_pkgname=vhba-module
_module=vhba
pkgver=20120422
pkgrel=2
pkgdesc="DKMS controlled drivers for VHBA module for CDEMU"
arch=('any')
url="http://cdemu.sourceforge.net/"
license=GPL2
depends=('dkms' 'bash')
provides=('vhba-module' "vhba-module=${pkgver}")
conflicts=('vhba-module')
optdepends=('linux-headers: build the module against Arch kernel'
            'linux-lts-headers: build the module against LTS Arch kernel')
install="${pkgname}.install"
source=("http://downloads.sourceforge.net/cdemu/${_pkgname}-${pkgver}.tar.bz2"
        dkms.conf
        60-vhba.rules)
md5sums=('d97372da1d270d1605742b2995fb6678'
         '21aaa88968d26279462a1c4f7d8d43b2'
         'ddeaf9c9530d2830234d084baacd89b5')

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    install -d "${pkgdir}/usr/src/${_module}-${pkgver}"
    cp -a * "${pkgdir}/usr/src/${_module}-${pkgver}"
    rm -r "${pkgdir}/usr/src/${_module}-${pkgver}/debian"
    install -Dm664 "${srcdir}/dkms.conf" "${pkgdir}/usr/src/${_module}-${pkgver}/dkms.conf"
    install -Dm664 "${srcdir}/60-vhba.rules" "${pkgdir}/usr/lib/udev/rules.d/60-vhba.rules"
    sed "s|@VERSION@|${pkgver}|g" -i "${pkgdir}/usr/src/${_module}-${pkgver}/dkms.conf"
}  
