# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=dkms-vhba
_pkgname=vhba-module
_module=vhba
pkgver=20120422
pkgrel=1
pkgdesc="DKMS controlled drivers for VHBA module for CDEMU"
arch=('any')
url="http://cdemu.sourceforge.net/"
license=GPL2
makedepends=('linux-headers' 'dkms' 'bash')
provides=("vhba-module=${pkgver}-${pkgrel}")
conflicts=('vhba-module')
install="${pkgname}.install"
source=("http://downloads.sourceforge.net/cdemu/$_pkgname-$pkgver.tar.bz2"
        dkms.conf
        60-vhba.rules)
md5sums=('d97372da1d270d1605742b2995fb6678'
         '5ed370b5fcde55fdb03a995d4e1985b1'
         'b43457346d3d3eafdcc4d74607c76259')

package() {
    cd ${srcdir}/${_pkgname}-${pkgver}

    mkdir -p ${pkgdir}/usr/src/${_module}-${pkgver}
    cp -a * ${pkgdir}/usr/src/${_module}-${pkgver}
    rm -r ${pkgdir}/usr/src/${_module}-${pkgver}/debian
    cp ${srcdir}/dkms.conf ${pkgdir}/usr/src/${_module}-${pkgver}
    install -Dm664 ${srcdir}/60-vhba.rules ${pkgdir}/usr/lib/udev/rules.d/60-vhba.rules
    sed "s|PACKAGE_VERSION=\"0\"|PACKAGE_VERSION=\""${pkgver}"\"|g" -i ${pkgdir}/usr/src/${_module}-${pkgver}/dkms.conf
    sed "s|_ver=0|_ver="${pkgver}"|g" -i ${srcdir}/../${pkgname}.install
}  
