# Maintainer: rtfreedman  (rob<d0t>til<d0t>freedman<aT>googlemail<d0t>com

pkgname=phasex-git
_gitname="phasex"
pkgver=4d28a72
pkgrel=1
pkgdesc="An experimental MIDI softsynth with flexible phase modulation and oscillator/LFO sources"
arch=('i686' 'x86_64')
url="https://github.com/williamweston/phasex"
license=('GPLv3+')
depends=('lash')
makedepends=('git')
install="phasex-git.install"
provides=('phasex')
conflicts=('phasex')

source=("git://github.com/williamweston/phasex" 
        "phasex-git.install")
md5sums=('SKIP'
         '5e76b605e90fa9659afb0bdd0a474fe5')

pkgver() {
  cd "$_gitname"
  git describe --always | sed 's|-|.|g'
#  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "$_gitname"
  cp pixmaps/phasex-icon-32x32.png pixmaps/phasex-icon.png
  aclocal
  autoconf
  automake
  autoheader
# see INSTALL for use of --enable-parts and --enable-cpu-power
  ./configure \
	--prefix=/usr \
	--enable-arch=native \
	--enable-parts=2 \
	--enable-cpu-power=3
  make
}
 
package() { 
  cd "$_gitname"
  # fix desktop file
  sed -i 's@/usr/share/phasex/pixmaps/phasex-icon.png@phasex@' misc/phasex.desktop

  make DESTDIR="$pkgdir/" install
  
  # cleanup
  for fn in compile depcomp missing version-sh install-sh; do \
	rm "$pkgdir/usr/share/phasex/$fn"
  done
  for s in 24 36 72 96 192 256 512; do
    rm -fr "$pkgdir/usr/share/icons/hicolor/${s}x${s}"
  done
}

# vim:set ts=2 sw=2 et:
