# Maintainer: Mateus Rodrigues Costa <charles.costar@gmail.com>
# Contributor: stjhimy <stjhimy@gmail.com>
# Contributor: CYB3R <dima@golovin.in>
# Contributor: Sarkasper <kasper.menten@gmx.com>
# Contributor: Scias <shining.scias@gmail.com>
# Contributor: darzki <darzki@o2.pl>
# Contributor: N30N <archlinux@alunamation.com>

pkgname=lwks-beta
pkgver=12.0.1.D
pkgrel=1
pkgdesc="Lightworks is a professional video editing suite"
arch=('x86_64')
url="http://www.lwks.com/"
license=('custom')
depends=('gtk3' 'portaudio' 'libgl' 'glu' 'ffmpeg-compat' 'libedit' 'nvidia-cg-toolkit')
optdepends=('nvidia-utils: only for nVidia users')
provides=('lightworks')
conflicts=('lightworks')
source=("http://www.lwks.com/dmpub/lwks-$pkgver-amd64.deb")
sha256sums=('231f9eb43c947e7302ca57b1be01b7bcf8508e77d98eb1f1f75c53935d463a59')

package() {
	msg2 "Extracting data.tar.gz"
	bsdtar -zxf data.tar.gz -C "$pkgdir"

	msg2 "Moving udev folder from /lib to /usr/lib"
	mv "$pkgdir"/lib/udev "$pkgdir"/usr/lib
	rmdir "$pkgdir"/lib

	msg2 "Copying copyright file and creating a license dir"
	install -Dm644 "$pkgdir"/usr/share/doc/lightworks/copyright \
	"$pkgdir"/usr/share/licenses/lightworks/copyright
	ln -sr "$pkgdir"/usr/share/licenses/lightworks "$pkgdir"/usr/share/licenses/$pkgname

	msg2 "Changing some needed permissions"
	chmod a+rw "$pkgdir"/usr/share/lightworks/Preferences
	chmod a+rw "$pkgdir"/usr/share/lightworks/"Audio Mixes"
}
