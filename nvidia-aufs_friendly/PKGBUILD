# $Id: PKGBUILD 189199 2013-06-29 10:08:46Z foutrelis $
# Maintainer : Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-aufs_friendly
pkgver=319.32
_extramodules=extramodules-3.9-aufs_friendly
pkgrel=2
pkgdesc="NVIDIA drivers for linux-aufs_friendly"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-aufs_friendly>=3.9' 'linux-aufs_friendly<3.10' "nvidia-libgl" "nvidia-utils=${pkgver}")
makedepends=('linux-aufs_friendly-headers>=3.9' 'linux-aufs_friendly-headers<3.10')
conflicts=('nvidia-96xx' 'nvidia-173xx' 'nvidia-304xx-aufs_friendly')
license=('custom')
install=nvidia.install
options=(!strip)

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('978339ff37c3207f3a39d3952721dcdb')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
   _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('d181d856768868823df3bda4607a5315')
fi

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}/kernel"
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-aufs_friendly.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
