# Maintainer: Moritz Maxeiner <moritz@ucworks.org>

pkgname=comp-scale-svn
pkgver=83648
pkgrel=1
pkgdesc="Enlightenment e17 module: Scale windows down to see them all. Side by side."
arch=('i686' 'x86_64')
url="http://svn.enlightenment.org/svn/e/trunk/E-MODULES-EXTRA/comp-scale/"
license=('MIT')
depends=('enlightenment17-svn')
makedepends=('subversion')
provides=('comp-scale')
options=('!libtool')
source=(comp-scale_e-revision-83743-to-83752.patch)
md5sums=('fb3303bb5525e65bab085cb26d44fa73')
sha256sums=('ac4782f8a9d2d731f37d1bb777acbd0b7e8b5dd707dde9b5fdfcabc2e141a4e3')
conflicts=('e-modules-extra')

_svntrunk=http://svn.enlightenment.org/svn/e/trunk/E-MODULES-EXTRA/comp-scale/
_svnmod=comp-scale

build() {
  cd "$srcdir"

  msg "Connecting to SVN server...."
  if [ -d "$_svnmod/.svn" ]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"

  # Apply the patch if necessary
  cd "$srcdir/$_svnmod"  
  if [[ ! -z $(egrep -r e_comp_evas_get | sed '/.svn/d') ]]; then
    msg "Patching for e_comp api change from r83743 to r83752..."
    patch --forward -p0 -i "$srcdir/comp-scale_e-revision-83743-to-83752.patch"
  fi

  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir/" install

  install -D -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
  install -D -m644 COPYING-PLAIN $pkgdir/usr/share/licenses/$pkgname/COPYING-PLAIN
}
