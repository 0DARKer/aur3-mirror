# original from Zhou Xin <soul916@hotmail.com> Pulse added by M. S. <bomba1985@googlemail.com>

pkgname=mplayer-vdpau-pulse-svn
realname=mplayer
pkgver=31826
pkgrel=1
pkgdesc="A movie player for linux with Nvidia VDPAU / Pulse support, based on extra/mplayer."
arch=('i686' 'x86_64')
depends=('libxxf86dga' 'libxv' 'libmad' 'giflib' 'cdparanoia' 'libxinerama' 'yasm'
         'sdl' 'lame' 'libtheora' 'xvidcore' 'zlib' 'libmng' 'libxss'
         'libgl' 'smbclient' 'aalib' 'jack' 'libcaca'
         'x264>=20100410' 'faac' 'lirc-utils' 'ttf-dejavu' 'libxvmc' 'libjpeg>=8'
	 'enca' 'libvdpau>=0.4' 'opencore-amr' 'libdca'
	 'nvidia')
license=('GPL')
url="http://www.mplayerhq.hu/"
makedepends=('unzip' 'mesa' 'live-media>=2010.01.13' 'subversion' 'nvidia' 'libvdpau')
provides=("${realname}=${pkgver}")
#provides=('mplayer')
conflicts=('mplayer')
replaces=('mplayer')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
source=(mplayer.desktop mplayer.png )
sha256sums=('6f9bf1a1f771689ebfb6cc42a0f95a4b8a5d299bfac62cc67fdb0f1aff6f7c96'
	    '061739a8cc267748ad58f168034cc39785d949a5262772535e1b347c7842af48')

_svntrunk=svn://svn.mplayerhq.hu/mplayer/trunk
_svnmod=mplayer-vdpau-svn

build() {
  # Custom CFLAGS break the mplayer build
  unset CFLAGS LDFLAGS

  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  ./configure --prefix=/usr \
  	--enable-runtime-cpudetection \
	--disable-gui \
	--disable-arts \
	--disable-liblzo \
	--disable-speex \
      	--disable-openal \
	--disable-fribidi \
	--disable-libdv \
	--disable-musepack \
	--disable-esd \
	--disable-mga \
	--enable-xvmc \
	--language=all \
      	--confdir=/etc/mplayer \
	--extra-cflags="-fno-strict-aliasing" \
	--enable-pulse \
	--enable-vdpau || return 1

  [ "$CARCH" = "i686" ] &&  sed 's|-march=i486|-march=native|g' -i config.mak

  make || return 1
  make -j1 DESTDIR=${pkgdir} install || return 1
  install -Dm644 etc/{codecs.conf,input.conf,example.conf} ${pkgdir}/etc/mplayer/ || return 1
  install -dm755 ${pkgdir}/usr/share/mplayer/
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf ${pkgdir}/usr/share/mplayer/subfont.ttf || return 1
  rm -rf ${pkgdir}/usr/share/mplayer/font
  #desktop file  FS#14770
  install -Dm644 ${srcdir}/mplayer.desktop ${pkgdir}/usr/share/applications/mplayer.desktop || return 1
  install -Dm644 ${srcdir}/mplayer.png ${pkgdir}/usr/share/pixmaps/mplayer.png || return 1
}


