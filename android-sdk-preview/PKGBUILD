# Maintainer: Joel Pedraza <joel@joelpedraza.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Gordin <9ordin @t gmail dot com>

pkgname=android-sdk-preview
_stablepkgname=android-sdk
pkgver=r21.1_rc2
pkgrel=1
pkgdesc='Google Android SDK Preview'
arch=('i686' 'x86_64')
url="http://developer.android.com/sdk/index.html"
license=('custom')
depends=('java-runtime' 'alsa-lib' 'openal' 'libstdc++5' 'libxv'
         'ncurses' 'sdl' 'swt' 'zlib')
if [[ $CARCH = x86_64 ]]; then
  depends=('java-runtime' 'lib32-alsa-lib' 'lib32-openal' 'lib32-libstdc++5'
           'lib32-libxv' 'lib32-ncurses' 'lib32-sdl' 'lib32-zlib' 'swt')
fi
provides=('android-sdk=r21.1')
conflicts=('android-sdk')
install=${_stablepkgname}.install
optdepends=('android-udev: udev rules for Android devices'
            'android-sdk-platform-tools: adb, aapt, aidl, dexdump and dx')
source=(http://dl.google.com/android/repository/tools_$pkgver-linux.zip
        ${_stablepkgname}.png
        ${_stablepkgname}.desktop
        ${_stablepkgname}.sh
        ${_stablepkgname}.csh
        ${_stablepkgname}.conf
        license.html
        source.properties)
sha1sums=('27dbd8423188c2a1af460d561bee0952c7ac8a04'
          'c249134310ae91dccce1ad839b3985211674ab71'
          'e834f53e13d5926ec7be3fd775fa80aa6f328eb6'
          '78f8574e651c9bf8b7515ecb30c7ef93edbc4a96'
          '08c85aab7523e22b298891c7047bc0e7adbf3437'
          '145bdf3eb41a56574b289c1577a24bc47097ec83'
          'bfb91be7e0b602d765b7a1fcaf0ce1b7e1a93faa'
          'c5e93968e1410288594f00f27a45c42edbcc0971')
package() {
  cd "$pkgdir"
  install -Dm755 "$srcdir/${_stablepkgname}.sh" etc/profile.d/${_stablepkgname}.sh
  install -Dm755 "$srcdir/${_stablepkgname}.csh" etc/profile.d/${_stablepkgname}.csh
  install -Dm644 "$srcdir/${_stablepkgname}.conf" etc/ld.so.conf.d/${_stablepkgname}.conf
  mkdir -p opt/${_stablepkgname}
  cp -a "${srcdir}/tools" opt/${_stablepkgname}
  install -Dm655 "$srcdir/${_stablepkgname}.png" usr/share/pixmaps/${_stablepkgname}.png
  install -Dm655 "$srcdir/${_stablepkgname}.desktop" \
    usr/share/applications/${_stablepkgname}.desktop
  install -Dm644 "$srcdir/license.html" \
    usr/share/licenses/${_stablepkgname}/license.html
  install -Dm644 "${srcdir}/source.properties" \
    "${pkgdir}/opt/android-sdk/tools/source.properties"
  if [[ $CARCH = i686 ]]; then
    rm -rf ${pkgdir}/opt/android-sdk/tools/lib/{monitor-,}x86_64 \
      ${pkgdir}/opt/android-sdk/tools/lib/lib64* \
      ${pkgdir}/opt/android-sdk/tools/emulator64-*
  fi

  # Fix broken permissions
  chmod -R o=g opt/${_stablepkgname}
}
