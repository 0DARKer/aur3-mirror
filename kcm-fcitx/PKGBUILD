# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: poplarch <poplarch@gmail.com>

pkgname=kcm-fcitx
pkgver=0.3.4
pkgrel=1
pkgdesc="KDE Config Module for Fcitx"
arch=('i686' 'x86_64')
url="https://github.com/fcitx/kcm-fcitx"
license=('GPL')
depends=('fcitx>=4.2.4' 'kdebase-runtime')
makedepends=('cmake' 'intltool' 'automoc4')
source=(http://fcitx.googlecode.com/files/${pkgname}-${pkgver}.tar.xz)
md5sums=('7af49109937f11c21b208ac2bf6c562d')

build(){
    cd "$srcdir/${pkgname}-${pkgver}"
    rm -rf build
    mkdir build
    cd build
    
    cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release ..
    make
}

package ()
{
    cd "$srcdir/${pkgname}-${pkgver}/build"
    make DESTDIR=${pkgdir} install
}
