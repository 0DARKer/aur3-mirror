# Maintainer: Graziano Giuliani <graziano.giuliani@gmail.com>
pkgname=biosdevname
pkgver=0.5.1
pkgrel=1
pkgdesc="Udev helper for naming devices per BIOS names"
arch=(i686 x86_64)
url="http://linux.dell.com/files/biosdevname"
license=('GPL')
depends=(pciutils)
makedepends=(zlib)
source=(http://linux.dell.com/cgi-bin/cgit.cgi/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.bz2)
md5sums=('20055a7f4ba5c1df56f6d074385227c5')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./autogen.sh
  ./configure --disable-rpath --prefix=/usr \
    --bindir=/usr/bin --sbindir=/usr/bin \
    --libdir=/usr/bin --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  mv $pkgdir/lib $pkgdir/usr
  sed -i $pkgdir/usr/lib/udev/rules.d/71-biosdevname.rules \
    -e 's!/sbin/!/bin/!'
}

# vim:set ts=2 sw=2 et:
