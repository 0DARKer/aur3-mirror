# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=eclipse-androidproguardscala
pkgver=0.0.48
pkgrel=1
pkgdesc="An Eclipse builder for Android projects using Scala"
arch=('any')
url="https://github.com/banshee/AndroidProguardScala"
license=('GPL')
depends=('eclipse-scala-ide' 'eclipse-android')

source=($pkgname-$pkgver.zip::"https://s3.amazonaws.com/androidproguardscala/UpdateSiteForAndroidProguardScalaEcosystem/stable-scala210-e38/com.restphone.androidproguardscala.updatesite.zip")
md5sums=('11592311572ceab3dd755742a65b03c5')

package() {
  _dest="${pkgdir}"/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse
  cd "${srcdir}"

  # Features
  for _f in features/*.jar; do
    _dir="${_dest}/${_f/.jar}"
    mkdir -p "${_dir}"
    unzip "${_f}" -d "${_dir}"
  done

  # Plugins
  for _p in plugins/*.jar; do
    install -Dm644 "${_p}" "${_dest}/${_p}"
  done
}

