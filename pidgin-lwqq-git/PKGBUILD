# Maintainer: Felix Yan <felixonmars@gmail.com>

# Defaulting to preview branch, change to master if you want a more stable version
_BRANCH=preview

pkgname=pidgin-lwqq-git
pkgver=20130120
pkgrel=1
pkgdesc="A pidgin plugin based on lwqq, a excellent safe useful library for webqq protocol"
arch=('i686' 'x86_64')
url="https://github.com/xiehuc/pidgin-lwqq"
license=('GPL3')
depends=('libpurple' 'curl' 'libev' 'hicolor-icon-theme')
optdepends=('telepathy-haze: Empathy plugin support')
makedepends=('git' 'cmake')
provides=(pidgin-lwqq)
conflicts=(pidgin-lwqq)
options=(!strip)

_gitroot=git://github.com/xiehuc/pidgin-lwqq.git
_gitname=pidgin-lwqq

build()
{
  cd ${srcdir}
  msg "Connecting to the GIT server...."

  if [[ -d "$srcdir"/$_gitname ]] ; then
    cd $_gitname
    git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
    cd $_gitname
  fi

  git checkout $_BRANCH

  rm -rf "$srcdir/$_gitname/build"
  mkdir "$srcdir/$_gitname/build"
  cd "$srcdir/$_gitname/build"

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DWITH_LIBEV=On -DCMAKE_BUILD_TYPE=Debug -DVERBOSE=3 ..
  make
}

package()
{
  cd "$srcdir/$_gitname/build"
  make DESTDIR="${pkgdir}" install
}
