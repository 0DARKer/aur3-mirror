# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=digikam-devel
_appname=digikam
pkgver=3.0.0_beta1a
pkgrel=2
pkgdesc="Digital photo management application for KDE - Unstable version"
arch=("i686" "x86_64")
url="http://www.digikam.org"
license=('GPL')
depends=('kdepimlibs' 'opencv' 'liblqr' 'qjson' 'kdeedu-marble') # 'libkipi' 'libkexiv2' 'libkdcraw' 'libksane' - doesn't work with KDE 4.9 versions, compile internal ones
makedepends=('automoc4' 'cmake' 'boost' 'doxygen' 'hugin' 'libgpod')
optdepends=("libgpod: support for Apple iPod audio devices"
	"hugin: for the Panorama tool")
conflicts=('digikam' 'kipi-plugins' 'libkface' 'libkgeomap' 'libkvkontakte' 'libmediawiki' 'libkipi' 'libkexiv2' 'libkdcraw' 'libksane')
provides=('digikam' 'kipi-plugins' 'libkface' 'libkgeomap' 'libkvkontakte' 'libmediawiki')
install="$pkgname.install"
source=("http://download.kde.org/unstable/$_appname/$_appname-${pkgver/_/-}.tar.bz2")
md5sums=('71bad5e3c7b551a50ee7339321f9dfdc')

build() {
  cd $srcdir/$_appname-${pkgver/_/-}
  mkdir build
  cd build
  cmake .. -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DDIGIKAMSC_USE_PRIVATE_KDEGRAPHICS=ON
  make
}

package(){
  cd $srcdir/$_appname-${pkgver/_/-}/build
  make DESTDIR=$pkgdir install
}
