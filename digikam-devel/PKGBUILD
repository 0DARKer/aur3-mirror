# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=digikam-devel
_appname=digikam
pkgver=2.6.0_rc
pkgrel=1
pkgdesc="Digital photo management application for KDE - Unstable version"
arch=("i686" "x86_64")
url="http://www.digikam.org"
license=('GPL')
depends=('kdepimlibs' 'opencv' 'liblqr' 'qjson' 'libkipi' 'libkexiv2' 'libkdcraw' 'libksane' 'kdeedu-marble')
makedepends=('automoc4' 'cmake' 'boost' 'doxygen' 'hugin' 'libgpod')
optdepends=("libgpod: support for Apple iPod audio devices"
	"hugin: for the Panorama tool")
conflicts=('digikam' 'kipi-plugins' 'libkface' 'libkgeomap' 'libkvkontakte' 'libmediawiki')
provides=('digikam' 'kipi-plugins' 'libkface' 'libkgeomap' 'libkvkontakte' 'libmediawiki')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/$_appname/$_appname-software-compilation-${pkgver/_/-}.tar.bz2")
md5sums=('271892857156464617a0513b5ec108d8')

build() {
  cd $srcdir/$_appname-software-compilation-${pkgver/_/-}
  mkdir build
  cd build
  cmake .. -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package(){
  cd $srcdir/$_appname-software-compilation-${pkgver/_/-}/build
  make DESTDIR=$pkgdir install
}
