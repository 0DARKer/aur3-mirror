# Contributor: Luca Bennati <lucak3 AT gmail DOT com>
# Maintainer: Glaucous <glakke1 at gmail dot com>

pkgname=apitrace-git
pkgver=20111117
pkgrel=1
pkgdesc="Graphics API Tracing"
arch=('x86_64' 'i686')
url="https://github.com/apitrace/apitrace"
license=('BSD')
depends=('python2' 'libgl')
makedepends=('cmake>=2.8' 'git')
optdepends=('qt: GUI support' 'qjson: GUI support')
provides=('apitrace')
conflicts=('apitrace')
source=('LICENSE')
md5sums=('85536f1e169f24a548762a3cbbcd227c')

_gitroot='git://github.com/apitrace/apitrace.git'
_gitname='apitrace'

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}" && git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  cd "${srcdir}"
#unset LDFLAGS
  cmake -H"${_gitname}" -Bbuild -DCMAKE_INSTALL_PREFIX=/usr -DPYTHON_EXECUTABLE='/usr/bin/python2.7'
  make -C build
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}/" install

  mkdir -p "${pkgdir}"/usr/share/{doc,licenses/apitrace}
  msg "Moving docs to /usr/share/doc"
  mv "${pkgdir}"/usr/{doc,share/doc/apitrace}
  msg "Moving LICENSE to /usr/share/licenses"
  mv "${pkgdir}"/usr/share/{doc,licenses}/apitrace/LICENSE
}

# vim:set ts=2 sw=2 et:
