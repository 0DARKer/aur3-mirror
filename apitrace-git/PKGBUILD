# Contributor: Luca Bennati <lucak3 AT gmail DOT com>
# Maintainer: Glaucous <glakke1 at gmail dot com>

pkgname=apitrace-git
pkgver=20130312
pkgrel=1
pkgdesc="Graphics API Tracing"
arch=('x86_64' 'i686')
url="https://github.com/apitrace/apitrace"
license=('BSD')
depends=('python2' 'libgl')
makedepends=('cmake>=2.8' 'git' 'mesa')
optdepends=('qt: GUI support' 'qjson: GUI support')
provides=('apitrace')
conflicts=('apitrace')
source=('https://gist.github.com/mosra/5146653/raw/029b374f6537d99afad4541bc9a70e26057d657f/apitrace.patch')
md5sums=('4000854e1143865f15eee75436f2ab93')

_gitroot='git://github.com/apitrace/apitrace.git'
_gitname='apitrace'

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}" && git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"

  msg2 "Applying Arch specific patch"
  # Patch is needed in order to build on Arch
  cd apitrace && patch -Np1 -i ${srcdir}/apitrace.patch

  msg "Starting build..."

  cd "${srcdir}"
  cmake -H"${_gitname}" -Bbuild -DCMAKE_INSTALL_PREFIX=/usr -DPYTHON_EXECUTABLE='/usr/bin/python2.7'
  make -C build
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}/" install

  mkdir -p "${pkgdir}"/usr/share/licenses/apitrace
  msg "Linking license"
  cd "${pkgdir}"/usr/share/licenses/apitrace
  ln -s ../../doc/apitrace/LICENSE.txt LICENSE
}

# vim:set ts=2 sw=2 et:
