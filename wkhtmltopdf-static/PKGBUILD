# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Maintainer : David Phillips <dbphillipsnz at remove these words I've written gmail dot com>
 
pkgname=wkhtmltopdf-static
pkgver=0.12.2
pkgrel=2
pkgdesc="Shell utility to convert HTML to PDF using Webkit and Qt (upstream static build)"
arch=('i686' 'x86_64')
url="http://wkhtmltopdf.org/"
license=('GPL3')
conflicts=('wkhtmltopdf')
provides=("wkhtmltopdf=${pkgver}")
depends=('bzip2' 'expat' 'fontconfig' 'freetype2' 'gcc-libs' 'glib2' 'glibc' 'graphite' 'harfbuzz' 'libjpeg-turbo' 'libpng' 'libpng12' 'libx11' 'libxau' 'libxcb' 'libxdmcp' 'libxext' 'libxrender' 'pcre' 'zlib')


# Debian packages are already stripped, so don't bother re-attempting
options=('!strip')

source_i686=(http://downloads.sourceforge.net/project/wkhtmltopdf/${pkgver}/wkhtmltox-${pkgver}_linux-wheezy-i386.deb)
sha256sums_i686=('cf9213627476382cf5010d42fa4fba7699e7f5bf7794888b34e748978a30b708')

source_x86_64=(http://downloads.sourceforge.net/project/wkhtmltopdf/${pkgver}/wkhtmltox-${pkgver}_linux-wheezy-amd64.deb)
sha256sums_x86_64=('75f3379b8bf3998f9875270a12219263ee25ab3e97a511f37137c199b1f8c40a')

package() {
	msg "Unpacking data.tar.xz (Debian package files)"
	tar -xf data.tar.xz -C "${pkgdir}"
	cd "${pkgdir}"

	# Debian package has everything in /usr/local/{bin,lib,include,...}
	# but we want it in /usr/{bin,lib,include,...}
	msg "Moving files from /usr/local/ to /usr/"
	for dir in $(ls -1 usr/local) ; do
		mkdir -vp "usr/${dir}"
		mv -v "usr/local/${dir}"/* "usr/${dir}"
		rmdir -v "usr/local/${dir}"
	done
	rmdir -v usr/local

	# Fix permissions differences between Debian and Arch
	# All Directories => rwxr-xr-x
	msg "Fixing permissions on:"
	msg2 "Directories"
	find ./ -type d -exec chmod -cR 755 "{}" \;

	# Non-bin and non-lib Files => rw-r--r--
	msg2 "Files"
	find ./usr/{include,share} -type f -exec chmod -cR 644 "{}" \;
}
