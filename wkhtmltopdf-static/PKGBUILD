# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Maintainer : David Phillips <dbphillipsnz at remove these words I've written gmail dot com>
 
pkgname=wkhtmltopdf-static
pkgver=0.12.1
pkgrel=1
pkgdesc="Shell utility to convert HTML to PDF using Webkit and Qt (upstream static build)"
arch=('i686' 'x86_64')
url="http://wkhtmltopdf.org/"
license=('GPL3')
conflicts=('wkhtmltopdf')
provides=('wkhtmltopdf=0.12.1')
depends=('libpng12')
options=('!strip')

if [ "$CARCH" = "i686" ]; then
	_arch="i386"
	sha1sums=('e5386aaff183667aa5d373221e20f5e9c990e1dd')
else
	_arch="amd64"
	sha1sums=('07d397b32f91722de683e8777df2ddd6c49662c2')
fi

source=(http://downloads.sourceforge.net/project/wkhtmltopdf/0.12.1/wkhtmltox-0.12.1_linux-wheezy-${_arch}.deb)

 
package() {
	tar -xf data.tar.xz -C "${pkgdir}"

	cd ${pkgdir} 

	# /usr/local -> /usr
	mv usr/local/ usr_
	rmdir usr
	mv usr_ usr

	# Fix permissions differences between Debian and Arch
	# All Directories => rwxr-xr-x
	find ./ -type d -exec chmod -R 755 "{}" ";"

	# Non-bin and non-lib Files => rw-r--r--
	find ./usr/{include,share} -type f -exec chmod -R 644 "{}" ";"
}
