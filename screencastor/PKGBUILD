# Maintainer: crazyelf5

pkgname=screencastor
pkgver=1.0.2
pkgrel=2
pkgdesc="A powerful screencasting app written in python"
arch=('any')
url="http://forum.ubuntu-fr.org/viewtopic.php?id=440555"
license=('GPL')
depends=('python2' 'pygtk' 'pygtksourceview2' 'vte' 'ffmpeg' 'sed' 'bash' 'imagemagick' 'hicolor-icon-theme')
options=(!emptydirs)
install=$pkgname.install
source=(http://hizo.fr/linux/${pkgname}/${pkgname}_${pkgver}.tar.gz
        ffpresets.tar.xz
        screencastor.desktop)
md5sums=('64a58d88e51526b41a50955b12935b15'
         '84ce652c551db5a8ffe6714d255c1344'
         '1deced02f3b2ffad041a0a1dd038b0f9')

package() {
  cd "$srcdir/$pkgname"

  install -dm755 "$pkgdir"/usr/{bin,share/{applications,icons/hicolor/256x256/apps,screencastor}}
  install -m755 go_screencastor.sh "$pkgdir/usr/bin/screencastor"
  install -m644 ../screencastor.desktop "$pkgdir/usr/share/applications"
  install -m644 screencastor.png "$pkgdir/usr/share/icons/hicolor/256x256/apps"
  install -m644 screencastor.png "$pkgdir/usr/share/screencastor"
  cp -r locale "$pkgdir/usr/share"
  install -m644 ./{screencastor.glade,screencastor.py,screencastor.sh} "$pkgdir/usr/share/screencastor"

  sed -i 's|./screencastor.py -d --glade ./screencastor.glade|/usr/share/screencastor/screencastor.py --locale /usr/share/locale -g /usr/share/screencastor/screencastor.glade|' "$pkgdir/usr/bin/screencastor"
  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' "$pkgdir/usr/share/screencastor/screencastor.py"

  for i in /usr/share/ffmpeg/*.ffpreset; do rm -f "$srcdir"/$(basename $i); done
  install -dm755 "$pkgdir/usr/share/ffmpeg"
  find "$srcdir/" -name *.ffpreset -exec install -m644 '{}' "$pkgdir/usr/share/ffmpeg" \;
  
  chmod +x "$pkgdir"/usr/{bin/screencastor,share/screencastor/screencastor.{glade,py,sh}}
}

# vim:set ts=2 sw=2 et:
