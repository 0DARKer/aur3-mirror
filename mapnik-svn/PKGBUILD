# Contributor: Joerg Hansen (joerg dot hansen at gmx dot net)

pkgname=mapnik-svn
pkgver=1782
pkgrel=1
pkgdesc="Free toolkit for developing mapping applications."
url="http://mapnik.org/"
arch=(i686 x86_64)
license=('LGPL')
depends=('freetype2' 'libxml2' 'icu' 'boost' 'pycairo' 'cairomm' 'gdal' 'curl' )
makedepends=('subversion' 'scons' 'pkgconfig')
conflicts=('mapnik')
provides=('mapnik')
source=('iostream.patch')
md5sums=('04f9e7f1a52c877e1db3f49bb84a44dd')

_svntrunk=http://svn.mapnik.org/trunk
_svnmod=mapnik

build() {
    cd ${srcdir}

    if [ -d $_svnmod/.svn ]; then
        ( cd $_svnmod && svn up -r $pkgver )
    else
        svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
    fi

    msg "SVN checkout done or server timeout"
    msg "Starting make..."

    cp -r $_svnmod $_svnmod-build
    cd $_svnmod-build

    # Patch SConstruct so libs end up in /usr/lib and not in /usr/lib64 on x86_64
    sed -i -e "/LIBDIR_SCHEMA=/s/lib64/lib/" SConstruct

    python scons/scons.py configure || return 1
    python scons/scons.py install DESTDIR="$pkgdir" PREFIX=/usr || return 1

    rm -rf ${srcdir}/$_svnmod-build
}
