pkgname='lwatch'
pkgver='0.6.2'
pkgrel=1
pkgdesc="Colourizing a system logs for easier reading."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/${pkgname}/"
license=('GPL2')
depends=('pcre')
backup=('etc/lwatch.conf')
options=('!purge')
source=("http://download.sourceforge.net/project/${pkgname}/current/${pkgname}-${pkgver}.tar.bz2"{,.asc})
md5sums=('8cad9f315a7116d98d6640573b2569b0'
         'SKIP')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/ \
              --exec-prefix=/usr \
              --datarootdir=/usr/share
  make
  # Manually adjusts paths in man pages without needing docbook-to-man
  sed --in-place 's!//\(var\|etc\)!/\1!' doc/lwatch.1 doc/lwatch.conf.5
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
