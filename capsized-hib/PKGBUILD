# Maintainer: Sam S. <smls75@gmail.com>

pkgname=capsized-hib
pkgver=0_20130603
_hibver=06032013
pkgrel=1
pkgdesc='A science-fiction action platformer (Humble Bundle version)'
url='http://www.capsizedgame.com/'
arch=('i686' 'x86_64')
license=('custom:commercial')
depends=('libogg' 'openal' 'libtheora' 'libvorbis')
makedepends=('imagemagick')
options=('!strip' '!upx')
PKGEXT='.pkg.tar'
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Manually download it to \"$(pwd)\", or set up a hib:// DLAGENT in /etc/makepkg.conf."; exit 1')

_installer="capsized-${_hibver}-bin"
source=("hib://${_installer}"
        'capsized-hib.desktop')
md5sums=('03f01a81a6cec860e6e114cc49af585b'
         'a6f5bc2ddf20690545cf095d59eec37b')

build() {
  cd $srcdir

  # Remove files for wrong architecture
  [ $CARCH == "i686"   ] && rm -r "data/"{lib64,NePlusUltra.bin.x86_64}
  [ $CARCH == "x86_64" ] && rm -r "data/"{lib,NePlusUltra.bin.x86}

  # Remove bundled libraries (use distro versions instead)
  rm "data/lib"*/{libogg.so.0,libopenal.so.1,libtheora.so.0,libtheoradec.so.1}
  rm "data/lib"*/{libvorbis.so.0,libvorbisfile.so.3}
}

package() {
  cd $srcdir
  _target="/opt/Capsized"

  # Install desktop entry
  install -Dm644 "${pkgname}.desktop" \
                 "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install icon
  convert "data/Capsized.bmp" -resize 256x256 "capsized.png"
  install -Dm644 "capsized.png" "${pkgdir}/usr/share/pixmaps/capsized.png"

  # Install game files
  mkdir -p "${pkgdir}${_target}"
  mv -T "data" "${pkgdir}${_target}"
  
  # Install launch script
  [ $CARCH == "i686" ] && _arch='x86' || _arch='x86_64'
  echo -e "#!/bin/sh\ncd ${_target} && ./NePlusUltra.bin.${_arch}" > "launcher.sh"
  install -Dm755 "launcher.sh" "${pkgdir}/usr/bin/capsized"
}
