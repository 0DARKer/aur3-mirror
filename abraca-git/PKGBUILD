# Maintainer: XZS <d.f.fischer at web dot de>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Harley Laue <losinggeneration@gmail.com>
# Contributer: Zachary W. (hi117) <zacharyw09264@gmail.com>
# Contributer: J. W. Birdsong  <jwbirdsong AT gamil DOT com>

_pkgname=abraca
pkgname=abraca-git
pkgver=0.7.1.103.g6593ed6
pkgrel=1
pkgdesc="A GTK3 client for the XMMS2 music player, with a focus on collections. Written in Vala"
url="http://abraca.xmms.se"
license=('GPL')
arch=('i686' 'x86_64')
depends=('gtk3' 'libgee06' 'xmms2' 'hicolor-icon-theme')
makedepends=('vala>=0.1.7' 'git')
conflicts=('abraca')
provides=('abraca')
install=abraca.install
source=('abraca::git://github.com/Abraca/Abraca.git#branch=master')
md5sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --always | sed -e 's|-|.|g'
}

build() {
  cd "$srcdir/$_pkgname"

  # Pull in subzero
  git submodule update --init --recursive

  LDFLAGS="-lm" ./waf configure --prefix=/usr
  ./waf build
}

package() {
  cd "$srcdir/$_pkgname"

  ./waf  --destdir="$pkgdir" install
# install icons
  mkdir -p "$pkgdir"/usr/share/icons/hicolor/scalable/apps
  mv -f "$pkgdir"/usr/share/icons/{,hicolor/scalable/apps/}abraca.svg
  for _width in '16' '32' '128' '192'; do
    install -Dm644 data/abraca-${_width}.png \
    "$pkgdir"/usr/share/icons/hicolor/${_width}x${_width}/apps/abraca.png
  done
}

