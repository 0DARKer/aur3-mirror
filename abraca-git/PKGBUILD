# Maintainer:  Harley Laue <losinggeneration@gmail.com>
# Contributer: Zachary W. (hi117) <zacharyw09264@gmail.com>
# Contributer: J. W. Birdsong  <jwbirdsong AT gamil DOT com>

pkgname=abraca-git
pkgver=20121109
pkgrel=2
pkgdesc="A GTK3 client for the XMMS2 music player, with a focus on collections.  Written in Vala"
url="http://abraca.xmms.se"
license=("GPL")
arch=('i686' 'x86_64')
depends=('gtk3' 'libgee' 'xmms2' 'hicolor-icon-theme')
makedepends=('vala>=0.1.7' 'git' 'scons')
conflicts=('abraca')
replaces=('abraca')
provides=('abraca')
install=abraca.install
_gitroot='git://github.com/Abraca/Abraca.git'
_gitname='abraca'
source=()
md5sums=()

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # Fix scons to use python2
  sed -i "s|env python|env python2|" scons

  ./scons PREFIX=/usr
}

package() {
  cd "$srcdir/$_gitname-build"

  ./scons  DESTDIR="$pkgdir" install
# install icons
  mkdir -p $pkgdir/usr/share/icons/hicolor/scalable/apps
  mv -f $pkgdir/usr/share/icons/{,hicolor/scalable/apps/}abraca.svg
  cd $srcdir/abraca
  for _width in '16' '32' '128' '192'; do
    install -Dm644 data/abraca-${_width}.png \
    "$pkgdir"/usr/share/icons/hicolor/${_width}x${_width}/apps/abraca.png
  done
}

