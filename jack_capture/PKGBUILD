# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: hm_b <holger@music-nerds.net>
# Contributor: Leslie P. Polzer <polzer@stardawn.org>
# Contributor: Philipp Ãœberbacher <murks at lavabit dot com>

pkgname=jack_capture
pkgver=0.9.66
pkgrel=1
pkgdesc="Simple CLI recording tool for JACK"
arch=('i686' 'x86_64')
url="http://www.notam02.no/arkiv/src/"
license=('GPL')
depends=('jack' 'lame' 'liblo')
optdepends=('meterbridge: levels monitor'
            'xdialog: gtk2 gui')
source=("$url$pkgname-$pkgver.tar.gz")
md5sums=('83d2675b0c393883dc197a6d449a4cf6')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package(){
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX="$pkgdir/usr" install

  # gtk2 gui
  install -Dm755 ${pkgname}_gui \
    "$pkgdir/usr/bin/${pkgname}_gui"

  # doc
  install -Dm644 README \
    "$pkgdir/usr/share/doc/$pkgname/README"
}

