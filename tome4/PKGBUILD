# Maintainer: AlexanderR <alexander r at gmx com>

pkgname=tome4
pkgver='1.0.0beta43'
pkgrel=1
pkgdesc="An open-source, single-player, role-playing roguelike game set in the world of Eyal."
arch=('i686' 'x86_64')
url="http://tome.te4.org/"
license=('custom' 'GPL3')
depends=('mesa' 'glu' 'openal' 'libvorbis'
	'sdl-hg>=6687' 'sdl_ttf-hg>=222' 'sdl_image-hg>=339'
	'ttf-bitstream-vera' 'ttf-droid')
# another common font is ttf-fixedsys-excelsior-linux, but it is in AUR, so let it be
makedepends=('premake4' 'zip')
options=(!makeflags emptydirs)
source=("http://te4.org/dl/t-engine/t-engine4-src-${pkgver}.tar.bz2"
	build.patch
	font.patch)

md5sums=('b0208070c32027f20b0955eeaec531c7'
         'f5fa47440245816e1f133ee38fcb6a50'
         '79a41970d91362d6462b0da27f7e2a8e')

export CFLAGS="-g -march=i486 -O0 -pipe"

build() {
  cd "$srcdir"/t-engine4-src-${pkgver}

  patch -p1 < "${srcdir}/build.patch"
  patch -p1 < "${srcdir}/font.patch"

  sed 's|/opt/SDL-2.0/include/SDL2|/usr/include/SDL2|' -i premake4.lua

  premake4 gmake

  CFLAGS="-I${srcdir}/sdl/include/SDL2" \
  LDFLAGS="-L${srcdir}/sdl/lib" \
  config='release' make
}

package() {
  cat <<EOF > tome4
#!/bin/sh
cd /usr/lib/${pkgname}
./t-engine
EOF

  cd t-engine4-src-${pkgver}

  install -d "${pkgdir}"/usr/share/doc/${pkgname}
  install -m644 CONTRIBUTING CREDITS "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 COPYING-TILES "${pkgdir}/usr/share/licenses/${pkgname}/COPYING-TILES"
  install -Dm755 t-engine "${pkgdir}/usr/lib/${pkgname}/t-engine"
  cp -r bootstrap/ game/ "${pkgdir}"/usr/lib/${pkgname}
  install -Dm755 "${srcdir}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  zip -d "${pkgdir}"/usr/lib/tome4/game/engines/te4-0.9.43.teae \
	data/font/Vera.ttf \
	data/font/VeraBd.ttf \
	data/font/VeraIt.ttf \
	data/font/VeraMono.ttf \
	data/font/VeraMoBd.ttf \
	data/font/DroidSans.ttf \
	data/font/DroidSansMono.ttf \
	data/font/DroidSans-Bold.ttf \
	data/font/DroidSerif.ttf \
	data/font/DroidSerif-Bold.ttf \
	data/font/DroidSerif-Italic.ttf \
	data/font/DroidSerif-BoldItalic.ttf
}
