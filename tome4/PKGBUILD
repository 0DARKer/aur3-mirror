# Maintainer: Harms <thotro at lyse dot net>
# Contributor Pascal Gross√© <pascal.grosse@gmail.com>

pkgname=tome4
pkgver='1.1.3'
pkgrel='1'
pkgdesc="An open-source, single-player, role-playing roguelike game set in the world of Eyal."
arch=('i686' 'x86_64')
url="http://tome.te4.org/"
license=('custom' 'GPL3')
depends=('libgl' 'glu' 'openal' 'libvorbis'
	'sdl2-hg>=7893-1' 'sdl2-ttf-hg>=234-1' 'sdl2-image-hg>=380-1'
	'ttf-bitstream-vera' 'ttf-droid' 'libpng>=1.6' )
# another common font is ttf-fixedsys-excelsior-linux, but it is in AUR, so let it be
makedepends=('premake4' 'zip' 'unzip')
options=(!makeflags emptydirs)
source=("http://te4.org/dl/t-engine/t-engine4-src-${pkgver}.tar.bz2"
	aur-${pkgname}-${pkgver}-${pkgrel}.patch
	tome4.desktop)

md5sums=('2d645af462ca4792752de1a6afaf4f87'
         '69a3d069841e729047937e1783239328'
         '1a0aade275931a5a2216a943b7dbd285')

build() {
    cd "$srcdir"/t-engine4-src-${pkgver}

    patch -p1 < "${srcdir}/aur-${pkgname}-${pkgver}-${pkgrel}.patch"

    premake4 gmake
    make config=release
}

package() {
	# Generate the launcher.
	cat <<EOF > tome4
#!/bin/sh
cd "/opt/${pkgname}"
./t-engine
EOF

# Installing into /opt/${pkgname} because tome4 is a self-contained piece of software.
_inst_dir="${pkgdir}/opt/${pkgname}"

	# Extract and install the icon.
	unzip -oj -qq "${srcdir}/t-engine4-src-${pkgver}/game/engines/te4-${pkgver}.teae" \
		"data/gfx/te4-icon.png" -d "${srcdir}"
	install -Dm644 "${srcdir}/te4-icon.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
	
	# Install the documents.
	install -Dm644 "${srcdir}/t-engine4-src-${pkgver}/CONTRIBUTING" "${pkgdir}/usr/share/doc/${pkgname}/CONTRIBUTING"
	install -Dm644 "${srcdir}/t-engine4-src-${pkgver}/CREDITS" "${pkgdir}/usr/share/doc/${pkgname}/CREDITS"
	
	# Install the custom license into standard location.
	install -Dm644 "${srcdir}/t-engine4-src-${pkgver}/COPYING-TILES" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING-TILES"

	# Install the game executable, and the launcher.
	install -Dm755 "${srcdir}/t-engine4-src-${pkgver}/t-engine" "${_inst_dir}/t-engine"
	install -Dm755 "${srcdir}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
	install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
	
	# Install the game data.
	cp -r "${srcdir}/t-engine4-src-${pkgver}/bootstrap" "${_inst_dir}/"
	cp -r "${srcdir}/t-engine4-src-${pkgver}/game" "${_inst_dir}/"
	
	# Delete built-in fonts from the game.
	zip -d -qq "${_inst_dir}/game/engines/te4-${pkgver}.teae" \
		data/font/Vera.ttf \
		data/font/VeraBd.ttf \
		data/font/VeraIt.ttf \
		data/font/VeraMono.ttf \
		data/font/VeraMoBd.ttf \
		data/font/DroidSans.ttf \
		data/font/DroidSansMono.ttf \
		data/font/DroidSans-Bold.ttf \
		data/font/DroidSerif.ttf \
		data/font/DroidSerif-Bold.ttf \
		data/font/DroidSerif-Italic.ttf \
		data/font/DroidSerif-BoldItalic.ttf
}
