# Maintainer: AlexanderR <alexander r at gmx com>

pkgname=tome4
pkgver='1.0.0beta42'
pkgrel=1
pkgdesc="An open-source, single-player, role-playing roguelike game set in the world of Eyal."
arch=('i686' 'x86_64')
url="http://tome.te4.org/"
license=('custom' 'GPL3')
depends=('mesa' 'sdl-hg' 'sdl_ttf-hg' 'openal' 'libvorbis' 'glu')
makedepends=('premake4')
options=(!makeflags emptydirs)
source=("http://te4.org/dl/t-engine/t-engine4-src-${pkgver}.tar.bz2"
	build.patch)

md5sums=('4b35e53d7856656dc309251bde355a10'
         '5f36eaac5ce550289853eca0cc126d48')

#export CFLAGS="-g -march=i486 -O0 -pipe"

build() {
  _build_lib SDL_image
  

  cd "$srcdir"/t-engine4-src-${pkgver}
  patch -p1 < "${srcdir}/build.patch"

  premake4 gmake

  CFLAGS="-I${srcdir}/sdl/include/SDL2" \
  LDFLAGS="-L${srcdir}/sdl/lib" \
  config='release' make
}

package() {
  cat <<EOF > tome4
#!/bin/sh
cd /usr/lib/${pkgname}
./t-engine
EOF

  cd t-engine4-src-${pkgver}

  install -d "${pkgdir}"/usr/share/doc/${pkgname}
  install -m644 CONTRIBUTING CREDITS "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 COPYING-TILES "${pkgdir}/usr/share/licenses/${pkgname}/COPYING-TILES"
  install -Dm755 t-engine "${pkgdir}/usr/lib/${pkgname}/t-engine"
  cp -r bootstrap/ game/ "${pkgdir}"/usr/lib/${pkgname}
  install -Dm755 "${srcdir}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
}

_build_lib() {
  _hgroot="http://hg.libsdl.org/$1"
  _hgrepo="$1"

  cd "$srcdir"
  msg "Connecting to Mercurial server...."

  [ -d $_hgrepo ] || hg clone -r 330 $_hgroot $_hgrepo

  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."

  cd $_hgrepo
  ./autogen.sh # autoreconf does not work :P

  rm -rf build
  mkdir build && cd build

  ../configure --prefix=/usr --disable-shared --enable-static
  make
  make prefix="$srcdir/sdl" install
}
