# Maintainer: AlexanderR <alexander r at gmx com>
# Contributor Pascal Gross√© <pascal.grosse@gmail.com>

pkgname=tome4
pkgver='1.0.1'
pkgrel=1
pkgdesc="An open-source, single-player, role-playing roguelike game set in the world of Eyal."
arch=('i686' 'x86_64')
url="http://tome.te4.org/"
license=('custom' 'GPL3')
depends=('libgl' 'glu' 'openal' 'libvorbis'
	'sdl-hg>=6687' 'sdl_ttf-hg>=222' 'sdl_image-hg>=339-2'
	'ttf-bitstream-vera' 'ttf-droid')
# another common font is ttf-fixedsys-excelsior-linux, but it is in AUR, so let it be
makedepends=('premake4' 'zip')
options=(!makeflags emptydirs)
source=("http://te4.org/dl/t-engine/t-engine4-src-${pkgver}.tar.bz2"
        tome4build.patch
	ToME_icon.png
	tome4.desktop)

md5sums=('9d6b9c60ea9c766545c454eab89c9671'
         'd1629b66b1983fea16fd983ea57f7c56'
         'bee138535d887efa7f0dbe783dc3bd17'
         '1a0aade275931a5a2216a943b7dbd285')

export CFLAGS="-march=native -O1 -pipe"

build() {
    cd "$srcdir"/t-engine4-src-${pkgver}

    patch -p1 < "${srcdir}/tome4build.patch"

    premake4 gmake
    make
}

package() {
  cat <<EOF > tome4
#!/bin/sh
cd /usr/lib/${pkgname}
./t-engine
EOF

  cd t-engine4-src-${pkgver}

  install -d "${pkgdir}"/usr/share/doc/${pkgname}
  install -Dm644 "$srcdir"/ToME_icon.png "$pkgdir/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 "$srcdir/${pkgname}.desktop" "$pkgdir/usr/share/applications/${pkgname}.desktop"
  install -m644 CONTRIBUTING CREDITS "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 COPYING-TILES "${pkgdir}/usr/share/licenses/${pkgname}/COPYING-TILES"
  install -Dm755 t-engine "${pkgdir}/usr/lib/${pkgname}/t-engine"
  cp -r bootstrap/ game/ "${pkgdir}"/usr/lib/${pkgname}
  install -Dm755 "${srcdir}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  zip -d "${pkgdir}"/usr/lib/tome4/game/engines/te4-${pkgver}.teae \
	data/font/Vera.ttf \
	data/font/VeraBd.ttf \
	data/font/VeraIt.ttf \
	data/font/VeraMono.ttf \
	data/font/VeraMoBd.ttf \
	data/font/DroidSans.ttf \
	data/font/DroidSansMono.ttf \
	data/font/DroidSans-Bold.ttf \
	data/font/DroidSerif.ttf \
	data/font/DroidSerif-Bold.ttf \
	data/font/DroidSerif-Italic.ttf \
	data/font/DroidSerif-BoldItalic.ttf
}
