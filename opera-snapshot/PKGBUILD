# Maintainer: aboutallcz 
pkgname=opera-snapshot # Set to opera if you want to install as your main/stable build
_bigrelease=11.10
_buildver=2085
_rclevel=3
pkgver=${_bigrelease}b${_rclevel}
pkgrel=1
pkgdesc="Internet Browser - Barracuda Release Candidate 3 -  Friday, 8. April - build 2085 - http://my.opera.com/desktopteam/blog/2011/04/08/barracuda-rc3"
url="http://www.opera.com/browser/next/"
depends=('gcc-libs' 'libxt' 'freetype2' 'libxext')
optdepends=('gtk2: GTK integration'
	    'kdebase-runtime: KDE4 integration'
	    'gstreamer0.10-base-plugins: HTML5 open codecs support'
	    'gstreamer0.10-good: HTML5 open codecs support'
	    'gstreamer0.10-ffmpeg: HTML5 not so open codecs support'
	    'gstreamer0.10-bad-plugins: HTML5 not so open codecs support')
install=opera-snapshot.install
options=(!strip !zipman)
license=('custom:opera')
arch=('i686' 'x86_64')
_arch=i386
[ "$CARCH" = "x86_64" ] && _arch=x86_64
source=(http://snapshot.opera.com/unix/4988_11.10-2085/opera-${_bigrelease}-${_buildver}.${_arch}.linux.tar.xz)
md5sums=('5abfd9a97df0e262021a8af29a36c283') 
[ "$CARCH" = "x86_64" ] && md5sums=('4cb02bf5801abdcf2a8f3f9429da7213')

# Uncomment the following line, if you want your User Agent to include Arch Linux.
#_opdistro="Arch Linux"
 
package() {

	# Check if the $pkgname includes a suffix and if so set ${_suffix} for a suffix install.
	[ ${pkgname/opera/} ] && _suffix="--suffix ${pkgname/opera-/}"

	# 'Install' Opera into $pkgdir
	opera-${_bigrelease}-${_buildver}.${_arch}.linux/install --prefix /usr ${_suffix} --repackage "${pkgdir}/usr"
	install -D -m 644 "${pkgdir}/usr/share/${pkgname}/defaults/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"

	# Insert an Arch User Agent string if set
	if [ -n "${_opdistro}" ]
	then
		mkdir -p "${pkgdir}/usr/share/${pkgname}/custom/defaults"
		echo "[ISP]" > "${pkgdir}/usr/share/${pkgname}/custom/defaults/operaprefs.ini"
		echo "Id=${_opdistro}" >> "${pkgdir}/usr/share/${pkgname}/custom/defaults/operaprefs.ini"
		chmod 644 "${pkgdir}/usr/share/${pkgname}/custom/defaults/operaprefs.ini"
	fi

}
