# Maintainer: GERGE
pkgname=opera-snapshot # Set to opera if you want to install as your main/stable build
_bigrelease=11.60
_buildver=1177
_rclevel=1
pkgver=${_bigrelease}.${_buildver}
pkgrel=1
pkgdesc="Development snapshots of Opera Web Browser"
url="http://my.opera.com/desktopteam/blog/"
depends=('gcc-libs' 'libxt' 'freetype2' 'libxext')
optdepends=('flashplugin: Flash support'
	    'openjdk6: Java support'
	    'gstreamer0.10-plugins: HTML5 support')
install=opera-snapshot.install
options=(!strip !zipman)
license=('custom:opera')
arch=('i686' 'x86_64')
_arch=i386
[ "$CARCH" = "x86_64" ] && _arch=x86_64
source=(http://snapshot.opera.com/unix/sneeze_${_bigrelease}-${_buildver}/opera-${_bigrelease}-${_buildver}.${_arch}.linux.tar.xz)
md5sums=('2675f3e365f7dbe6e47e9d77db942d39') 
[ "$CARCH" = "x86_64" ] && md5sums=('783b9b1614f356cd1e493919b93ebbca')

# Comment the following line, if you do not want your User Agent to include Arch Linux.
_opdistro="Arch Linux"
 
package() {

	# Check if the $pkgname includes a suffix and if so set ${_suffix} for a suffix install.
	[ ${pkgname/opera/} ] && _suffix="--suffix ${pkgname/opera-/}"

	# 'Install' Opera into $pkgdir
	opera-${_bigrelease}-${_buildver}.${_arch}.linux/install --prefix /usr ${_suffix} --repackage "${pkgdir}/usr"
	install -D -m 644 "${pkgdir}/usr/share/${pkgname}/defaults/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"

	# Insert an Arch User Agent string if set
	if [ -n "${_opdistro}" ]
	then
		mkdir -p "${pkgdir}/usr/share/${pkgname}/custom/defaults"
		echo "[ISP]" > "${pkgdir}/usr/share/${pkgname}/custom/defaults/operaprefs.ini"
		echo "Id=${_opdistro}" >> "${pkgdir}/usr/share/${pkgname}/custom/defaults/operaprefs.ini"
		chmod 644 "${pkgdir}/usr/share/${pkgname}/custom/defaults/operaprefs.ini"
	fi

}
