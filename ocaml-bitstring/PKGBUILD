# Maintainer: Sergei Lebedev <superbobry@gmail.com>
# Contributor: Pierre Bourdon <delroth@gmail.com>

pkgname=ocaml-bitstring
pkgver=2.0.2
pkgrel=3
pkgdesc="An OCaml library for Erlang-style bit patterns."
arch=('i686' 'x86_64')
license=('GPL2')
url="http://code.google.com/p/bitstring/"
depends=('ocaml')
makedepends=('time' 'ocaml-findlib')
source=("http://bitstring.googlecode.com/files/$pkgname-$pkgver.tar.gz")
md5sums=('0c9fa24a0e5201e8bb61f355b9058fe9')

build() {
    cd "$srcdir/$pkgname-$pkgver"

    ./configure --prefix=/usr
    make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  install -dm 755 "$OCAMLFIND_DESTDIR/stublibs"
  make install
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
