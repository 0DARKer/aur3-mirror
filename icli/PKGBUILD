# Maintainer: Jens Adam <jra@byte.cx>

_perlmod=App-Icli
pkgname=icli
pkgver=0.48
pkgrel=1
pkgdesc='Icinga Command Line Interface'
url='http://finalrewind.org/projects/App-Icli/'
license=('custom:WTFPL')
arch=('any')
depends=('perl-datetime' 'perl-datetime-format-strptime' 'perl-datetime-timezone' 'perl-list-moreutils' 'perl-term-size')
makedepends=('perl-test-compile' 'perl-test-pod')
optdepends=('icinga')
options=('!emptydirs')
source=("http://finalrewind.org/projects/${_perlmod}/${_perlmod}-${pkgver}.tar.gz")
md5sums=('bc8a49aa80784553cd880ecafe059597')

build() {
  cd "${srcdir}"/${_perlmod}-${pkgver}
  perl Build.PL installdirs=vendor destdir="${pkgdir}" \
    --icli-object-file=/var/spool/icinga/objects.cache \
    --icli-status-file=/var/spool/icinga/status.dat    \
    --icli-command-file=/var/spool/icinga/rw/icinga.cmd
  ./Build
}

check() {
  cd "${srcdir}"/${_perlmod}-${pkgver}
  ./Build test
}

package() {
  cd "${srcdir}"/${_perlmod}-${pkgver}
  ./Build install
  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}
