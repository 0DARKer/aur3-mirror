# Maintainer: Estevao Valadao <estevao@archlinux-br.org>
# Contributor: Tetsumaki <http://goo.gl/YMBdA>
# Contributor: Dave Reisner <d@falconindy.com>
# Contributor: Lee.MaRS <leemars@gmail.com>
# Contributor: freedom

pkgname=freetype2-cleartype
pkgver=2.4.6
pkgrel=1
pkgdesc="TrueType font rendering library with cleartype patch"
arch=('i686' 'x86_64')
license=('GPL')
url="http://freetype.sourceforge.net/"
depends=('zlib')
conflicts=('freetype2')
provides=("freetype2=${pkgver}")
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2
        freetype-2.2.1-subpixel-disable-quantization.diff
        bytecode.patch
        freetype-2.3.0-enable-spr.patch
        freetype-2.2.1-enable-valid.patch
        freetype-2.2.1-memcpy-fix.patch)
sha256sums=('24a4a57f3a6859887e91f90f93f754cfc7bf9ab9246a3a696435a0c6a7a1e92a'
            '429d15582de26b760e6ff6b816a5047c60b115c97726fd8ee2e3313a7a677a91'
            'b17e812808845f84f466304c759f7a0e0790e87de3f50beddba78eefa0da9033'
            'cda2efd0c5d13788adf1868a2ea730f725e11972a4f52a391a8ada818b4dd18e'
            '54c83a91b0b2ad7edad7df00a2c26a11ca18431a8e323db9471268a139c46f7e'
            '241643f1086eaa843e0d026840ac479222afdff66a39b093fd67620facbe99a8')

build() {
  cd "${srcdir}/freetype-${pkgver}"
  patch -Np1 -i "${srcdir}/freetype-2.2.1-subpixel-disable-quantization.diff"
  patch -Np0 -i "${srcdir}/bytecode.patch"
  patch -Np1 -i "${srcdir}/freetype-2.3.0-enable-spr.patch"
  patch -Np1 -i "${srcdir}/freetype-2.2.1-enable-valid.patch"
  patch -Np1 -i "${srcdir}/freetype-2.2.1-memcpy-fix.patch"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/freetype-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
