# Maintainer: JKAbrams <JKAbrams at AUR>
# Contributer: Pascal E. <archlinux at hardfalcon dot net>
# Contributor: Giovanni Scafora <giovanni at archlinux dot org>
# Contributor: Daniel Plaza <daniel at plaza.espi dot gmail.com>

pkgname=pcsclite-libudev
pkgver=1.8.8
pkgrel=1
pkgdesc="PC/SC Architecture smartcard middleware library. This version uses udev instead of libusb"
arch=('i686' 'x86_64')
url="https://alioth.debian.org/projects/pcsclite/"
license=('BSD')
depends=('udev')
makedepends=('pkgconfig')
provides=('pcsclite')
conflicts=('pcsclite')
options=('!libtool' '!docs')
source=(https://alioth.debian.org/frs/download.php/3862/pcsc-lite-1.8.8.tar.bz2
        pcscd)
sha256sums=('fe66354a7e738d3ef8b4e572c7e447b85894da9262381fbf004e8abcc12885e7'
         '22eafb28f96af68a081611b6c2c0229fac025fa3dbd43ed4826a69a07cabe78c')

build() {
  cd "${srcdir}/pcsc-lite-${pkgver}"

  ./configure --prefix=/usr \
              --enable-libudev \
              --disable-libusb \
              --sysconfdir=/etc \
              --enable-usbdropdir=/usr/lib/pcsc/drivers

  make
}

package() {
  cd "${srcdir}/pcsc-lite-${pkgver}"

  make DESTDIR=${pkgdir} install

  install -D ${srcdir}/pcsc-lite-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -D -m755 ${srcdir}/pcscd ${pkgdir}/etc/rc.d/pcscd
  install -d ${pkgdir}/usr/lib/pcsc/drivers
}
