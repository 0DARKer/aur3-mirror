# Maintainer: Valere Monseur <valere_monseur@hotmail.com>

pkgname=ytree
pkgver=1.97
pkgrel=1
pkgdesc="A curses-based file manager similar to DOS Xtree(tm)"
arch=('i686' 'x86_64' 'armv6h')
url="http://www.han.de/~werner/ytree.html"
license=('GPL')
depends=('ncurses' 'readline')
backup=('etc/ytree.conf')
source=(http://www.han.de/~werner/$pkgname-$pkgver.tar.gz)
md5sums=('2d5a7388feca60f5955fa9033c46ce47')

build() {
  cd $srcdir/$pkgname-$pkgver

  # disable experimental CLOCK_SUPPORT
  cp Makefile Makefile.old
  grep -v CLOCK_SUPPORT Makefile.old > Makefile

  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  install -d $pkgdir/usr/bin
  install -d $pkgdir/usr/share/man/man1
  install -d $pkgdir/usr/share/man/es/man1

  install -D -m644 $pkgname.conf $pkgdir/etc/$pkgname.conf
  install -D -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING

  make DESTDIR=$pkgdir/usr install
}
