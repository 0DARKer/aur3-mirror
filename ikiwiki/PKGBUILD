# Maintainer: Michael Williams <michael@dinosaur.io>

pkgname=ikiwiki
pkgver=3.20120419
pkgrel=1
pkgdesc="wiki/blog compiler"
arch=(any)
license=('GPL')
url="http://ikiwiki.info"
depends=(perl-yaml perl-text-markdown perl-cgi-session perl-cgi-formbuilder perl-timedate perl-html-parser perl-html-scrubber perl-mail-sendmail perl-time-duration perl-uri perl-html-template perl-locale-gettext perl-rpc-xml perl-yaml-libyaml)
source=("http://ftp.debian.org/debian/pool/main/i/${pkgname}/${pkgname}_${pkgver}.tar.gz")
md5sums=('0ce3494ebe36cce6e42c53844aae8cca')

build() {
  cd "$srcdir/$pkgname"

  # Install module into the vendor directories
  perl Makefile.PL PREFIX="/usr" INSTALLDIRS=vendor
  make PREFIX="/usr"
}

package() {
  cd "$srcdir/$pkgname"

  make install DESTDIR="$pkgdir"

  # otherwise perl breaks
  find "$pkgdir" -name '.packlist' -or -name '*.pod' -exec rm '{}' +

  #RST plugin docutils hack
  sed -i 's/env python/python2/' "$pkgdir"/usr/lib/ikiwiki/plugins/rst
}
