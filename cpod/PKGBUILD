# Maintainer: nmeum <nmeum-aur@8pit.net>

pkgname=cpod
pkgver=1.1
pkgrel=1
pkgdesc="Yet another cron friendly podcatcher."
url="http://github.com/nmeum/${pkgname}"
license=("GPL")
arch=("i686" "x86_64")
makedepends=("go")
source=("https://github.com/nmeum/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=("7efcbe65d14fc6f3e4b0d6504dbbf5806e86f91848031efba23ae4470b432fc7")
_godir="go-src/src/github.com/nmeum"

prepare() {
	mkdir -p "${_godir}"
	ln -snf "../../../../${pkgname}-${pkgver}" "${_godir}/${pkgname}"
}

build() {
	cd "${srcdir}/${_godir}/${pkgname}"
	export GOPATH="${srcdir}/go-src"

	go build -o "${pkgname}"
	pod2man --section=1 --center="${pkgname} Manual" \
		--name="${pkgname}" --release="${pkgname} ${pkgver}" README.pod > "${pkgname}.1"
}

package() {
	cd "${srcdir}/${_godir}/${pkgname}"
	install -Dm755 "${pkgname}"   "${pkgdir}/usr/bin/${pkgname}"
	install -Dm644 "${pkgname}".1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}
