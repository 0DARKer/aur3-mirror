#! /bin/bash
# Maintainer: jorge_barroso <jorge.barroso.11@gmail.com>
# Contributor: Ray Griffin <rorgoroth@googlemail.com>
# Contributor: Jonathan Wiersma <arch aur at jonw dot org>

pkgname=freeciv-sdl
_pkgname=freeciv
pkgver=2.3.4
pkgrel=1
pkgdesc="A multiuser clone of the famous Microprose game of Civilization - SDL Client"
arch=('i686' 'x86_64')
url="http://freeciv.org"
license=('GPL')
depends=('gtk2' 'hicolor-icon-theme' 'sdl_image' 'sdl_mixer')
conflicts=('freeciv')
options=('!libtool')
install=$_pkgname.install
source=(http://downloads.sourceforge.net/sourceforge/$_pkgname/$_pkgname-$pkgver.tar.bz2)
sha512sums=('51d89321cf7b5b08d2338f8d76051ebde745c85e4ebad21b9ff2109696edfdc79ad43d7f8d2bf989400ce5e71f28127835dc4b4e60635fb71ebb3a2fb149269a')

build() {
  cd "$srcdir"/$_pkgname-$pkgver

  ./configure --prefix=/usr --enable-client=sdl --enable-shared --without-ggz-client || return 1

  make || return 1

  sed 's/gtk2/sdl/g' -i client/$_pkgname.desktop || return 1
}

package() {
  cd "$srcdir"/$_pkgname-$pkgver

  make DESTDIR="$pkgdir" install || return 1

  install -m644 client/$_pkgname.desktop "$pkgdir"/usr/share/applications/$_pkgname.desktop
}
