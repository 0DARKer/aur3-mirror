# Maintainer : speps <speps at aur dot archlinux dot org>

_name=libgsf
pkgname=lib32-$_name
pkgver=1.14.26
pkgrel=1
pkgdesc="The GNOME Structured File Library is a utility library for reading and writing structured file formats."
arch=('x86_64')
url="http://www.gnome.org/"
license=('GPL' 'LGPL')
depends=("$_name" 'lib32-glib2' 'lib32-libxml2' 'lib32-bzip2')
makedepends=('gcc-multilib' 'intltool')
source=("http://ftp.gnome.org/pub/GNOME/sources/$_name/1.14/$_name-$pkgver.tar.xz")
options=('!libtool')
md5sums=('3c5a4fbd16a727c36974078e6d0e9575')

build() {
  cd "$srcdir/$_name-$pkgver"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"  

  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --sysconfdir=/etc \
              --mandir=/usr/share/man \
              --localstatedir=/var \
              --enable-static=no \
              --without-bonobo \
              --without-python
  make 
}

package() {
  cd "$srcdir/$_name-$pkgver"
  make DESTDIR="$pkgdir/" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install

  # Removing unneeded files
  rm -rf ${pkgdir}/{usr/{share,bin,include},etc}
}
