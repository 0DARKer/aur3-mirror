# Maintainer : SpepS <dreamspepser at yahoo dot it>

_pkg=libgsf
pkgname=lib32-$_pkg
pkgver=1.14.18
pkgrel=4
pkgdesc="The GNOME Structured File Library is a utility library for reading and writing structured file formats."
arch=('x86_64')
url="http://www.gnome.org/"
license=('GPL' 'LGPL')
depends=("$_pkg" 'lib32-glib2' 'lib32-libxml2' 'lib32-bzip2')
makedepends=('gcc-multilib')
source=("http://ftp.gnome.org/pub/GNOME/sources/${_pkg}/1.14/${_pkg}-${pkgver}.tar.bz2")
options=('!libtool')
md5sums=('2c15d875b1931c40c7e7a2368f3ed948')
sha256sums=('16d29e82f7eba68f919f2588ea5328973490fe6d4a7a5f160e8af89b387c9a95')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"  

  cd "${srcdir}/${_pkg}-${pkgver}"

  ./configure \
	--prefix=/usr \
	--libdir=/usr/lib32 \
	--sysconfdir=/etc \
	--mandir=/usr/share/man \
	--localstatedir=/var \
	--disable-static \
	--without-bonobo \
	--without-python

  make 
}

package() {
  cd "${srcdir}/${_pkg}-${pkgver}"

  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install

  # Removing unneeded files
  rm -rf "${pkgdir}/"{usr/{share,bin,include},etc}
}
