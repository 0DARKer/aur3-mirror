# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=karlyriceditor
pkgver=1.3
pkgrel=1
pkgdesc="A karaoke lyrics editor supporting LRC, LRCv2, Ultrastar, CD+G."
arch=(i686 x86_64)
url="http://www.karlyriceditor.com/"
license=('GPL3')
depends=('qt' 'ffmpeg')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz"
        "$pkgname.desktop")
md5sums=('5679fcbe97d1a8195d8da7136031f199'
         '91d4f8eec987aa889ba53a1b5a001b80')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  qmake 
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # bin
  install -Dm755 bin/$pkgname \
    "$pkgdir/usr/bin/$pkgname"

  # desktop file
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 src/images/application_icon.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
