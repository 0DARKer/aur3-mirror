# Maintainer: Claudio Kozicky <claudiokozicky@gmail.com>
# Contributor: JD Steffen <jd@steffennet.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=smpeg
pkgname=lib32-$_pkgbasename
pkgver=0.4.4
pkgrel=9
pkgdesc="SDL MPEG Player Library (32-bit)"
arch=(x86_64)
url="http://icculus.org/smpeg/"
license=(LGPL)
depends=(lib32-sdl lib32-gcc-libs)
makedepends=(gcc-multilib lib32-mesa lib32-glu)
optdepends=(lib32-gtk: to use gtv  lib32-glu: to use glmovie)
options=(!libtool !makeflags)
source=(http://ftp.su.se/pub/lunar/source-archive/$_pkgbasename-$pkgver.tar.gz
        $_pkgbasename-$pkgver-gcc41.patch)
md5sums=(59c76ac704088ef5539210190c4e1fe3
         8b979a58307d7196655758bd3d2466c4)

prepare()
{
    cd "$srcdir"/$_pkgbasename-$pkgver
    patch -p1 < ../$_pkgbasename-$pkgver-gcc41.patch
}

build()
{
    cd "$srcdir"/$_pkgbasename-$pkgver
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export LIBS=-L/usr/lib32
    export PKG_CONFIG_PATH=/usr/lib32/pkgconfig
    ./configure --prefix=/usr --libdir=/usr/lib32
    make LDFLAGS+=-lstdc++
}

package()
{
    cd "$srcdir"/$_pkgbasename-$pkgver
    make DESTDIR="$pkgdir" install

    #fix aclocal warnings
    sed -i "s#(AM_PATH_SMPEG#([AM_PATH_SMPEG]#" "$pkgdir"/usr/share/aclocal/smpeg.m4

    rm -r "$pkgdir"/usr/{bin,include,share,man}
}
