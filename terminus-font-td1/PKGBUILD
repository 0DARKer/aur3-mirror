# $Id: PKGBUILD 68042 2012-03-17 11:38:36Z giovanni $
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Kristoffer Fossg√•rd <kfs1@online.no>

_pkgname=terminus-font
pkgname=${_pkgname}-td1
pkgver=4.38
pkgrel=1
pkgdesc="A superb, monospace bitmap font (for X11 and console) with td1 patch (centered ascii tilde)"
arch=('any')
url="http://sourceforge.net/projects/terminus-font/"
license=('GPL2' 'custom:OFL')
depends=('xorg-fonts-encodings' 'xorg-fonts-alias' 'xorg-font-utils' 'fontconfig')
conflicts=('terminus-font')
replaces=('xserver-terminus-fonts')
install=terminus-font-td1.install
source=("http://downloads.sourceforge.net/project/${_pkgname}/${_pkgname}-${pkgver}/${_pkgname}-${pkgver}.tar.gz")
md5sums=('a8e792fe6e84c86ed2b6ed3e2a12ba66')

build() { 
  cd "${srcdir}/${_pkgname}-${pkgver}"

  patch < alt/td1.diff
  chmod +x configure

  ./configure --prefix=/usr \
              --x11dir=/usr/share/fonts/local \
              --psfdir=/usr/share/kbd/consolefonts
}
package()
{
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}/OFL.TXT" \
                 "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
