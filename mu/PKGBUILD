# Maintainer: megadriver <megadriver at gmx dot com>
# Contributor: Tom Willemsen <tom at ryuslash dot org>
# Contributor: Uwe <moc liamg skcuneelu <-- look what I did there>
# Contributor: Nicolas Pouillard <nicolas dot pouillard at gmail dot com>

pkgname=mu
pkgver=0.9.9
pkgrel=5
pkgdesc="A collection of utilities for indexing and searching Maildirs"
arch=('i686' 'x86_64')
url="http://www.djcbsoftware.nl/code/mu"
install=$pkgname.install
license=('GPL3')
depends=('xapian-core' 'gmime' 'sqlite3')
# Uncomment the next line if you want mug and msg2pdf
#makedepends=('webkitgtk2')
optdepends=('cronie: Updating index regularly')
source=("http://mu0.googlecode.com/files/mu-$pkgver.tar.gz")
md5sums=('ae8e7d38cdbdd3986b541e7e3f44c2d6')

build() {
    cd $srcdir/mu-$pkgver
    # Uncomment the next line if you want mug and msg2pdf
    #sed -i 's|MUGDIR|"/usr/share/pixmaps"|' toys/mug/mug.c
    sed -e 's|subtitle{|subtitle |' -e 's|version}}|version}|' -i mu4e/mu4e.texi
    # Replace "none" with "gtk2" if you want mug and msg2pdf
    ./configure --prefix=/usr --with-gui=none
    make
}

package() {
  cd $srcdir/mu-$pkgver
  make DESTDIR=$pkgdir install
  install -Dm644 COPYING $pkgdir/usr/share/licenses/mu/COPYING
  # Uncomment the next lines if you want mug and msg2pdf
  #install -m755 toys/msg2pdf/msg2pdf $pkgdir/usr/bin/msg2pdf
  #install -m755 toys/mug/mug $pkgdir/usr/bin/mug
  #install -Dm644 toys/mug/mug.svg $pkgdir/usr/share/pixmaps/mug.svg
}
