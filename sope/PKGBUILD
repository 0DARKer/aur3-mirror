# Maintainer: Steven Hiscocks <steven [at] hiscocks [dot] me [dot] uk>
# Contributor: Andre Wayand <aur-sope@awayand.sleepmail.com>
pkgname=sope
pkgver=2.0.5
pkgrel=1
pkgdesc="application server used by SOGo"
arch=('i686' 'x86_64')
url="http://www.sogo.nu/files/downloads/SOGo/Sources/"
license=('GPL')
depends=('gnustep-base')
makedepends=('gcc-objc')
options=('!strip')
source=(
    "http://www.sogo.nu/files/downloads/SOGo/Sources/SOPE-${pkgver}.tar.gz"
    "sope_configure.patch"
)
md5sums=('94924e33de9f7d0e8781085cccca9cd6'
         '2ebc9e430d1b16d4b607c287d5003d8c')

build() {
  cd "${srcdir}/SOPE"
  patch configure ../sope_configure.patch
  ./configure --prefix="/usr" --disable-strip
  make
}

package() {
  cd "${srcdir}/SOPE"
  make install DESTDIR="${pkgdir}"
}
