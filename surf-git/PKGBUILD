# Maintainer: Army
pkgname=surf-git
_pkgname=surf
pkgver=0.311.e0e4a84
pkgrel=1
pkgdesc="a WebKit based browser"
arch=('i686' 'x86_64')
url="http://surf.suckless.org/"
license=('custom:MIT/X')
depends=('webkitgtk2')
makedepends=('git')
provides=('surf' 'surf-hg')
conflicts=('surf' 'surf-hg')
source=('git+http://git.suckless.org/surf')
md5sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  echo "0.$(git rev-list --count HEAD).$(git describe --always)"
}

prepare() {
  cd "$_pkgname"

  # applying patches
  if ls $SRCDEST/*\.diff > /dev/null;then for i in $SRCDEST/*\.diff;do msg "applying $i"; git apply $i;done;fi

  # custom config
  if [ -e $SRCDEST/config.h ];then msg "using custom config.h";cp $SRCDEST/config.h .;else msg "use default config.h";fi
}

build() {
  cd "$_pkgname"

  make PREFIX=/usr DESTDIR=$pkgdir
}

package() {
  cd "$_pkgname"

  make PREFIX=/usr DESTDIR=$pkgdir install
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
