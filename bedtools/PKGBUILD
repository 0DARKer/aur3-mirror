# Maintainer: Piotr Balwierz
# Maintainer: Christian Krause ("wookietreiber") <kizkizzbangbang@googlemail.com>
pkgname=bedtools
pkgver=2.22.1
pkgrel=2
pkgdesc='powerful toolset for genome arithmetic'
arch=('i686' 'x86_64')
url="https://github.com/arq5x/bedtools2"
license=('GPL2')
depends=('bash' 'zlib')
makedepends=('python2')
source=("https://github.com/arq5x/bedtools2/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('101c16371e0fd0bb88d55c206e1436d8')

build()
{
	cd $srcdir/${pkgname}2
	sed -i 's|python|python2|' "${srcdir}"/${pkgname}2/Makefile
	make
}

check()
{
	cd $srcdir/${pkgname}2
	make test
}

package()
{
	cd $srcdir/${pkgname}2
	for f in bin/* ; do
	install -Dm755 $f $pkgdir/usr/bin/$(basename $f)
	done
}
