# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Sebastian KÃ¶hler <sebkoehler@whoami.org.uk> 

pkgname=perl-sys-info
_pkgname=Sys-Info
pkgver=0.78
pkgrel=1
pkgdesc="Sys::Info - Fetch information from the host system"
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl' 'perl-sys-info-base>=0.7802' 'perl-sys-info-driver-linux>=0.7900' 'perl-test-sys-info>=0.20')
url="http://search.cpan.org/dist/$_pkgname"
source=(http://search.cpan.org/CPAN/authors/id/B/BU/BURAK/$_pkgname-$pkgver.tar.gz)
sha512sums=('b472f02b4cce23fb711a9401fb766bdae4afdb6c1cbd329c9c7f1519ffe7617db0a969a87a1c0d43f20ee425fc83be79450a2a3ec51dcd61d91facd862dd9123')

build() {
  PERL=/usr/bin/perl
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "$srcdir/${_pkgname}-${pkgver}"
  $PERL Makefile.PL
  make
}

check() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  make test
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# vim:set ts=2 sw=2 et:
