# Maintainer: Uro≈° Vampl <mobile.leecher at gmail dot com>

pkgname=fltk-snapshot
pkgver=1.3.0r9671
pkgrel=1
_name=fltk
_ver=1.3.x-r9671
pkgdesc="Graphical user interface toolkit for X; svn snapshot, plus TigerVNC patch"
arch=('i686' 'x86_64')
license=('custom:LGPL')
url="http://www.fltk.org/"
depends=('libjpeg' 'libpng' 'libxft' 'libxinerama' 'libxcursor'
         'hicolor-icon-theme' 'desktop-file-utils' 'xdg-utils')
options=('!docs')
install=fltk.install
conflicts=('fltk')
provides=('fltk')
source=(http://ftp.easysw.com/pub/$_name/snapshots/$_name-$_ver.tar.bz2
        tigervnc.patch)
md5sums=('b91342317c9344d34457fa0b2884daba'
         '20b5efa1b0b123223b9d7c43d169b1a4')

build() {
  cd "$srcdir/$_name-$_ver"
  patch -p1 -i "$srcdir/tigervnc.patch"
  autoconf --force
  ./configure --prefix=/usr --enable-shared
  make
}

package() {
  cd "$srcdir/$_name-$_ver"
  make DESTDIR="$pkgdir" install
  (cd fluid; make DESTDIR="$pkgdir" install install-linux)
  chmod 644 "$pkgdir"/usr/lib/*.a
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
