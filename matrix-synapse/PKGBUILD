# Maintainer: Ivan Shapovalov <intelfx100@gmail.com>

pkgname=matrix-synapse
_pkgver=0.9.1
pkgver="${_pkgver//-/.}"
pkgrel=1
pkgdesc="Matrix reference homeserver"
license=('Apache')
arch=('any')
url="https://github.com/matrix-org/synapse"
depends=('python2-syutil' 'python2-twisted' 'python2-service-identity'
         'python2-pyopenssl' 'python2-yaml' 'python2-pyasn1' 'python2-pynacl'
         'python2-daemonize' 'python2-py-bcrypt' 'python2-frozendict'
		 'python2-pillow' 'python2-pydenticon' 'python2-matrix-angular-sdk')
makedepends=('python2-twisted' 'python2-mock' 'python2-setuptools_trial')
source=("synapse-${_pkgver}.tar.gz::https://github.com/matrix-org/synapse/archive/v${_pkgver}.tar.gz"
		'twisted-15.2.1-compat.patch'
		'sysusers-synapse.conf')
md5sums=('0c2cc92134fe1930eb2e404f77142388'
         '26b5f2fc69d390af40086386dafe1f3b'
         'dfbffdd307c5559357a2ff51a1906700')
backup=('etc/synapse/log_config.yaml')
install='synapse.install'

prepare() {
	cd "synapse-$_pkgver"
	# be compatible with python2-twisted 15 in arch repos
	patch -Np1 -i "$srcdir/twisted-15-compat.patch"
}

build() {
	cd "synapse-$_pkgver"
	python2 setup.py build 
}

package() {
	cd "synapse-$_pkgver"
	python2 setup.py install --root "${pkgdir}" --optimize=1 --skip-build

	for file in "$pkgdir/usr/lib/python2.7/site-packages/synapse"/{app,push}/*.py; do
		sed -re 's|env python$|&2.7|' -i "$file"
	done

	install -Dm644 "contrib/systemd/log_config.yaml" \
		"$pkgdir/etc/synapse/log_config.yaml"

	install -Dm644 "contrib/systemd/synapse.service" \
		"$pkgdir/usr/lib/systemd/system/synapse.service"

	install -Dm644 "$srcdir/sysusers-synapse.conf" \
		"$pkgdir/usr/lib/sysusers.d/synapse.conf"
}

# vim: set ts=4 sw=4 tw=0 ft=sh :
