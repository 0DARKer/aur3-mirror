# Maintainer: grimi <grimi at poczta dot fm>
# Maintainer: David Spicer <azleifel at googlemail dot com>
# Contributor: mightyjaym <jm.ambrosino@free.fr>

pkgname=lib32-gmp
pkgver=5.0.1
pkgrel=2
pkgdesc="A free library for arbitrary precision arithmetic (32-bit)"
arch=('x86_64')
url="http://gmplib.org/"
groups=('lib32')
depends=('gmp' 'lib32-gcc-libs')
makedepends=('gcc-multilib')
license=('LGPL3')
options=(!libtool)
source=(ftp://ftp.gnu.org/gnu/gmp/gmp-${pkgver}.tar.bz2)
md5sums=('6bac6df75c192a13419dfd71d19240a7')

build() {
  cd "${srcdir}"/gmp-${pkgver}
  ./configure ABI='32' CC='gcc -m32' CXX='g++ -m32' \
     PKG_CONFIG_PATH='/usr/lib32/pkgconfig' \
     --prefix=/usr --enable-cxx --libdir=/usr/lib32
  make
  make check
}

package() {
  cd "${srcdir}"/gmp-${pkgver}
  make DESTDIR="${pkgdir}" install
  # install includes , they are diffrent for 32 bit
  install -d "${pkgdir}"/usr/lib32/gmp 
  mv "${pkgdir}"/usr/include "${pkgdir}"/usr/lib32/gmp/include
  rm -rf "${pkgdir}"/usr/share
}


