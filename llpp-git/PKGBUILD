# Maintainer: Michael Witten <mfwitten>
# Contributor: Vladimir Gorbunov <zaza>
# Contributor: Earnest

# The following revision worked for me:
#
#    434799fab275d0aa36f09e2d73f49b3070f697b0

# The various phases of the build can be controlled
# with the following variables (use an empty value
# to mean false; if you're hacking on the source,
# I RECOMMEND skipping the _source and _clean phases
# after getting things set up, and then simply managing
# them by hand):

_source=yes
_clean=yes
_clean_keep_3rd_party_deps=    # Don't remove the `3rdp' directory.
_compile=yes
_install=yes
#_install=          # do not install

######################################################################

pkgname=llpp-git
pkgver=1371.434799f
pkgver()
{
  cd "$srcdir/$_repo"
  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}
pkgrel=1
pkgdesc="Lightweight but fast and featureful PDF viewer based on MuPDF"

arch=(i686 x86_64)
url=http://repo.or.cz/w/llpp.git
license=(GPL)

provides=(llpp)
conflicts=(llpp)

depends=(mesa)
optdepends=('xsel: Text selection')
makedepends=(
  git
  wget
  unzip
  make
  gcc
  ocaml
)

options=('!strip')

_repo=repo

source=(
  llpp.desktop
  ${_source:+"$_repo"::git+git://repo.or.cz/llpp.git}
)
md5sums=(
  9c4891f779eac44569671ee3a9cc0335
  ${_source:+SKIP}
)

build()
{
  #### Clean ####

    if [[ $_clean ]]; then

      msg "Cleaning ..."
      cd "$srcdir/$_repo"
      git clean -fdx ${_clean_keep_3rd_party_deps:+-e /3rdp/}

    fi

  #### Compile ####

    if [[ $_compile ]]; then
    
      msg "Compiling ..."
      cd "$srcdir/$_repo"
      MAKEFLAGS="$MAKEFLAGS -j1" bash buildall.sh

    fi
}

package()
{
  #### Install  ####

    if [[ $_install ]]; then
    
      msg "Installing ..."
      cd "$srcdir"
      install -D llpp.desktop "$pkgdir"/usr/share/applications/llpp.desktop
      cd "$_repo"
      install -D llpp "$pkgdir"/usr/bin/llpp

    fi
}
