# Contributor: holos
# Contributor: Michael Witten <mfwitten>
# Contributor: Vladimir Gorbunov <zaza>
# Contributor: Earnest
# Contributor: Attila Toth <menta>
# Contributor: Flu

pkgname=llpp-git
pkgver=20.r72.g1a709c6
pkgrel=1
pkgdesc='Fast, featureful PDF viewer based on MuPDF.'
arch=('i686' 'x86_64')
url="http://repo.or.cz/w/llpp.git"
license=('custom')
provides=('llpp')
conflicts=('llpp')
install=llpp.install
depends=('desktop-file-utils' 'freetype2' 'glu' 'jbig2dec' 'openjpeg2'
         'libgl' 'libjpeg-turbo' 'libx11')
makedepends=('git' 'mupdf' 'ninja' 'ocaml>=4.02' 'ocaml-lablgl')
optdepends=(
  'xsel: Text selection'
  'file: llppac file type recognition'
  'gzip: llppac gzip archives'
  'xz: llppac xz archives'
  'bzip2: llpac bzip2 archives'
  'djvulibre: llppac djvu conversion'
  'ghostscript: llppac postscript, dvi, and djvu conversion'
  'princexml: llppac html conversion'
  'unoconv: llppac office document conversion'
  'librsvg: llppac svg conversion (preferred)'
  'inkscape: llppac svg conversion (alternative)'
  'imagemagick: llppac image conversion'
)
source=('git://repo.or.cz/llpp.git'
        'openjp2.patch'
        'fix-build.patch'
        'native-code.patch')
sha256sums=('SKIP'
            'ce692ef2cd2cbbbab860ae2fe3d805f09f4b58c79128df8964b98febff5bb2d9'
            '1fc8cec77cd9f9c41f212874c10fd4f15ba1cd87d9cf01c1a3faa42d0a064f67'
            'ff7b21a77ad8416bdf8c87d2e3a2eb1fe085aeaf19ab8c8ba1638eefb29ac6d2')

pkgver() {
  cd llpp
  git describe --tags | sed 's/^v//;s/-/.r/;s/-/./'
}

prepare() {
  cd llpp
  patch -p1 < ../openjp2.patch
  patch -p1 < ../fix-build.patch
  patch -p1 < ../native-code.patch
}

build() {
  cd llpp
  sh configure.sh .
  ninja
  make -C misc/completions
}

package() {
  cd llpp
  install -Dm755 build/llpp "$pkgdir"/usr/bin/llpp
  install -Dm755 misc/llppac "$pkgdir"/usr/bin/llppac
  install -Dm755 misc/gc.py "$pkgdir"/usr/lib/llpp/gc
  install -Dm644 misc/llpp.desktop "$pkgdir"/usr/share/applications/llpp.desktop
  install -Dm644 README "$pkgdir"/usr/share/licenses/llpp/README
  make -C misc/completions DESTDIR="$pkgdir" PREFIX=/usr install
}
