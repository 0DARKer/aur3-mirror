# Maintainer: Michael Witten <mfwitten>
# Contributor: Vladimir Gorbunov <zaza>
# Contributor: Earnest
# Contributor: Attila Toth <menta>
# Contributor: Flu

# The following revision worked for me:
#
#    51b6043066dd0a649d4e734e739cacb55c8b9f2c

# The various phases of the build can be controlled
# with the following variables (use an empty value
# to mean false). If you're hacking on the source,
# I RECOMMEND skipping the _source and _clean phases
# after getting things set up, and then simply managing
# them by hand.

_source=yes
_clean=yes
_clean_keep_3rd_party_deps=    # Don't remove the `3rdp' directory.
_compile=yes
_install=yes
#_install=                     # do not install

######################################################################

pkgname=llpp-git
pkgver=1725.51b6043
pkgver()
{
  cd "$srcdir/$_repo"
  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}
pkgrel=1
pkgdesc="Lightweight but fast and featureful PDF viewer based on MuPDF"

arch=(i686 x86_64)
url=http://repo.or.cz/w/llpp.git
license=(GPL)

provides=(llpp)
conflicts=(llpp)

depends=(glu)

optdepends=(
  # llpp
  'xsel: Text selection'

  # llppac
  'file: required by llppac for automatic file type recognition'
  "gzip: required by llppac for handling of MIME types 'application/x-{gzip,compress}'"
  "xz: required by llppac for handling of MIME type 'application/x-xz'"
  "bzip2: required by llppac for handling of MIME type 'application/x-bzip2'"
  'djvulibre: required by llppac for djvu conversion'
  'ghostscript: required by llppac for ps, dvi, and djvu conversion'
  'princexml: required by llppac for html conversion'
  'unoconv: required by llppac for conversion of office documents'
  'librsvg: required by llppac as preferred option for svg conversion'
  'inkscape: required by llppac as alternative option for svg conversion'
  'imagemagick: required by llppac for image conversion'
)

makedepends=(
  git
  wget
  unzip
  sh
  make
  gcc
  ocaml
)

options=('!strip')

_repo=repo

source=(
  llpp.desktop
  ${_source:+"$_repo"::git+git://repo.or.cz/llpp.git}
)
md5sums=(
  9c4891f779eac44569671ee3a9cc0335
  ${_source:+SKIP}
)

build()
{
  #### Clean ####

    if [[ $_clean ]]; then

      msg "Cleaning ..."
      cd "$srcdir/$_repo"
      git clean -fdx ${_clean_keep_3rd_party_deps:+-e /3rdp/}

    fi

  #### Compile ####

    if [[ $_compile ]]; then
    
      msg "Compiling ..."
      cd "$srcdir/$_repo"
      sh buildall.sh

    fi
}

package()
{
  #### Install  ####

    if [[ $_install ]]; then
    
      msg "Installing ..."
      cd "$srcdir"
      install -D llpp.desktop "$pkgdir"/usr/share/applications/llpp.desktop
      cd "$_repo"
      install -D llpp        "$pkgdir"/usr/bin/llpp
      install    misc/llppac "$pkgdir"/usr/bin/llppac

    fi
}
