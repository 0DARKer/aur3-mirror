# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: flan_suse

# This icon theme was created by the Shimmer Project
# to better integrate into Xfce and match their
# Xfce themes. It is based on the Elementary icons,
# and uses upstream GNOME icons, as well.

pkgname=elementary-xfce-icons
pkgver=0.3
pkgrel=1
pkgdesc='Elementary icons with better Xfce support'
arch=(any)
url=https://github.com/shimmerproject/elementary-xfce
license=(GPL2)
depends=(gnome-icon-theme)
options=(!strip)
conflicts=($pkgname-git)
optdepends=('xfce-theme-albatross: matching Shimmer Project Xfce theme'
    'xfce-theme-bluebird: matching Shimmer Project Xfce theme'
    'xfce-theme-greybird: matching Shimmer Project Xfce theme')
source=($pkgname-$pkgver.tar.gz::https://github.com/shimmerproject/${pkgname%-*}/archive/v$pkgver.tar.gz)
sha256sums=('5169cdd658679547e4f1f5434ed3e49ebce182e1d244c4585bdb56d63a199690')
sha512sums=('ed0ba50afc0abca7b95384664302739ca520ca5e0dc58ac002bb08ed22ec6d2cec8f773c33183d2a9a784b8e657058e18632a9dcf2bb4febfbc4c972fe67292f')

package() {
    install -d "$pkgdir"/usr/share/icons/
    cp -rf ${pkgname%-*}-$pkgver/${pkgname%-*}{,-dark} "$pkgdir"/usr/share/icons/
    install -Dm644 ${pkgname%-*}-$pkgver/README "$pkgdir"/usr/share/doc/$pkgname/README
}
