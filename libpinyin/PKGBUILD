# Maintainer: WU Jun <quark at lihdd dot net>

pkgname=libpinyin
pkgver=0.2.99.20110609
branch=97e8d2f
commit=97e8d2ff85c1d611df507b079c6bca0c339ff00a
pkgrel=1
pkgdesc="Library to deal with pinyin."
arch=('i686' 'x86_64')
url="https://github.com/libpinyin/libpinyin"
license=('GPL')
depends=('db')
makedepends=('automake' 'libtool' 'autoconf')

source=(
  https://github.com/libpinyin/$pkgname/tarball/$commit
)

md5sums=(
  '5eadd5757182dc04e7af277305e84008'
)

build() {
  cd ${srcdir}/libpinyin-${pkgname}-${branch}

  # patch src/Makefile.am, let it generate shared library and install headers
  sed -i 's/noinst_LTLIBRARIES\s*=\s*libpinyin\.la/lib_LTLIBRARIES = libpinyin.la/;s/^.*_LDFLAGS\s*=\s*-static//' src/Makefile.am || return 1 

  (aclocal && libtoolize --force && autoheader && automake -a && autoconf) || return 1
  (./configure --prefix=/usr && make) || return 1
}

package() {
  cd ${srcdir}/libpinyin-${pkgname}-${branch}
  make DESTDIR=${pkgdir} install || return 1

  # manually rm .la files
  rm ${pkgdir}/usr/lib/*.la

  # manually copy headers
  headers=`find src/ -name \*.h`
  header_dest="${pkgdir}/usr/include/libpinyin/"
  install -d $header_dest || return 1

  for i in $headers; do
    install -m 644  $i $header_dest || return 1
  done
}
