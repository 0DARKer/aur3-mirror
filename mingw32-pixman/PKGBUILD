# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=mingw32-pixman
pkgver=0.24.2
pkgrel=1
pkgdesc="Pixman library (mingw32)"
arch=(any)
url="http://xorg.freedesktop.org"
license=('LGPL' 'MPL')
depends=(mingw32-runtime)
makedepends=(mingw32-gcc)
options=(!strip)
source=(http://xorg.freedesktop.org/releases/individual/lib/pixman-${pkgver}.tar.bz2
revert-reject-trapezoids-where-top-is-above-the-edges.patch)
sha1sums=('233eed3cd16ee1b2a842140477503d2c68b74418'
          'ee36251d47ccefd5e276cabdba9917fbb2be787a')

build() {
  cd "${srcdir}/pixman-${pkgver}"
  patch -Np1 -i "${srcdir}/revert-reject-trapezoids-where-top-is-above-the-edges.patch"
  export CFLAGS="-O2 -mms-bitfields"
  export PKG_CONFIG_LIBDIR="/usr/i486-mingw32/lib/pkgconfig/"
  unset LDFLAGS PKG_CONFIG_PATH
  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST \
    --disable-gtk
  make
}

package() {
  cd "${srcdir}/pixman-${pkgver}"
  make DESTDIR="${pkgdir}" install

  cd "$pkgdir"
  find . -name '*.a' -o -name '*.dll' -o -name '*.exe' \
    | xargs -rtl1 i486-mingw32-strip -g
  rm -rf usr/i486-mingw32/share
}
