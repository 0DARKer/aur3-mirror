# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=mingw32-pixman
pkgver=0.22.2
pkgrel=1
arch=(any)
pkgdesc="Pixman library (mingw32)"
url="http://xorg.freedesktop.org"
license=('LGPL' 'MPL')
depends=()
makedepends=(mingw32-gcc)
options=(!strip)
source=(http://xorg.freedesktop.org/releases/individual/lib/pixman-${pkgver}.tar.bz2)
sha1sums=('ad2b828ce4280472f5933d8bb5f0f4d583aed7f3')

build() {
  cd "${srcdir}/pixman-${pkgver}"
  export CFLAGS="-O2 -mms-bitfields"
  export PKG_CONFIG_LIBDIR="/usr/i486-mingw32/lib/pkgconfig/"
  unset LDFLAGS PKG_CONFIG_PATH
  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST \
    --disable-gtk
  make
}

package() {
  cd "${srcdir}/pixman-${pkgver}"
  make DESTDIR="${pkgdir}" install

  cd "$pkgdir"
  find . -name '*.a' -o -name '*.dll' -o -name '*.exe' \
    | xargs -rtl1 i486-mingw32-strip -g
  rm -rf usr/i486-mingw32/share
}
