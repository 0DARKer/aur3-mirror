# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=mingw32-pixman
pkgver=0.18.4
pkgrel=1
arch=(i686 x86_64)
pkgdesc="Pixman library (mingw32)"
depends=()
makedepends=(mingw32-gcc)
options=(!strip)
license=('LGPL' 'MPL')
url="http://xorg.freedesktop.org"
source=(http://xorg.freedesktop.org/releases/individual/lib/pixman-${pkgver}.tar.bz2)
sha1sums=('011b00fb33d6e5021a680bcc74f600f44d709d8d')

build()
{
  cd "$srcdir/pixman-$pkgver"
  export CFLAGS="-O2 -mms-bitfields"
  export PKG_CONFIG_PATH="/usr/i486-mingw32/lib/pkgconfig/"
  unset LDFLAGS
  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST \
    --disable-gtk
  make
}

package() {
  cd "$srcdir/pixman-$pkgver"
  make install DESTDIR="$pkgdir"

  cd "$pkgdir"
  find . -name '*.a' -o -name '*.dll' -o -name '*.exe' \
    | xargs -rtl1 i486-mingw32-strip -g
  rm -rf usr/i486-mingw32/share
}
