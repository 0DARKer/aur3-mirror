# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=librsvg
pkgname=lib32-$_pkg
pkgver=2.32.1
pkgrel=1
pkgdesc="SAX-based renderer for SVG files into a GdkPixbuf"
arch=('x86_64')
url="http://librsvg.sourceforge.net/"
license=('LGPL')
depends=("$_pkg" 'lib32-gtk2' 'lib32-libcroco')
makedepends=('gcc-multilib' 'lib32-libgsf')
options=('!libtool' '!emptydirs')
source=("http://ftp.gnome.org/pub/gnome/sources/${_pkg}/2.32/${_pkg}-${pkgver}.tar.bz2")
md5sums=('4b00d0fee130c936644892c152f42db7')

build() {
  cd "${srcdir}/${_pkg}-${pkgver}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure \
	--prefix=/usr \
	--libdir=/usr/lib32 \
	--with-croco \
	--disable-static \
	--with-svgz \
	--enable-gtk-theme
  make
}

package() {
  cd "${srcdir}/${_pkg}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  rm -fr "${pkgdir}"/{usr/{bin,include,share},etc}
}
