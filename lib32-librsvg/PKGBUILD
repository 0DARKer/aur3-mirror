# Maintainer: speps <speps at aur dot archlinux dot org>

_name=librsvg
pkgname=lib32-$_name
pkgver=2.36.4
pkgrel=1
pkgdesc="SAX-based renderer for SVG files into a GdkPixbuf"
arch=('x86_64')
url="http://librsvg.sourceforge.net/"
license=('LGPL')
depends=("$_name" 'lib32-gtk2' 'lib32-libcroco')
makedepends=('gcc-multilib' 'lib32-libgsf' 'lib32-gtk3')
options=('!libtool' '!emptydirs')
source=("http://ftp.gnome.org/pub/gnome/sources/$_name/${pkgver%.*}/$_name-$pkgver.tar.xz")
md5sums=('3c94524c8ccf668e30b236f409239f54')

build() {
  cd "$srcdir/$_name-$pkgver"

  # lib32 flags
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --enable-static=no \
              --with-croco \
              --with-svgz \
              --enable-introspection=no
  make
}

package() {
  cd "$srcdir/$_name-$pkgver"
  make DESTDIR="$pkgdir/" install

  # remove unneeded stuff
  rm -fr ${pkgdir}/{usr/{bin,include,share},etc}
}
