# Maintainer: SpepS <dreamspepser at yahoo dot it>

_name=librsvg
pkgname=lib32-$_name
pkgver=2.34.2
pkgrel=1
pkgdesc="SAX-based renderer for SVG files into a GdkPixbuf"
arch=('x86_64')
url="http://librsvg.sourceforge.net/"
license=('LGPL')
depends=("$_name" 'lib32-gtk2' 'lib32-libcroco')
makedepends=('gcc-multilib' 'lib32-libgsf' 'lib32-gtk3')
options=('!libtool' '!emptydirs')
source=("http://ftp.gnome.org/pub/gnome/sources/$_name/${pkgver%.*}/$_name-$pkgver.tar.bz2")
md5sums=('4a7eda78019cb89d4e7ae7c841480399')

build() {
  cd "$srcdir/$_name-$pkgver"

  # lib32 flags
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --enable-static=no \
              --with-croco \
              --with-svgz
  make
}

package() {
  cd "$srcdir/$_name-$pkgver"

  make DESTDIR="$pkgdir/" install

  # remove unneeded stuff
  rm -fr ${pkgdir}/{usr/{bin,include,share},etc}
}
