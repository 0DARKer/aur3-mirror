# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=librsvg
pkgname=lib32-$_pkg
pkgver=2.34.0
pkgrel=1
pkgdesc="SAX-based renderer for SVG files into a GdkPixbuf"
arch=('x86_64')
url="http://librsvg.sourceforge.net/"
license=('LGPL')
depends=("$_pkg" 'lib32-gtk2' 'lib32-libcroco')
makedepends=('gcc-multilib' 'lib32-libgsf' 'lib32-gtk3')
options=('!libtool' '!emptydirs')
source=("http://ftp.gnome.org/pub/gnome/sources/${_pkg}/${pkgver%.*}/${_pkg}-${pkgver}.tar.bz2")
md5sums=('5eb2fcd0765aa5e47a4999985e3c27ab')

build() {
  cd "$srcdir/$_pkg-$pkgver"

  # lib32 flags
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  # python2 fix
  sed -i "s|\(env python\).*|\12|" rsvg.in

  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --with-croco \
              --disable-static \
              --with-svgz \
              --enable-gtk-theme
  make
}

package() {
  cd "$srcdir/$_pkg-$pkgver"

  make DESTDIR="$pkgdir/" install

  # remove unneeded stuff
  rm -fr "$pkgdir"/{usr/{bin,include,share},etc}
}
