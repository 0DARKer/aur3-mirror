# Maintainer: Mark Foxwell <fastfret79@archlinux.org.uk>
# Contributor: Nicolas Martyanoff <khaelin@gmail.com> 

pkgname=tokyocabinet
pkgver=1.4.47
pkgrel=1
pkgdesc="a modern implementation of DBM"
arch=('i686' 'x86_64')
url="http://1978th.net/tokyocabinet"
license=('LGPL')
makedepends=('gcc>=3.1' 'make' 'pkgconfig')
depends=('zlib' 'bzip2')
source=("http://1978th.net/tokyocabinet/${pkgname}-${pkgver}.tar.gz")
md5sums=('3d94fe2aebf5d9ff0478ed895bc46fc9')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --enable-off64 --enable-fastest
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
