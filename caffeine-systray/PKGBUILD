# Maintainer: Shimi Chen <shimi.chen@gmail.com>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: LookTJ/Taylor Lookabaugh <jesus.christ.i.love@gmail.com>
# Contributor: alessandro_ufms
# Contributer: Bluthund <bluthund23@gmail.com>

pkgname=caffeine-systray
pkgver=2.4.1.498.7c8910c
pkgrel=3
epoch=1
pkgdesc="This is the latest version still supporting the traditional systray rather than the Unity appindicator, and lacking any bugfixes since the drop of support. Hopefully a proper fork will be set up soon."
arch=(any)
url=https://launchpad.net/caffeine
license=(GPL3)
depends=(dconf gtk2 hicolor-icon-theme kaa-metadata python2-dbus python2-gobject
    python2-notify python2-xdg python2-xlib)
conflicts=(caffeine-bzr)
makedepends=(bzr)
options=(!emptydirs !libtool)
install=$pkgname.install
source=($pkgname::bzr+http://bazaar.launchpad.net/~caffeine-developers/${pkgname%-*}/main/
    $pkgname-signal.patch
    $pkgname-py2fix.patch)
sha256sums=('SKIP'
    '32c5c65ce98f0e62b37482e999b8fffe8915ffa870bf8f7a4e7adf6bfa0c73d3'
    '79bc0e23100d3345f9f1e59cc4739a13c4020efbd7ea3996a7bbba3d9668da20')
sha512sums=('SKIP'
    'c8d32008f4bd605967f0736167f3a966c47db127dd54e93862cd22248c0fef472f566f86514978ddc7d9738641550e0618e06d560496b13a4665b3a7bacb731b'
    '3c89853c0fd5798dbd0afb9c2df884fe7992a1606157cfba62d00c1f199e3600fdab27b43a916c4464a9475e7caada64759a39d2e060dcd3253163907cf92f89')

pkgver() {
    _commit=498

    cd $pkgname/
    bzr revert -r$_commit
    echo $(cat VERSION).$_commit.$(bzr testament -r$_commit | awk '/sha1/ { print $2 }' | head -c7)
}

prepare() {
    cd $pkgname/
    find -name '*.py' -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
    chmod +x setup.py
    patch -p1 -i ../$pkgname-signal.patch
    patch -p1 -i ../$pkgname-py2fix.patch
}

package() {
    cd $pkgname/
    ./setup.py install --root="$pkgdir"
}
