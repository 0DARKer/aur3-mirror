# Maintainer: David Spicer <azleifel at gmail dot com>
# Contributor: Mathias Fussenegger <dasjordi@gmail.com>

pkgname=vdr-streamdev
_pluginname=streamdev
pkgver=0.6.1
pkgrel=1
pkgdesc="VDR server and client plugins that implement the Video Transfer Protocol (VTP)"
arch=('i686' 'x86_64')
url="http://projects.vdr-developer.org/projects/plg-streamdev"
license=('GPL2')
depends=('vdr>=1.7.42')
backup=('var/lib/vdr/plugins/streamdev-server/streamdevhosts.conf')
install=$pkgname.install
source=(http://projects.vdr-developer.org/attachments/download/1580/$pkgname-$pkgver.tgz)
md5sums=('e611df2dd9fc75b21e068a8c0aabcc68')

build() {
  cd "$srcdir/$_pluginname-$pkgver"

  make all
}

package() {
  cd "$srcdir/$_pluginname-$pkgver"

  # VDR directory environment
  _vdrCONFDIR="/var/lib/vdr"
  _vdrINCDIR="/usr/include"
  _vdrLIBDIR="/usr/lib/vdr"
  _vdrLOCDIR="/usr/share/locale"

  make DESTDIR="$pkgdir/" install

  # Install configuration etc. files
  install -d -m755 "$pkgdir/$_vdrCONFDIR/plugins/$_pluginname-server"
  install -m755 $_pluginname-server/externremux.sh "$pkgdir/$_vdrCONFDIR/plugins/$_pluginname-server"
  install -m644 $_pluginname-server/streamdevhosts.conf "$pkgdir/$_vdrCONFDIR/plugins/$_pluginname-server"

  # Install documents
  install -d -m755 "$pkgdir/usr/share/doc/$pkgname"
  for _file in CONTRIBUTORS HISTORY PROTOCOL README; do
    install -m644 $_file "$pkgdir/usr/share/doc/$pkgname"
  done
}
