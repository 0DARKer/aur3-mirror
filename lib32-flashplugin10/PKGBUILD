# $Id$

pkgname=lib32-flashplugin10
_licensefile='Reader_Player_AIR_WWEULA-Combined-20080204_1313.pdf'
pkgver=10.3.183.15
pkgrel=1
pkgdesc='Adobe Flash Player v10 (32-bit) - multilib version for x86_64 with working HW accelerated scaling'
url='http://kb2.adobe.com/cps/142/tn_14266.html'
arch=('x86_64')
makedepends=('unzip')
depends=('mozilla-common' 'lib32-libxt' 'lib32-gtk2' 'lib32-nss' 'lib32-curl' 'lib32-alsa-plugins' 'nspluginwrapper' 'hicolor-icon-theme')
provides=('flashplayer' 'flashplugin')
optdepends=('lib32-libvdpau: GPU acceleration on Nvidia card')
license=('custom')
install=flashplugin.install
backup=(etc/adobe/mms.cfg)
source=('http://fpdownload.macromedia.com/get/flashplayer/installers/archive/fp_10.3.183.15_archive.zip'
        "http://www.adobe.com/products/eulas/pdfs/${_licensefile}"
        mms.cfg)
noextract=("fp_10.3.183.15_archive.zip")
md5sums=('0d24891c28a25ba7d664f1a870739e58'
         '1636037610ee2aa35c5fb736a697b7e0'
         '60cc9835163b56ab8dddbb50ee293967')
options=(!strip)

package() {
    cd ${srcdir}
    unzip fp_10.3.183.15_archive.zip -x 'fp_10.3.183.15_archive/10_3r183_15_debug/*' \
					'fp_10.3.183.15_archive/10_3r183_15/flashplayer10_3r183_15_mac*' \
					'fp_10.3.183.15_archive/10_3r183_15/flashplayer10_3r183_15_win*' \
					'fp_10.3.183.15_archive/10_3r183_15/uninstall*' \
					'fp_10.3.183.15_archive/10_3r183_15/flashplayer10_3r183_15_linux_sa*'
    tar -xnf ${srcdir}/fp_10.3.183.15_archive/10_3r183_15/flashplayer10_3r183_15_linux.tar.gz 
    install -Dm755 libflashplayer.so "${pkgdir}/usr/lib32/mozilla/plugins/libflashplayer.so"
    install -Dm755 usr/bin/flash-player-properties "${pkgdir}/usr/bin/flash-player-properties"
    for i in 16x16 22x22 24x24 32x32 48x48; do
        install -Dm644 usr/share/icons/hicolor/${i}/apps/flash-player-properties.png \
            "${pkgdir}/usr/share/icons/hicolor/${i}/apps/flash-player-properties.png"
    done
    install -Dm644 usr/share/applications/flash-player-properties.desktop "${pkgdir}/usr/share/applications/flash-player-properties.desktop"
    install -Dm644 "${_licensefile}" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.pdf"
    install -Dm644 ${srcdir}/mms.cfg "${pkgdir}/etc/adobe/mms.cfg"
    # Install npwrapper:
    mkdir -p "${pkgdir}"/usr/lib/mozilla/plugins/
    touch "${pkgdir}"/usr/lib/mozilla/plugins/npwrapper.libflashplayer.so
    chmod 755 "${pkgdir}"/usr/lib/mozilla/plugins/npwrapper.libflashplayer.so
}
