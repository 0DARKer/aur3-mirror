# Contributer: N30N <archlinux@alunamation.com>

pkgname="collada-dom"
pkgver=2.3.1
pkgrel=1
pkgdesc="An API that provides a C++ object representation of a COLLADA XML instance document."
url="http://collada-dom.sourceforge.net/"
license=("custom")
arch=("i686" "x86_64")
depends=("libxml2" "boost" "pcre")
makedepends=("cmake")
source=("http://downloads.sf.net/${pkgname}/collada_dom-${pkgver}-src.tgz")
md5sums=("a74d19c1187806a713cec90c2f0f692c")

build() {
	[ -d build ] && rm -rf build
	mkdir build
	cd build
	cmake \
		-DCMAKE_INSTALL_PREFIX=/usr \
		"../${pkgname}-${pkgver}"
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install

	install -D -m644 ../${pkgname}-${pkgver}/License_Folder/license_e.txt \
		"${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: set noet ff=unix:
