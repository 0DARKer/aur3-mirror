# Contributor: Diego Principe <cdprincipe@at@gmail@dot@com>
# Contributor: Flamelab <panosfilip@gmail.com

pkgname=gnome-shell-git
_realname=gnome-shell
pkgver=3.9.3.2.g580bd67
pkgrel=1
pkgdesc="The next generation GNOME Shell. Experimental, GNOME 3.0 version."
arch=('i686' 'x86_64')
url="http://live.gnome.org/GnomeShell"
license=('GPL2')

depends=('mutter' 'gconf' 'dconf' 'gjs' 'gnome-menus' 'gnome-desktop' 'libcroco' 'libcanberra' 'libpulse' 'telepathy-glib-newest' 'polkit-gnome' 'gobject-introspection' 'evolution-data-server' 'gtk3' 'gnome-bluetooth' 'gstreamer0.10')
makedepends=('intltool' 'gnome-doc-utils' 'git' 'gnome-common-git')

provides=("${_realname}")
conflicts=("${_realname}")

options=(!libtool !emptydirs)
install=gnome-shell-git.install

source=('git://git.gnome.org/gnome-shell')
md5sums=('SKIP')

pkgver() {
    cd ${srcdir}/${_realname}
    git describe --always | sed 's|-|.|g'
}

build() {
    cd ${srcdir}/${_realname}
    ./autogen.sh \
      --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gnome-shell \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile
    make
}

package() {
   cd ${srcdir}/${_realname}
   make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

   sed -i s'|#!/usr/bin/python|#!/usr/bin/python2|'g ${pkgdir}/usr/bin/gnome-shell-extension-tool

   install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
   gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-shell ${pkgdir}/etc/gconf/schemas/*.schemas
   rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}

