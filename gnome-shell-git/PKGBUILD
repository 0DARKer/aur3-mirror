# Contributor: Diego Principe <cdprincipe@at@gmail@dot@com>
# Contributor: Flamelab <panosfilip@gmail.com>
# Contributor: rectec <rectec[at]mail[dot]com>
# Maintainer: Yosef Or Boczko <yoseforb@gmail.com>

_pkgname=gnome-shell
pkgname=$_pkgname-git
pkgver=3.11.2.58.g090af35
pkgrel=1
pkgdesc="The next generation GNOME Shell"
arch=(i686 x86_64)
url="http://live.gnome.org/GnomeShell"
license=(GPL2)
depends=("mutter>=3.11.1" accountsservice caribou evolution-data-server gcr "gjs>=1.39.0" gnome-bluetooth
         gnome-session gnome-settings-daemon gnome-themes-standard gsettings-desktop-schemas
         libcanberra-pulse libcroco libgdm libsecret network-manager-applet
         telepathy-logger telepathy-mission-control unzip systemd)
makedepends=(git intltool gtk-doc gnome-control-center)
optdepends=('network-manager-applet: shell integration for networkmanager')
options=('!libtool' '!emptydirs')
install=gnome-shell.install
groups=(gnome)
conflicts=('gnome-shell')
replaces=('gnome-shell')
provides=("gnome-shell=3.11.2")
source=('git://git.gnome.org/gnome-shell'
        'nm-libexecdir.patch')
sha256sums=('SKIP'
            'e5bb10ad2e5c3e0fde3d05babd1bfdda701e553e02d493f7e54cb7832ce7e607')

prepare() {
  cd "$srcdir/$_pkgname"

  # FS#30747 FS#32730 Problems due to libexecdir different from NM
  patch -Np1 -i "$srcdir/nm-libexecdir.patch"

  # https://bugzilla.gnome.org/show_bug.cgi?id=705779
  # patch -Np1 -i "$srcdir/Make-the-search-entry-behave-better-in-RTL-locales.patch"
}

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "$srcdir/$_pkgname"
  PYTHON=/usr/bin/python2 ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gnome-shell \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile  --enable-systemd \
      --enable-gtk-doc

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="${pkgdir}" install
}
