pkgname=mingw-w64-allegro-static
pkgver=5.0.9
pkgrel=2
pkgdesc="Portable library mainly aimed at video game and multimedia programming (mingw-w64, static)"
arch=(any)
url="http://alleg.sourceforge.net"
license=("custom")
makedepends=(mingw-w64-gcc cmake)
depends=(mingw-w64-crt
mingw-w64-freetype
mingw-w64-physfs
mingw-w64-libvorbis
mingw-w64-flac
mingw-w64-libpng
mingw-w64-openal
mingw-w64-dumb
mingw-w64-libjpeg-turbo)
options=(!libtool !strip !buildflags)
conflicts=(mingw-w64-allegro)
provides=(mingw-w64-allegro)
source=("http://downloads.sourceforge.net/alleg/allegro-$pkgver.tar.gz"
"Toolchain-i686-w64-mingw32.cmake"
"Toolchain-x86_64-w64-mingw32.cmake")
md5sums=('59fb41dccc300be0044cfad5fff0ca81'
         'da10ad00a161800e0b011a77c3efbf76'
         'adea9ccdeea1784c7cd30af195f2a8cb')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "$srcdir/allegro-$pkgver"
  sed -i "s,DS3DALG_DEFAULT,GUID_NULL,g" "addons/audio/dsound.cpp"
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir "build-${_arch}" && pushd "build-${_arch}"
    PATH="/usr/${_arch}/bin:$PATH" cmake \
      -DCMAKE_INSTALL_PREFIX:PATH=/usr/${_arch} \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_TOOLCHAIN_FILE=${srcdir}/Toolchain-${_arch}.cmake \
      -DDUMB_LIBRARY=/usr/${_arch}/lib/libdumb.a \
      -DWANT_DEMO=OFF \
      -DWANT_DOCS=OFF \
      -DWANT_EXAMPLES=OFF \
      -DFLAC_STATIC=ON \
      -DFREETYPE_LIBRARY=/usr/${_arch}/lib/libfreetype.a \
      -DFLAC_LIBRARY=/usr/${_arch}/lib/libFLAC.a \
      -DOGG_LIBRARY=/usr/${_arch}/lib/libogg.a \
      -DOPENAL_LIBRARY=/usr/${_arch}/lib/libOpenAL32.dll.a \
      -DPHYSFS_LIBRARY=/usr/${_arch}/lib/libphysfs.a \
      -DSHARED=OFF \
      -DVORBISFILE_LIBRARY=/usr/${_arch}/lib/libvorbisfile.a \
      -DVORBIS_LIBRARY=/usr/${_arch}/lib/libvorbis.a \
      -DZLIB_LIBRARY=/usr/${_arch}/lib/libz.a \
      ..
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/allegro-$pkgver/build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}