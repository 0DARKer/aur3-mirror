# Maintainer: Zauber Paracelsus <zauber@gridmail.org>
# PKGBUILD was modified from firestorm-bin package created by chungy
# Fix for crash on file dialogs caused by libpng pulled from secondlife-bin
pkgname=inworldz-bin
pkgver=2.0.1.31257
_pngver=1.5.1
pkgrel=2
pkgdesc="The official viewer for InWorldz, an online virtual world."
url="https://inworldz.com/"
license=('GPL')
depends=('openal' 'gnome-vfs' 'gtk2' 'libpng' 'dbus-glib' 'libidn' 'sdl' 'mesa')
optdepends=('gstreamer0.10: For video support - may need good, bad and ugly plugins')
[ "$CARCH" = "x86_64" ] && depends=('lib32-gtk2' 'lib32-libpng' 'lib32-openal' 'lib32-dbus-glib' 'lib32-libidn' 'lib32-sdl' 'lib32-mesa')
[ "$CARCH" = "x86_64" ] && optdepends=('lib32-nvidia-utils: GL support for NVIDIA drivers')
arch=('i686' 'x86_64')
install=inworldz.install
source=("http://inworldz.com/Imprudence/test-builds/InWorldz_Beta_i686_${pkgver}.tar.bz2"
"http://downloads.sourceforge.net/sourceforge/libpng/libpng-${_pngver}.tar.xz"
 'inworldz.install' 'inworldz.desktop' 'inworldz.launcher')
sha256sums=('dccc8904b03b8922d19d9197d44fa37ffa1b9ac324465181d70efd178be39321'
            'd230837ad959b395bf7e2937e0136e6037208a29cda83000bf8b88a3692e8c69'
            'cf9c786716dc2fb7043671f8edd81e047e2ca02dd10129213c6d57abbfdfbab1'
            'f2155704ba49c961a64d693523306adf0e2f157fadb6155fac57c8d7d83c6428'
            '76bd0b992ee1110f93fafd115dcb6c3d447bc0855a34b2daf742c0d975bafca4')

build() {
  # build compatible lib32-libpng
  msg "Building libpng..."

  if [ "$CARCH" = "x86_64" ] ; then
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  fi

  cd "${srcdir}/libpng-${_pngver}"

  ./configure
  make
}

package() {
    cd "${srcdir}"

    # Rename Data Directory
    mv InWorldz_Beta_i686_${pkgver}/ inworldz

    # Fix fontconfig >= 2.9.0 issue
    #ln -si libfontconfig.so.1.4.4 firestorm/lib/libfontconfig.so.1

    # Install Desktop File
    install -D -m644 "${srcdir}"/inworldz.desktop \
        "${pkgdir}"/usr/share/applications/inworldz.desktop

    # Install Icon File
    install -D -m644 "${srcdir}"/inworldz/inworldz_icon.png \
        "${pkgdir}"/usr/share/pixmaps/inworldz_icon.png

    # Install Launcher
    install -D -m755 "${srcdir}"/inworldz.launcher \
        "${pkgdir}"/usr/bin/inworldz

    # Move Data to Destination Directory
    install -d "${pkgdir}"/opt/
    mv inworldz/ "${pkgdir}"/opt/

    # Change Permissions of files to root:games
    chown -R root:games "${pkgdir}"/opt/inworldz
    chmod -R g+r "${pkgdir}"/opt/inworldz

    # Make Binary Group-Executable
    chmod g+x "${pkgdir}"/opt/inworldz/inworldz
    
    # Install libpng we built
	install -D -m755 $srcdir/libpng-${_pngver}/.libs/libpng15.so.15.1.0 \
    $pkgdir/opt/inworldz/lib
    
    cd $pkgdir/opt/inworldz/lib
	ln -s libpng15.so.15.1.0 libpng15.so.15
	ln -s libpng15.so.15.1.0 libpng15.so
}
