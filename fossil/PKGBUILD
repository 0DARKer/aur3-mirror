#Maintainer: Konstantin Plotnikov <kostyapl at gmail dot com>
pkgname=fossil
pkgver=1.23
pkgrel=2
_date_pkgver=20120808112557
pkgdesc="Simple, high-reliability, distributed software configuration management"
arch=('i686' 'x86_64')
license=('BSD')
url="http://www.fossil-scm.org/"
depends=('openssl')

source=(
    "fossil-src-${_date_pkgver}.tar.gz"::"http://www.fossil-scm.org/download/fossil-src-${_date_pkgver}.tar.gz"
    "fossil-xinetd"
)

md5sums=('023f2d45b2435a854f8b9190d1aee05b'
         '62f6fb3739191b352c3e2995ced9be4e')


package() {
    cd fossil-src-${_date_pkgver}
    
    ./configure --prefix=/usr
    
    # headers and translate targets are problematic with parallel jobs
    make -j1 bld bld/headers

    # build fossil
    make

    # install fossil
    install -Dm755 ${pkgname} \
        ${pkgdir}/usr/bin/${pkgname}

    # install license
    install -Dm644 COPYRIGHT-BSD2.txt \
        ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

    # install xinetd script
    install -Dm644 $srcdir/fossil-xinetd $pkgdir/etc/xinetd.d/fossil

    # create symlink "/usr/bin/fsl" pointing to "/usr/bin/fossil"
    cd ${pkgdir}/usr/bin && ln -s ${pkgname} fsl
} 
