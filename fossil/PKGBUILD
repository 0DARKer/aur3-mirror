#Maintainer: Nathan Owe <ndowens.aur at gmail dot com>

pkgname="fossil"
pkgver=20110113195528947
pkgrel=1
pkgdesc="Simple, high-reliability, distributed software configuration management"
arch=('i686' 'x86_64')
license=('BSD')
url="http://www.fossil-scm.org/"
depends=('openssl')
source=("http://www.fossil-scm.org/download/fossil-src-$pkgver.tar.gz")
md5sums=('0525a7adefdbec9a92fe7fea38242956')

build() {
	cd ${pkgname}-src-${pkgver}

	# headers and translate targets are problematic with parallel jobs
	make -j1 bld bld/headers
	make
}

package() {
	cd ${pkgname}-src-${pkgver}

	install -Dm755 ${pkgname} \
		${pkgdir}/usr/bin/${pkgname}

	install -Dm644 COPYRIGHT-BSD2.txt \
		${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
} 
