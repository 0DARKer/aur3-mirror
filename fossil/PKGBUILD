# Maintainer: Daniel YC Lin <dlin.tw at gmail>
# Contributor : Konstantin Plotnikov <kostyapl at gmail dot com>
pkgname=fossil
pkgver=1.25
pkgrel=2
_date_pkgver=20130216000435
pkgdesc="Simple, high-reliability, distributed software configuration management"
arch=('i686' 'x86_64' 'sh4')
license=('BSD')
url="http://www.fossil-scm.org"
depends=('openssl')
makedepends=('wget')
conflicts=('fossil-fossil')
provides=('fossil')
source=("fossil-xinetd" "bash.completion"
  "fossil-src-${_date_pkgver}.tar.gz"::"http://www.fossil-scm.org/download/fossil-src-${_date_pkgver}.tar.gz"
)
build() {
  cd $srcdir/fossil-src-${_date_pkgver}
  ./configure --prefix=/usr
  # headers and translate targets are problematic with parallel jobs
  make -j1 bld bld/headers
  make
}

package() {
  cd $srcdir/fossil-src-${_date_pkgver}
  mkdir -p ${pkgdir}/usr/bin/
  install -Dm755 ${pkgname} ${pkgdir}/usr/bin/${pkgname}
  install -Dm644 COPYRIGHT-BSD2.txt \
          ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -Dm644 $srcdir/fossil-xinetd $pkgdir/etc/xinetd.d/fossil
  install -Dm644 $srcdir/bash.completion $pkgdir/usr/share/bash-completion/completions/fossil
}
sha1sums=('a5a753e13fbc43dcd670542193a12736586d8977'
          'd49fe8b0a4b7bcb046394f722ee60fd688e39397'
          '16bf2d05ec62cd704b2cbc2bb9388de2a1b5ac97')
