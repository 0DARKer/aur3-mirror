#Maintainer: Nathan Owe <ndowens.aur at gmail dot com>
pkgname=fossil
pkgver=20110413120518
pkgrel=1
pkgdesc="Simple, high-reliability, distributed software configuration management"
arch=('i686' 'x86_64')
license=('BSD')
url="http://www.fossil-scm.org/"
depends=('openssl')
source=("http://www.fossil-scm.org/download/fossil-src-${pkgver}.tar.gz")
md5sums=('a044c50bf9f097db6630a409fc2f90bd')

build() {
	cd ${pkgname}-src-${pkgver}

	# headers and translate targets are problematic with parallel jobs
	make -j1 bld bld/headers

	make
}

package() {
	cd ${pkgname}-src-${pkgver}

	install -Dm755 ${pkgname} \
		${pkgdir}/usr/bin/${pkgname}

	install -Dm644 COPYRIGHT-BSD2.txt \
		${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
} 
