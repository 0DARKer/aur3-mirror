# Maintainer: Ner0

pkgname=gpaste-daemon
pkgver=3.10
pkgrel=1
pkgdesc="A clipboard management daemon"
arch=('i686' 'x86_64')
url="http://www.imagination-land.org/tags/GPaste.html"
license=('GPL3')
depends=('gtk3' 'systemd' 'xcb-util-keysyms')
makedepends=('gobject-introspection' 'intltool' 'vala')
conflicts=('cinnamon-extension-gpaste' 'gpaste')
provides=('gpaste')
options=('!emptydirs' '!libtool')
install=$pkgname.install
source=("http://www.imagination-land.org/files/gpaste/gpaste-$pkgver.tar.xz")
md5sums=('c07c89d14925508fde98eac4b069324f')

build () {
  cd ${pkgname%-*}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
              --enable-systemd --disable-applet --disable-gnome-shell-extension \
              --without-controlcenterdir --disable-schemas-compile --enable-vala
  make
}

package () {
  cd ${pkgname%-*}-${pkgver}
  make DESTDIR="$pkgdir/" install

  install -Dpm644 data/completions/gpaste "$pkgdir/etc/bash_completion.d/gpaste"
  install -Dpm644 data/completions/_gpaste "$pkgdir/usr/share/zsh/site-functions/_gpaste"
  rm -rf "$pkgdir/no"
}
