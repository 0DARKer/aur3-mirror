# Maintainer: Ner0

pkgname=gpaste-daemon
pkgver=2.9.1
pkgrel=1
pkgdesc="A clipboard management daemon"
arch=('i686' 'x86_64')
url="https://github.com/Keruspe/GPaste"
license=('GPL3')
depends=('dbus-core' 'gtk3' 'libxml2' 'xcb-util-keysyms')
makedepends=('dbus-core' 'libxml2' 'gtk3' 'intltool' 'vala')
conflicts=('cinnamon-extension-gpaste' 'gpaste')
provides=('gpaste')
options=('!emptydirs')
install=$pkgname.install
source=("https://github.com/downloads/Keruspe/GPaste/gpaste-${pkgver}.tar.xz")
md5sums=('57450a9298cb410d1dcb471c8aa73538')

build () {
  cd ${pkgname%-*}-${pkgver}

  sed -i 's/\ --warn-all\ --warn-error//' Makefile.in bindings/gi.mk

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --libexecdir=/usr/lib \
              --enable-vala \
              --disable-static \
              --disable-applet \
              --disable-gnome-shell-extension \
              --disable-schemas-compile
  make ${MAKEFLAGS}
}

package () {
  cd ${pkgname%-*}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
