# Maintainer: Ner0

pkgname=gpaste-daemon
pkgver=2.9
pkgrel=1
pkgdesc="A clipboard management daemon"
arch=('i686' 'x86_64')
url="https://github.com/Keruspe/GPaste"
license=('GPL3')
depends=('dbus-core' 'gtk3' 'libxml2' 'xcb-util-keysyms')
makedepends=('dbus-core' 'libxml2' 'gtk3' 'intltool' 'vala')
conflicts=('cinnamon-extension-gpaste' 'gpaste')
provides=('gpaste')
options=('!emptydirs')
install=$pkgname.install
source=("https://github.com/downloads/Keruspe/GPaste/${pkgname%-*}-${pkgver}.tar.xz")
md5sums=('b2dc8a3138f050480cfd9d8229c0569f')

build () {
  cd ${pkgname%-*}-${pkgver}

  sed -i 's/\ --warn-error//' Makefile.in bindings/gi.mk

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --libexecdir=/usr/lib \
              --enable-vala \
              --disable-static \
              --disable-applet \
              --disable-gnome-shell-extension \
              --disable-schemas-compile
  make ${MAKEFLAGS}
}

package () {
  cd ${pkgname%-*}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
