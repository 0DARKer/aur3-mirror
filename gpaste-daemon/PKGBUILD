# Maintainer: Ner0

pkgname=gpaste-daemon
pkgver=3.0.1
pkgrel=2
pkgdesc="A clipboard management daemon"
arch=('i686' 'x86_64')
url="http://www.imagination-land.org/tags/GPaste.html"
license=('GPL3')
depends=('dbus-core' 'gtk3' 'libxml2' 'xcb-util-keysyms')
makedepends=('dbus-core' 'libxml2' 'gtk3' 'intltool' 'vala')
conflicts=('cinnamon-extension-gpaste' 'gpaste')
provides=('gpaste')
options=('!emptydirs')
install=$pkgname.install
source=("http://www.imagination-land.org/files/gpaste-$pkgver.tar.xz"
	'gcc-optional.patch')
md5sums=('eb99d3a5f0bb2ab763f1b3820d4c8e8a'
         '1aecba35bda56c636c1e0cafa383aa0e')

prepare() {
  cd ${pkgname%-*}-${pkgver}
  patch -Np1 -i "$srcdir/gcc-optional.patch"
}

build () {
  cd ${pkgname%-*}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc --enable-systemd \
              --disable-applet --disable-gnome-shell-extension \
              --without-controlcenterdir --disable-schemas-compile
  make
}

package () {
  cd ${pkgname%-*}-${pkgver}
  make DESTDIR="$pkgdir/" install

  install -Dpm644 data/completions/gpaste "$pkgdir/etc/bash_completion.d/gpaste"
  install -Dpm644 data/completions/_gpaste "$pkgdir/usr/share/zsh/site-functions/_gpaste"
}
