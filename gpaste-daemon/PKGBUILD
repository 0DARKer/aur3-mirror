# Maintainer: Ner0

pkgname=gpaste-daemon
pkgver=3.0.1
pkgrel=1
pkgdesc="A clipboard management daemon"
arch=('i686' 'x86_64')
url="http://www.imagination-land.org/tags/GPaste.html"
license=('GPL3')
depends=('dbus-core' 'gtk3' 'libxml2' 'xcb-util-keysyms' 'gnome-comtrol-center')
makedepends=('dbus-core' 'libxml2' 'gtk3' 'intltool' 'vala')
conflicts=('cinnamon-extension-gpaste' 'gpaste')
provides=('gpaste')
options=('!emptydirs')
install=$pkgname.install
source=("http://www.imagination-land.org/files/gpaste-$pkgver.tar.xz")
md5sums=('eb99d3a5f0bb2ab763f1b3820d4c8e8a')

build () {
  cd ${pkgname%-*}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc --enable-systemd \
              --disable-applet --disable-gnome-shell-extension \
              --disable-schemas-compile
  make
}

package () {
  cd ${pkgname%-*}-${pkgver}
  make DESTDIR="$pkgdir/" install

  install -Dpm644 data/completions/gpaste "$pkgdir/etc/bash_completion.d/gpaste"
  install -Dpm644 data/completions/_gpaste "$pkgdir/usr/share/zsh/site-functions/_gpaste"
}
