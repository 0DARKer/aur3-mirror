# Maintainer: HÃ¥vard Pettersson <haavard.pettersson@gmail.com>

pkgname=tox-weechat-git
pkgver=r110.4c13200
pkgrel=1

pkgdesc="Tox protocol plugin for WeeChat."
arch=('i686' 'x86_64')
url="https://github.com/haavardp/tox-weechat"
license=('GPL3')

depends=('tox-git' 'weechat>=1.0' 'jansson>=2.5')
makedepends=('git' 'cmake')

source=("${pkgname}::git://github.com/haavardp/tox-weechat.git")
md5sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${pkgname}"

  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make -j$(nproc)
}

package() {
  cd "${pkgname}"

  make DESTDIR="${pkgdir}/" install
}

