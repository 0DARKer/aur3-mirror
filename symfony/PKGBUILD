# Maintainer: Patrick Palka <patrick@parcs.ath.cx>
# Development: http://closure.ath.cx/aur-dev

pkgname=symfony
pkgver=1.4.11
pkgrel=1
pkgdesc="A web-application framework for PHP projects"
arch=('any')
url="http://symfony-project.org/"
license=('MIT')
depends=('php')
optdepends=('php-xsl: for compatibility with Propel')
source=("http://www.symfony-project.org/get/$pkgname-$pkgver.tgz"
        completion.bash)
install=$pkgname.install

package() {
    cd "$pkgname-$pkgver"

    install -vd "$pkgdir/usr/share"
    cp -vR . "$pkgdir/usr/share/$pkgname"

    install -vd "$pkgdir/usr/bin"
    ln -vs "../share/$pkgname/data/bin/$pkgname" "$pkgdir/usr/bin/"

    install -vDm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -vDm644 ../completion.bash "$pkgdir/etc/bash_completion.d/$pkgname"
}

md5sums=('179bc7c99c792313fbfe6b683da7cb7e'
         'a4f0ff6b167b839f569f71c5cd7ef29f')
