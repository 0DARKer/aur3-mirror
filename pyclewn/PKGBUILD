# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: LUO, Jian <jian (dot) luo (dot) cn (at) gmail (dot) com >
# Contributor: Ivan Sichmann Freitas <ivansichfreitas (at) gmail (dot) com>

pkgname=pyclewn
pkgver=2.0.1
pkgrel=1
pkgdesc="A Vim front-end to the gdb and pdb debuggers"
license=('GPL2')
arch=('any')
url="http://pyclewn.sourceforge.net"
depends=('python' 'gvim-python3>=7.4' 'gdb' 'python-pdb-clone')
install=pyclewn.install
source=(https://pypi.python.org/packages/source/p/${pkgname}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('996acbedbb7922109925f188efba4c71e83f1823c029f92dc00bb21449f024232ef55df0c74a9fd4bfa7229523ed9ad4301aa9dbb8a3c20424082fc6939f63db')

package() {
  cd ${pkgname}-${pkgver}

  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 README "${pkgdir}/usr/share/doc/${pkgname}/README"

  # install vimball
  install -d "${pkgdir}/usr/share/vim/vimfiles"
  vim -c "let g:vimball_home = \"${pkgdir}/usr/share/vim/vimfiles\"" \
    -S ./lib/clewn/runtime/${pkgname}-${pkgver}.vmb -c "q" < /dev/tty > /dev/tty
  # fix vimball garbage
  rm "${pkgdir}/usr/share/vim/vimfiles/doc/tags"
  sed -ri "s|${pkgdir}||g" "${pkgdir}/usr/share/vim/vimfiles/.VimballRecord"
}

# vim: ts=2 sw=2 et:
