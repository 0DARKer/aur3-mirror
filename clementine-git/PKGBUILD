# Mantainer: Adria Arrufat <swiftscythe@gmail.com>
# Contributor: Ike Devolder <ike.devolder@gmail.com>
# Contributor: Daniel Hill <daniel.hill@orcon.net.nz>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=clementine-git
_realname=clementine-player
pkgver=20111117
pkgrel=1
pkgdesc="A modern music player and library organiser and a port of Amarok 1.4, with some features rewritten to take advantage of Qt4."
arch=('i686' 'x86_64')
url="http://code.google.com/p/clementine-player/"
license=('GPL')
depends=('gstreamer0.10-base' 'taglib' 'glew' 'liblastfm' 'libgpod' 'libmtp' 'libplist' 'hicolor-icon-theme' 'qt' 'libimobiledevice' 'libspotify' 'python2-pyqt' 'qjson' 'protobuf')
optdepends=('gstreamer0.10-base-plugins: for more open formats'
            'gstreamer0.10-good-plugins: for use with "Good" plugin libraries'
            'gstreamer0.10-bad-plugins: for use with "Bad" plugin libraries'
            'gstreamer0.10-ugly-plugins: for use with "Ugly" plugin libraries')
makedepends=('git' 'boost' 'cmake' 'mesa')
provides=('clementine')
conflicts=('clementine' 'clementine-svn')
replaces=('clementine' 'clementine-svn')
source=()
sha512sums=()

_gitroot="https://code.google.com/p/${_realname}"
_gitname=${_realname}

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}" && git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DBUILDBOT_REVISION=${pkgver}
  make
}

package() {
  cd "${srcdir}/${_gitname}-build"

  #OMG gigantic sed line that fixes the "blob" install error
  sed -i -e '/"\/.*version[[:digit:]][-]*[[:digit:]]*bit/ s//"${CMAKE_INSTALL_PREFIX}\/bin/' -e '/clementine-spotifyblob/ s/$ENV{DESTDIR}//g' spotifyblob/blob/cmake_install.cmake
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
