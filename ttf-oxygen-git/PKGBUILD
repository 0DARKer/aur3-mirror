# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Jerome Leclanche <jerome.leclanche+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ttf-oxygen-git
pkgver=r163.25a3b45
pkgrel=1
pkgdesc="A desktop/gui font family for integrated use with the KDE desktop."
arch=("any")
license=("custom:OFL")
url="https://projects.kde.org/projects/playground/artwork/oxygen-fonts"
makedepends=('git' 'cmake' 'extra-cmake-modules' 'fontforge')
provides=("ttf-oxygen")
conflicts=("ttf-oxygen")
install="ttf-oxygen.install"
source=("git://anongit.kde.org/oxygen-fonts.git")
sha1sums=("SKIP")

pkgver() {
  cd oxygen-fonts
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../oxygen-fonts \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
