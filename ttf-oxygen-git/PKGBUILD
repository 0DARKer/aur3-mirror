# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=oxygen-fonts
pkgname=ttf-oxygen-git
pkgver=5.1.0.1.g25a3b45
pkgrel=1
pkgdesc="A desktop/gui font family for integrated use with the KDE desktop."
arch=("any")
license=("custom:OFL")
url="https://projects.kde.org/projects/playground/artwork/oxygen-fonts"
makedepends=("git" "cmake" "extra-cmake-modules")
provides=("ttf-oxygen")
conflicts=("ttf-oxygen")
install="ttf-oxygen.install"
source=("git://anongit.kde.org/$_pkgname.git")
sha256sums=("SKIP")

pkgver() {
	cd "$srcdir/$_pkgname"
	git describe --always | sed "s/^v//g" | sed "s/-/./g"
}

build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$_pkgname" -DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
