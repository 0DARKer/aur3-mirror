# Contributor: Vitaliy Berdinskikh ur6lad[at]archlinux.org.ua

pkgname=tomcat6
pkgver=6.0.32
pkgrel=1
pkgdesc="An open source software implementation of the Java Servlet and JavaServer Pages (JSP) technologies."
arch=('i686' 'x86_64')
url="http://tomcat.apache.org/"
license=('Apache')
groups=('apache-tomcat')
depends=('java-runtime')
provides=('tomcat')
conflicts=('tomcat')
backup=(etc/tomcat.d/server.xml etc/tomcat.d/tomcat-users.xml 
		etc/tomcat.d/catalina.policy etc/tomcat.d/catalina.properties
		etc/conf.d/tomcat)
install=tomcat.install
source=(http://apache.infocom.ua/tomcat/tomcat-6/v${pkgver}/bin/apache-tomcat-${pkgver}.tar.gz tomcat.conf.d)

package() {
	# Linux style directoies, part 1
	mkdir -p $pkgdir/etc/tomcat.d
	mkdir -p $pkgdir/usr/share/tomcat
	mkdir -p $pkgdir/var/lib/tomcat/localhost
	mkdir -p $pkgdir/var/log/tomcat
	mkdir -p $pkgdir/srv/tomcat
	ln -sf /var/lib/tomcat/localhost $pkgdir/usr/share/tomcat/work
	ln -sf /var/log/tomcat $pkgdir/usr/share/tomcat/logs
	ln -sf /tmp $pkgdir/usr/share/tomcat/temp

	cd $srcdir/apache-tomcat-$pkgver

	# Remove unnecessary files
	rm -f LICENSE bin/*.{exe,bat,tar.gz}
	rm -fr temp work logs

	# commons-daemon.jar is too old
	rm bin/commons-daemon.jar

	# Copy!
	cp -R * $pkgdir/usr/share/tomcat
	ln -sf /usr/share/java/commons-daemon/commons-daemon.jar \
		$pkgdir/usr/share/tomcat/bin/commons-daemon.jar

	# Linux style directoies, part 2
	mv $pkgdir/usr/share/tomcat/conf/* $pkgdir/etc/tomcat.d
	rm -r $pkgdir/usr/share/tomcat/conf
	ln -sf /etc/tomcat.d $pkgdir/usr/share/tomcat/conf
	mv $pkgdir/usr/share/tomcat/webapps $pkgdir/srv/tomcat
	ln -sf /srv/tomcat/webapps $pkgdir/usr/share/tomcat/webapps

	# Tomcat's variables especially for tomcat6-daemon
	install -D -m644 $srcdir/tomcat.conf.d $pkgdir/etc/conf.d/tomcat

	# Set grants
	chmod 770 $pkgdir/etc/tomcat.d $pkgdir/var/{lib,log}/tomcat $pkgdir/srv/tomcat
	chmod 660 $pkgdir/etc/tomcat.d/*
}

md5sums=('928a960268adf610a7d6fe5c4fcd0b20'
         '2c5caa52038c922369337dc86d8152e6')
sha256sums=('505e649d1ffcf746e66be8295c8244a2949349dedf678e9f2a659c4736968c5e'
            '19642e01a5f120e37d2baa781fb44212d2a01206871209c6dbaeeb2bab670002')
