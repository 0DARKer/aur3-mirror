# Maintainer: Spyhawk

pkgname=etlegacy-mod-win32-git
pkgver=pre.sdl2.599.g77fc6a1
pkgrel=1
arch=('any')
url="http://www.etlegacy.com/"
license=('GPL3')
pkgdesc="Wolfenstein: Enemy Territory 2.60b compatible client/server (etlegacy mod win32)"
depends=('etlegacy-mod-git')
makedepends=('git' 'cmake' 'mingw-w64-gcc' 'mingw-w64-lua')
options=('!strip' 'staticlibs' '!buildflags')
source=('git://github.com/etlegacy/etlegacy.git')
md5sums=('SKIP')
_gitname=etlegacy

pkgver() {
    cd "$srcdir/$_gitname"
    git describe --always | sed -r 's/^v//;s/-/./g;'
}

build() {
    cd "$srcdir/$_gitname"

    # build type and path
    cmakeopts+=(
        "-D CMAKE_TOOLCHAIN_FILE=./cmake/Toolchain-cross-mingw-linux.cmake"
        "-D CMAKE_BUILD_TYPE=Debug"
        "-D CMAKE_INSTALL_PREFIX=/usr"
        "-D INSTALL_DEFAULT_BASEDIR=/usr/lib/etlegacy"
        "-D INSTALL_DEFAULT_BINDIR=bin"
        "-D INSTALL_DEFAULT_MODDIR=lib/etlegacy"
    )

    # options
    cmakeopts+=(
        "-D BUILD_SERVER=0"
        "-D BUILD_CLIENT=0"
        "-D BUILD_MOD=1"
        "-D BUILD_MOD_PK3=1"
        "-D BUILD_PAK3_PK3=0"
        "-D BUNDLED_LIBS=0"
        "-D INSTALL_OMNIBOT=0"
    )

    cmake ${cmakeopts[@]} .
    make
}

package() {
    cd "$srcdir/$_gitname"

    # mod
    mkdir -p $pkgdir/usr/lib/$_gitname/legacy
    install legacy/* -t $pkgdir/usr/lib/$_gitname/legacy
    mv $pkgdir/usr/lib/$_gitname/legacy/etl_bin*.pk3 $pkgdir/usr/lib/$_gitname/legacy/etl_bin_win32.pk3
}

# vim:set ts=4 sw=2 et:
