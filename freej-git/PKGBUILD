# Contributor: fred_99 <bobrenard@free.fr>

pkgname=freej-git
pkgver=20110311
pkgrel=1
pkgdesc="Realtime video mixer and linear video editor."
arch=('i686' 'x86_64')
url="http://freej.dyne.org/"
license=('GPL3')
depends=('sdl' 'libpng' 'libvorbis' 'libtheora'
         'ffmpeg' 'alsa-lib' 'fftw' 'jack-audio-connection-kit'
         'libjpeg' 'liblo' 'slang' 'libshout' 'python2')
makedepends=('git' 'pkgconfig' 'qt')
optdepends=("qt: qfreej")
conflicts=('freej')

_gitroot="git://git.dyne.org/freej.git"
_gitname="freej"

build() {
  cd "$srcdir"
  if [[ -d $_gitname ]]; then
    cd $_gitname && git pull origin && cd ..
  else
    git clone "$_gitroot" "$_gitname"
  fi

  [[ -d "$_gitname-build" ]] && rm -rf "$_gitname-build"
  git clone "$_gitname" "$_gitname-build"
  cd "$_gitname-build"
  export PYTHON=python2
  ./autogen.sh
  ./configure --prefix=/usr --enable-static-mozjs \
    --enable-qt-gui \
   CXXFLAGS="-g -D__STDC_CONSTANT_MACROS"
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
}


# vim:set ts=2 sw=2 et:
