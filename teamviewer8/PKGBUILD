# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=teamviewer8
pkgver=8.0.16675
pkgrel=3
pkgdesc="All-In-One Software for Remote Support and Online Meetings"
arch=('i686' 'x86_64')
url="http://www.teamviewer.com"
license=('custom')

if [ $CARCH == 'i686' ]; then
  source=("teamviewer_linux-${pkgver}.deb::http://www.teamviewer.com/download/version_8x/teamviewer_linux.deb") 
  md5sums=('8615365b444eb3927cefbbf3dbed6ffa')
  depends=('alsa-lib' 'libsm' 'libxext' 'freetype2' 'libxtst')
elif [ $CARCH == 'x86_64' ]; then
  source=("teamviewer_linux_x64-${pkgver}.deb::http://www.teamviewer.com/download/version_8x/teamviewer_linux_x64.deb")
  md5sums=('4f718ad52ea9091fab4218968a3c1fd7')
  depends=('lib32-libsm' 'lib32-freetype2' 'lib32-gcc-libs' 'lib32-alsa-lib' 'lib32-libxtst' 'lib32-libxdamage')
fi

build() {
	cd "${srcdir}"
        tar -xf data.tar.gz
}

package() {
	cd "${srcdir}"
	cp -R opt "${pkgdir}"
	find "${pkgdir}" -type d | xargs chmod 755
	find "${pkgdir}" -type f | xargs chmod 644
	chmod 755 "${pkgdir}/opt/teamviewer8/tv_bin/teamviewerd"
	chmod 755 "${pkgdir}/opt/teamviewer8/tv_bin/script/teamviewer"
	chmod 755 "${pkgdir}/opt/teamviewer8/tv_bin/script/teamviewer_desktop"
	find "${pkgdir}/opt/teamviewer8/tv_bin/script" -type f -name tvw* | xargs chmod 755
	chmod -R 755 "${pkgdir}/opt/teamviewer8/tv_bin/wine/bin"
	rm "${pkgdir}/opt/teamviewer8/tv_bin/xdg-utils/xdg-email~"
	find "${pkgdir}/opt/teamviewer8/tv_bin/xdg-utils" -type f -name xdg* | xargs chmod 755
	install -dm 755 "${pkgdir}/etc"
	ln -s "/opt/teamviewer8/config" "${pkgdir}/etc/teamviewer"
	install -dm 755 "${pkgdir}/var/log"
	ln -s "/opt/teamviewer8/logfiles" "${pkgdir}/var/log/teamviewer"
	install -dm 755 "${pkgdir}/usr/bin"
	ln -s "/opt/teamviewer8/tv_bin/script/teamviewer" "${pkgdir}/usr/bin/"
	install -dm 755 "${pkgdir}/usr/lib/systemd/system"
	install -m 644 "${pkgdir}/opt/teamviewer8/tv_bin/script/teamviewerd.service" "${pkgdir}/usr/lib/systemd/system/teamviewerd.service"
	install -dm 755 "${pkgdir}/usr/share/applications" 
	ln -s "/opt/teamviewer8/tv_bin/desktop/teamviewer-teamviewer8.desktop" "${pkgdir}/usr/share/applications/"
	install -dm 755 "${pkgdir}/usr/share/licenses/teamviewer8"
	ln -s "/opt/teamviewer8/License.txt" "${pkgdir}/usr/share/licenses/teamviewer8/"
}
