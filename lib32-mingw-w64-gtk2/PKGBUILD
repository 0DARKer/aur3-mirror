pkgname=lib32-mingw-w64-gtk2
pkgver=2.24.13
pkgrel=1
pkgdesc="GTK+ is a multi-platform toolkit (v2) (mingw-w64, 32-bit)"
arch=(any)
url="http://www.gtk.org"
license=("LGPL")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt
mingw-w64-cairo
mingw-w64-gdk-pixbuf2
mingw-w64-glib2
mingw-w64-pango
mingw-w64-atk)
options=(!libtool !strip !buildflags)
source=("http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-${pkgver}.tar.xz")
md5sums=('e949406751df51e1e40e85628005a069')

_architectures="i686-w64-mingw32"

build() {
  unset LDFLAGS
  export CFLAGS="-O2 -mms-bitfields"
  export CXXFLAGS="${CFLAGS}"
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/gtk+-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --disable-modules \
      --disable-cups \
      --with-gdktarget=win32 \
      --disable-glibtest \
      --enable-gtk-doc-html=no
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' -o -name '*.manifest' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm "$pkgdir/usr/${_arch}/bin/gtk-builder-convert"
    rm -r "$pkgdir/usr/${_arch}/"{etc,share/{aclocal,gtk-2.0,locale}}
  done
}