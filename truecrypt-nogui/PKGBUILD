# Maintainer: Corrado Primier <ilbardo.gmail.com>

pkgname=truecrypt-nogui
_pkgname=truecrypt
pkgver=7.0a
pkgrel=1
pkgdesc="Free open-source cross-platform disk encryption software (CLI)"
url="http://www.truecrypt.org/"
arch=('i686' 'x86_64')
license=('custom') # TrueCrypt License v2.6
depends=('fuse>=2.8.0' 'device-mapper' 'wxbase')
optdepends=('sudo: mounting encrypted volumes as nonroot users')
conflicts=('truecrypt')
provides=('truecrypt')
# N.B. Truecrypt's web-based source download is incompatible with
# makepkg. Source has been placed on ftp.archlinux.org instead
source=("ftp://ftp.archlinux.org/other/tc/${_pkgname}-${pkgver}.tar.gz" \
        'ftp://ftp.archlinux.org/other/tc/pkcs-2.20.tar.gz')
md5sums=('0a61616bc5c5ad90e876b4014c004ac9'
         'ce6a707b79411e82e8e558aa03e764b0')

build() {
  export PKCS11_INC="${srcdir}/pkcs-2.20"

  cd ${srcdir}/${_pkgname}-${pkgver}-source || return 1
  make NOGUI=1
}

package() {
  # Install Binary
  install -D -m755 ${srcdir}/${_pkgname}-${pkgver}-source/Main/${_pkgname} \
    ${pkgdir}/usr/bin/${_pkgname}

  # Install License
  install -D -m644 ${srcdir}/${_pkgname}-${pkgver}-source/License.txt \
    ${pkgdir}/usr/share/licenses/${pkgname}/License.txt
}
