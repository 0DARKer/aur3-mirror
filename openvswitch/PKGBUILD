pkgname="openvswitch"
pkgver="1.5.0"
pkgrel=1
pkgdesc="A multilayer virtual switch"
arch=("i686" "x86_64")
url='http://openvswitch.org/'
license=('Apache')
options=('!upx' '!strip')
conflicts=( 'openvswitch-git')
groups=()
optdepends=('dkms: for building openvswitch kernel modules supplied by this version of openvswitch')
install="$pkgname.install"
source=(http://openvswitch.org/releases/$pkgname-$pkgver.tar.gz
        dkms.conf openvswitch.init openvswitch.conf)

sha256sums=('c5ae2a4dff14cf14e537ab4974f9af76c9d6747f300360199505734112d29da6'
            '1aaa56e979a1f1e13a5025fbabef8ba41019f105154300fcc5f5b7545e8aa826'
            'c40f8a9431c978dcba3eec2feb9b62d67edb5555c2fc1a728e674dca5a3b53cc'
            '41ab294d15826d15b77828472be0154d46301a97ade2b857352987d1225a4c9c')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -e 's;python python2.4 python2.5;python2 &;' -i ./configure
  ./configure --with-python=python2 --prefix=/usr --localstatedir=/var

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -d "$pkgdir/usr/src" \
    "$pkgdir/etc/rc.d" "$pkgdir/etc/conf.d" "$pkgdir/etc/$pkgname" \
    "$pkgdir/var/run/$pkgname" "$pkgdir/usr/share/$pkgname"
  cp -a "$srcdir/$pkgname-$pkgver" "$pkgdir/usr/src/"
  install -t "$pkgdir/usr/src/$pkgname-$pkgver/" "$startdir/dkms.conf"
  install -m 744 "$srcdir/$pkgname.init" "$pkgdir/etc/rc.d/$pkgname"
  install -m 644 "$srcdir/$pkgname.conf" "$pkgdir/etc/conf.d/$pkgname"
  install -t "$pkgdir/usr/share/$pkgname" "$srcdir/$pkgname-$pkgver/vswitchd/vswitch.ovsschema"

  # This line replaces references to $pkgdir with /usr/src/openvswitch
  sed -e "s;$srcdir/;/usr/src/openvswitch/;g" -i $(grep -rlIs "$srcdir/" $pkgdir) # | awk -F':' '{print $1}' | sort | uniq)
}

# vim:set ts=2 sw=2 et:

