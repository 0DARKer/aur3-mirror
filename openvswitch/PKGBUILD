pkgname="openvswitch"
pkgver="1.7.0"
pkgrel=1
pkgdesc="A multilayer virtual switch"
arch=("i686" "x86_64")
url='http://openvswitch.org/'
license=('Apache')
options=('!upx' '!strip')
conflicts=( 'openvswitch-git')
groups=()
optdepends=('dkms: for building openvswitch kernel modules supplied by this version of openvswitch')
install="$pkgname.install"
backup=('etc/conf.d/openvswitch')
source=(http://openvswitch.org/releases/$pkgname-$pkgver.tar.gz
        dkms.conf openvswitch.init openvswitch.conf README.archlinux)

sha256sums=('8e3942cbce80048fb2ce8077be5d35bbe50bb12a1feba33d7c1db68ce711eb53'
            '1aaa56e979a1f1e13a5025fbabef8ba41019f105154300fcc5f5b7545e8aa826'
            'e08596f483cc64e46ddbdc60ab452bbd68b99179af3aef5b02fe6f1ff7a0efb1'
            'a45dd67ce3776b6ca918f199d35ae6b21cc6eced65eb79f8b65527048d8b1996'
            '7c1983974b7e7d982b730484886d264d34f49a211724d1492be9ae5e14d9e07f')

make() {
  if which colormake &>/dev/null; then
    /usr/bin/env colormake $MAKEFLAGS $@
  else
    /usr/bin/env make $MAKEFLAGS $@
  fi
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --localstatedir=/var

  make PYTHON="python2"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make PYTHON="python2" DESTDIR="$pkgdir" install

  install -d "$pkgdir/usr/src" \
    "$pkgdir/etc/rc.d" "$pkgdir/etc/conf.d" "$pkgdir/etc/$pkgname" \
    "$pkgdir/var/run/$pkgname" "$pkgdir/usr/share/$pkgname" \
    "$pkgdir/usr/share/$pkgname/doc"
  cp -a "$srcdir/$pkgname-$pkgver" "$pkgdir/usr/src/"
  install -t "$pkgdir/usr/src/$pkgname-$pkgver/" "$startdir/dkms.conf"
  install -t "$pkgdir/usr/share/$pkgname" "$srcdir/$pkgname-$pkgver/vswitchd/vswitch.ovsschema"
  install -m 744 "$srcdir/$pkgname.init" "$pkgdir/etc/rc.d/$pkgname"
  install -m 644 "$srcdir/$pkgname.conf" "$pkgdir/etc/conf.d/$pkgname"
  install -m 644 "$srcdir/README.archlinux" "$pkgdir/usr/share/$pkgname/doc"

  # This line replaces references to $pkgdir with /usr/src/openvswitch
  sed -e "s;$srcdir/;/usr/src/openvswitch/;g" -i $(grep -rlIs "$srcdir/" $pkgdir)
}

# vim:set ts=2 sw=2 et:

