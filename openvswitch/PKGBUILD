pkgname="openvswitch"
pkgver="1.4.0"
pkgrel=1
pkgdesc="A multilayer virtual switch"
arch=("i686" "x86_64")
url='http://openvswitch.org/'
license=('Apache')
options=('!upx' '!strip')
conflicts=( 'openvswitch-git')
groups=()
depends=('dkms')
install="$pkgname.install"
source=(http://openvswitch.org/releases/$pkgname-$pkgver.tar.gz
        dkms.conf
        openvswitch.init)
noextract=()
sha256sums=('27c71e4536d992f46f806179bd9947acc742a12996f977c6cfa6c549a6a9c25a'
            'f88510fd9887b9bd9e2f242f741f1d96a2e494df7f7e38c8f1181922e8b5f322'
            '97849b68cb1b56487fc17c4acaa9fa70e95331b7154d856248d7bb68b5e39465')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --with-python=python2 --prefix=/usr --localstatedir=/var

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -d "$pkgdir/usr/src" \
    "$pkgdir/etc/rc.d" "$pkgdir/etc/$pkgname" \
    "$pkgdir/var/run/$pkgname" "$pkgdir/usr/share/$pkgname"
  cp -a "$srcdir/$pkgname-$pkgver" "$pkgdir/usr/src/"
  install -t "$pkgdir/usr/src/$pkgname-$pkgver/" "$startdir/dkms.conf"
  install -m 744 "$srcdir/$pkgname.init" "$pkgdir/etc/rc.d/$pkgname"
  install -t "$pkgdir/usr/share/$pkgname" "$srcdir/$pkgname-$pkgver/vswitchd/vswitch.ovsschema"

  # This line replaces references to $pkgdir with /usr/src/openvswitch
  sed -e "s;$srcdir/;/usr/src/openvswitch/;g" -i $(grep -riIs "$srcdir/" $pkgdir | awk -F':' '{print $1}' | sort | uniq)
}

# vim:set ts=2 sw=2 et:


