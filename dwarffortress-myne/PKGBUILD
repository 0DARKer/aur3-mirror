# $Id$
# Maintainer: Nick Hu <nickhu00@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Daenyth <Daenyth+Arch [AT] gmail [DOT] com>
# Contributor: djnm <nmihalich [at} gmail dott com>
pkgname=dwarffortress-myne
pkgver=0.34.11
_pkgver=34_11
pkgrel=2
pkgdesc="A single-player fantasy game. You control a dwarven outpost or an adventurer in a randomly generated persistent world. Packed with the Myne tileset, some binary patches, and CLA's colour scheme"
arch=(i686 x86_64)
url="http://www.bay12games.com/dwarves/"
install="$pkgname.install"
license=('custom:dwarffortress')
depends=(gtk2 glu glew libsndfile openal sdl_ttf)
makedepends=(git unrar perl sed xdelta3)
if [[ $CARCH == 'x86_64' ]]; then
  depends=(lib32-gtk2 lib32-glu lib32-glew lib32-libsndfile lib32-openal
  lib32-libxdamage lib32-ncurses lib32-sdl_ttf)
  optdepends=('lib32-nvidia-utils: If you have nvidia graphics'
              'lib32-catalyst-utils: If you have ATI graphics'
              'lib32-alsa-lib: for alsa sound'
              'lib32-libpulse: for pulse sound')
fi
backup=('opt/df_linux-myne/data/init/colors.txt'
        'opt/df_linux-myne/data/init/init.txt'
        'opt/df_linux-myne/data/init/d_init.txt'
        'opt/df_linux-myne/data/init/interface.txt')
        
source=(http://www.bay12games.com/dwarves/df_${_pkgver}_linux.tar.bz2
        CLA_graphic_set_v15-STANDALONE.rar::"http://dffd.wimbli.com/download.php?id=5945&f=CLA_graphic_set_v15-STANDALONE.rar"
        binary_patches_v1.delta
        'git+https://github.com/svenstaro/dwarf_fortress_unfuck.git'
        dwarffortress-myne
        dwarffortress-myne.desktop
        dwarffortress-myne.png)
noextract=('CLA_graphic_set_v15-STANDALONE.rar')
md5sums=('33e26a93e5914f7545fa1aaa53706eeb'
         '4c159086065063ccc9479a80cab9b1cc'
         '9ddf69a707b3507501be5fb99ff6d35b'
         'SKIP'
         '4446ede47a184a16e9c4139381c485bc'
         '6e0e792a0e5f3a3e4dc1f24f1fbd291c'
         'b1d51f82400073af9bb179e34a9209d0')

prepare() {
  unrar x CLA_graphic_set_v15-STANDALONE.rar
}

build() {
  cd $srcdir/dwarf_fortress_unfuck
  make
}

package() {
  mv $srcdir/df_linux $srcdir/df_linux-myne
  cd $srcdir/df_linux-myne

  # Dwarf Fortress binary patches:
  # armorstand-capacity.dif
  # custom-reagent-size.dif
  # deconstruct-heapfall.dif
  # deconstruct-teleport.dif
  # hospital-overstocking.dif
  # long-patrol-duty.dif
  # patches.txt
  # training-ammo.dif
  # weaponrack-unassign.dif

  xdelta3 -d -f -s $srcdir/df_linux-myne/libs/Dwarf_Fortress $srcdir/binary_patches_v1.delta $srcdir/df_linux-myne/libs/Dwarf_Fortress

  install -dm755 $pkgdir/opt/
  cp -r $srcdir/df_linux-myne/ $pkgdir/opt/

  # Convert from DOS to Unix
  for i in $srcdir/CLA/*/*/*.txt
  do
    perl -pi -e 's/\r\n/\n/g' $i
  done

  # Myne tileset and colour scheme
  sed -i 's/CLA/Myne/g' $srcdir/CLA/data/init/init.txt # Comment out this line for CLA graphics instead
  cp -r $srcdir/CLA/* $pkgdir/opt/df_linux-myne/
  rm -r $pkgdir/opt/df_linux-myne/raw/graphics/example
  rm $pkgdir/opt/df_linux-myne/raw/graphics/graphics_example.txt

  # Yay for precompiled stuff with junk permissions! :D
  find $pkgdir/opt/df_linux-myne -type d -exec chmod 755 {} +
  find $pkgdir/opt/df_linux-myne -type f -exec chmod 644 {} +

  install -Dm755 $srcdir/dwarffortress-myne $pkgdir/usr/bin/dwarffortress-myne

  chmod 755 $pkgdir/opt/df_linux-myne/libs/Dwarf_Fortress
  install -Dm755 $srcdir/dwarf_fortress_unfuck/libs/libgraphics.so $pkgdir/opt/df_linux-myne/libs/libgraphics.so
  rm $pkgdir/opt/df_linux-myne/libs/{libgcc_s.so.1,libstdc++.so.6}

  install -d -m775 -o root -g games $pkgdir/opt/df_linux-myne/data/save

  # This probably is overkill, but I don't know what specific files df needs permission for in here.
  chmod 775 $pkgdir/opt/df_linux-myne
  chown root:games $pkgdir/opt/df_linux-myne
  chown -R root:games $pkgdir/opt/df_linux-myne/data
  find $pkgdir/opt/df_linux-myne/data -type d -exec chmod 775 {} +
  find $pkgdir/opt/df_linux-myne/data -type f -exec chmod 664 {} +

  # Desktop launcher with icon
  install -Dm644 $srcdir/dwarffortress-myne.desktop $pkgdir/usr/share/applications/dwarffortress.desktop
  install -Dm644 $srcdir/dwarffortress-myne.png     $pkgdir/usr/share/pixmaps/dwarffortress.png

  install -Dm644 $srcdir/df_linux-myne/readme.txt $pkgdir/usr/share/licenses/dwarffortress-myne/readme.txt
}

# vim:set ts=2 sw=2 et:
