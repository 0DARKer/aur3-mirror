#
# PKGBULD FROM: JT Wilkinson <jowilkin at tu dot archserver dot org>
#
# Note: Apache ant and java must be on the path for make to succeed.
#       If either was installed as a dependency during the build, you
#       will need to log out and then log back in for the changes to
#       your path and JAVA_HOME environment variable to take effect.
#
pkgname=rstudio-server-git
pkgver=20120802
pkgrel=1
pkgdesc="A new integrated development environment (IDE) for R"
arch=('i686' 'x86_64')
url="http://www.rstudio.org/"
license=('custom')
depends=('r>=2.11.1' 'qt>=4.8' 'boost-libs>=1.42' 'shared-mime-info' 'qtwebkit')
makedepends=('git' 'cmake>=2.6' 'boost>=1.42' 'java-environment' 'apache-ant' 'unzip' 'openssl' 'pam' 'pango' 'zlib' 'bzip2')
provides=('rstudio-desktop')
conflicts=('rstudio-desktop')
install="${pkgname}.install"

source=('rc.d'
	'rstudio-server-git.install')

md5sums=('7f4a2141409f1acdd432834f7b505f67'
	'309c5da2dd2088017ba424bdf6da591e')

_gitroot="git://github.com/rstudio/rstudio.git"
_gitname="rstudio"

build() {
  cd "${srcdir}"
  echo "Entering ${srcdir}"
  if [ -d "$_gitname" ] ; then
    msg "Updating repository from server...."
    cd "$_gitname"
    git pull
  else
    msg "Cloning from GIT server...."
    git clone "$_gitroot" "$_gitname"
  fi

  cd "${srcdir}/$_gitname"
  msg "GIT checkout done or server timeout"

  # Install gwt - this creates files inside the src package that are needed for the build
  cd "${srcdir}/$_gitname/dependencies/common"
  msg "Downloading and installing gwt"
  ./install-gwt
  msg "Downloading and installing dictionaries"
  ./install-dictionaries
  msg "Downloading and installing mathjax"
  ./install-mathjax

  if [ -d "${srcdir}/$_gitname/build" ]; then
    msg "Clean previous build directory..."
    rm -rf "${srcdir}/$_gitname/build"
  fi


  # Configure with cmake in a new buld directory as recommended in the rstudio INSTALL file
  msg "Configuring the makefile with cmake"
  mkdir "${srcdir}/$_gitname/build"
  cd "${srcdir}/$_gitname/build"

  # Configure cmake to install to /opt/rstudio
  cmake -DRSTUDIO_TARGET=Server -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr/lib/rstudio ..
}

package() {
  msg "Starting make and install...."
  cd "${srcdir}/$_gitname/build"
  make DESTDIR="$pkgdir" install

  msg "Installing license...."
  install -D -m644 "${srcdir}/$_gitname/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  #rc.d file
  install -Dm755 ../../../rc.d "${pkgdir}"/etc/rc.d/rstudio
}

