#
# Maintainer: gabx <arnaud.gaboury@gmail.com>
#
# Note: Apache ant and java must be on the path for make to succeed.
#       If either was installed as a dependency during the build, you
#       will need to log out and then log back in for the changes to
#       your path and JAVA_HOME environment variable to take effect.
#
pkgname=rstudio-server-git
pkgver=v0.98.761.r0.g08473b4
pkgrel=1
pkgdesc="A new integrated development environment (IDE) for R programming language"
arch=('i686' 'x86_64')
url="http://www.rstudio.org/"
license=('AGPL3')
depends=('r>=2.11.1' 'boost-libs>=1.5' 'util-linux' 'gcc-libs')
makedepends=('git' 'cmake>=2.8' 'boost>=1.5' 'java-runtime' 'apache-ant' 'unzip' 'openssl' 'bzip2' 'pango' 'pam' 'zlib')
install="${pkgname}.install"
conflicts=('rstudio-server' 'r-mkl' )
source=('git://github.com/rstudio/rstudio.git' 'conf.d' 'rc.d' 'rstudio-server.service')
md5sums=('SKIP'
	'f7b4b7805859ab7c28678ac01650efc1'
         'a810efa1aca6052e4a170fd2dc9fb2a4'
         'f4c42e68d572d1c5f8303f82f5e174b4')


_gitname="rstudio"

pkgver() {
  cd "${srcdir}/$_gitname"
  git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}"
  echo "Entering ${srcdir}"
  if [ -d "$_gitname" ] ; then
    msg "Updating repository from server..."
    cd "$_gitname"
    git pull
  else
    msg "Cloning from GIT server..."
    git clone "$_gitroot" "$_gitname"
  fi

  cd "${srcdir}/$_gitname"
  msg "GIT checkout done or server timeout"

  # Install gwt - this creates files inside the src package that are needed for the build
  cd "${srcdir}/$_gitname/dependencies/common"
  msg "Downloading and installing gwt"
  ./install-gwt
  msg "Downloading and installing dictionaries"
  ./install-dictionaries
  msg "Downloading and installing mathjax"
  ./install-mathjax
  msg "Downloading and installing pandoc"
  ./install-pandoc 
  msg "Downloading and installing rmarkdown"
  ./install-rmarkdown
  # Small hack
  sed -i 's/init.d/rc.d/' ${srcdir}/rstudio/src/cpp/server/extras/admin/rstudio-server.in
  # rmv user Rprofile.r for building
  if [ -n $R_PROFILE_USER ]; then
  unset R_PROFILE_USER
  fi
  # Configure with cmake in a new buld directory as recommended in the rstudio INSTALL file
  if [ -d "${srcdir}/$_gitname/build" ]; then
    msg "Clean previous build directory..."
    rm -rf "${srcdir}/$_gitname/build"
  fi
  mkdir "${srcdir}/$_gitname/build"
  cd "${srcdir}/$_gitname/build"
  # Configure cmake 
    cmake -DRSTUDIO_TARGET=Server -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr/lib/rstudio-server -DCMAKE_DL_LIBRARIES=/usr/lib64/libdl.so -DCMAKE_LIBR_DOC_DIR=/usr/share/doc/R  -DCMAKE_LIBR_EXECUTABLE=/usr/bin/R  -DCMAKE_LIBR_HOME=/usr/lib64/R -DCMAKE_LIBR_INCLUDE_DIRS=/usr/include/R -DCMAKE_LIBR_CORE_LIBRARY=usr/lib64/R/lib/libR.so ..
}

package() {
  msg "Starting make and install..."
  cd "${srcdir}/$_gitname/build"
  make DESTDIR="$pkgdir" install
  msg "Install adittional files..."
  install -d "${pkgdir}/etc/pam.d"
  install -Dm 644 "${pkgdir}/usr/lib/rstudio-server/extras/pam/rstudio" "${pkgdir}/etc/pam.d/rstudio"
  install -d "${pkgdir}/srv"
  mv "${pkgdir}/usr/lib/rstudio-server/www" "${pkgdir}/srv/rstudio"
  rm -rf "${pkgdir}/usr/lib/rstudio-server/extras"
  install -Dm 755 "${srcdir}/rc.d" "${pkgdir}/etc/rc.d/rstudio-server"
  install -Dm 644 "${srcdir}/conf.d" "${pkgdir}/etc/conf.d/rstudio-server"
  install -d "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 "${srcdir}/rstudio-server.service" "${pkgdir}/usr/lib/systemd/system/rstudio-server.service"
  install -d "${pkgdir}/etc/rstudio"
}
