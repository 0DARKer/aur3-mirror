# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libkdcraw-frameworks-git
pkgver=r938.46898ec
pkgrel=1
pkgdesc="A C++ interface used to decode RAW picture. KF5 Frameworks branch (GIT version)"
url='https://projects.kde.org/projects/kde/kdegraphics/libs/libkdcraw'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kconfig' 'ki18n' 'libraw' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'git')
conflicts=('libkdcraw')
provides=('libkdcraw')
source=("git://anongit.kde.org/libkdcraw")
sha1sums=('SKIP')
install=libkdcraw-frameworks-git.install

pkgver() {
  cd libkdcraw
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
  cd libkdcraw
  git checkout frameworks
}

build() {
  cd build
  cmake ../libkdcraw \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}

