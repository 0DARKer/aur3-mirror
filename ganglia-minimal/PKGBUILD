_pkgbase=ganglia
pkgname=ganglia-minimal
pkgver=3.6.0
pkgrel=1
pkgdesc="A version of the ganglia package without gmetad, to reduce dependencies."
arch=('i686' 'x86_64')
url="http://ganglia.sourceforge.net/"
license=('BSD')
depends=('apr' 'confuse' 'python2' 'pcre')
conflicts=('ganglia')
options=('!libtool')
install='ganglia.install'
backup=('etc/ganglia/gmond.conf')
source=("http://downloads.sourceforge.net/ganglia/$_pkgbase-$pkgver.tar.gz"
        'gmond.service'
        'ganglia.install'
        'uid.patch')
md5sums=('05926bb18c22af508a3718a90b2e9a2c'
         '25ebc6cf829089a9f330ad177befdd45'
         '5720885c44d8bb244674f5454753e987'
         'a0d469dd2b9ec40dbe20377c51ca904c')

prepare() {
  cd "$srcdir/$_pkgbase-$pkgver"

  for patch in $srcdir/*.patch; do
    msg2 "Applying $(basename $patch)"
    patch -Np1 -i $patch
  done

  # FIXME: Hopefully this will soon no longer be required?
  msg2 "Copying systemd units into source as workaround for bug"
  cp "$srcdir/gmond.service" "$srcdir/$_pkgbase-$pkgver/gmond/gmond.service.in"
}

build() {
  cd "$srcdir/$_pkgbase-$pkgver"

  ./configure --prefix=/usr --libdir=/usr/lib --sysconfdir=/etc/ganglia \
              --enable-gexec --enable-status \
              --with-python=/usr/bin/python2
  make
}

package() {
  cd "$srcdir/$_pkgbase-$pkgver"
  make DESTDIR="$pkgdir" install

  # Move sbin to bin
  mv "$pkgdir/usr/sbin"/* "$pkgdir/usr/bin/"
  rmdir "$pkgdir/usr/sbin"

  mkdir -p "$pkgdir/usr/lib/ganglia/python_modules"

  msg2 "Generating default gmond.conf"
  ./gmond/gmond --default_config > "$pkgdir/etc/ganglia/gmond.conf"

  install -m 644 -D COPYING "$pkgdir/usr/share/licenses/${pkgname}/COPYING"
}
