# Maintainer: Jan Stępień <jstepien@users.sourceforge.net>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=gnuplot-luaterm
pkgver=4.4.2
pkgrel=2
pkgdesc="Plotting package which outputs to X11, PostScript, PNG, GIF, TikZ and others" 
arch=('i686' 'x86_64') 
url="http://www.gnuplot.info" 
license=('custom') 
depends=('readline' 'gd' 'wxgtk' 'cairo' 'libjpeg' 'lua') 
conflicts=('gnuplot')
provides=('gnuplot')
makedepends=('texinfo' 'emacs') 
_realname=gnuplot
install=gnuplot.install 
source=("http://downloads.sourceforge.net/sourceforge/$_realname/$_realname-$pkgver.tar.gz")
sha1sums=('35666be22e877ade5e272199d3d81555c141cef5')

build() {
  cd $srcdir/$_realname-$pkgver

  # fix default source location; use the GDFONTPATH variable to modify at runtime 
  sed -i 's|/usr/X11R6/lib/X11/fonts/truetype|/usr/share/fonts/TTF|' src/variable.c
  sed -i 's|/X11R6/lib/X11/fonts/Type1|/usr/share/fonts/Type1|' src/variable.c

  ./configure --prefix=/usr --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --libexecdir=/usr/bin \
              --with-gihdir=/usr/share/gnuplot \
              --datadir=/usr/share \
              --with-readline=gnu \
              --with-lua
  make pkglibexecdir=/usr/bin || return 1
}


package() {
  cd $srcdir/$_realname-$pkgver
  make prefix=$pkgdir/usr libexecdir=$pkgdir/usr/bin \
        mandir=$pkgdir/usr/share/man \
        infodir=$pkgdir/usr/share/info \
        pkglibexecdir=$pkgdir/usr/bin \
        GIHDIR=$pkgdir/usr/share/gnuplot \
        TEXMFLOCAL=$pkgdir/usr/share/texmf-dist \
        texdir=$pkgdir/usr/share/texmf/tex/latex/gnuplot \
        datadir=$pkgdir/usr/share install || return 1

  install -Dm644 lisp/dotemacs $pkgdir/usr/share/emacs/site-lisp/dotemacs || return 1
  install -Dm644 Copyright $pkgdir/usr/share/licenses/$pkgname/Copyright || return 1

  rm -f $pkgdir/usr/share/texmf-dist/ls-R
}
