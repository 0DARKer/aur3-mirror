# Contributer: Jamie <dyscoria@googlemail.com>

pkgname=nethack-x11
pkgver=3.4.3
pkgrel=4
pkgdesc='A single-player roguelike dungeon exploration game'
arch=('i686' 'x86_64')
url="http://www.nethack.org/"
license=('custom')
install=nethack-x11.install
depends=('gzip' 'ncurses' 'libxaw' 'libxmu' 'libxext' 'libxt' 'libx11')
conflicts=('nethack')
source=(http://downloads.sourceforge.net/nethack/nethack-343-src.tgz
        nethack-x11.patch)
md5sums=('21479c95990eefe7650df582426457f9'
         '4f90180ce9e306c003c7a1117f8ee6dd')

build(){
  cd "${srcdir}/nethack-${pkgver}"

  sh sys/unix/setup.sh || return 1

  patch -Np1 -i ../nethack-x11.patch || return 1

  make || return 1
  make PREFIX="${pkgdir}/usr" install || return 1

  sed -e 's|HACKDIR=.*/pkg/usr/|HACKDIR=/usr/|' \
      -i "${pkgdir}/usr/bin/nethack"

  install -D -m 0644 dat/license \
      "${pkgdir}/usr/share/licenses/nethack/license"
}
