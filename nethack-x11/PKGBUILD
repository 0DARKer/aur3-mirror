# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jamie <dyscoria@googlemail.com>
pkgname=nethack-x11
pkgver=3.4.3
pkgrel=7
pkgdesc='A single-player roguelike dungeon exploration game'
arch=('x86_64' 'i686')
url="http://www.nethack.org/"
license=('custom')
depends=('libxaw')
conflicts=('nethack')
backup=('etc/nethackrc')
source=("http://downloads.sourceforge.net/nethack/nethack-343-src.tgz"
        "nethack-x11.patch"
        "nethack-x11.png::http://bugs.gentoo.org/attachment.cgi?id=86458"
        "nethack-x11.desktop")
md5sums=('21479c95990eefe7650df582426457f9'
         '4f90180ce9e306c003c7a1117f8ee6dd'
         '22c24250220dd2f526eb09b43b96bc6a'
         'c0c7e16a2ef911c32fc33568f6b34522')

build() {
  cd "$srcdir/nethack-$pkgver"

  msg2 "Configuring..."
  sh sys/unix/setup.sh

  msg2 "Patching..."
  patch -Np1 -i ../nethack-x11.patch

  msg2 "Compiling..."
  make
}

package() {
  cd "$srcdir/nethack-$pkgver"

  make PREFIX="${pkgdir}/usr" install
  sed -e 's:HACKDIR=.*/pkg/usr/:HACKDIR=/usr/:' \
    -i "$pkgdir/usr/bin/nethack"
  # Make nethack read /etc/nethackrc at startup
  sed 's:26:26\nNETHACKOPTIONS=@/etc/nethackrc\nexport NETHACKOPTIONS:' \
    -i "$pkgdir/usr/bin/nethack"
  # Create /etc/nethackrc with OPTIONS=windowtype:X11
  echo "OPTIONS=windowtype:X11" > nethackrc
  install -Dm644 nethackrc "$pkgdir/etc/nethackrc"
  install -Dm644 dat/license \
    "$pkgdir/usr/share/licenses/$pkgname/license"

  # Example nethackrc
  install -Dm644 win/X11/nethack.rc \
    "$pkgdir/usr/share/doc/$pkgname/nethackrc"

  cd ..
  msg2 "Packaging menu entry and icon..."
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
