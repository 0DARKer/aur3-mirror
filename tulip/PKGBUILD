# Contributor: Thomas Aynaud <taynaud at gmail dot com>
# Contributor: moostik <mooostik_at_gmail.com>
# Maintainer: Alexey Stukalov <astukalov at gmail.com>

pkgname=tulip
pkgver=4.3.0
pkgrel=1
pkgdesc="An information visualization framework dedicated to the analysis and visualization of relational data"
arch=('i686' 'x86_64')
url="http://www.tulip-software.org"
license=('LGPL')
depends=('python2' 'qt4' 'libxml2' 'glew' 'freetype2' 'doxygen' 'libxml2' 'qtwebkit' 'ftgl')
optdepends=('python-sphinx: generates docucentation for python bindings')
makedepends=('cmake')
source=("http://downloads.sourceforge.net/project/auber/${pkgname}/${pkgname}_${pkgver}/${pkgname}-${pkgver}_src.tar.gz"
	"${pkgname}.profile"
	crash_handling.patch)
md5sums=('edaf4424875cd336f8dbc4b02047096d'
         'fa35918a3844ba4cbf494ac5ce8007f7'
         '86c0f601723616189ad43138b712a20e')

prepare() {
  cd "${srcdir}/${pkgname}"
  patch -p1 -i ../crash_handling.patch
  mkdir build
}

build() {
  cd "${srcdir}/${pkgname}/build"
  
  QT_ROOT=/usr/lib/qt4 cmake .. \
  	-DCMAKE_BUILD_TYPE=Release \
  	-DCMAKE_SKIP_RPATH=ON \
  	-DCMAKE_INSTALL_PREFIX="${pkgdir}/opt/${pkgname}" \
  	-DQT_MOC_EXECUTABLE=/usr/lib/qt4/bin/moc
  make
}

package() {
  cd "${srcdir}/${pkgname}/build"
  make install
  # strip ${pkgdir} from tulip-config
  sed -i -e "s!${pkgdir}!!g" ${pkgdir}/opt/tulip/bin/tulip-config
  # shell profile for running tulip
  install -D "${srcdir}/${pkgname}.profile" "${pkgdir}/etc/profile.d/${pkgname}.sh"
}

