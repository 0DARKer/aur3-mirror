# Maintainer: Bjorn Neergaard (neersighted) <bjorn@neersighted.com>
# Contributor: Elkin Cruz <agnelkincc@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgroot=shedskin
pkgname=shedskin-git
pkgver=latest
pkgrel=2
pkgdesc='Experimental (restricted) Python-to-C++ compiler (development version)'
url='http://code.google.com/p/shedskin/'
license=('GPL3')
source=("${pkgroot}::git+https://gitorious.org/shedskin/mainline.git")
sha1sums=('SKIP')
arch=('any')
depends=('pcre' 'gc' 'python2')
makedepends=('git' 'python2')
conflicts=('shedskin')
provides=('shedskin')

pkgver() {
  cd "${srcdir}/${pkgroot}"

  # Get the version number.
  git describe --always | sed 's/^v//;s/-/./g'
}

prepare() {
  cd "${srcdir}/${pkgroot}"

  # Patch Python versions.
  sed -i -e 's/python\b/python2/g' $(grep -rl 'python\b')
}

build() {
  cd "${srcdir}/${pkgroot}"

  # Build it!
  python2 setup.py build
}

package() {
  cd "${srcdir}/${pkgroot}"

  # Install the program.
  python2 setup.py install \
    --root="${pkgdir}" \
    --prefix=/usr
}

# vim: ft=sh ts=2 sw=2 et
