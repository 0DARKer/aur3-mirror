# Maintainer: Neer Sighted (neersighted) <neersighted@myopera.com>
# Contributor: Elkin Cruz <agnelkincc@gmail.com>
# Contributor: Alexander Rødseth <rodseth@gmail.com>

pkgname=shedskin-git
pkgver=0.9.3.6.g86f101d
pkgrel=4
pkgdesc='Experimental (restricted) Python-to-C++ compiler (development version)'
url='http://code.google.com/p/shedskin/'
license=('GPL3')
source=('shedskin::git://gitorious.org/shedskin/mainline.git')
md5sums=('SKIP')
sha1sums=('SKIP')
sha256sums=('SKIP')
arch=('any')
depends=('pcre' 'gc' 'python2')
makedepends=('git')
conflicts=('shedskin')
provides=('shedskin')

pkgver() {
  cd "${srcdir}/shedskin"

  git describe --always | sed 's/^v//;s/-/./g'
}

prepare() {
  cd "${srcdir}/shedskin"

  # Patch Python versions.
  sed -i -e "s/python\b/python2/g" `grep -lir "python\b"`
  sed -i -e "s/lpython2\b/lpython/g" `grep -lir "lpython2\b"`
}

build() {
  cd "${srcdir}/shedskin"

  # Build it!
  python2 setup.py build
}

package() {
  cd "${srcdir}/shedskin"

  # Install the program.
  python2 setup.py install --root="${pkgdir}" --prefix=/usr
}

# vim: set ts=2 sw=2 et:
