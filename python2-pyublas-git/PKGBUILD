pkgname=python2-pyublas-git
pkgver=20110227
pkgrel=1
pkgdesc="Seamless glue layer between Numpy and Boost.Ublas"
arch=('i686' 'x86_64')
url="http://mathema.tician.de/software/pyublas"
license=('BSD')
depends=('boost' 'python2-numpy')
makedepends=('python2-distribute' 'git')

_gitroot="http://git.tiker.net/trees/pyublas.git"
_gitname="pyublas"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  python2 ./configure.py --boost-inc-dir=/usr/include/boost \
                         --boost-lib-dir=/usr/lib \
                         --boost-python-libname=boost_python-mt \
                         --prefix=/usr

  python2 setup.py build
}

package() {
   cd "${srcdir}/${_gitname}-build"

   python2 setup.py install --root="${pkgdir}" --skip-build --optimize=1
}
