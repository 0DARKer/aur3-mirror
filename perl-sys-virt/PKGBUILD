# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
_author="Daniel P. Berrange"
_perlmod="sys-virt"
pkgname=perl-${_perlmod}
pkgver=0.9.7
pkgrel=1
pkgdesc="Represent and manage a libvirt hypervisor connection"
arch=('any')
url="http://search.cpan.org/dist/Sys-Virt/"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.10.0')
makedepends=('perl-test-pod-coverage' 'perl-xml-xpath' 'libvirt')
options=(!emptydirs)
source=("http://www.cpan.org/authors/id/D/DA/DANBERR/Sys-Virt-${pkgver}.tar.gz")
sha512sums=('53768a6123170622c5ba84c77bb8a8a8a89de6ac1f27b68228cdb242cd919d50f9977f3806e10cd9b37e3c11405322d6320361c6b9a11eaf966b386ef8ce0063')

build() {
  cd "${srcdir}/Sys-Virt-${pkgver}"

  # Install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "${srcdir}/Sys-Virt-${pkgver}"
  make install DESTDIR="${pkgdir}/"

  # From Fedora RPM spec file - remove empty '.packlist' and '*.bs' files
  find "${pkgdir}" -type f \( -name .packlist -o -name '*.bs' -empty \) | xargs rm -vf
}

# vim:set ts=2 sw=2 et:
