# Maintainer: Ray Rashif <schiv@archlinux.com>
# Contributor: galiyosha@gmail.com
# Contributor: Jon Kristian Nilsen <jokr.nilsen@gmail.com>

pkgname=libffado-svn
pkgver=2186
pkgrel=1
pkgdesc="Driver for FireWire audio devices (development version)"
arch=('i686' 'x86_64')
url="http://www.ffado.org/"
license=('GPL')
depends=('libiec61883' 'libavc1394' 'libsigc++'
         'libxml++' 'alsa-lib' 'dbus-c++' 'libconfig')
makedepends=('subversion' 'scons' 'python2-pyqt')
optdepends=('python2-pyqt: mixer applet')
provides=('libffado')
conflicts=('libffado' 'ffado-svn')
replaces=('ffado-svn')
source=('python2-qt.patch')
md5sums=('10aa837f5521da213caa93e1ddc58c0e')

_svntrunk=http://subversion.ffado.org/ffado/trunk/libffado
_svnmod=libffado

_scons-conf() {
  scons DEBUG=True \
        PREFIX=/usr \
        MANDIR=/usr/share/man \
        UDEVDIR=/usr/lib/udev/rules.d \
        COMPILE_FLAGS="$CFLAGS" \
        ENABLE_OPTIMIZATIONS=True $@
}

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # BUILD
  #

  # python2 fix
  patch -Np1 -i "$srcdir/python2-qt.patch"

  _scons-conf
}

package() {
  cd "$srcdir/$_svnmod-build"

  scons DESTDIR="$pkgdir" WILL_DEAL_WITH_XDG_MYSELF="True" install

  # desktop files & icons
  install -Dm644 support/xdg/ffado.org-ffadomixer.desktop \
    "$pkgdir/usr/share/applications/ffadomixer.desktop"

  install -Dm644 support/xdg/hi64-apps-ffado.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
