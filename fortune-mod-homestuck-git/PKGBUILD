# Maintainer: Hector A Escobedo <ninjahector dot escobedo at gmail dot com>

pkgname=fortune-mod-homestuck-git
pkgver=r31.964c149
pkgrel=1
pkgdesc="Colorized quotes from characters in the Homestuck webcomic"
arch=('any')
url="http://mspaintadventures.com/"
license=('None')
depends=('fortune-mod')
makedepends=('git' 'm4' 'make' 'ncurses')
install=usage.install
source=(git://github.com/HectorAE/fortunes.git)
md5sums=('SKIP')

_gitname=fortunes

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_gitname"
  ./configure
  make all-homestuck
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir/" install
}
