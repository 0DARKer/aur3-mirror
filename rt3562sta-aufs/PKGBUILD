# $Id: PKGBUILD 121240 2014-10-22 21:35:43Z thomas $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Based on SUSE spec https://build.opensuse.org/package/files?package=rt3562sta&project=driver%3Awireless
# port to aufs kernel: GI_Jack <iamjacksemail@hackermail.com>

_pkgname=rt3562sta
pkgname=rt3562sta-aufs
pkgver=2.4.1.1_r1
_patchrel=${pkgver/*_r/}
pkgrel=12
pkgdesc="Ralink RT3562 PCI WLAN adaptors kernel module(aufs_friendly kernel)"
arch=(i686 x86_64)
url="http://www.mediatek.com/en/Products/support.php?sn=501"
license=('GPL')
depends=('linux-aufs_friendly')
makedepends=('linux-aufs_friendly-headers')
install=$_pkgname.install
source=($_pkgname-$pkgver.tar.gz::https://github.com/mtorromeo/rt3562sta-linux/archive/r${_patchrel}.tar.gz)

build() {
	_kernver=$(pacman -Q linux | cut -d . -f 2 | cut -f 1 -d -)
	KERNEL_RELEASE=$(cat /usr/lib/modules/extramodules-3.$_kernver-aufs_friendly/version)

	cd "$srcdir/rt3562sta-linux-r$_patchrel"

	# clean up this mess of mixing RT2860STA with RT3562STA
	# in documentation files
	mv RT2860STA.dat RT3562STA.dat
	mv RT2860STACard.dat RT3562STACard.dat
	sed -i 's/2860/3562/g' *STA* iwpriv_usage.txt

	# as we change the default name of the interface from raX to wlanX, change respective references in documentation, too
	sed -i 's|ra0|wlan0|g' *.txt README* *.dat
	sed -i 's|ra1|wlan1|g' *.txt README* *.dat
	sed -i 's|ra2|wlan2|g' *.txt README* *.dat

	export EXTRA_CFLAGS="-DVERSION=$pkgver"

	# this Makefile is far too strict...
	echo "LINUX_SRC = /usr/lib/modules/$KERNEL_RELEASE/build" >> Makefile

	make
}

package() {
	_kernver=$(pacman -Q linux-aufs_friendly | cut -d . -f 2 | cut -f 1 -d -)
	depends=("linux-aufs_friendly>=3.${_kernver}" "linux-aufs_friendly<3.$(expr ${_kernver} + 1)")
	KERNEL_VERSION=$(cat /usr/lib/modules/extramodules-3.$_kernver-aufs_friendly/version)
	msg "Kernel = $KERNEL_VERSION"

	cd "$srcdir/rt3562sta-linux-r$_patchrel"

	install -Dm 0640 RT3562STA.dat "$pkgdir/etc/Wireless/RT3562STA/RT3562STA.dat"
	install -Dm 0644 os/linux/$_pkgname.ko "$pkgdir/usr/lib/modules/extramodules-3.$_kernver-aufs_friendly/$_pkgname.ko"
	install -dm 0755 "$pkgdir/usr/share/doc/$_pkgname"
	install -m 0644 iwpriv_usage.txt README* RT3562STA* sta_ate_iwpriv_usage.txt "$pkgdir/usr/share/doc/$_pkgname"

	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
	sed -i "s|extramodules-.*-aufs_friendly|extramodules-3.$_kernver-aufs_friendly|" "$startdir/$_pkgname.install"
}

sha256sums=('6fd6306b6fb3886bbf9d284a28cc065a4cdfea18a03708e99aa4c7a038bcf02f')
