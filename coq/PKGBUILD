# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: acieroid
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: George Giorgidze <giorgidze@gmail.com>
pkgname=coq
pkgver=8.4
pkgrel=4
pkgdesc='Formal proof management system.'
arch=('i686' 'x86_64')
url='http://coq.inria.fr/'
license=('GPL')
options=('!emptydirs')
depends=('gtk2' 'ocaml')
makedepends=('lablgtk2' 'camlp5-transitional' 'netpbm' 'hevea' 'texlive-latexextra' 'texlive-fontsextra' 'texlive-pictures')
source=("http://coq.inria.fr/distrib/V$pkgver/files/coq-$pkgver.tar.gz"
  "ocaml-400.patch")
md5sums=('f28662cd687f66ed3c372ca3d35ea928'
         '74398c301a9abc4c7b60c7650a4106cb')

build() {
  cd "coq-$pkgver"

  patch -uNp1 -i $srcdir/ocaml-400.patch

  ./configure \
    -prefix '/usr' \
    -mandir '/usr/share/man' \
    -configdir '/etc/xdg/coq/' \
    -opt \
    -with-doc yes

  # The Makefile doesn't seem to support building in parallel (weird failures)
  make -j1 doc
  make -j1
}

package() {
  cd "coq-$pkgver"

  make COQINSTALLPREFIX="$pkgdir" install
  make COQINSTALLPREFIX="$pkgdir" install-doc
}
