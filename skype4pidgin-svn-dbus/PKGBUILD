# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: DonVla <donvla@users.sourceforge.net>

pkgname=skype4pidgin-svn-dbus
pkgver=615
pkgrel=1
pkgdesc="Skype plugin for Pidgin and Empathy (with X11 and dbus support)"
arch=("i686" "x86_64")
url="http://skype4pidgin.googlecode.com/svn/trunk/"
license=('GPL3')
depends=('libpurple' 'skype')
makedepends=('pkg-config')
conflicts=('skype4pidgin' 'skype4pidgin-svn')
replaces=('skype4pidgin' 'skype4pidgin-svn')
install=${pkgname}.install
source=(Makefile.patch)
_svntrunk=http://skype4pidgin.googlecode.com/svn/trunk/
_svnmod=skype4pidgin-read-only
sha256sums=('fdf095911d5e81b4f8fd36d8059125339eb3ebb0409c6e481c671cc55f245193')

build() {
  cd "${srcdir}"

  if [ -d ${_svnmod}/.svn ]; then
    (cd ${_svnmod} && svn up -r ${pkgver})
  else
    svn co ${_svntrunk} --config-dir ./ -r ${pkgver} ${_svnmod}
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_svnmod}-build"
  cp -r "${srcdir}/${_svnmod}" "${srcdir}/${_svnmod}-build"
  cd "${srcdir}/${_svnmod}-build"

  patch -p0 -i ${srcdir}/Makefile.patch

  make all
}

package() {
  cd "${srcdir}/${_svnmod}-build"

  install -D -m 0755 libskype.so "${pkgdir}$(pkg-config --variable=plugindir purple)/libskype.so"
  install -D -m 0755 libskype_dbus.so "${pkgdir}$(pkg-config --variable=plugindir purple)/libskype_dbus.so"

  install -D -m 0644 icons/16/skype.png ${pkgdir}/usr/share/pixmaps/pidgin/protocols/16/skype.png
  install -D -m 0644 icons/22/skype.png ${pkgdir}/usr/share/pixmaps/pidgin/protocols/22/skype.png
  install -D -m 0644 icons/48/skype.png ${pkgdir}/usr/share/pixmaps/pidgin/protocols/48/skype.png
}
