# Maintainer: Tom Wambold <tom5760@gmail.com>
pkgname=core
pkgver=4.7
pkgrel=1
pkgdesc="Common Open Research Emulator"
arch=('i686' 'x86_64')
url="http://cs.itd.nrl.navy.mil/work/core/"
license=('BSD')
depends=('libev' 'ebtables' 'iproute2' 'python2' 'bridge-utils' 'tkimg'
         'xterm')
makedepends=('dia' 'help2man' 'imagemagick')
conflicts=('core-svn')
backup=('etc/core/core.conf' 'etc/core/perflogserver.conf')
source=("http://downloads.pf.itd.nrl.navy.mil/core/source/$pkgname-$pkgver.tar.gz"
        'core-daemon.service'
        'python2.patch'
        'rj45-ifconfig.patch')
md5sums=('ebf695b34b882887b2437112e3a46fb8'
         '256e39a074f36f7f1683e88324682c9e'
         '215d9122b2370385a09d1d19cd4e9d2c'
         '523c1ea1166e9406cb2926ebcd7affc4')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 < ../python2.patch
  patch -p1 < ../rj45-ifconfig.patch

  ./configure CPPFLAGS=-Wno-strict-aliasing PYTHON=/usr/bin/python2 --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  rm "$pkgdir/etc/init.d/core-daemon"
  rmdir "$pkgdir/etc/init.d"

  install -D "$srcdir/core-daemon.service" "$pkgdir/usr/lib/systemd/system/core-daemon.service"
}

# vim:set ts=2 sw=2 et:
