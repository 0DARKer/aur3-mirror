# Maintainer: Tom Wambold <tom5760@gmail.com>
pkgname=core
pkgver=4.2
pkgrel=1
pkgdesc="Common Open Research Emulator"
arch=('i686' 'x86_64')
url="http://cs.itd.nrl.navy.mil/work/core/"
license=('BSD')
depends=(libev ebtables iproute2 python2 bridge-utils tkimg xterm lxc)
makedepends=(dia)
optdepends=(nrl-quagga quagga)
source=(http://downloads.pf.itd.nrl.navy.mil/core/source/$pkgname-src-$pkgver.tgz
        cored
        configure-libev-4.patch
        python-Makefile-quoting.patch
        python2.patch)
md5sums=('86768aba833c5f6631590ee1b311a582'
         'b4c0bf514fb46bb97a459e9eabcec27b'
         'b6a9d6487bd876e19af59eed891c8594'
         '46abc97c5b638562ae068eabb166f02c'
         '3b8278cfeb1a333c189c291a97b36bd6')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 < ../configure-libev-4.patch
  patch -p1 < ../python-Makefile-quoting.patch
  patch -p1 < ../python2.patch

  ./bootstrap.sh
  ./configure PYTHON=/usr/bin/python2 libev_CFLAGS="-D_GNU_SOURCE -DEV_COMPAT3=1 -DEV_PROTOTYPES=1" libev_LIBS= --prefix=/usr

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  rm "$pkgdir/etc/init.d/core"
  rmdir "$pkgdir/etc/init.d"

  install -D "$srcdir/cored" "$pkgdir/etc/rc.d/cored"
}

# vim:set ts=2 sw=2 et:
