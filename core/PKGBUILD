# Maintainer: Tom Wambold <tom5760@gmail.com>
pkgname=core
pkgver=4.4
pkgrel=3
pkgdesc="Common Open Research Emulator"
arch=('i686' 'x86_64')
url="http://cs.itd.nrl.navy.mil/work/core/"
license=('BSD')
depends=('libev' 'ebtables' 'iproute2' 'python2' 'bridge-utils' 'tkimg'
         'xterm')
makedepends=('dia' 'help2man' 'imagemagick')
conflicts=('core-svn')
backup=('etc/core/core.conf' 'etc/core/perflogserver.conf')
source=("http://downloads.pf.itd.nrl.navy.mil/core/source/$pkgname-$pkgver.tar.gz"
        'cored'
        'core-emulator.service'
        'python2.patch'
        'netns-mount-root-slave.patch'
        'core-gui-wish-version.patch')
md5sums=('159b459c9b8a6320ddd20955b601007f'
         'a72620009008adf36f0ebb222de0df93'
         'ff15ae39fb384863cfb154741830eb70'
         '76d354310aff5d4a4a53afb2860a0518'
         'd4a04fa92e923226f0e83eeb9b858ddd'
         'dabf3af70ba80a413da9879509be0259')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 < ../python2.patch
  patch -p1 < ../netns-mount-root-slave.patch
  patch -p1 < ../core-gui-wish-version.patch

  ./configure PYTHON=/usr/bin/python2 --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  rm "$pkgdir/etc/init.d/core"
  rmdir "$pkgdir/etc/init.d"

  install -D "$srcdir/cored" "$pkgdir/etc/rc.d/cored"
  install -D "$srcdir/core-emulator.service" "$pkgdir/usr/lib/systemd/system/core-emulator.service"
}

# vim:set ts=2 sw=2 et:
