# Maintainer: Tom Wambold <tom5760@gmail.com>
pkgname=core
pkgver=4.5
pkgrel=2
pkgdesc="Common Open Research Emulator"
arch=('i686' 'x86_64')
url="http://cs.itd.nrl.navy.mil/work/core/"
license=('BSD')
depends=('libev' 'ebtables' 'iproute2' 'python2' 'bridge-utils' 'tkimg'
         'xterm')
makedepends=('dia' 'help2man' 'imagemagick')
conflicts=('core-svn')
backup=('etc/core/core.conf' 'etc/core/perflogserver.conf')
source=("http://downloads.pf.itd.nrl.navy.mil/core/source/$pkgname-$pkgver.tar.gz"
        'cored'
        'core-emulator.service'
        'python2.patch'
        'core-gui-wish-version.patch'
        'rj45-ifconfig.patch')
md5sums=('b1f045886a2fe0a5bb18e7d6ea7e03d8'
         'a72620009008adf36f0ebb222de0df93'
         'a49c24c341b53a969ee93806b701915c'
         '1b22a0ad28499813c1dd35c36d507d8a'
         'd72eb6ca3bc9ed388532e310a8de5735'
         '523c1ea1166e9406cb2926ebcd7affc4')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 < ../python2.patch
  patch -p1 < ../core-gui-wish-version.patch
  patch -p1 < ../rj45-ifconfig.patch

  ./configure PYTHON=/usr/bin/python2 --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  rm "$pkgdir/etc/init.d/core"
  rmdir "$pkgdir/etc/init.d"

  install -D "$srcdir/cored" "$pkgdir/etc/rc.d/cored"
  install -D "$srcdir/core-emulator.service" "$pkgdir/usr/lib/systemd/system/core-emulator.service"
}

# vim:set ts=2 sw=2 et:
