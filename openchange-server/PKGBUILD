# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>
# Contributor: Michael Hansen <zrax0111 gmail com>
# Contributor: ngoonee <ngoonee.talk@gmail.com>
# Contributor: Adam Russell <adamlr6+arch@gmail.com>
pkgname=openchange-server
_pkgname=openchange
_codename=NANOPROBE
_dlid=246
pkgver=2.2
pkgrel=4
pkgdesc="A portable, open source implementation of Microsoft Exchange server \
and Exchange protocols."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://www.openchange.org"
license=('GPL3')
depends=('boost' 'file' 'libical' 'python2' 'samba>=4.1.11' 'sqlite3' 'perl-parse-yapp' 'mysql-python')
makedepends=('ccache' 'docbook-xsl' 'libxslt')
optdepends=('mod_wsgi' 'python2-pylons' 'python2-lxml' 'python2-pycurl')
options=(!makeflags)
conflicts=('openchange')
replaces=('openchange')
provides=('openchange')
# Releases are mirrored at http://tracker.openchange.org/projects/openchange/files
source=("http://tracker.openchange.org/attachments/download/${_dlid}/${_pkgname}-${pkgver}-${_codename}.tar.gz"
        "ocsmanager.service"
        "readme.ocsmanager"
        "readme.rpcproxy")

sha256sums=('f15eb26d16370e85f01649300dd6722c31f0750437a4b60acd604a79439cf688'
            '45bd19e2a5725a94692ae606086be6d57423375c9b1c0eb5322c6e09ef2b5fb3'
            '77e75ff7fe3572bc0a1e63b1cfe7c8bafb0fa5ccd3f57e2a89fe731f458ef676'
            '0a532e3086bb59267e6d4c03c8b0aa7133f717e5636d21a99601c8c0d75b1e77')

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}-${_codename}

    PYTHON_CALLERS="$(find ${srcdir}/${_pkgname}-${pkgver}-${_codename} -name '*.py')
                    $(find ${srcdir}/${_pkgname}-${pkgver}-${_codename} -name 'configure.ac')
                    setup/openchange_newuser setup/openchange_provision
                    mapiproxy/services/web/rpcproxy/rpcproxy.wsgi"
    sed -i -e "s|/usr/bin/env python$|/usr/bin/env python2|" \
           -e "s|python-config|python2-config|" \
           -e "s|bin/python|bin/python2|" \
        ${PYTHON_CALLERS}

    # Fix linking of boost_thread in autoconf test
    sed -i -e "s|-lboost_thread\$BOOST_LIB_SUFFIX|-lboost_thread\$BOOST_LIB_SUFFIX -lboost_system\$BOOST_LIB_SUFFIX|" \
        configure.ac

    # Fix type error in openchang-provision
    sed 's@  self.server_id)@  (self.server_id,))@' \
        -i python/openchange/mailbox.py

    export PYTHON=/usr/bin/python2

    export PKG_CONFIG_PATH="/usr/samba/lib/pkgconfig:/usr/lib/pkgconfig"
    ./autogen.sh
    ./configure --prefix=/usr \
                --sbindir=/usr/bin \
                --enable-pyopenchange \
                --with-modulesdir=/usr/lib/samba/modules \
                --with-datadir=/usr/share/samba

    # Fix build on ARCH!=x86_64
    rm mapiproxy/libmapi{proxy,store}/backends/*.po
    rm mapiproxy/util/*.po

    make FLEX=/usr/bin/flex BISON=/usr/bin/bison || return 1
}

package() {
    _pyver=`python2 -c 'import sys; print(sys.version[:3])'`

    cd ${srcdir}/${_pkgname}-${pkgver}-${_codename}
    make DESTDIR="$pkgdir/" install

    cd ${pkgdir}/usr/lib/
    ln -s libmapi.so libmapi.so.0
    ln -s libocpf.so libocpf.so.0

    # Install OCSManager setup files
    cd ${srcdir}/${_pkgname}-${pkgver}-${_codename}/mapiproxy/services/ocsmanager
    find ${pkgdir} -name '*.pth' -delete
    install -vdm755 ${pkgdir}/usr/share/samba/setup/ocsmanager
    cp -Rv * ${pkgdir}/usr/share/samba/setup/ocsmanager
    install -vm644 ${srcdir}/ocsmanager.service \
                   ${pkgdir}/usr/share/samba/setup/ocsmanager/ocsmanager.service
    install -vm644 ${srcdir}/readme.ocsmanager \
                   ${pkgdir}/usr/share/samba/setup/ocsmanager/README.arch

    find ${pkgdir}/usr/lib/python${_pyver}/site-packages/ -name '*.py' | \
         xargs sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|"

    # Install rpcproxy setup files
    cd ${srcdir}/${_pkgname}-${pkgver}-${_codename}/mapiproxy/services/web/rpcproxy
    install -vdm755 ${pkgdir}/usr/share/samba/setup/rpcproxy
    cp -Rv * ${pkgdir}/usr/share/samba/setup/rpcproxy
    install -vm644 ${srcdir}/readme.rpcproxy \
                   ${pkgdir}/usr/share/samba/setup/rpcproxy/README.arch
}
