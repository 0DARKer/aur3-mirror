
# Maintainer: RunningDroid <rningdrd AT tormail.net>

pkgname=lwjgl-git
pkgver=20130305
pkgrel=1
pkgdesc="Lightweight Java Game Library - for use in game projects in Java. | git version"
arch=('i686' 'x86_64')
url="http://lwjgl.org"
license=('BSD')
changelog=changes.log
makedepends=('git' 'apache-ant')
provides=(lwjgl)
conflicts=(lwjgl)
options=(!strip)
# preparing for pacman 4.1
#source=(git+https://github.com/LWJGL/lwjgl.git)
#md5sums=('SKIP')

_gitroot=https://github.com/LWJGL/lwjgl.git
_gitname=lwjgl

# Again, preparing for pacman 4.1
#pkgver() {
#  cd "$srcdir/$_gitname"
#  echo "$(git shortlog | grep -c '\s\+').$(git describe --always)"
#}

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ant all

}

package() {
  cd "$srcdir/$_gitname-build"

  # install licenses
  install -d "$pkgdir/usr/share/licenses/$pkgname/3rdparty"
  install -m644 -t "$pkgdir/usr/share/licenses/$pkgname/3rdparty" doc/3rdparty/*
  install -m644 -t "$pkgdir/usr/share/licenses/$pkgname" doc/LICENSE

  # install docs
  install -d "$pkgdir/usr/share/doc/$pkgname"
  install -m644 -t "$pkgdir/usr/share/doc/$pkgname" doc/{CREDITS,README,lwjgl_hidden_switches.text}

  # install everything else
  install -d "$pkgdir/usr/share/lwjgl/native"
  mv libs/{linux,macosx,solaris,windows} "$pkgdir/usr/share/lwjgl/native"

  install -d "$pkgdir/usr/share/lwjgl/jar"
  install -m644 -t "$pkgdir/usr/share/lwjgl/jar" libs/*

  install -d "$pkgdir/usr/share/lwjgl/res"
  mv res/* "$pkgdir/usr/share/lwjgl/res"

}

# vim:set ts=2 sw=2 et:
