# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=uhub-git
pkgver=0.4.1.40.gc26e8aa
pkgrel=1
pkgdesc="A hub for the ADC network. (GIT Version)"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.uhub.org/"
depends=('sqlite3' 'systemd')
makedepends=('cmake')
source=('git://github.com/janvidar/uhub.git'
        'uhub.service'
        'uhub.tmpfiles')
sha1sums=('SKIP'
          'a72699fb05ba6ad48a3ff0d18d8ef2904c29c6a5'
          'fa06f77fe35eee8dea6c8d2be81486a2ecc35a5b')
install="uhub.install"
backup=('etc/uhub/motd.txt'
        'etc/uhub/plugins.conf'
        'etc/uhub/rules.txt'
        'etc/uhub/uhub.conf'
        'etc/uhub/users.conf')
_gitname="uhub"

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --always | tr - .)"
}

prepare() {
  cd "${_gitname}"
  sed 's|/var/log/uhub.log|/var/log/uhub/uhub.log|g' -i doc/plugins.conf
}

build() {
  cd "${_gitname}"
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DSYSTEMD_SUPPORT=ON -DLINK_SUPPORT=ON

  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 doc/users.conf "${pkgdir}/etc/uhub/users.conf"
  install -Dm644 doc/rules.txt "${pkgdir}/etc/uhub/rules.txt"
  touch "${pkgdir}/etc/uhub/motd.txt"

  install -Dm644 "${srcdir}/uhub.service" "${pkgdir}/usr/lib/systemd/system/uhub.service"
  install -Dm644 "${srcdir}/uhub.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/uhub.conf"

  install -d "${pkgdir}/var/log/uhub"
  install -dm750 "${pkgdir}/var/lib/uhub"

  install -Dm644 doc/init.d.RedHat/etc/logrotate.d/uhub "${pkgdir}/etc/logrotate.d/uhub"

  install -Dm644 doc/uhub.1 "${pkgdir}/usr/share/man/man1/uhub.1"
  install -Dm644 doc/uhub-passwd.1  "${pkgdir}/usr/share/man/man1/uhub-passwd.1"

  install -Dm644 doc/architecture.txt "${pkgdir}/usr/share/doc/uhub/architecture.txt"
  install -Dm644 doc/extensions.txt "${pkgdir}/usr/share/doc/uhub/extensions.txt"
  install -Dm644 doc/getstarted.txt "${pkgdir}/usr/share/doc/uhub/getstarted.txt"
}
