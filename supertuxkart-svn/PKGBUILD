# Maintainer: t3ddy  <t3ddy1988 "at" gmail {dot} com>

pkgname=supertuxkart-svn
pkgver=10488
pkgrel=1
pkgdesc="A new and improved version of TuxKart, a kart racing game featuring Tux and his friends"
url="http://supertuxkart.sourceforge.net/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('sdl>=1.2' 'libvorbis' 'freealut' 'libgl' 'freeglut' 'irrlicht-svn' 'fribidi')
makedepends=('subversion')
provides=('supertuxkart')
conflicts=('supertuxkart' 'supertuxkart-alpha')

_svnmod="supertuxkart"
_svntrunk="https://supertuxkart.svn.sourceforge.net/svnroot/supertuxkart/main/trunk"

build() {
  msg "Starting SVN checkout..."
  cd $srcdir
    if [ -d $_svnmod/.svn ]; then
      (cd $_svnmod && svn up -r $pkgver)
    else
      svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
    fi
  msg2 "SVN checkout done or server timeout"

  msg "Starting make..."

  if [ -d ${srcdir}/$_svnmod-build ]; then
    rm -rf $_svnmod-build
  fi
  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build

#   cd $_svnmod
  ./autogen.sh
  ./configure --prefix=/usr --datadir=/usr/share

  # those configure flags really mean nothing...
  sed -i "s#/usr/local#/usr#" src/io/file_manager.cpp
  sed -i "s#/games##" $(grep -Rl "/games" *)

  make
}

package() {
#   cd ${srcdir}/$_svnmod
  cd ${srcdir}/$_svnmod-build

  make DESTDIR=${pkgdir} install

  # fix executable location...
  install -dm755 ${pkgdir}/usr/bin
  mv ${pkgdir}/usr/supertuxkart ${pkgdir}/usr/bin/supertuxkart

  sed -i "s#usr/supertuxkart#usr/bin/supertuxkart#" ${pkgdir}/usr/share/applications/supertuxkart.desktop


  # fix problems with tracks data
  cp -r ${srcdir}/$_svnmod/data/tracks ${pkgdir}/usr/share/supertuxkart/data/
  rm ${pkgdir}/usr/share/supertuxkart/data/tracks/Makefile.am

  # fix problems with karts data
  cp -r ${srcdir}/$_svnmod/data/karts ${pkgdir}/usr/share/supertuxkart/data/
  rm ${pkgdir}/usr/share/supertuxkart/data/karts/Makefile.am
}