# Maintainer: Michal Donat <michal.donat@atlas.cz>
# Contributor: t3ddy  <t3ddy1988 "at" gmail {dot} com>
# Contributor: Lluixhi <lluixhi (at) Gmail (dot) com>

pkgname=supertuxkart-svn
pkgver=12305
pkgrel=1
pkgdesc="A kart racing game featuring Tux and his friends - development version"
url="http://supertuxkart.sourceforge.net/"
license=("cc-by-sa-3.0")
arch=('i686' 'x86_64')
makedepends=("subversion" "cmake")
depends=("sdl>=1.2" "libvorbis" "freealut" "libgl" "freeglut" "fribidi")
conflicts=("supertuxkart")
replaces=("supertuxkart-cmakesvn")

_svntrunk="https://supertuxkart.svn.sourceforge.net/svnroot/supertuxkart/main/trunk/"
_svnmod="${pkgname%-svn}"

build() {

	# Get source

	cd "${srcdir}"
	msg "Connecting to SVN server...."

	if [[ -d "$_svnmod/.svn" ]]; then
	  cd "$_svnmod" && svn up
	else
	  svn co "$_svntrunk" "$_svnmod"
	  cd $_svnmod
	fi

	msg "SVN checkout done or server timeout"
	msg "Starting build..."

	# Build irrlicht
	
	cd lib/irrlicht/source/Irrlicht/
	NDEBUG=1 make
	cd -

	# Build stk
	
	[ -d "build" ] && rm -r build
	mkdir build && cd build
	
	cmake -DCMAKE_INSTALL_PREFIX=/usr ..
	make
}

package() {
	cd "${_svnmod}/build"
	make DESTDIR=${pkgdir} install
}
