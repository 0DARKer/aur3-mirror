# Maintainer: Lone_Wolf <lonewolf@xs4all.nl>
# Contributor: Perry3D <perry3d@gmail.com>

pkgname=mesa-demos-git
pkgver=20120117
_realver=8.1
pkgrel=1
pkgdesc="The mesa demos (glxinfo, glxgears, ...), built from the git master branch."
arch=(i686 x86_64)
depends=('glew' 'freeglut')
makedepends=('git')
provides=("mesa-demos")
conflicts=('mesa-demos')
url="http://mesa3d.org/"
license=(custom)
source=(LICENSE)
md5sums=('5c65a0fe315dd347e09b1f2826a1df5a')
options=()

_gitroot="git://anongit.freedesktop.org/mesa/demos"
_gitname="demos"

build() 
{
  msg "Connecting to git.freedesktop.org GIT server...."
  if [ -d $srcdir/$_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi
  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf $srcdir/$_gitname-build
  cp -rH $srcdir/$_gitname $srcdir/$_gitname-build
  cd $srcdir/$_gitname-build

  ./autogen.sh --prefix=/usr
  make
}
package()
{
  cd $srcdir/$_gitname-build
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/mesa-demos-git"
  install -m755 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa-demos-git/"
}
