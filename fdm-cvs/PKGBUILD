# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: Grigorios Bouzakis <grbzks@gmail.com>

pkgname=fdm-cvs
pkgver=20111116
pkgrel=1
pkgdesc='Program to fetch mail and deliver it in various ways'
url='http://fdm.sourceforge.net/'
license=('BSD')
arch=('i686' 'x86_64')
depends=('openssl' 'tdb')
makedepends=('cvs')
options=('!makeflags')
source=('GNUmakefile.patch')
sha1sums=('efc159c63d78eaeb20473d3595f6a9c2bcfd9a3e')

_cvsroot=':pserver:anonymous@fdm.cvs.sourceforge.net:/cvsroot/fdm'
_cvsmod='fdm'

conflicts=('fdm')

build() {
	cd "${srcdir}"
	cvs -z3 -d "${_cvsroot}" co -f "${_cvsmod}"
	cd "${_cvsmod}"

	patch -p1 -i ../GNUmakefile.patch
	./configure
	make
}

package() {
	cd "${srcdir}/${_cvsmod}"
	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}"/usr/share/{doc,licenses}/"${pkgname}"
	cp -r examples "${pkgdir}/usr/share/doc/${pkgname}/examples/"
	sed -n '3,17p' fdm.c > "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
