# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: Grigorios Bouzakis <grbzks@gmail.com>

pkgname=fdm-cvs
pkgver=20110308
pkgrel=1
pkgdesc='Program to fetch mail and deliver it in various ways'
arch=('i686' 'x86_64')
url='http://fdm.sourceforge.net/'
license=('BSD')
depends=('openssl' 'tdb')
makedepends=('cvs')
options=('!makeflags')
source=('GNUmakefile.patch'
        'LICENSE')
sha1sums=('efc159c63d78eaeb20473d3595f6a9c2bcfd9a3e'
          '8fd6ebad858da3db991233160ed70a4bcfbb04bf')

_cvsroot=':pserver:anonymous@fdm.cvs.sourceforge.net:/cvsroot/fdm'
_cvsmod='fdm'

conflicts=('fdm')

build() {
	cd "$srcdir"
	cvs -z3 -d $_cvsroot co -D $pkgver -f $_cvsmod
	cd $_cvsmod

	patch -p1 -i ../GNUmakefile.patch
	./configure
	make
}

package() {
	cd "$srcdir/$_cvsmod"
	make DESTDIR="$pkgdir" install

	install -d "$pkgdir/usr/share/doc/$pkgname/examples"
	install examples/*.conf "$pkgdir/usr/share/doc/$pkgname/examples/"
	install -D ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
