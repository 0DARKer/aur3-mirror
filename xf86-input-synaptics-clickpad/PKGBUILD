# Maintainer:  Jesse Jaara	 <gmail.com: jesse.jaara
# Contributor: Matt Monaco	 <0x01b.net: dgbaley27>
# Contributor: Jan de Groot	 <archlinux.org: jgc>
# Contributor: Tobias Powalowski <archlinux.org: tpowa>
# Contributor: Thomas BÃ¤chler	 <archlinux.org: thomas>
# Contributor: Alexander Baldeck <archlinux.org: alexander>

_realname=xf86-input-synaptics
_suseurl='FILE::https://api.opensuse.org/public/source/openSUSE:Factory/xorg-x11-driver-input/FILE?rev=67f06298aa561ce2152cad26f92cf4ae&'
pkgname=$_realname-clickpad
pkgver=1.5.0
pkgrel=3
pkgdesc="Synaptics driver for notebook touchpads (with clickpad and led support)"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxtst')
makedepends=('xorg-server-devel>=1.10.99.902' 'libxi' 'libx11')
optdepends=('synaptics-led')
conflicts=('xorg-server<1.10.99.902')
replaces=('synaptics')
provides=('synaptics' $_realname)
conflicts=('synaptics' $_realname)
groups=('xorg-drivers' 'xorg')
options=(!libtool)
backup=('etc/X11/xorg.conf.d/10-synaptics.conf')
source=(http://xorg.freedesktop.org/releases/individual/driver/${_realname}-${pkgver}.tar.bz2
        ${_suseurl//FILE/xf86-input-synaptics-wait.diff}
        ${_suseurl//FILE/xf86-input-synaptics-settings.diff}
        ${_suseurl//FILE/xf86-input-synaptics-add-clickpad-support.diff}
        ${_suseurl//FILE/xf86-input-synaptics-clickpad-doc-update.diff}
        ${_suseurl//FILE/xf86-input-synaptics-add-led-support.diff}
        ${_suseurl//FILE/xf86-input-synaptics-led-double-tap.diff}
        ${_suseurl//FILE/xf86-input-synaptics-fix-clickpad-capabilities.diff}
        ${_suseurl//FILE/xf86-input-synaptics-clickpad-threshold.diff}
        ${_suseurl//FILE/xf86-input-synaptics-move-threshold.diff}
        ${_suseurl//FILE/xf86-input-synaptics-filter-bogus-coord.diff}
        10-synaptics.conf)

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  for p in "${source[@]}"; do
    p="${p%%::*}"
    [[ "$p" =~ ^.+\.diff$ ]] || continue
    msg2 "Applying patch: $p"
    patch -p1 -i "../$p"
  done

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d"
  install -m644 "${srcdir}/10-synaptics.conf" "${pkgdir}/etc/X11/xorg.conf.d/"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

  rm -rf "${pkgdir}/usr/share/X11"
}

md5sums=('41ee749ecbfef98f7fba708cb2afae87'
         '3be2279df8d5c37b631b5dd16ecdf90e'
         'a33569b9953881d35b58fc233a87a145'
         '19ba0a59d3946933f038ff656549c6c3'
         'b0b62d0c7b2e0f0b0c25e37a8e45e332'
         '549f9e6b6837c25dfa742e5dd0ae3988'
         '6e8189aed09097f28046e66682a62fd8'
         'e75ae28038ce2cb76175fb178a261561'
         '33aada7d6f9f427b2df0f8cf281b1a70'
         '097d0776943d61cfa5373b5c87dc717b'
         'e25a610a879faa4138c46524be2d49cc'
         '3b81a81b958dfe3cac3cdef7ee85f1ce')
