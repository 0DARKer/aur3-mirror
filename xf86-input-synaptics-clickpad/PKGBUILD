# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Powalowski  <tpowa@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Matt Monaco <dgbaley27@0x01b.net>
# Maintainer: jesse jaara

_realname=xf86-input-synaptics
pkgname=${_realname}-clickpad
pkgver=1.4.1
pkgrel=3
pkgdesc="Synaptics driver for notebook touchpads (with clickpad and led support)"
arch=(i686 x86_64)
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxtst')
makedepends=('xorg-server-devel' 'libxi' 'libx11')
optdepends=('synaptics-led')
conflicts=('xorg-server<1.10.0' "${_realname}")
provides=(${_realname})
groups=('xorg-drivers' 'xorg')
options=(!libtool)
backup=('etc/X11/xorg.conf.d/10-synaptics.conf')
source=(http://xorg.freedesktop.org/releases/individual/driver/${_realname}-${pkgver}.tar.bz2
        synaptics-clickpad-support.patch
        10-synaptics.conf)

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i ../synaptics-clickpad-support.patch
  ./configure --prefix=/usr
  make
}
package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d"
  install -m644 "${srcdir}/10-synaptics.conf" "${pkgdir}/etc/X11/xorg.conf.d/"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

  rm -rf "${pkgdir}/usr/share/X11"
}

sha1sums=('e41201476f4bc8658291808d2d6ef2e0535179ae'
          'e3b479491fe07062cf70432e8bf6fdba4e9f0435'
          '68e1f4ef5e1038231d210eb422fa4d18c5922f0f')
