# Maintainer:  Jesse Jaara	 <gmail.com: jesse.jaara
# Contributor: Matt Monaco	 <0x01b.net: dgbaley27>
# Contributor: Jan de Groot	 <archlinux.org: jgc>
# Contributor: Tobias Powalowski <archlinux.org: tpowa>
# Contributor: Thomas BÃ¤chler	 <archlinux.org: thomas>
# Contributor: Alexander Baldeck <archlinux.org: alexander>

pkgname=xf86-input-synaptics-clickpad
pkgver=1.5.99
pkgrel=4
pkgdesc="Synaptics driver for laptops with clickpad devices (Chase Douglas branch v4 + Takeshi's led work)"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxtst')
makedepends=('xorg-server-devel' 'libxi' 'libx11')
optdepends=('synaptics-led')
provides=('synaptics' 'xf86-input-synaptics')
conflicts=('synaptics' 'xf86-input-synaptics')
groups=('xorg-drivers' 'xorg')
options=(!libtool)
backup=('etc/X11/xorg.conf.d/10-synaptics.conf')
source=('10-synaptics.conf' 'clickpad_led_tap.patch' 'mtdev_fix.patch')
__gitroot="git://people.freedesktop.org/~cndougla/xf86-input-synaptics"
_gitname="xf86-input-synaptics"

build() {
  cd "${srcdir}/"

  msg "Connecting to GIT server...."

  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${__gitroot} ${_gitname} --depth=1 && cd ${_gitname}
    git checkout clickpad-v2
  fi
  cd "${srcdir}/"
  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf build
  cp -r ${_gitname} build
  cd build

  patch -p1 -i ../clickpad_led_tap.patch
  patch -p1 -i ../mtdev_fix.patch

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/build"

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d"
  install -m644 "${srcdir}/10-synaptics.conf" "${pkgdir}/etc/X11/xorg.conf.d/"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

  rm -rf "${pkgdir}/usr/share/X11"
}

md5sums=('3b81a81b958dfe3cac3cdef7ee85f1ce'
         '11924c68d71e1670f11c30b85b358c17'
         'dc64a9a2cbd7b609caaa185e317f9fe6')
