# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Powalowski  <tpowa@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Matt Monaco <dgbaley27@0x01b.net>
# Maintainer: jesse jaara <jesse.jaara@gmail.com>

_realname=xf86-input-synaptics
pkgname=${_realname}-clickpad
pkgver=1.5.0
pkgrel=2
pkgdesc="Synaptics driver for notebook touchpads (with clickpad and led support)"
arch=(i686 x86_64)
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxtst')
makedepends=('xorg-server-devel' 'libxi' 'libx11')
optdepends=('synaptics-led')
conflicts=('xorg-server<1.10.0' "${_realname}")
provides=(${_realname})
groups=('xorg-drivers' 'xorg')
options=(!libtool)
backup=('etc/X11/xorg.conf.d/10-synaptics.conf')
source=(http://xorg.freedesktop.org/releases/individual/driver/${_realname}-${pkgver}.tar.bz2
        synaptics-clickpad-support.patch
        10-synaptics.conf)

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i ../synaptics-clickpad-support.patch
  ./configure --prefix=/usr
  make
}
package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d"
  install -m644 "${srcdir}/10-synaptics.conf" "${pkgdir}/etc/X11/xorg.conf.d/"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

  rm -rf "${pkgdir}/usr/share/X11"
}

md5sums=('41ee749ecbfef98f7fba708cb2afae87'
         '0fa8c1ac5bc239f2c5d78e4cabe0645b'
         '3b81a81b958dfe3cac3cdef7ee85f1ce')
md5sums=('41ee749ecbfef98f7fba708cb2afae87'
         'f24bea817d4416bf7856fdf0447a8200'
         '3b81a81b958dfe3cac3cdef7ee85f1ce')
