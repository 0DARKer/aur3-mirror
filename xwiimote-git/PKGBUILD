# Maintainer:  Matthew Monaco <net 0x01b dgbaley27>

_gitroot="git://github.com/dvdhrm/xwiimote.git"
_gitname="xwiimote"

pkgname=xwiimote-git
pkgver=20120222
pkgrel=3
pkgdesc="Open Source Nintendo Wii Remote Linux Device Driver"
url="https://github.com/dvdhrm/xwiimote"
license=('BSD')
arch=('i686' 'x86_64')
backup=('etc/X11/xorg.conf.d/50-xorg-fix-xwiimote.conf')
depends=('ncurses' 'udev')
makedepends=('git')
conflicts=("${pkgname/-git/}")
provides=("${pkgname/-git/}")
options=('!libtool')

build() {

	cd "$srcdir"

	if [[ -d "$_gitname" ]]; then
		msg2 "Cleaning and updating git repo"

		cd "$_gitname"
		git clean -dfx
		git pull

	else
		msg2 "Cloning $_gitroot"

		git clone "$_gitroot"
		cd "$_gitname"
	fi

	./autogen.sh --prefix=/usr
	make
}

package() {

	cd "$srcdir/$_gitname"

	make DESTDIR="$pkgdir" install
	install -D COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
	install -D res/50-xorg-fix-xwiimote.conf "$pkgdir/etc/X11/xorg.conf.d/50-xorg-fix-xwiimote.conf"
}

# vim: set noet :
