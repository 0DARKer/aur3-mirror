# Maintainer: Josh <moc tod liamg ta suoecal>

pkgname=duo_unix
pkgver=1.8
pkgrel=1
pkgdesc='Login utility and PAM module for Duo Security two-factor authentication'
arch=('i686' 'x86_64')
url='http://www.duosecurity.com/docs/duounix'
license=('custom:GPL2')
depends=('openssl' 'pam')
install=$pkgname.install
backup=('etc/duo/login_duo.conf' 'etc/duo/pam_duo.conf')
source=(https://github.com/downloads/duosecurity/$pkgname/$pkgname-$pkgver.tar.gz)
sha1sums=('a474dbf89e5e7062351b389be761d871f89a8f1f')
options=('!libtool')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Setting pam module to /lib/security
  # Otherwise it installs to /lib64/security on Arch x86_64 which doesn't work correctly
  # More config options here: https://github.com/duosecurity/duo_unix
  ./configure --with-pam=/lib/security --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # The license is GPL, but with a couple clarifications/exceptions (copy the LICENSE file)
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
