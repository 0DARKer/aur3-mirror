#Maintainer: Jonathan Fine (were.Vire AT gmail DOT com)

pkgname=tiled-qt
pkgver=0.6.1
pkgrel=1
pkgdesc='Tiled is a general purpose tile map editor, built to be flexible and easy to use.'
arch=('i686' 'x86_64')
url='http://mapeditor.org'
license=('GPL')
depends=('qt')
conflicts=(tiled)
replaces=(tiled)
source=(http://sourceforge.net/projects/tiled/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz
	tiled.desktop tmxviewer.desktop)
md5sums=('e025a4b39e318f8f33a7ad6a28b02874'
         '3c44178b349de1c1a5f56b08d229c2f4'
	 'f64196e99d9c50bc106258350c64d8e1')

build() {
        cd $srcdir/$pkgname-$pkgver
        qmake -r PREFIX=/usr
        make
}

package() {
	cd $srcdir
	install -D -m 644 tiled.desktop "$pkgdir/usr/share/applications/tiled.desktop"
	install -D -m 644 tmxviewer.desktop "$pkgdir/usr/share/applications/tmxviewer.desktop"
	cd $pkgname-$pkgver
        make install INSTALL_ROOT=$pkgdir

	install -D -m 644 "src/tiled/images/tiled-icon-16.png" "$pkgdir/usr/share/icons/hicolor/16x16/apps/tiled-icon.png"
	install -D -m 644 "src/tiled/images/tiled-icon-32.png" "$pkgdir/usr/share/icons/hicolor/32x32/apps/tiled-icon.png"
	install -D -m 644 "src/tiled/images/tiled-icon.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/tiled-icon.svg"

	cd src/libtiled
	for i in *.h
	do
		install -D -m 644 $i "$pkgdir/usr/include/libtiled/$i"
	done
}
