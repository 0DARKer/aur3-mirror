# Maintainer: Anthony Cornehl <accornehl[at]gmail[dot]com>
# https://github.com/twinshadow/abs-ofed
pkgname=mstflint
pkgver=3.0
_subver=0.5
_commit=gff93670
pkgrel=1
pkgdesc="Mellanox firmware burning application"
groups=('ofed')
url="http://www.openfabrics.org"
arch=('x86_64' 'i686')
license=('GPL2' 'custom:"Open Fabrics Alliance BSD"')
depends=('bash' 'gcc-libs' 'zlib')
source=("http://www.openfabrics.org/downloads/${pkgname}/${pkgname}-${pkgver}-${_subver}.${_commit}.tar.gz")
# cksum start
md5sums=('1eee1e918643dfeeac6328a989663307')
sha512sums=('402dac0eb007d55ddc0623c8dc498dd4ea3d9240c0b6b29106b871a73ac42ecf971fa4034b5af780ec6bb46ff5cade7a0b9aa90df9a1f7bb68ca727859b8196d')
# cksum end

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}