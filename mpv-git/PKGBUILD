# $Id: PKGBUILD 99197 2013-10-25 12:45:06Z giovanni $
# Maintainer: Rudolf Polzer <divVerent@xonotic.org>
# Original-Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Eivind Uggedal <eivind@uggedal.com>

pkgname=mpv-git
_gitname="mpv"
pkgver=0.37022.4083ae1
pkgrel=1
pkgdesc='Video player based on MPlayer/mplayer2 (git version)'
arch=('i686' 'x86_64')
license=('GPL')
url='http://mpv.io'
depends=(
         'portaudio' 'ffmpeg' 'lcms2' 'libdvdread' 'libcdio-paranoia' 'libxinerama'
         'libquvi' 'mpg123' 'libxv' 'libxkbcommon' 'libva' 'lirc-utils'
         'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils' 'lua51'
         'libxss' 'sdl2' 'smbclient' 'libguess' 'libdvdnav'
        )
makedepends=('mesa' 'python-docutils'  'git')
provides=('mpv')
conflicts=('mpv')
options=(!emptydirs)
install=mpv.install
source=('git+http://github.com/mpv-player/mpv#branch=master')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  microver="$(git log -1 --pretty=format:%h )"
  minorver="$(git rev-list --count HEAD)"
  echo "0.$minorver.$microver"
}

build() {
  CFLAGS="$CFLAGS -I/usr/include/samba-4.0"
  cd "$srcdir/$_gitname"
  ./bootstrap.py
  # BLOCKS:
  # 1. Global flags.
  # 2. Features not available on Linux (but which waf configure tries to detect
  #    anyway).
  # 3. Features not available on Arch Linux.
  # 4. Features only available in AUR (when buillding from AUR, feel free to
  #    delete these and adjust dependencies above to enable these).
  # To audit this: the waf configure run should output as few "no" as possible.
  ./waf configure --prefix=/usr \
    --confdir=/etc/mpv \
    \
    --disable-cocoa \
    --disable-coreaudio \
    --disable-corevideo \
    --disable-direct3d \
    --disable-dsound \
    --disable-gl-win32 \
    --disable-wasapi \
    \
    --disable-libbs2b \
    --disable-rsound \
    --disable-sndio

  ./waf build
}

package() {
  CFLAGS="$CFLAGS -I/usr/include/samba-4.0"
  cd "$srcdir/$_gitname"
  ./waf install --destdir="$pkgdir"

  install -d "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 etc/{input,example}.conf \
    "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 DOCS/{encoding.rst,tech-overview.txt} \
    "$pkgdir"/usr/share/doc/mpv
  install -m755 TOOLS/mpv_identify.sh "$pkgdir"/usr/bin
}
