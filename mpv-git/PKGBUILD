# Maintainer:  Philip Sequeira <phsequei@gmail.com>
# Contributor: Rudolf Polzer <divVerent@xonotic.org>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Eivind Uggedal <eivind@uggedal.com>

# WARNING: The configure script will automatically enable any optional
# features it finds support for on your system. If you want to build a
# package that will be installed on other machines, you might have to
# explicitly disable some of them in the configure below, or add them
# as dependencies.

pkgname=mpv-git
_gitname=mpv
pkgver=0.6.2_2185_g9b45b48
pkgrel=2
pkgdesc='Video player based on MPlayer/mplayer2 (git version)'
arch=('i686' 'x86_64')
license=('GPL')
url='http://mpv.io'
depends=(
  'ffmpeg' 'hicolor-icon-theme' 'xdg-utils' 'desktop-file-utils'
  # Anything below can be left out if not needed.
  'lcms2' 'libcdio-paranoia' 'libdvdnav' 'libguess' 'libxinerama'
  'libxrandr' 'libxss' 'libxv' 'lirc-utils' 'lua' 'mpg123' 'smbclient'
  'wayland' 'libxkbcommon' 'portaudio'
  # Notes:
  #  libxkbcommon is only needed for wayland.
  #  If you remove portaudio, make sure to remove --enable-portaudio
  #  from the configure below.
)
optdepends=('youtube-dl: for youtube-dl_mpv.sh')
makedepends=('git' 'python-docutils' 'mesa' 'ladspa') # mesa, ladspa optional
provides=('mpv')
conflicts=('mpv')
options=('!emptydirs')
install=mpv.install
source=('git+https://github.com/mpv-player/mpv')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  _curtag="$(git rev-list --tags --max-count=1)"
  _tagver="$(git describe --tags $_curtag | sed 's:^v::')"
  _commits="$(git rev-list --count HEAD --since=$_tagver)"
  _sha="$(git rev-parse --short HEAD)"
  printf "%s_%s_g%s" $_tagver $_commits $_sha
}

prepare() {
  cd "$srcdir/$_gitname"
  ./bootstrap.py
}

build() {
  cd "$srcdir/$_gitname"

  CFLAGS="$CFLAGS -I/usr/include/samba-4.0"

  ./waf configure --prefix=/usr \
    --confdir=/etc/mpv \
    --enable-joystick \
    --enable-zsh-comp \
    --enable-libmpv-shared \
    --enable-portaudio

  ./waf build
}

package() {
  cd "$srcdir/$_gitname"
  ./waf install --destdir="$pkgdir"

  install -d "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 etc/{input,example}.conf \
    "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 DOCS/{encoding.rst,tech-overview.txt} \
    "$pkgdir"/usr/share/doc/mpv

  install -m755 TOOLS/mpv_identify.sh "$pkgdir"/usr/bin
  install -m755 TOOLS/youtube-dl_mpv.sh "$pkgdir"/usr/bin
}
