# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

_pkgname=rhythmbox
pkgname=$_pkgname-git
pkgver=2.99.1.74.g09f8355
pkgrel=1
pkgdesc="An iTunes-like music player/libary"
arch=(i686 x86_64)
license=(GPL2)
url=http://www.rhythmbox.org
depends=(desktop-file-utils gstreamer0.10-good-plugins gstreamer0.10-python
    gvfs-afc json-glib libdmapsharing libgnome-media-profiles libgpod libmtp
    libnotify libpeas libsecret libwebkit3 lirc-utils media-player-info
    totem-plparser)
makedepends=(brasero git gnome-common gnome-doc-utils
    gobject-introspection gtk-doc intltool yelp-tools)
optdepends=('brasero: cd burning'
    'gstreamer0.10-bad-plugins: Extra media codecs'
    'gstreamer0.10-ffmpeg: Extra media codecs'
    'gstreamer0.10-ugly-plugins: Extra media codecs')
provides=($_pkgname=$pkgver)
conflicts=($_pkgname)
options=(!libtool !emptydirs !strip)
install=$_pkgname.install
source=($pkgname::git://git.gnome.org/$_pkgname)
sha256sums=('SKIP')
sha512sums=('SKIP')

pkgver() {
    cd $pkgname/
    git describe | sed 's/^v//;s/-/./g'
}

build() {
    cd $pkgname/
    export CFLAGS+=" -g -O1"
    export CXXFLAGS+=" -g -O1"
    PYTHON=/usr/bin/python2 ./autogen.sh
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/rhythmbox \
        --localstatedir=/var \
        --disable-static \
        --enable-daap \
        --enable-python \
        --enable-musicbrainz \
        --with-mdns=avahi \
        --disable-scrollkeeper
    make
}

package() {
    make -C $pkgname DESTDIR="$pkgdir" install
}
