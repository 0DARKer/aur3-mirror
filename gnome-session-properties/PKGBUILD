# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=gnome-session-properties
pkgver=3.10.0
pkgrel=1
pkgdesc="The GNOME Session Properties Tool"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('gtk3' 'hicolor-icon-theme' 'dbus-glib')
makedepends=('gnome-common' 'gtk-doc' 'intltool')
options=('!emptydirs')
install=gnome-session.install
url="http://www.gnome.org"
source=("http://ftp.gnome.org/pub/gnome/sources/gnome-session/${pkgver%.*}/gnome-session-${pkgver}.tar.xz"
        '0001-Remove-unneeded-stuff.patch')
sha512sums=('036adc7ecff7f5aa1319bfac636b6b77925c689cb92880224cf665456dca5a55299025971a2bcdb841e27334ddc0cd6125241b39e48a30c3cc0b02c22612f5c9'
            '5b7f17d2b9499a43f3735732903b877d7af6e10c7e59f5658058288018a964ae0037fbd6ff84310e4251e80571390405d6c7e3d08edaa6c4c80e5906fce4bae6')

prepare() {
  cd "${srcdir}/gnome-session-${pkgver}"
  patch -p1 -i "${srcdir}/0001-Remove-unneeded-stuff.patch"
}

build() {
  cd "${srcdir}/gnome-session-${pkgver}"
  autoreconf -vfi
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/gnome-session-${pkgver}"
  make DESTDIR="${pkgdir}" install

  files=$(find "${pkgdir}" -name session-properties.png -o -name session-properties.svg)
  for f in ${files}; do
    mv "${f}" "${f/apps\/session-/apps\/gnome-session-}"
  done

  sed -ri '/^Icon/ s/session-/gnome-session-/g' \
    "${pkgdir}/usr/share/applications/gnome-session-properties.desktop"
}

# vim:set ts=2 sw=2 et:
