# Maintainer: Mantas M. <grawity@gmail.com>
pkgname=realmd
pkgver=0.15.1
pkgrel=2
pkgdesc="DBus service for configuring kerberos and other online identities."
arch=(i686 x86_64)
url="http://freedesktop.org/software/realmd/"
license=(GPL3)
depends=(dbus krb5 openldap polkit)
makedepends=(docbook-xsl intltool packagekit python2 xmlto)
source=("http://www.freedesktop.org/software/realmd/releases/$pkgname-$pkgver.tar.gz"
        "http://www.freedesktop.org/software/realmd/releases/$pkgname-$pkgver.tar.gz.sig")
sha1sums=('90584250adb7365d8363b92701981d0aac4f5222'
          'SKIP')
validpgpkeys=('C0F67099B808FB063E2C81117BFB1108D92765AF')

build() {
  cd "$pkgname-$pkgver"
  ./configure \
    --prefix=/usr           \
    --sbindir=/usr/bin      \
    --sysconfdir=/etc       \
    --localstatedir=/var    \
    --with-distro=defaults  ;
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
} 

# vim: ts=2:sw=2:et
