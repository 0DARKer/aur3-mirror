# Maintainer: Alexander Minges <alexander.minges@gmail.com>
pkgname=ssmlib
_pkgname=ssm
pkgver=1.3
pkgrel=2
pkgdesc="A C++ toolkit for superposition of macromolecules"
arch=('i686' 'x86_64')
url="https://launchpad.net/ssm"
license=('LGPL')
depends=('mmdb' 'libccp4')
source=(ftp://ftp.ccp4.ac.uk/opensource/$_pkgname-$pkgver.tar.bz2
	ssm-configure.ac.patch
	ssm-makefile.am.patch
	ssm.pc.in)
sha256sums=('bd443a025b70b4bd7a2294e52b56e7d8cf5f10cf4b882683129899449e010496'
            'de58ab874d55487f153c1e095942dc0d1378cacc62936bef07edf3ae9e8e2095'
            '985e43c104cfe4c89a48cfab7a7b89920ffb97cbfc4301f6f2de0794c839ee14'
            '7b4ee7473beb5ba59f73a569b43b65fb07f3d5d0bb5f47854ba2a5c7e93023cc')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  
  cp $srcdir/ssm.pc.in ./ssm.pc.in
  patch -Np0 -i "$srcdir/ssm-configure.ac.patch"
  patch -Np0 -i "$srcdir/ssm-makefile.am.patch"
  aclocal
  libtoolize --automake --copy
  autoconf
  automake --copy --add-missing --gnu
  ./configure --prefix=/usr \
              --with-mmdb=/usr \
              --enable-ccp4 \
              --enable-shared \
              --disable-static
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
} 
