# Maintainer: yugrotavele <yugrotavele at archlinux dot us>

pkgname=gmtp
pkgver=1.1.0
pkgrel=2
pkgdesc="A simple MP3 player client for MTP based devices"
arch=('i686' 'x86_64')
url="http://chewy509.110mb.com/gMTP.html"
license=('BSD')
depends=('dconf' 'flac' 'gtk3' 'libmtp' 'libid3tag' 'libusb' 'libvorbis')
install=$pkgname.install
source=(http://chewy509.110mb.com/$pkgname-$pkgver-i386.tar.gz)
md5sums=(ead38e4a06231e5b00e26c9721f0be13)

build() {
  cd "$srcdir/gMTP"
  sed -i 's:PREFIX ?= /usr/local:PREFIX ?= /usr:g' Makefile
  make gtk3  
}

package() {
  cd "$srcdir/gMTP"
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  make DESTDIR="$pkgdir" install-gtk3
  make DESTDIR="$pkgdir" register-gsettings-schemas
  rm "$pkgdir/usr/share/glib-2.0/schemas/gschemas.compiled"
}


