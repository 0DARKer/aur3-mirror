# Contributor: codestation <codestation404@gmail.com>

pkgname=vitamtp-git
pkgver=20130503
pkgrel=1
pkgdesc="Library to interact with Vita's USB MTP protocol"
arch=("i686" "x86_64")
url="https://github.com/yifanlu/VitaMTP"
license=('GPL')
depends=('libusb' 'libxml2')
options=('!libtool')

source=(vitamtp.patch
        80-psvita.rules
        acx_pthread.m4
        byteorder.m4
        stdint.m4)

md5sums=('e0f0d0167a9bc8a4f8c46dfb420e2b61'
         '27bb9f540735e06bcd7a1b44bf9c5632'
         'a400a915b3b2d2d8c7cf40c815ddc607'
         'b07007186a3b70f3d8822628e66b340c'
         '848040885f93cf134a6fbe99a003d667')

_gitroot="git://github.com/yifanlu/VitaMTP.git"
_gitname="vitamtp"

build() {
  cd "${srcdir}"
  msg "Connecting to Git server...."

  if [ -d $_gitname ]; then
    cd $_gitname && git pull origin
  else
    git clone $_gitroot $_gitname
  fi

  msg "Git checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"

  cd "${srcdir}/${_gitname}-build"
  mkdir "${srcdir}/${_gitname}-build/m4"
  cp ../*m4 "${srcdir}/${_gitname}-build/m4"
  patch -p1 < ../vitamtp.patch || return 1
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}-build"
  make DESTDIR="${pkgdir}" install
  install -Dm644 ../80-psvita.rules "${pkgdir}/usr/lib/udev/rules.d/80-psvita.rules"
}
