# Maintainer: codestation <codestation404@gmail.com>

pkgname=vitamtp-git
pkgver=20130504
pkgrel=1
pkgdesc="Library to interact with Vita's USB MTP protocol"
arch=("i686" "x86_64")
url="https://github.com/yifanlu/VitaMTP"
license=('GPL')
depends=('libusb' 'libxml2')
options=('!libtool')

source=(vitamtp.patch)

md5sums=('d947c66342d9c9d2845f7daf92728d27')

_gitroot="git://github.com/yifanlu/VitaMTP.git"
_gitname="vitamtp"

build() {
  cd "${srcdir}"
  msg "Connecting to Git server...."

  if [ -d $_gitname ]; then
    cd $_gitname && git pull origin
  else
    git clone $_gitroot $_gitname
  fi

  msg "Git checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"

  cd "${srcdir}/${_gitname}-build"

  # add libusb and libxml2 to the deps
  patch -p1 < ../vitamtp.patch || return 1
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}-build"
  make DESTDIR="${pkgdir}" install
}
