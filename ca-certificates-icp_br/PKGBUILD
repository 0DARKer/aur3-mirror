# Maintainer: Tiago Brait <tiagobrait AT gmail DOT com>

#2014 tiagobrait
pkgname=ca-certificates-icp_br
pkgver=20141912
pkgrel=1
pkgdesc="Brazilian government Certification Authorities"
arch=('any')
url="http://www.iti.gov.br/icp-brasil/certificados/188-atualizacao/4530-ac-raiz"
license=('GPL')
depends=('ca-certificates')
makedepends=('unzip')
install='ca-certificates-icp_br.install'
source=(
    'http://acraiz.icpbrasil.gov.br/credenciadas/CertificadosAC-ICP-Brasil/ACcompactado.zip'
    )

sha512sums=(
    'c2777e058734f5b70f6d9d9550cd36596e6002ecfefbe2836b495c640de133f55859339e0438ce76fcf70c3bc3221ae2204ce99608dcbe9561f9b13f237fb9c9'
    )

package() {
  local cert_tag='icp_br'
  cd ${srcdir}
  for cert in $(find * -name '*.crt'); do
    #'tag' each certificate's filename, so it's easier to identify them, since 
    #looks like we cannot put them in a subdirectory anymore.
    local new_cert_name=${cert_tag}.${cert}
    
    #avanced debugging techniques
    #echo  "> processing  $cert (to become $new_cert_name)"

    #some certificates are distributed with a .crt extension but are actually
    #in DER format  instead of PEM (probably because government). If that's the
    #case, we convert them to PEM so everything works properly.
    if [ "$(file $cert | cut -f2 -d\: | tr -d ' ')" = "data" ]; then
      local cert_der=${cert}.der
      mv ${cert}  ${cert_der}
      /usr/bin/openssl x509 -in ${cert_der} -inform der -out ${new_cert_name}
      rm $cert_der
      msg2 "Converted '$(basename $cert)' from DER to PEM"
    else
      mv ${cert} ${new_cert_name}
    fi
  done
  install -d -m0755 $pkgdir/etc/ca-certificates/trust-source/anchors
  install -m0644 *.crt $pkgdir/etc/ca-certificates/trust-source/anchors
}
