pkgname=qtspeech-git
pkgver=v1.1.r2.21.ge53bf78
pkgrel=1
pkgdesc="A Qt Speech Module. (GIT Version)"
arch=('i686' 'x86_64')
url="http://qt-project.org/wiki/QtSpeech"
license=('GPL3' 'LGPL' 'FDL' 'custom')
depends=('qt5-base' 'speech-dispatcher')
conflicts=('qtspeech')
provides=('qtspeech')
source=('git+https://codereview.qt-project.org/qt/qtspeech')
sha1sums=('SKIP')

pkgver() {
  cd qtspeech
  echo "$(git describe --long --tags | tr - .)"
}

build() {
  cd qtspeech
  qmake-qt5
  make
}

package() {
  make -C qtspeech INSTALL_ROOT="${pkgdir}" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "${pkgdir}/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;
}

