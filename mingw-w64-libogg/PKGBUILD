# Maintainer: naelstrof <naelstrof@gmail.com> 
pkgname=mingw-w64-libogg
pkgver=1.3.0
pkgrel=2
pkgdesc="Ogg bitstream and framing library (mingw-w64)"
arch=(any)
url="http://xiph.org"
license=('BSD')
makedepends=('mingw-w64-gcc' 'mingw-w64-binutils')
depends=('mingw-w64-crt')
options=('!libtool' '!strip' '!buildflags')
source=(http://downloads.xiph.org/releases/ogg/libogg-${pkgver}.tar.gz)
md5sums=('0a7eb40b86ac050db3a789ab65fe21c2')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  for _arch in ${_architectures}; do
      mkdir -p ${srcdir}/libogg-${pkgver}-build-${_arch}
      cd ${srcdir}/libogg-${pkgver}-build-${_arch}  
      ${srcdir}/libogg-${pkgver}/configure --enable-shared --enable-static --prefix=/usr/${_arch} --build=$CHOST --host=${_arch}
      make
  done
}

package() {
  for _arch in ${_architectures}; do
      cd ${srcdir}/libogg-${pkgver}-build-${_arch}  
      make DESTDIR="$pkgdir" install
      rm -r $pkgdir/usr/${_arch}/share/doc
  done
}
