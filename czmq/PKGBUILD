# Maintainer: Friedrich Weber <fred@reichbier.de>
pkgname=czmq
pkgver=2.2.0
pkgrel=2
pkgdesc="High-level C binding for 0MQ"
arch=('i686' 'x86_64')
url="http://czmq.zeromq.org"
license=('LGPL')
depends=('gcc-libs' 'zeromq>=4.0')
sha1sums=('2f4fd8de4cf04a68a8f6e88ea7657d8068f472d2'
          '028d18a59381029ff4ffec5a6cfcfbe8a34e6e6b')
makedepends=()
source=(http://download.zeromq.org/$pkgname-$pkgver.tar.gz
        czmq-0001-Use-_DEFAULT_SOURCE-instead-of-_BSD_SOURCE.patch)

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 -i ../czmq-0001-Use-_DEFAULT_SOURCE-instead-of-_BSD_SOURCE.patch
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  mv "$pkgdir/usr/bin/makecert" "$pkgdir/usr/bin/makecert.czmq"
}
