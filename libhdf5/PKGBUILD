# based on http://repos.archlinux.org/wsvn/packages/hdf5/trunk/PKGBUILD
# Maintainer: (epsilom) Xavier Corredor <xavier.corredor.llano (a) gmail.com>

pkgname=libhdf5
real_pkgname=hdf5
pkgver=1.8.7
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="HDF5 - static library"
url="http://www.hdfgroup.org/HDF5/"
license=('custom')
depends=('zlib' 'sh')
makedepends=('time')
source=(ftp://ftp.hdfgroup.org/HDF5/current/src/${real_pkgname}-${pkgver/_/-}.tar.bz2)
options=('!libtool')
md5sums=('8859e2abc5258596302c7618722a48ad')

build() {
  cd $srcdir/${real_pkgname}-${pkgver/_/-}
  export CFLAGS="${CFLAGS/O2/O0}"
  export CXXFLAGS="${CFLAGS}"
  ./configure --prefix=/usr \
    --enable-hl \
    --enable-threadsafe \
    --enable-linux-lfs \
    --enable-production \
    --with-pic \
    --docdir=/usr/share/doc/hdf5/ \
    --with-pthread=/usr/lib/ \
    --disable-sharedlib-rpath
  make || return 1
}

package() {
  install -d -m755 $pkgdir/usr/lib
  install -m644 $srcdir/${real_pkgname}-${pkgver/_/-}/src/.libs/libhdf5.a $pkgdir/usr/lib/
  install -m644 $srcdir/${real_pkgname}-${pkgver/_/-}/hl/src/.libs/libhdf5_hl.a $pkgdir/usr/lib/
}

