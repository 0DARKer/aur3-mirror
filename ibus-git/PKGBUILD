# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Jekyll Wu <adaptee@gmail.com>
# Contributor: riverscn <riverscn@gmail.com>
# Contributor: Lee.MaRS <leemars@gmail.com>

_pkgname=ibus
pkgname=$_pkgname-git
pkgver=1.5.2.2418
pkgrel=1
pkgdesc="Next Generation Input Bus for Linux"
arch=(i686 x86_64)
license=(LGPL2.1)
url=http://$_pkgname.googlecode.com
depends=(hicolor-icon-theme gtk2 gconf)
optdepends=(notification-daemon)
makedepends=(dconf gnome-common gobject-introspection gtk-doc intltool iso-codes vala)
provides=($_pkgname=$pkgver)
options=(!emptydirs !libtool)
conflicts=($_pkgname)
install=$_pkgname.install
source=($pkgname::git://github.com/$_pkgname/$_pkgname.git)
sha256sums=('SKIP')
sha512sums=('SKIP')

pkgver() {
    cd $pkgname/
    echo $(git describe --tags | sed 's/-/./g').$(git shortlog | grep -c '\s\+')
}

prepare() {
    cd $pkgname/
    # Add a missing file which would break gnome-autogen.sh otherwise
    touch ChangeLog
    sed -i 's:exec python:&2:' setup/$_pkgname-setup.in ui/gtk2/$_pkgname-ui-gtk.in
}

build() {
    cd $pkgname/
    ./autogen.sh \
        PYTHON=/usr/bin/python2 \
        --prefix=/usr \
        --libexecdir=/usr/lib/$_pkgname \
        --sysconfdir=/etc \
        --enable-gtk3 \
        --enable-gtk-doc \
        --enable-gtk-doc-html \
        --with-gconf-schema-file-dir=/usr/share/gconf/schemas
    make
}

package() {
    make -C $pkgname DESTDIR="$pkgdir" install
}
