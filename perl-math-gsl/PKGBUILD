# Contributor: Anton Leontiev <bunder /at/ t-25.ru>
pkgname=perl-math-gsl
pkgver=0.25
pkgrel=1
pkgdesc="Perl module providing interface to the GNU Scientific Library using SWIG"
arch=("i686" "x86_64")
url="http://search.cpan.org/dist/Math-GSL/"
license=("GPL")
options=(!emptydirs)
depends=("perl" "gsl")
makedepends=("perl-extutils-pkgconfig")
checkdepends=("perl-test-exception" "perl-test-class" "perl-test-most")
source=(http://search.cpan.org/CPAN/authors/id/L/LE/LETO/Math-GSL-$pkgver.tar.gz)
md5sums=("38107703938ef98628f98a2baf521c8c")

build() {
  cd "$srcdir/Math-GSL-$pkgver"
  PERL_MM_USE_DEFAULT=1 perl Build.PL --installdirs vendor --destdir "$pkgdir"
  perl Build
}

check() {
  cd "$srcdir/Math-GSL-$pkgver"
  perl Build test
}

package() {
  cd "$srcdir/Math-GSL-$pkgver"
  perl Build install
}
