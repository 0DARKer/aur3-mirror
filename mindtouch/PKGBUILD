# Maintainer: Paul Gideon Dann <pdgiddie_AT_gmail_DOT_com>

pkgname=('mindtouch')  # Workaround for AUR
true && pkgname=('mindtouch' 'mindtouch-setup')
pkgver=10.1.0
pkgrel=1
pkgdesc="An enterprise wiki and collaborative portal"

arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/dekiwiki"
license=('GPL')
makedepends=()
options=(!strip)

_releasename="Pipestone"
_basename="MindTouch_Core_Source"
source=(
  http://sourceforge.net/projects/dekiwiki/files/MindTouch%20Core%20Source/${_releasename}%20${pkgver}/${_basename}_${pkgver}.tar.gz
  path_dirs.patch
  curl_followlocation.patch
  config.patch
  filters.patch
  init-script
  logrotate
  mindtouch-update-db
  nginx-rewrites
)
md5sums=('8d67b9b5f177795ce87cf07c47c1b816'
         '409025a7c3084779c2562514f6150ab3'
         '6ebc32a12a532100cc6cf62991f9f6aa'
         '5daae0c35a6f70db6da866e18a3cfb65'
         'e121a3c243bc2a1f0586ac8fa7370fae'
         'efebfbb5aa53106432a2c3ad5b80a94d'
         'b1d88aad11afee0cd4f474898cfee8f4'
         '12b625256ca760c42517ca3792d6e1f0'
         '641ae4287742af4561e7dd94e1a4a4bf')

build() {
  cd "$srcdir/${_basename}_${pkgver}"
  for patch in $srcdir/*.patch; do
    msg2 $(basename $patch)
    patch -Np1 -i $patch
  done
}

package_mindtouch() {
  depends=(imagemagick mono php-gd poppler html2text wv)
  optdepends=('princexml: to export documents to PDF')
  install=(mindtouch.install)

  # PHP Frontend
  cd $srcdir/${_basename}_${pkgver}
  local webapp=$pkgdir/usr/share/webapps/mindtouch
  install -d $pkgdir/usr/share/webapps
  cp -R web $webapp
  rm -rf $webapp/config
  install -d -o http $pkgdir/etc/webapps/mindtouch
  ln -s /etc/webapps/mindtouch/LocalSettings.php $webapp

  # Mono Backend
  install -d -o http $pkgdir/srv/http/.wapi  # For Mono
  install -D $srcdir/init-script $pkgdir/etc/rc.d/mindtouch
  install -D -m 644 $srcdir/logrotate $pkgdir/etc/logrotate.d/mindtouch

  # Workaround to allow use of MySQL UNIX socket
  ln -s /usr/lib/mono/4.0/Mono.Posix.dll $webapp/bin

  # Set up storage directories
  install -d -o http $pkgdir/var/lib/mindtouch/attachments
  ln -s /var/lib/mindtouch/attachments $webapp

  install -d -o http $pkgdir/var/lib/mindtouch/cache
  ln -s /var/lib/mindtouch/cache $webapp/bin

  rm -rf $webapp/bin/_x002F_deki
  install -d -o http $pkgdir/var/lib/mindtouch/licenses
  ln -s /var/lib/mindtouch/licenses $webapp/bin/_x002F_deki

  # Upgrade script
  install -d $pkgdir/usr/bin
  install $srcdir/mindtouch-update-db $pkgdir/usr/bin

  # Nginx
  install -d $pkgdir/usr/share/mindtouch
  install -m 644 $srcdir/nginx-rewrites $pkgdir/usr/share/mindtouch
}

package_mindtouch-setup() {
  depends=(mindtouch)

  cd $srcdir/${_basename}_${pkgver}
  local webapp=$pkgdir/usr/share/webapps/mindtouch
  install -d $pkgdir/usr/share/webapps/mindtouch
  cp -R web/config $webapp/config
  chown -R http:root $webapp/config
  for file in LocalSettings.php mindtouch.host.conf mindtouch.deki.startup.xml; do
    ln -s /etc/webapps/mindtouch/$file $webapp/config
  done
}

# vim:set ts=2 sw=2 et:
