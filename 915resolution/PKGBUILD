# Maintainer: neersighted <n@neersighted.com>
# Contributor: legolas558 <legolas558_at_users.sourceforge.net> 2009-10-23
# Contributor: nightfrost <khashayar_naderehvandi@yahoo.co.uk>

pkgname=915resolution
pkgver=0.5.3
pkgrel=1
pkgdesc='A tool to modify the video BIOS of the 800 and 900 series Intel graphics chipsets'
url='http://915resolution.mango-lang.org/'
license=('PublicDomain')
source=("http://915resolution.mango-lang.org/${pkgname}-${pkgver}.tar.gz")
md5sums=('ed287778a53d02c31a7a6a52bc146291')
sha1sums=('377a32d33f1987adf372341c8c6664d5673da9b8')
sha256sums=('b64cab834b5e410bca555dc9db8e69f62f6f02496942f35ff4a68f3f27f1b542')
arch=('i686' 'x86_64')
backup=('etc/conf.d/915resolution')

prepare() {
  cd ${srcdir}/915resolution-${pkgver}

  # Clean out the bundled binaries.
  make clean
}

build() {
  cd ${srcdir}/915resolution-${pkgver}

  # Build it!
  make
}

package() {
  cd ${srcdir}/915resolution-${pkgver}

  # Install the program.
  install -Dm755 915resolution ${pkgdir}/usr/sbin/915resolution
  install -Dm755 dump_bios ${pkgdir}/usr/sbin/915dump_bios

  # Install the license.
  install -Dm755 LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
