# Maintainer: Bjorn Neergaard (neersighted) <bjorn@neersighted.com>
# Contributor: legolas558 <legolas558@users.sourceforge.net>
# Contributor: nightfrost <khashayar_naderehvandi@yahoo.co.uk>

pkgroot=915resolution
pkgname=915resolution
pkgver=0.5.3
pkgrel=2
pkgdesc='A tool to modify the video BIOS of the 800 and 900 series Intel graphics chipsets'
url='http://915resolution.mango-lang.org/'
license=('PublicDomain')
source=("http://915resolution.mango-lang.org/${pkgroot}-${pkgver}.tar.gz")
sha1sums=('377a32d33f1987adf372341c8c6664d5673da9b8')
arch=('i686' 'x86_64')
backup=('etc/conf.d/915resolution')

prepare() {
  cd "${srcdir}/${pkgroot}-${pkgver}"

  # Clean out the bundled binaries.
  make clean
}

build() {
  cd "${srcdir}/${pkgroot}-${pkgver}"

  # Build it!
  make
}

package() {
  cd "${srcdir}/${pkgroot}-${pkgver}"

  # Install the program.
  install -Dm755 915resolution "${pkgdir}/usr/sbin/915resolution"
  install -Dm755 dump_bios "${pkgdir}/usr/sbin/915dump_bios"

  # Install the license.
  install -Dm755 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ft=sh ts=2 sw=2 et
