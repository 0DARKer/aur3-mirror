# Contributor: Piotr Kujawski <piotr@lao.pl>
# Based on uswsusp PKGBUILD by Giorgio Lando <patroclo7@gmail.com>
# http://aur.archlinux.org/packages.php?ID=14093
pkgname="uswsusp-cvs"
pkgver=20081021
pkgrel=1
pkgdesc="Latest CVS version of uswsusp, fresh whitelist, but no encrypt support."
arch=(i686 x86_64)
url="http://suspend.sourceforge.net"
license=(GPL)
depends=(lzo2 zlib libx86)
makedepends=(cvs gcc make)
conflicts=(suspend uswsusp)
replaces=(suspend uswsusp)
provides=(uswsusp)
backup=(etc/suspend.conf)
install=uswsusp.install
source=("uresume-hook" "uresume-install")
md5sums=('c0c73b3ba0969722a7b2a87f22d856d4' '74f05dbedbc8d0c0900cdea5f5dd395a')

_cvsroot=":pserver:anonymous@suspend.cvs.sf.net:/cvsroot/suspend"
_cvsmod="suspend"

build() {
  cd ${srcdir}
  if [ -d $_cvsmod/CVS ]
  then
    cd $_cvsmod
    cvs -z3 update -d
  else
    cvs -z3 -d $_cvsroot co -f $_cvsmod || return 1
    cd $_cvsmod
  fi

  ./autogen.sh || return 1
  ./configure --enable-compress --prefix=/usr --sysconfdir=/etc || return 1
  make DESTDIR=$pkgdir install || return 1
  install -D -m 644 ${startdir}/uresume-hook $pkgdir/lib/initcpio/hooks/uresume || return 1
  install -D -m 644 ${startdir}/uresume-install $pkgdir/lib/initcpio/install/uresume || return 1
}
