# Maintainer: Eric Engestrom <aur [at] engestrom [dot] ch>
# Contributor: Rasi <rasi@xssn.at>
# Contributor: Sean Pringle <sean.pringle@gmail.com>
# Contributor: SanskritFritz (gmail)

pkgname=rofi-git
_gitname=rofi
pkgver=r268.dfa2915
pkgrel=1
pkgdesc="Popup window switcher roughly based on superswitcher, requiring only xlib and xft. DaveDavenport's fork"
arch=('i686' 'x86_64')
url="https://github.com/DaveDavenport/${_gitname}"
license=('MIT')
conflicts=('simpleswitcher-git' 'simpleswitcher-dd-git')
depends=('libx11' 'libxft' 'freetype2' 'libxdg-basedir' 'libxinerama')
makedepends=('git' 'clang')
provides=('rofi')
source=("git+$url")
md5sums=('SKIP')

pkgver() {
  cd "$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_gitname"
  autoreconf --install
  ./configure
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" install
}
