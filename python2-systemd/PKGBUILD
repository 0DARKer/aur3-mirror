# Maintainer: Steven Hiscocks <steven@hiscocks.me.uk>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>

pkgname=('python2-systemd')
pkgdesc='python2 bindings for systemd'
pkgver=198
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2' 'LGPL2.1' 'MIT')
depends=('systemd' 'python2')
makedepends=('acl' 'cryptsetup' 'dbus-core' 'docbook-xsl' 'gobject-introspection' 'gperf'
             'gtk-doc' 'intltool' 'kmod' 'libcap' 'libgcrypt'  'libmicrohttpd' 'libxslt'
             'linux-api-headers' 'pam' 'quota-tools' 'xz')
options=('!libtool')
source=("http://www.freedesktop.org/software/systemd/systemd-$pkgver.tar.xz"
        'use-split-usr-path.patch')
md5sums=('26a75e2a310f8c1c1ea9ec26ddb171c5'
         '76bf83fe34c5b40533abc5dc940576a6')

build() {
  cd "systemd-$pkgver"

  # hang onto this until we do the /{,s}bin merge
  patch -Np1 <"$srcdir/use-split-usr-path.patch"

  ./configure \
      PYTHON=python2 \
      PYTHON_CONFIG=python2-config \
      --enable-static \
      --libexecdir=/usr/lib \
      --localstatedir=/var \
      --sysconfdir=/etc \
      --enable-introspection \
      --enable-gtk-doc \
      --disable-audit \
      --disable-ima \
      --with-sysvinit-path= \
      --with-sysvrcnd-path=

  make -j5
}

check() {
  make -C "systemd-$pkgver" check
}

package() {
    
  make -C "systemd-$pkgver" DESTDIR="$pkgdir/_systemd" install

  mkdir -p "$pkgdir/usr/lib"

  cp -r "$pkgdir/_systemd/usr/lib/python2.7" "$pkgdir/usr/lib/"

  rm -r "$pkgdir/_systemd"
}

# vim: ft=sh syn=sh et
