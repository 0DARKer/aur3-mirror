# Maintainer: Anshuman Bhaduri <anshuman at bhadurian dot com>
# Contributor: Adam Russell <adamlr6+arch@gmail.com>
# Contributor: halim <sagikliwon@gmail.com>
# Contributor: Alexey "Mr.Cat" Bakhirkin <abakhirkin(at)gmail.com>
# Contributor: Andr√© Klitzing <aklitzing () online () de>
# Contributor: Yumi Nanako <yumileroy [at] yahoo.com>

_pkgname=thg
pkgname=tortoisehg-hg
pkgver=3.2.17203
pkgrel=1
pkgdesc="Graphical tools for Mercurial"
arch=('any')
url="http://tortoisehg.bitbucket.org/"
license=('GPL2')
depends=('python2' 'mercurial>=2.9' 'python2-pyqt>=4.10' 'python2-qscintilla')
makedepends=('mercurial' 'setuptools')
optdepends=('python2-iniparse: ini file support'
            'python2-pygments: syntax highlighting'
            'python2-nautilus: Python binding for Nautilus components')
provides=('tortoisehg')
conflicts=('tortoisehg')
source=("hg+https://bitbucket.org/tortoisehg/${_pkgname}#branch=stable"
        'config.py')
sha256sums=('SKIP'
            '413f7747d602af6c38d311cc27ebdf5185142b20fa0d726dff5fcb4f8af3977f')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  # Version string will be similar to 2.7.2.14004
  echo $(hg parents --template {latesttag}).$(hg identify -n)
}

build() {
  cd "${srcdir}/${_pkgname}"
  cp "${srcdir}/config.py" "${srcdir}/${_pkgname}/tortoisehg/util"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}"

  install -Dm 644 "contrib/mergetools.rc" \
    "${pkgdir}/etc/mercurial/hgrc.d/thgmergetools.rc"
  install -Dm 644 "contrib/thg.desktop" \
    "${pkgdir}/usr/share/applications/thg.desktop"
  #install -Dm 644 "icons/scalable/apps/thg-logo.svg" \
  #  "${pkgdir}/usr/share/pixmaps/thg_logo.svg"

  # Remove VCS leftovers.
  find "${pkgdir}" -type d -name .hg -exec rm -r '{}' +
}
