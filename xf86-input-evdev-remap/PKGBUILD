# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <Alexander@archlinux.org>
# Contributor: Yegorius <yegorius@domic.us>

_pkgname=xf86-input-evdev
pkgname=${_pkgname}-remap
pkgver=2.8.0
pkgrel=1
pkgdesc="X.org evdev input driver with keycode remapping"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc' 'systemd-tools' 'mtdev')
makedepends=('xorg-server-devel' 'resourceproto' 'scrnsaverproto')
provides=('xf86-input-evdev')
conflicts=('xorg-server<1.14.0' 'xf86-input-evdev' 'X-ABI-XINPUT_VERSION<19' 'X-ABI-XINPUT_VERSION>=20')
options=('!libtool' '!makeflags')
groups=('xorg-drivers' 'xorg')
source=(${url}/releases/individual/driver/${_pkgname}-${pkgver}.tar.bz2
	evdev-remap.patch)
#sha256sums=('2f5b4403955f8e82603acff7f8f21a67e89a7c0230e1506c6924c4b9dd8f0e57')

prepare () {
  cd "$srcdir/${_pkgname}-${pkgver}"
  patch -Np1 -i "$srcdir/evdev-remap.patch"
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${_pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${_pkgname}/"
}

md5sums=('e9bef0779d364cb588aa93a0ee6736c8'
         '233aec861aff735df67caa177839e3d9')
