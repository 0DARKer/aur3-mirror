# Maintainer: Xavier Devlamynck <magicrhesus@ouranos.be>
# Contributor: Klaus Kurzmann <mok@mnet-online.de>
# Contributor: Maxime de Roucy <maxime.deroucy@gmail.com>
# Contributor: Maurizio Porrato <maurizio.porrato@gmail.com>

pkgbase=sflphone
pkgname=sflphone
pkgver=1.2.2
pkgrel=1
pkgdesc="SIP/IAX2 compatible softphone"
arch=('i686' 'x86_64')
url="http://www.sflphone.org/"
license=('GPL3')
depends=('libpulse' 'gsm>=0.9.7' 'libzrtpcpp' 'log4c' 'check' 'libyaml' 'libwebkit3' 'celt' 'dbus-c++' 'libnotify' 'speex')
makedepends=('autoconf' 'astyle' 'gnome-doc-utils' 'libgnomeui')
source=("https://projects.savoirfairelinux.com/attachments/download/5064/${pkgbase}-${pkgver}.tar.gz")
sha1sums=('42ddd866677f3dbc8d1c1517f178a54e4c9d4625')
options=(!makeflags)

build() {
  # PJSIP
  cd ${srcdir}/${pkgbase}-${pkgver}/daemon/libs/pjproject-2.0.1
  ./configure --disable-ilbc-codec
  #./configure
  make dep
  make
  
  # daemon
  cd ${srcdir}/${pkgbase}-${pkgver}/daemon
  ./autogen.sh
  ./configure --prefix=/usr --disable-ilbc
  #./configure --prefix=/usr
  make

  # Gnome client
  cd "${srcdir}/${pkgbase}-${pkgver}/gnome"
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgbase}-${pkgver}/daemon
  make DESTDIR=${pkgdir} install
  mkdir ${pkgdir}/usr/lib/sflphone/audio
  mv ${pkgdir}/usr/lib/sflphone/codecs/ ${pkgdir}/usr/lib/sflphone/audio/
  
  cd "${srcdir}/${pkgbase}-${pkgver}/gnome"
  make DESTDIR=${pkgdir} install
}

# vim:set ts=2 sw=2 et:
