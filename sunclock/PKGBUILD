# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Firmicus <firmicus@gmx.net>

pkgname=sunclock
pkgver=3.57
pkgrel=1
pkgdesc="A sophisticated astronomical clock for the X Window system"
arch=('i686' 'x86_64')
url="http://www.arvernes.com/wiki/index.php/Sunclock"
license=('GPL')
depends=('libjpeg-turbo' 'libpng' 'libxpm')
makedepends=('imake')
source=("http://www.arvernes.com/V1/pages/compteur_dl.php?/files/jpd/${pkgname}-${pkgver}.tar.bz2"
        "ftp://ftp.ac-grenoble.fr/ge/geosciences/sunclock/data_packages/medium_images_270804.tar.bz2"
        "ftp://ftp.ac-grenoble.fr/ge/geosciences/sunclock/data_packages/huge.jpg"
        "disable_zlib.patch"
        "${pkgname}.desktop")
md5sums=('eaff2880cd0f81de22a28e74e071369d'
         'c81f6e0fc9ca48452bb2390a37b36ed3'
         '4f0c8fc61e9b545ac337e84df12331b7'
         '04b25149d5f2f2573c65192e6d6b0b0e'
         '1857fc30b03ca09d8d9babc8c9857dc6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Apply a Debian patch to disable zlib
  patch -Np1 -i ../disable_zlib.patch

  # Fix path to 'emx' binary
  sed -i "s_/share/sunclock/bin_/bin_" Imakefile

  xmkmf
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Install program files
  make DESTDIR="${pkgdir}" \
       BINDIR=/usr/bin \
       EMXBINDIR=/usr/bin \
       SHAREDIR=/usr/share/${pkgname} \
       MANDIR=/usr/share/man/man1 install

  # Install data files
  mkdir -p "${pkgdir}/usr/share/${pkgname}/earthmaps/jpeg"
  install -m644 ../images/medium/*.jpg ../huge.jpg \
    "${pkgdir}/usr/share/${pkgname}/earthmaps/jpeg"

  # Install a desktop entry
  install -Dm644 wm_icons/sunclock2.xpm "${pkgdir}/usr/share/pixmaps/${pkgname}.xpm"
  install -Dm644 ../${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
