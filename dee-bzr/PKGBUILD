# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Michal Gawronski <dinth1906@gmail.com>
# Contributor: Sevenseven

pkgname=dee-bzr
_pkgname=dee
pkgver=431
pkgrel=1
pkgdesc="Model to synchronize multiple instances over DBus"
arch=('i686' 'x86_64')
url="https://launchpad.net/dee"
license=('LGPL')
depends=('glib2' 'icu')
makedepends=('bzr' 'gnome-common' 'gobject-introspection' 'python2' 'vala')
options=('!libtool')
provides=('dee')
conflicts=('dee')
source=('bzr+lp:dee')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  bzr revno
}

build() {
  cd "$srcdir/$_pkgname"
  
  export PYTHON='/usr/bin/python2'
  ./autogen.sh  --prefix='/usr' --sysconfdir='/etc' --localstatedir='/var' --disable-{static,tests}
  make
}

package() {
  cd "$srcdir/$_pkgname"

  make DESTDIR="${pkgdir}" install
}
