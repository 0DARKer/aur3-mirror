# $Id: PKGBUILD 356 2008-04-18 22:56:27Z aaron $
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=phylip
pkgver=3.69
pkgrel=1
pkgdesc="phylip is a collection of programmes for evolutive genetics and inferring phylogenies"
arch=(i686 x86_64)
url="http://evolution.genetics.washington.edu/phylip.html"
source=("http://evolution.gs.washington.edu/phylip/download/phylip-$pkgver.tar.gz" phylip-license-text)
depends=('libxaw')
license=('custom')
md5sums=('7ad50618173efc2a471d93bfca22042c' '7398d58a965740cf2e54e3deb2859977')

build() {
  cd "$srcdir/$pkgname-$pkgver/src"
  make all || return 1
  make EXEDIR="$pkgdir/usr/bin" put || return 1

# Put fonts in the proper place.
  mkdir -p "$pkgdir/usr/share/phylip/"
  mv $pkgdir/usr/bin/font* "$pkgdir/usr/share/phylip"
  cp "$pkgdir/usr/share/phylip/font1" "$pkgdir/usr/share/phylip/fontfile"

  # /usr/bin/factor is part of coreutils - factor -> factor_phylip
  mv "$pkgdir/usr/bin/factor" "$pkgdir/usr/bin/factor_phylip"

  /bin/install -D -m 644 "$srcdir/phylip-license-text" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
