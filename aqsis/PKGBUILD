# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Gino Pezzin <pezzin@gmail.com>
pkgname=aqsis
pkgver=1.8.2
pkgrel=1
pkgdesc="A high quality, photorealistic, 3D rendering solution"
arch=('i686' 'x86_64')
url="http://www.aqsis.org"
license=('GPL2')
optdepends=('qt4: graphical interface' 'libgl: ptview')
depends=('boost-libs' 'openexr' 'hicolor-icon-theme' 'shared-mime-info' 'desktop-file-utils')
makedepends=('cmake' 'boost' 'openexr' 'libpng' 'qt4' 'mesa')
options=('!libtool')
install=aqsis.install
source=(http://downloads.sourceforge.net/project/aqsis/aqsis-source/$pkgver/$pkgname-$pkgver.tar.gz)
md5sums=('399967e99f12cfbd1a7385c4e1d39c3b')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  mkdir -p "$srcdir"/$pkgname-build
  cd "$srcdir"/$pkgname-build

  cmake "$srcdir"/$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc/aqsis
  make
}

package() {
  cd "$srcdir"/$pkgname-build
  make DESTDIR="$pkgdir" install
}
