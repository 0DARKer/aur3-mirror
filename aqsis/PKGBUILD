# Former contributor: Gino Pezzin <pezzin@gmail.com>
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=aqsis
pkgver=1.6.0
pkgrel=3
pkgdesc="A high quality, photorealistic, 3D rendering solution"
arch=('i686' 'x86_64')
url="http://www.aqsis.org"
license=('GPL2')
depends=('libtiff' 'openexr' 'boost-libs')
optdepends=('fltk: required for framebuffer rendering')
makedepends=('cmake' 'boost' 'libxslt')
options=('!libtool')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('31ca940b7894ba536f107c42e480541d')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  mkdir -p "$srcdir"/$pkgname-build
  cd "$srcdir"/$pkgname-build

  cmake "$srcdir"/$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc/aqsis
  make
}

package() {
  cd "$srcdir"/$pkgname-build
  make DESTDIR="$pkgdir" install
}
