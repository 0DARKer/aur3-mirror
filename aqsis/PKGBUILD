# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Former contributor: Gino Pezzin <pezzin@gmail.com>
pkgname=aqsis
pkgver=1.6.0
pkgrel=5
pkgdesc="A high quality, photorealistic, 3D rendering solution"
arch=('i686' 'x86_64')
url="http://www.aqsis.org"
license=('GPL2')
depends=('libtiff' 'openexr' 'boost-libs' 'hicolor-icon-theme' 'shared-mime-info')
optdepends=('fltk: required for framebuffer rendering')
makedepends=('cmake' 'boost' 'libxslt')
options=('!libtool')
install=aqsis.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz \
	missing_includes.diff)
md5sums=('31ca940b7894ba536f107c42e480541d'
	'88814f68b3641dc1e6382ee14aae5051')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  patch -Np1 < "$srcdir"/missing_includes.diff || true

  mkdir -p "$srcdir"/$pkgname-build
  cd "$srcdir"/$pkgname-build

  export CXXFLAGS="${CXXFLAGS} -DBOOST_FILESYSTEM_VERSION=2"
  cmake "$srcdir"/$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc/aqsis
  make
}

package() {
  cd "$srcdir"/$pkgname-build
  make DESTDIR="$pkgdir" install
}
