pkgname=mate-desktop
pkgver=0.0.0
pkgrel=4
pkgdesc="Library with common API for various MATE modules"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=(mate-conf startup-notification gtk2)
makedepends=(mate-doc-utils intltool)
options=(!emptydirs)
url="http://matsusoft.com.ar/projects"
groups=('mate')
source=(http://matsusoft.com.ar/uploads/gnu-linux/mate/${pkgname}.tar.gz)
sha256sums=('536de6fab4b0ae180f5dbd7506891d8363d014df6534c602802ca03538465d34')

build() {
	cd "$srcdir/$pkgname"
	# Python2 fix
	sed -i '1s|#!/usr/bin/env python$|&2|' mate-about/mate-about
	
	PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
		--localstatedir=/var --disable-static \
		--with-mate-distributor="Arch Linux AUR" \
		--disable-scrollkeeper
	make
}

package() {
	cd "$srcdir/$pkgname"
	make DESTDIR="$pkgdir" install
	
	# prevent conflicts errors
	mv "${pkgdir}/usr/share/omf" "${pkgdir}/usr/share/mate-omf"
	rm "${pkgdir}/usr/share/pixmaps/apple-green.png"
	rm "${pkgdir}/usr/share/pixmaps/apple-red.png"
	rm "${pkgdir}/usr/share/pixmaps/gtkvim.png"
	rm "${pkgdir}/usr/share/pixmaps/kterm.xpm"
	rm "${pkgdir}/usr/share/pixmaps/monitor.png"
	rm "${pkgdir}/usr/share/pixmaps/vnterm.xpm"
}
