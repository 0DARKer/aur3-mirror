# Maintainer: Atte Virtanen <aten.email@gmail.com>
# Contributor: Cesar Alcalde <lambda512 _at_ gmail.com>
pkgname=scmccid
pkgver=5.0.29
pkgrel=1
pkgdesc="Binary driver for the SCM smart card readers"
arch=('i686' 'x86_64')
url="http://support.identive-group.com/downloads.php"
license=('custom')
depends=('pcsclite' 'libusb-compat')
backup=('usr/local/scm/ini/scmccid.ini')
install=$pkgname.install

case "$CARCH" in
	i686)
		source=("http://support.identive-group.com/download.php?file=driver/${pkgname}_${pkgver}_linux_32bit.tar.gz")
		md5sums=('bd1388f09e1a78e337414c68fb2c3991')
	;;
	x86_64)
		source=("http://support.identive-group.com/download.php?file=driver/${pkgname}_${pkgver}_linux_64bit.tar.gz")
		md5sums=('66d026c1feff82807476eb19f4e4c2bb')
	;;
esac

package() {
	case "$CARCH" in
		i686)   cd "$srcdir/${pkgname}_${pkgver}_linux_32bit" ;;
		x86_64) cd "$srcdir/${pkgname}_${pkgver}_linux_64bit" ;;
	esac

	bundle_path=`pkg-config libpcsclite --variable=usbdropdir`

	# Installation of ini file
	# This path seems to be hardcoded in the driver. Maybe it can be modified with sed
	mkdir -p $pkgdir/usr/local/scm/ini
	cp -f scmccid.ini $pkgdir/usr/local/scm/ini/

	# Installation of the driver bundle(s)
	mkdir -p $pkgdir/$bundle_path
	cp -rf ./proprietary/*.bundle $pkgdir/$bundle_path

	# Create symbolic link from open source pcscd bundle path
	mkdir -p $pkgdir/usr/local/pcsc/drivers

	cd ./proprietary
	for bundle in *.bundle; do
		ln -sf $bundle_path/$bundle $pkgdir/usr/local/pcsc/drivers/$bundle
	done
	cd ..

	# Copy license to standard location
	mkdir -p $pkgdir/usr/share/licenses/$pkgname
	cp ./proprietary/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE

	# Copy help to standard location
	# Contains instructions to customize the scmccid.ini
	mkdir -p $pkgdir/usr/share/doc/$pkgname
	cp ./ReadmeFirst.txt $pkgdir/usr/share/doc/$pkgname/
}
