# Maintainer: Heiko Baums <heiko@baums-on-web.de>
# Contributor: wido <widomaker2k7@gmail.com>

pkgname=bitdefender
pkgver=7.6
pkgrel=6
_build=3
_guiver=1.0
_guibuild=3
pkgdesc="BitDefender's Personal UNIX Workstation Antivirus"
arch=('i686' 'x86_64')
[ "$CARCH" = "i686"   ] && ARCH=i586
[ "$CARCH" = "x86_64" ] && ARCH=x86_64
url="http://www.bitdefender.com/"
depends=('gtk2' 'libstdc++5' 'atk' 'fontconfig' 'libxext' 'libxrender' 'libxrandr' 'libxi' 'libxcursor' 'libxfixes' 'pango' 'glib2' 'libxinerama' 'libsm')
install=bitdefender.install
license=('custom')
source=(http://download.bitdefender.com/repos/rpm/bitdefender/$ARCH/bitdefender-scanner-$pkgver-$_build.$ARCH.rpm
        http://download.bitdefender.com/repos/rpm/bitdefender/$ARCH/bitdefender-scanner-gui-$_guiver-$_guibuild.$ARCH.rpm
        bitdefender.sh
        bitdefender.conf)
md5sums=('9d90c7b4133ae6648e4371f4e92d7853'
         'd7812ffcc01137a41d738170a28be8a1'
         'f74775c74b44ceabafcc0dbab0ff2318'
         '6df89a673aec1ad103745da47c50dc46')
[ "$CARCH" = "x86_64" ] && md5sums=('3da9c645fc7e3f1b2b02e5906dc362b1'
                                    'a752eb979172de0f7cfa21d723b806d1'
                                    'f74775c74b44ceabafcc0dbab0ff2318'
                                    '6df89a673aec1ad103745da47c50dc46')

package() {
    cd $srcdir
    cp -r $srcdir/opt $pkgdir
    cp -r $srcdir/usr $pkgdir

    # generate configuration
    sed -i 's|\$\$DIR|/opt/BitDefender-scanner|g' $pkgdir/opt/BitDefender-scanner/etc/bdscan.conf.dist 
    mv $pkgdir/opt/BitDefender-scanner/etc/bdscan.conf.dist $pkgdir/opt/BitDefender-scanner/etc/bdscan.conf

    # add "LicenseAccepted = True" to bdscan.conf
    echo "" >> "$pkgdir/opt/BitDefender-scanner/etc/bdscan.conf"
    echo "LicenseAccepted = True" >> "$pkgdir/opt/BitDefender-scanner/etc/bdscan.conf"

    # generate GUI configuration
    sed -i 's|\$\$DIR|/opt/BitDefender-scanner|g' $pkgdir/opt/BitDefender-scanner/etc/bdgui.conf.dist 
    mv $pkgdir/opt/BitDefender-scanner/etc/bdgui.conf.dist $pkgdir/opt/BitDefender-scanner/etc/bdgui.conf

    # fix obsolete update server
    sed -i "s|upgrade\.bitdefender\.com|upgrade1\.bitdefender\.com|g" "$pkgdir/opt/BitDefender-scanner/etc/bdscan.conf"

    # profile
    install -Dm755 $startdir/bitdefender.sh $pkgdir/etc/profile.d/bitdefender.sh

    # create the /etc symlink
    ln -s /opt/BitDefender-scanner/etc $pkgdir/etc/BitDefender-scanner
    mkdir -p $pkgdir/usr/bin/
    ln -sf /opt/BitDefender-scanner/bin/bdgui $pkgdir/usr/bin/bdgui
    ln -sf /opt/BitDefender-scanner/bin/bdscan $pkgdir/usr/bin/bdscan

    # extract the plugins
    mkdir -p $pkgdir/opt/BitDefender-scanner/var/lib/scan/Plugins
    tar -C $pkgdir/opt/BitDefender-scanner/var/lib/scan/Plugins -xzf $pkgdir/opt/BitDefender-scanner/share/engines/Plugins.tar.gz

    # extract skins
    mkdir -p $pkgdir/opt/BitDefender-scanner/var/skins
    tar -C $pkgdir/opt/BitDefender-scanner/var/skins -xzf $pkgdir/opt/BitDefender-scanner/var/skins/Default.tar.gz

    # add /opt/BitDefender-scanner/var/lib to ld.so.conf.d
    install -Dm644 $srcdir/bitdefender.conf $pkgdir/etc/ld.so.conf.d/bitdefender.conf

    # add bash completion
    install -Dm 644 $pkgdir/opt/BitDefender-scanner/share/contrib/bash_completion/bdscan $pkgdir/etc/bash_completion.d/bdscan
}
