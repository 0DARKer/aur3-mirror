# Maintainer: Diego Principe <cdprincipe@at@gmail@dot@com>
# Contributor: Brett Lempereur <sordidpanda at gmail dot com>
# Contributor: Flamelab <panosfilip at gmail dot com>

_pkgname="gnome-shell-extensions"
pkgname="gnome-shell-extensions-git"
pkgver=3.9.3
pkgrel=1
pkgdesc="User-created extensions for the GNOME Shell environment"
arch=('i686' 'x86_64')
url="http://live.gnome.org/GnomeShell/Extensions"
license=('GPL2')
depends=(
    "gnome-shell"
    "gtk3"
)
makedepends=(
    "git"
    "gnome-common"
    "intltool"
    "perl"
    "pkg-config"
)
provides=(
    "gnome-shell-extensions"
)
conflicts=(
    "gnome-shell-extensions"
)
options=(!libtool !emptydirs)
install=gnome-shell-extensions-git.install
source=('git://git.gnome.org/gnome-shell-extensions')
md5sums=('SKIP')

pkgver()
{
  cd ${srcdir}/${_pkgname}
  git describe --always | sed 's|-|.|g'
}

build()
{
    cd ${srcdir}/${_pkgname}

    # Configure the package.
    ./autogen.sh --prefix=/usr --disable-schemas-compile --enable-extensions=all

    # Build.
    make

}

package()
{
    cd ${srcdir}/${_pkgname}
    make DESTDIR="${pkgdir}" install
}

