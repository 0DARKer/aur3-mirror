# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Chris Baker <youremail@domain.com>

pkgname=sleepisdeath
pkgver=16
pkgrel=1
pkgdesc="A storytelling game for two players by Jason Rohrer"
arch=('i686' 'x86_64')
url="http://sleepisdeath.net/"
license=('custom:commercial')
depends=('libpng' 'mesa' 'sdl')
install="${pkgname}.install"
source=("SleepIsDeath_v${pkgver}_UnixSource.tar.gz"
        "${pkgname}.png"
        "${pkgname}.desktop"
        "${pkgname}.sh")
md5sums=('1032f1d2065abc96ee7f80a8d6d09fda'
         '875d997ca4caefcbfe599f5e37361944'
         'f55575ef02cf2a1da79675fc9af12821'
         'ff4b365ec9522326b6d9478a5a9eed55')

build() {
  cd "${srcdir}/SleepIsDeath_v${pkgver}_UnixSource"
  ./runToBuild 1
}

package() {
  cd "${srcdir}/SleepIsDeath_v${pkgver}_UnixSource"

  # Install game files
  mkdir -p "${pkgdir}/opt/${pkgname}"
  cp -R graphics importOldCache languages loadingBay \
        resourceCache settings templates SleepIsDeathApp *.txt \
        "${pkgdir}/opt/${pkgname}"

  # Set permissions
  chgrp -R games "${pkgdir}/opt/${pkgname}"
  chmod -R g+w "${pkgdir}/opt/${pkgname}"

  # Install launchers
  install -Dm755 "../${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  ln -s "/usr/bin/${pkgname}" "${pkgdir}/usr/bin/SleepIsDeathApp"

  # Install pixmap and .desktop file
  install -Dm644 "../${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 "../${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}

# vim:set ts=2 sw=2 et:
