# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

_pkgname=gvfs
pkgname=$_pkgname-git
pkgver=20130119
pkgrel=1
pkgdesc="Virtual filesystem implementation for gio"
arch=(i686 x86_64)
url=http://gnome.org/
license=(GPL2)
depends=(fuse glib2-git gtk3 libarchive libbluray libcdio-paranoia libgphoto2 libimobiledevice
    libmtp libsoup-gnome obex-data-server smbclient udisks2)
makedepends=(git gtk-doc)
provides=($_pkgname=1.12.3 $_pkgname-{afc,afp,gphoto2,obexftp,smb})
conflicts=($_pkgname $_pkgname-{afc,afp,gphoto2,obexftp,smb})
options=(!libtool)
install=$_pkgname.install

_gitroot=git://git.gnome.org/$_pkgname
_gitname=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to git.gnome.org GIT server...."
    if [[ -d $_gitname/.git ]]; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone --depth 1 $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Starting build..."
    sed -i 's:cdio/paranoia:&/paranoia:' daemon/gvfsbackendcdda.c
    ./autogen.sh --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --libexecdir=/usr/lib/gvfs \
        --with-bash-completion-dir=/usr/share/bash-completion/completions
    make
}

package() {
    cd "$srcdir"/$_gitname-build/
    make DESTDIR="$pkgdir" install
}
