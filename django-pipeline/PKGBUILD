# Maintainer: Steven Allen <steven@stebalien.com>

pkgname='django-pipeline'
pkgver=1.1.27
pkgrel=2
pkgdesc="An asset packaging library for Django, providing both CSS and JavaScript concatenation and compression, built-in JavaScript template support, and optional data-URI image embedding."
arch=('any')
url="https://github.com/cyberdelia/django-pipeline"
license=('custom')
depends=('python2' 'django')
makedepends=('python2-sphinx')
optdepends=('yuicompressor: for css and js compression')
source=("http://pypi.python.org/packages/source/d/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('5df9984413be5270b211872dbf0a8fdc')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's|#!/usr/bin/python|#!/usr/bin/env python2|' \
      "pipeline/compressors/jsmin/jsmin.py"

  cd "docs/"
  make man
  gzip "_build/man/django-pipeline.1"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  rm -rf "$pkgdir"/usr/lib/python*/site-packages/tests/
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -D -m644 "docs/_build/man/django-pipeline.1.gz" "$pkgdir/usr/share/man/man1/django-pipeline.1.gz"
}
