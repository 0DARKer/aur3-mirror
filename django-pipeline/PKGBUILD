# Maintainer: Steven Allen <steven@stebalien.com>

pkgname='django-pipeline'
pkgver=1.1.26
pkgrel=1
pkgdesc="An asset packaging library for Django, providing both CSS and JavaScript concatenation and compression, built-in JavaScript template support, and optional data-URI image embedding."
arch=('any')
url="https://github.com/cyberdelia/django-pipeline"
license=('custom')
depends=('python2' 'django')
optdepends=('yuicompressor: for css and js compression')
source=("http://pypi.python.org/packages/source/d/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('6853f59f6aa09400bd7c4f03284c418d')

check() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py test
}
build() {
  sed -i 's|#!/usr/bin/python|#!/usr/bin/env python2|' \
      "$srcdir/$pkgname-$pkgver/pipeline/compressors/jsmin/jsmin.py"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
