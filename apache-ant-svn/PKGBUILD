# Maintainer: skydrome <skydrome@i2pmail.org>
# Contributor: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Andrew Wright <andreww@photism.org>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=apache-ant-svn
_pkgname=apache-ant
pkgver=1442023
pkgrel=1
pkgdesc="A java-based build tool - development version"
arch=('any')
url="http://ant.apache.org/"
license=('APACHE')
depends=('java-environment')
makedepends=('junit')
optdepends=('junit: to jave junit on the classpath in javac tasks')
source=("${_pkgname}.sh"
        "${_pkgname}.csh")
conflicts=("$_pkgname")
provides=("$_pkgname")

md5sums=('dbfa1e8dd4bab2f5821f5abe50835c83'
         'b3f67824043b6fb00667bb7a4007af42')

_ant_home=/usr/share/${_pkgname}
_svnmod=${_pkgname}
_svntrunk=http://svn.apache.org/repos/asf/ant/core/trunk

build() {
  cd ${srcdir}

  if [ -d ${_svnmod}/.svn ]; then
    cd ${_svnmod} && svn up
  else
    svn co ${_svntrunk} --config-dir ./ ${_svnmod}
    cd ${_svnmod}
  fi

  msg "Starting make..."

  ln -sf /usr/share/java/junit.jar lib/optional/junit.jar
  ./bootstrap.sh

  mkdir -p ${srcdir}/.m2
  ./bootstrap/bin/ant -Ddest=optional -Ddest.dir=${srcdir}/.m2 -f fetch.xml
  ./bootstrap/bin/ant -Ddest=optional -nouserlib -f fetch.xml

  sh build.sh dist-lite
}

package() {
  cd "${srcdir}/${_pkgname}/dist"

  install -dm755 ${pkgdir}/${_ant_home}/bin
  #cp -Rp etc ${pkgdir}/${_ant_home}
  find ./bin -type f -a ! -name \*.bat -a ! -name \*.cmd \
    -exec install -m755 {} ${pkgdir}/${_ant_home}/bin \;

  install -dm755 ${pkgdir}/usr/bin
  ln -s ${_ant_home}/bin/ant ${pkgdir}/usr/bin/ant

  install -dm755 ${pkgdir}/usr/share/java/${_pkgname}
  install -m644 lib/*.jar ${pkgdir}/usr/share/java/${_pkgname}
  ln -s /usr/share/java/${_pkgname} ${pkgdir}/${_ant_home}/lib

  # Symlink to junit so it's on the javac build path
  # http://bugs.archlinux.org/task/15229
  # http://ant.apache.org/manual/install.html#optionalTasks
  ln -sf ../junit.jar ${pkgdir}/usr/share/java/apache-ant/

  # fix python2 path
  sed -i 's|/usr/bin/python|/usr/bin/python2|' ${pkgdir}${_ant_home}/bin/runant.py

  # install profile.d script
  install -dm755 ${pkgdir}/etc/profile.d
  install -m755 "${srcdir}"/${_pkgname}.{csh,sh} ${pkgdir}/etc/profile.d/

  # License says NOTICE file should be redistributed for derivative works
  cd ${srcdir}/${_pkgname}
  install -dm755 ${pkgdir}/usr/share/licenses/${_pkgname}
  install -m644 LICENSE NOTICE ${pkgdir}/usr/share/licenses/${_pkgname}
}
