# Contributor: Andreas dot Wagner at em dot uni-frankfurt dot de

pkgname=perl-text-bibtex
_cpanname=Text-BibTeX
pkgver=0.55
pkgrel=1
pkgdesc="Parse BibTeX files"
arch=('any')
url="http://search.cpan.org/dist/${_cpanname}"
license=('GPL' 'PerlArtistic')
depends=('perl')
makedepends=('perl-config-autoconf>=0.16' 'perl-extutils-libbuilder' \
    'perl-capture-tiny' )
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/A/AM/AMBS/Text/${_cpanname}-${pkgver}.tar.gz) 

build() {
  cd $srcdir/${_cpanname}-${pkgver}
  perl Build.PL installdirs=vendor destdir=$pkgdir
  perl Build
  perl Build test
}

package() {
  cd $srcdir/${_cpanname}-${pkgver}
  perl Build install
  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete
}

md5sums=('84a2cc4e214cea2e7c47f1447784a02d')
