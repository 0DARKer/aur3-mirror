# Contributor: noonov <noonov@gmail.com>

pkgname=nkf
pkgver=2.1.1
pkgrel=2
pkgdesc="A yet another kanji code converter among networks, hosts and terminals"
arch=('i686' 'x86_64')
url="http://sourceforge.jp/projects/nkf/"
license=('custom:zlib')
depends=('glibc')
source=(http://dl.sourceforge.jp/nkf/48945/${pkgname}-${pkgver}.tar.gz)

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  sed -i "/^CFLAGS/s;$; ${CFLAGS};" Makefile

  make
  ./nkf -w <nkf.1j >nkf.1j.UTF-8
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  install -D -m755 nkf \
    ${pkgdir}/usr/bin/nkf
  install -D -m644 nkf.1 \
    ${pkgdir}/usr/share/man/man1/nkf.1
  install -D -m644 nkf.1j.UTF-8 \
    ${pkgdir}/usr/share/man/ja/man1/nkf.1
}

md5sums=('86240f7ed454fb4b65720b752c579dcb')
