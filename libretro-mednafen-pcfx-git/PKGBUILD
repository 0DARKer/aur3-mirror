# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive   <almostalive2003 at gmail dot com>

pkgname=libretro-mednafen-pcfx-git
pkgver=481.437489e
pkgrel=1
pkgdesc="libretro implementation of Mednafen PC-FX."
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/mednafen-libretro"
license=('GPL')
makedepends=('git')

_gitname=mednafen-libretro
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/mednafen_pcfx_libretro.info")
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}"
  make core=pcfx
}

package() {
  install -Dm644 "${_gitname}/mednafen_pcfx_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-mednafen-pcfx.so"
  install -Dm644 "mednafen_pcfx_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-mednafen-pcfx.info"
}
