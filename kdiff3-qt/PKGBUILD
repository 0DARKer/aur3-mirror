# Maintainer: xduugu
pkgname=kdiff3-qt
_pkgname=${pkgname%-*}
pkgver=0.9.97
pkgrel=1
pkgdesc="A file comparator/merge tool (lightweight qt version without KDE dependencies)"
arch=('i686' 'x86_64')
url="http://kdiff3.sourceforge.net/"
license=('GPL')
depends=('qt')
provides=("kdiff3=$pkgver")
conflicts=('kdiff3')
options=('!docs' '!emptydirs')
source=(http://downloads.sourceforge.net/project/$_pkgname/$_pkgname/$pkgver/$_pkgname-$pkgver.tar.gz)
md5sums=('30a71b474956c369ed7b38c6db080fc4')
sha256sums=('e3b716bb449c814d8c30817ec1ca23fba0ed9eee5a635e766c1f2b90ddb75a2a')

build() {
  cd "$srcdir/$_pkgname-$pkgver/src-QT4/"

  sed -i 's@/usr/local@/usr@g' kdiff3.pro

  qmake kdiff3.pro
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver/src-QT4/"
  make INSTALL_ROOT="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
