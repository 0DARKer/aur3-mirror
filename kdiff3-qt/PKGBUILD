# Maintainer: xduugu
pkgname=kdiff3-qt
_pkgname=${pkgname%-*}
pkgver=0.9.96
pkgrel=1
pkgdesc="A file comparator/merge tool (lightweight qt version without KDE dependencies)"
arch=('i686' 'x86_64')
url="http://kdiff3.sourceforge.net/"
license=('GPL')
depends=('qt')
provides=("kdiff3=$pkgver")
conflicts=('kdiff3')
options=('!docs' '!emptydirs')
source=(http://downloads.sourceforge.net/project/$_pkgname/$_pkgname/$pkgver/$_pkgname-$pkgver.tar.gz)
md5sums=('46b96befddf3448a3cb673f018c6f6f9')
sha256sums=('10e74ec47d57ccae13f71f836fd5bab85db46a40762398ca3a515b552e75d691')

build() {
  cd "$srcdir/$_pkgname-$pkgver/src-QT4/"

  sed -i 's@/usr/local@/usr@g' kdiff3.pro

  qmake kdiff3.pro
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver/src-QT4/"
  make INSTALL_ROOT="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
