# Maintainer: xduugu
pkgname=kdiff3-qt
pkgver=0.9.95
pkgrel=1
pkgdesc="A file comparator/merge tool (lightweight qt version without KDE dependencies)"
arch=('i686' 'x86_64')
url="http://kdiff3.sourceforge.net/"
license=('GPL')
depends=('qt')
provides=("kdiff3=$pkgver")
conflicts=('kdiff3')
options=('!docs' '!emptydirs')
source=(http://downloads.sourceforge.net/project/${pkgname%-*}/${pkgname%-*}/$pkgver/${pkgname%-*}-$pkgver.tar.gz)
md5sums=('652a98bf79ba762a8a646d4a0fddb323')
sha256sums=('0372cebc8957f256a98501a4ac3c3634c7ecffb486ece7e7819c90d876202f0f')

build() {
  cd "$srcdir/${pkgname%-*}-$pkgver/src-QT4/"

  sed -i 's@/usr/local@/usr@g' kdiff3.pro

  qmake kdiff3.pro
  make
}

package() {
  cd "$srcdir/${pkgname%-*}-$pkgver/src-QT4/"
  make INSTALL_ROOT="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
