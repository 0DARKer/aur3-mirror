# Maintainer: Bjorn Neergaard (neersighted) <bjorn@neersighted.com>
# Contributor: JD Horelick <jdhore1@gmail.com>
# Contributor: Mantas MikulÄ—nas <grawity@gmail.com>

pkgroot=charybdis
pkgname=charybdis-git
pkgver=latest
pkgrel=2
pkgdesc='A highly scalable IRCv3-compliant IRCd (development version)'
url='http://atheme.org/project/charybdis'
license=('GPL2')
options=('!strip')
install=charybdis.install
source=('git+https://github.com/atheme/charybdis.git'
        'charybdis.service'
        'charybdis.conf')
sha1sums=('SKIP'
          '19c2d19a7a856835ec1036441494bf02a2bbda97'
          '21bf883e38f5938884737c60342f6bc4795e8435')
arch=('i686' 'x86_64')
depends=('bison' 'flex' 'openssl' 'zlib')
makedepends=('git')
conflicts=('charybdis')
provides=('charybdis')

pkgver() {
  cd "${srcdir}/${pkgroot}"

  # Get the version number.
  git describe --always | sed 's/^v//;s/-/./g'
}

build() {
  cd "${srcdir}/${pkgroot}"

  # Configure the build.
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/charybdis \
    --localstatedir=/var \
    --enable-fhs-paths \
    --with-rundir=/run \
    --with-program-prefix=charybdis-

  # Build it!
  make
}

package() {
  cd "${srcdir}/${pkgroot}"

  # Install the program.
  make DESTDIR="${pkgdir}" install

  # Install the docs.
  install -Dm644 -d docs "${pkgdir}/usr/share/docs/charybdis"

  # Install the service.
  install -Dm644 "${srcdir}/charybdis.service" "${pkgdir}/usr/lib/systemd/system/charybdis.service"
  install -Dm644 "${srcdir}/charybdis.conf" "${pkgdir}/usr/lib/tmpfiles.d/charybdis.conf"
}

# vim: ft=sh ts=2 sw=2 et
