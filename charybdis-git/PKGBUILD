# Maintainer: neersighted <n@neersighted.com>
# Contributor: JD Horelick <jdhore1@gmail.com>
# Contributor: Mantas MikulÄ—nas <grawity@gmail.com>

pkgname=charybdis-git
pkgver=latest
pkgrel=2
pkgdesc='A highly scalable IRCv3-compliant IRCd (development version)'
url='http://atheme.org/project/charybdis'
license=('GPL2')
options=('!strip')
install=charybdis.install
source=('git://github.com/atheme/charybdis.git'
        'charybdis.service'
        'charybdis.conf')
md5sums=('SKIP'
         '434eea5f355c91509efb8d2e1937677a'
         '417e430719e91dce7be37e67de23bf41')
sha1sums=('SKIP'
          '19c2d19a7a856835ec1036441494bf02a2bbda97'
          '21bf883e38f5938884737c60342f6bc4795e8435')
sha256sums=('SKIP'
            '044f3567ed865365e76581942e1b28ba4f7193dd6c8022616ea0894295272062'
            '2fcc284f060cafa9a6df0b010de44833b248169ab11c6289f54227ab87068c29')
arch=('i686' 'x86_64')
depends=('bison' 'flex' 'openssl' 'zlib')
conflicts=('charybdis')
provides=('charybdis')

pkgver() {
  cd "${srcdir}/charybdis"

  git describe --always | sed 's/^v//;s/-/./g'
}

build() {
  cd "${srcdir}/charybdis"

  # Configure the build.
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/charybdis \
    --localstatedir=/var \
    --enable-fhs-paths \
    --with-rundir=/run \
    --with-program-prefix=charybdis-

  # Build it!
  make
}

package() {
  cd "${srcdir}/charybdis"

  # Install the program.
  make DESTDIR="${pkgdir}" install

  # Install the docs.
  install -Dm644 -d docs "${pkgdir}/usr/share/docs/charybdis"


  cd "${srcdir}"

  # Install the service.
  install -Dm644 charybdis.service "${pkgdir}/usr/lib/systemd/system/charybdis.service"
  install -Dm644 charybdis.conf "${pkgdir}/usr/lib/tmpfiles.d/charybdis.conf"
}

# vim: set ts=2 sw=2 et:
