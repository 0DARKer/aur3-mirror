# Maintainer: Gilrain <pierre.buard+aur gmail com>
pkgname=libreoffice-extension-grammalecte-fr
pkgver=0.4.2
pkgrel=1
pkgdesc="French grammar checker extension for LibreOffice."
arch=('any')
url="http://www.dicollecte.org/grammalecte/"
license=('GPL3' 'custom:MPL2' 'LGPL')
depends=('libreoffice-common')
groups=('libreoffice-extensions')
conflicts=('libreoffice-extension-languagetool')
provides=('libreoffice-extension-dictionaries-fr')
makedepends=('unzip')
changelog=changelog
source=("http://www.dicollecte.org/grammalecte/oxt/Grammalecte-v${pkgver}.oxt"
	"https://www.mozilla.org/MPL/2.0/index.txt")
noextract=(Grammalecte-v${pkgver}.oxt)
sha256sums=('523703d11352394b3faafbc3cd3a7d1d8313cb2f0c9b8782cc534ffe985fbd37'
	    'fab3dd6bdab226f1c08630b1dd917e11fcb4ec5e1e020e2c16f83a0a13863e85')

package() {
  install -dm755 ${pkgdir}/usr/lib/libreoffice/share/extensions
  unzip -q ${srcdir}/Grammalecte-v${pkgver}.oxt -d ${pkgdir}/usr/lib/libreoffice/share/extensions/grammalecte

  # fix world writable bit
  find "${pkgdir}/usr/lib/libreoffice/share/extensions/grammalecte/" \( -type d -exec chmod 755 {} \; \) -o \( -type f -exec chmod 644 {} \; \)

  install -Dm644 index.txt ${pkgdir}/usr/share/licences/${pkgname}/LICENSE
}
