# Maintainer: Lukas Braun <koomi+aur at hackerspace-bamberg dot de>
pkgname=s6-dns-musl
pkgver=2.0.0.1
pkgrel=1
pkgdesc="A suite of DNS client programs and libraries for Unix systems."
arch=('i686' 'x86_64')
url="http://www.skarnet.org/software/s6-dns"
license=('custom:ISC')
makedepends=('musl' 'skalibs>=2.1.0.0')
provides=('s6-dns=2.0.0.1')
options=('staticlibs')
changelog=CHANGELOG
source=(${url}/s6-dns-$pkgver.tar.gz)
sha256sums=('4b5e9174d113dc4762170571dc9e8e714585dd3d9403159d04b93d9b9d8e3834')

build() {
  cd "$srcdir/s6-dns-$pkgver"

  CC="musl-gcc" ./configure --enable-static-libc --with-include=/usr/include --bindir=/usr/bin
  make
}

package() {
  cd "$srcdir/s6-dns-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
