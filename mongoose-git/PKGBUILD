# Maintainer: WallaceIT

pkgname=mongoose-git
pkgver=20120817
pkgrel=1
pkgdesc="Small and quick-to-use web server; https/php/cgi support - git development version"
arch=('i686' 'x86_64')
license=('MIT')
optdepends=('php-cgi: for php support')
makedepends=('git')
url="http://code.google.com/p/mongoose/"
conflicts=('mongoose')
provides=('mongoose')
backup=('etc/mongoose/mongoose.conf')
source=(mongoose.conf
        rc.d.mongoose
        mongoose.service)
md5sums=('0a84c31ce93fa230251e40eff0e2d556'
         '2aea8278222400289576a626fd1ef33d'
         '30c2fad1e3130a913b3d8beadbccbdf0')

_gitroot="git://github.com/valenok/mongoose.git"
_gitname="mongoose"

build() {
  cd "$srcdir"

  msg2 "Connecting to GIT server..."
  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg2 "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi
  msg2 "GIT checkout done or server timeout."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  msg2 "Starting build..."
  make linux
}

package() {

  cd "$srcdir/$_gitname-build"

  install -D -m755 mongoose $pkgdir/usr/bin/mongoose
  install -D -m644 mongoose.1 $pkgdir/usr/share/man/man1/mongoose.1

  cd $srcdir

  install -D -m755 rc.d.mongoose $pkgdir/etc/rc.d/mongoose
  install -D -m644 mongoose.conf $pkgdir/etc/mongoose/mongoose.conf
  install -D -m644 mongoose.service $pkgdir/usr/lib/systemd/system/mongoose.service
}

# vim: set ft=sh ts=2 sw=2 et:
