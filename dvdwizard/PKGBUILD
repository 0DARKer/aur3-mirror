# Maintainer:  TDY <tdy@gmx.com>
# Contributor: Gour <gour@gour-nital.com>
# Contributor: Ray Kohler <ataraxia937@gmail.com>
# Contributor: Jan RÃ¼egg <rggjan@gmail.com>
# Contributor: BaSh <bash.lnx@gmail.com>
# Contributor: Bruno Aleci <brunoaleci@gmail.com>

pkgname=dvdwizard
pkgver=0.6.4
pkgrel=1
pkgdesc="A set of bash scripts for converting MPEG streams into DVDs with chapters and menus"
arch=('any')
url="http://dvdwizard.sourceforge.net/"
license=('GPL')
depends=('bc>=1.06' 'dvdauthor>=0.6.14' 'ffmpeg>=0.4.9' 'imagemagick>=6.3.5'
         'mjpegtools>=1.8.0' 'transcode>=1.0.2')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('87b26ba6ee710fdd7a14d0c7152a540e')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i '74,76 d; s,\(mkdir \)\([^-]\),\1-p \2,' Makefile
  make prefix=/usr
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -dm755 "$pkgdir/usr/bin"
  make prefix=/usr INSTALL=/bin/install DESTDIR="$pkgdir" install
}
