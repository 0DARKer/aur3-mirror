# Maintainer: Carson Reynolds <carson@k2.t.u-tokyo.ac.jp>
# Contributor: Mikhail Vorozhtsov <mikhail.vorozhtsov@gmail.com>

pkgname=gcc44
pkgver=4.4.4
pkgrel=2
pkgdesc="The GNU Compiler Collection (4.4.x)"
arch=('i686' 'x86_64')
url="http://gcc.gnu.org"
license=('GPL' 'LGPL' 'custom')
depends=('binutils>=2.20.1' 'mpfr>=2.4.2-2' 'cloog-ppl>=0.15.8' 'zlib' 'libelf')
options=('!libtool')
source=(http://www.mirrorservice.org/sites/sourceware.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.bz2
        gcc-hash-style-both.patch
        gcc_pure64.patch)
md5sums=('7ff5ce9e5f0b088ab48720bbd7203530'
         '6fd395bacbd7b6e47c7b74854b478363'
         '4030ee1c08dd1e843c0225b772360e76')

build() {
  cd "${srcdir}/gcc-${pkgver}" || return 1

  # Do not install libiberty
  sed -i -e 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in || return 1
  # Do not run fixincludes
  sed -i -e 's@\./fixinc\.sh@-c true@' gcc/Makefile.in || return 1

  patch -Np0 -i "${srcdir}/gcc-hash-style-both.patch" || return 1
  if test "${CARCH}" = "x86_64"; then
    patch -Np1 -i "${srcdir}/gcc_pure64.patch" || return 1
  fi

  echo ${pkgver} > gcc/BASE-VER || return 1

  rm -rf build || return 1
  mkdir build || return 1

  cd build || return 1
  ../configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --libdir=/usr/lib \
    --libexecdir=/usr/lib \
    --program-suffix=-4.4 \
    --enable-shared \
    --enable-languages=c,c++,fortran,objc,obj-c++ \
    --enable-__cxa_atexit \
    --disable-libstdcxx-pch \
    --disable-multilib \
    --disable-libgomp \
    --disable-libmudflap \
    --disable-libssp \
    --enable-clocale=gnu \
    --with-tune=generic \
    --with-cloog \
    --with-ppl \
    --with-system-zlib || return 1
  make || return 1
}

package() {
  cd "${srcdir}/gcc-${pkgver}/build" || return 1

  make DESTDIR="${pkgdir}" install || return 1
  for f in "${pkgdir}"/usr/share/info/*.info; do
    n=`basename "$f" | sed -e 's/\.info/-4.4.info/'` || return 1
    mv -f "$f" "${pkgdir}/usr/share/info/$n" || return 1
  done
  rm -rf "${pkgdir}/usr/share/man/man7" || return 1
  rm -rf "${pkgdir}/usr/share/locale" || return 1
  mv "${pkgdir}"/usr/lib/lib* \
     "${pkgdir}"/usr/lib/gcc/${CHOST}/${pkgver}/ || return 1

  # Install Runtime Library Exception
  install -Dm644 ../COPYING.RUNTIME \
    "${pkgdir}/usr/share/licenses/${pkgname}/RUNTIME.LIBRARY.EXCEPTION" \
    || return 1

  # Create links for gcc-4.4 build environment (useful for CUDA)
  mkdir -p $pkgdir/opt/gcc-4.4
  ln -s /usr/bin/gcc-4.4 $pkgdir/opt/gcc-4.4/gcc
  ln -s /usr/bin/g++-4.4 $pkgdir/opt/gcc-4.4/g++
}