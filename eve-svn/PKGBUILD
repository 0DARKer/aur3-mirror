# Maintainer: Doug Newgard <scimmia22 at outlook dot com>
# Contributor: twa022 <twa022 at gmail dot com>

pkgname=eve-svn
_pkgname=eve
pkgver=82811
pkgrel=2
pkgdesc="Lightweight browser based on WebKit and EFL"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('LGPL3')
depends=("elementary>=1.7.99" 'ewebkit-svn')
makedepends=('subversion')
options=('!libtool')
source=("svn+http://svn.enlightenment.org/svn/e/trunk/$_pkgname"
        "eldbus_update.patch")
md5sums=('SKIP'
         '54626b174d4b1766b8c0ef04e88d373e')

pkgver() {
  cd "$srcdir/$_pkgname"

  LC_ALL=C svn info | awk '/Last Changed Rev/ {print $4}'
}

prepare() {
  cd "$srcdir/$_pkgname"

  patch -Np0 < "../eldbus_update.patch"
}

build() {
  cd "$srcdir/$_pkgname"

  ./autogen.sh --prefix=/usr

  make
}

package() {
  cd "$srcdir/$_pkgname"

  make DESTDIR="$pkgdir" install
}

