# Maintainer: Hans Spath (johnLate) <inbox-529@hans-spath.de>

pkgname=aspectc++-svn
pkgver=5.766.254.41
pkgrel=1
pkgdesc="A set of C++ language extensions to facilitate aspect-oriented programming with C/C++"
arch=('i686' 'x86_64')
url="http://aspectc.org/"
license=('GPL') # Puma/COPYING, AspectC++/version.h, Ag++/version.h
depends=('libxml2' 'gcc-libs')
makedepends=('subversion')
provides=('aspectc++')
conflicts=('aspectc++')
# options=(!strip) # keep debug symbols

_svnname=aspectc++ # equal to base directory name in tar of bootstrap version
_bootstrapver=1.0 # 1.1 segfaults
_target=linux-release # linux: with debug information; linux-release: without

source=("$_svnname::svn+https://svn.aspectc.org/repos/AspectC++-Project/trunk"
        "http://aspectc.org/fileadmin/downloads/ac/$_bootstrapver/ac-bin-linux-${_bootstrapver}.tar.gz")
sha512sums=('SKIP'
            'b9968300c0816c0d91a4785eb4486e174f406e069f983bdd50f6654d112a6b2eaba65cfe26a0a1a742cfd9501bc7df7ce1d09259485e2ab4817124893b30c795')


pkgver() {
    cd "$_svnname"

    # incorporate revision numbers of svn externals
    echo -n  $(svnversion           | tr -d [A-z])
    echo -n .$(svnversion Puma      | tr -d [A-z])
    echo -n .$(svnversion AspectC++ | tr -d [A-z])
    echo    .$(svnversion Ag++      | tr -d [A-z])
}

build() {
    cd "$_svnname"

    # -D__STRICT_ANSI__: disables __int128 in cstdlib, fixes build error
    make -C Puma      TARGET="$_target" AC="$(pwd)/ac++" AC_OPTFLAGS="-D__STRICT_ANSI__"
    make -C AspectC++ TARGET="$_target" SHARED=1
    make -C Ag++      TARGET="$_target"
}

package() {
    _bindir="$pkgdir"/usr/bin

    install -dm 755 "$_bindir"

    cd "$_svnname"

    make -C Puma install PREFIX="$pkgdir"/usr

    install -Dm 755 ./AspectC++/bin/"$_target"/ac++ "$_bindir"/
    install -Dm 755      ./Ag++/bin/"$_target"/ag++ "$_bindir"/

    # TODO: docs
}

# vim:set ts=4 sw=4 et:
