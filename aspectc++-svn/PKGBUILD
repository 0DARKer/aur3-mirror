# Maintainer: Hans Spath (johnLate) <inbox-529@hans-spath.de>

pkgname=aspectc++-svn
pkgver=5.789.577.47
pkgrel=1
pkgdesc="A set of C++ language extensions to facilitate aspect-oriented programming with C/C++"
arch=('i686' 'x86_64')
url="http://aspectc.org/"
license=('GPL') # Puma/COPYING, AspectC++/version.h, Ag++/version.h
depends=('libxml2' 'gcc-libs')
makedepends=('subversion')
provides=('aspectc++')
conflicts=('aspectc++')
# options=(!strip) # keep debug symbols

_svnname=aspectc++ # equal to base directory name in tar of bootstrap version
_bootstrapver=1.2
_target=linux-release # linux: with debug information; linux-release: without

source=("$_svnname::svn+https://svn.aspectc.org/repos/AspectC++-Project/trunk"
        "ftp://akut.aspectc.org/releases/$_bootstrapver/ac-bin-linux-x86-64bit-${_bootstrapver}.tar.gz")
sha512sums=('SKIP'
            'c68992f4867236a160b86fd5923058905f48b25fccd0bb8bafea832ac59bfb956cad56ce03917508ec0518bd0969a39f5ac658c1ef1239c1733b64e95684d17b')


pkgver() {
    cd "$_svnname"

    # incorporate revision numbers of svn externals
    echo -n  $(svnversion           | tr -d [A-z])
    echo -n .$(svnversion Puma      | tr -d [A-z])
    echo -n .$(svnversion AspectC++ | tr -d [A-z])
    echo    .$(svnversion Ag++      | tr -d [A-z])
}

build() {
    cd "$_svnname"

    # -D__STRICT_ANSI__: disables __int128 in cstdlib, fixes build error
    make -C Puma      TARGET="$_target" AC="$(pwd)/ac++" AC_OPTFLAGS="-D__STRICT_ANSI__"
    make -C AspectC++ TARGET="$_target" SHARED=1
    make -C Ag++      TARGET="$_target"
}

package() {
    _bindir="$pkgdir"/usr/bin

    install -dm 755 "$_bindir"

    cd "$_svnname"

    make -C Puma install PREFIX="$pkgdir"/usr

    install -Dm 755 ./AspectC++/bin/"$_target"/ac++ "$_bindir"/
    install -Dm 755      ./Ag++/bin/"$_target"/ag++ "$_bindir"/

    # TODO: docs
}

# vim:set ts=4 sw=4 et:
