# Contributor: Libernux <dutchman55@gmx.com>
pkgname="brother-hl2130"
pkgver="2.1.0"
pkgrel=1
pkgdesc="LPR and CUPS driver for the Brother HL2130"
url="http://solutions.brother.com/linux/en_us/"
arch=('i686' 'x86_64')
license=('custom:brother commercial license')
depends=('cups')
install='brother-hl2130.install'
source=(
	"http://www.brother.com/pub/bsc/linux/dlf/hl2130lpr-2.1.0-1.i386.rpm"
	"http://www.brother.com/pub/bsc/linux/dlf/cupswrapperHL2130-2.0.4-2.i386.rpm"
	'cupswrapper-license.txt'
	'lpr-license.txt'
)
md5sums=(
	'fe8d1a4c20e6dc30529218f48d623fe4'
	'04dba3074c076be54c24fda8f6f2f630'
	'97ad0cffd216059e9d1d3121899d8646'
	'5e87a3dc0f3e3438c088eda0f3565f0d'
)
build() {
	cd "$srcdir/usr/local/Brother/Printer/HL2130/cupswrapper"
#  generate the ppd
	echo '#! /bin/sh' > genppd
	echo 'ppd_file_name=HL2130.ppd' >> genppd
	echo 'touch $ppd_file_name' >> genppd
	sed -n '/^cat <<ENDOFPPDFILE/,/^ENDOFPPDFILE/p' cupswrapperHL2130-2.0.4 >>genppd
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' genppd
	chmod +x genppd
	./genppd
#  generate the wrapper
	echo '#! /bin/sh' > genwrapper
	sed -n '/^printer_model/,/^device_model/p' cupswrapperHL2130-2.0.4 >>genwrapper
	echo 'brotherlpdwrapper=brlpdwrapperHL2130' >>genwrapper
	echo 'touch $brotherlpdwrapper' >>genwrapper
	sed -n '/cat <<!ENDOFWFILTER!/,/!ENDOFWFILTER!/p' cupswrapperHL2130-2.0.4 >>genwrapper
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' genwrapper
	chmod +x genwrapper
	./genwrapper
#  generate other
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' "$srcdir/usr/local/Brother/Printer/HL2130/cupswrapper/brcupsconfig4"
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' "$srcdir/usr/local/Brother/Printer/HL2130/inf/braddprinter"
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' "$srcdir/usr/local/Brother/Printer/HL2130/inf/brprintconflsr3"
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' "$srcdir/usr/local/Brother/Printer/HL2130/lpd/filterHL2130"
	install -d "$srcdir/usr/share"
	mv "$srcdir/usr/local/Brother/" "$srcdir/usr/share/brother"
	rm "$srcdir/usr/share/brother/Printer/HL2130/cupswrapper/cupswrapperHL2130-2.0.4"
	rm "$srcdir/usr/share/brother/Printer/HL2130/cupswrapper/genppd"
	rm "$srcdir/usr/share/brother/Printer/HL2130/cupswrapper/genwrapper"
	rm "$srcdir/usr/share/brother/Printer/HL2130/inf/setupPrintcap2"
	rm -rf "$srcdir/usr/local"
}
package() {
	cd "$pkgdir"
	cp -R "$srcdir/usr" "$pkgdir"
	cp -R "$srcdir/var" "$pkgdir"
	install -m 644 -D "$srcdir/usr/share/brother/Printer/HL2130/cupswrapper/HL2130.ppd" "$pkgdir/usr/share/cups/model/HL2130.ppd"
	install -m 755 -D "$srcdir/usr/share/brother/Printer/HL2130/cupswrapper/brlpdwrapperHL2130" "$pkgdir/usr/lib/cups/filter/brlpdwrapperHL2130"
	install -m 644 -D "$startdir"/cupswrapper-license.txt "${pkgdir}/usr/share/licenses/${pkgname}/cupswrapper-licence.txt"
	install -m 644 -D "$startdir"/lpr-license.txt "${pkgdir}/usr/share/licenses/${pkgname}/lpr-licence.txt"
}