# Maintainer:  danyf90 <daniele.formichelli@gmail.com>
# Contributor: Philipp 'TamCore' B. <philipp [at] tamcore [dot] eu>
# Contributor: Jakub Schmidtke <sjakub-at-gmail-dot-com>
# Contributor: Christoph Brill <egore911-at-gmail-dot-com>
# Contributor: Lubomir 'Kuci' Kucera <kuci24-at-gmail-dot-com>

pkgname=android-studio-dev
_pkgname=android-studio
pkgver=0.3.2
pkgrel=1
_build=132.893413
pkgdesc="A new Android development environment based on IntelliJ IDEA. Development branch"
arch=('i686' 'x86_64')
url="http://developer.android.com/sdk/installing/studio.html"
license=('APACHE')
depends=('android-sdk' 'fontconfig' 'java-environment' 'libxrender' 'mesa' 'python')
makedepends=('unzip' 'xorg-server-xvfb' 'ttf-dejavu')
optdepends=('android-sdk-platform-tools' 'android-sdk-build-tools')
provides=('android-studio=$pkgver')
conflicts=('android-studio' 'android-studio-beta' 'android-studio-canary')
options=('!strip')
install=$pkgname.install
source=(
	"http://dl-ssl.google.com/dl/android/studio/ide-zips/android-studio-ide-$_build-linux.zip"
	"$_pkgname.desktop")
sha512sums=(
	'7f0921ad8ca0f5d235d61b5bb8ad81d0f67641a04c1eabb0b17ec35a44cef8baee61aa8d85898556f98eb174da6446d6ecf00f53d1a0ae5c411159487e4fd9a5'
	'fa3567f328af45d265c4fabf3e41b55d8e8cccfa9675e745f07dd6ae41950dd53a2ef41ef1caee86643f5c2ddf7a7681ee17155e208a7b6fdae6c0537dfc0c94')

[ $CARCH = "x86_64" ] && depends=('python' 'lib32-fontconfig' 'lib32-mesa' 'lib32-libxrender' 'android-sdk')

prepare() {
	cd $srcdir/$_pkgname

	# extract the application icon
	unzip -qo lib/resources.jar artwork/icon_AS_128.png

	# enable anti aliasing
	echo "-Dswing.aatext=true" >> studio.vmoptions
	echo "-Dswing.aatext=true" >> studio64.vmoptions
}

package() {
	cd $srcdir/$_pkgname

	# application stuff
	install -d $pkgdir/{opt/$_pkgname,usr/bin}
	cp -a bin lib plugins $pkgdir/opt/$_pkgname
	ln -s /opt/android-studio/bin/studio.sh $pkgdir/usr/bin/android-studio

	# starter stuff
	install -Dm655 artwork/icon_AS_128.png $pkgdir/usr/share/pixmaps/$_pkgname.png
	install -Dm655 $srcdir/$_pkgname.desktop $pkgdir/usr/share/applications/$_pkgname.desktop

	chmod -R ugo+rX $pkgdir/opt
}