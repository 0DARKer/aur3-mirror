# Maintainer:   Lucky <archlinux@builds.lucky.li>

pkgname=vzctl-stable-git
_pkgname="${pkgname%-*-*}"
pkgver=20120519
_pkgver="3.2"
pkgrel=1
pkgdesc="OpenVZ containters control utility"
url="http://wiki.openvz.org/Download/vzctl"
license=("GPL")
arch=("i686" "x86_64")
depends=("iproute" "rsync" "openssh" "ploop")
makedepends=("git")
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=("openvz")
md5sums=("e997b5b1c241c7d8c779d7d93863c2d7")

_gitroot="git://git.openvz.org/pub/${_pkgname}"
_gitname="${_pkgname}"
_gitbranch="${_pkgver}-${pkgname:6:6}"

build() {
  cd "${srcdir}"

  #GIT {{{
  msg "Connecting to GIT server..."
  if [ -d "${_gitname}" ]; then
    (cd "${_gitname}" && git pull origin)
    msg "The local repository was updated."
  else
    git clone -b "${_gitbranch}" "${_gitroot}" "${_gitname}"
    msg "The remote repository was cloned."
  fi
  msg "GIT sync done or server timeout."
  cd "${srcdir}"
  if [ -d "${_gitname}-build" ]; then
    rm -rf "${_gitname}-build"
  fi
  cp -rf "${_gitname}" "${_gitname}-build"
  cd "${_gitname}-build"
  #}}}

  ./autogen.sh
  ./configure --prefix=/usr --enable-bashcomp --enable-logrotate --disable-static
  make
}

package() {
  cd "${srcdir}/${_pkgname}-build"

  install -Dm755 "${srcdir}/openvz" "${pkgdir}/etc/rc.d/openvz"
  make DESTDIR="${pkgdir}" install
}
