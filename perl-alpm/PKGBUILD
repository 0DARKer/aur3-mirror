# Packager: Justin Davis (juster) <jrcd83@gmail.com>

pkgname=perl-alpm
pkgver=3.00
pkgrel=1
pkgdesc='ArchLinux Package Manager backend library.'
arch=(i686 x86_64)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl 'pacman>=4.0')
url=https://metacpan.org/release/ALPM
source=("http://search.cpan.org/CPAN/authors/id/J/JU/JUSTER/ALPM-$pkgver.tar.gz")
md5sums=(40cff9b699ef7717334dba6149fe2d4c)
sha512sums=(ebc60e08b69209b3b92385780f9cb1068a4feefcbc69bfa4830fe4199e44c1c388abe34c9c4238f40065c936500def2f466730b428485b3b6f99ea7117a97bcc)
_dir="$srcdir/ALPM-$pkgver"

build()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$_dir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
