# Contributor: matthiaskrgr <matthiaskrgr _strange_curverd_character_ freedroid D0T org>

pkgname=gource-git
pkgver=20120212
pkgrel=1
pkgdesc="software version control visualization"
license=(GPL3)
arch=(i686 x86_64)
url=http://code.google.com/p/gource/
makedepends=('git' 'autoconf' 'boost' 'glmath')
depends=('sdl_image' 'pcre' 'glew' 'boost-libs' 'freetype2')
conflicts=('gource')
provides=('gource')
changelog=changelog
source=('changelog')
sha1sums=('aa73caf2f06aa34035768d684cd3651f923a4374')

_gitroot="git://github.com/acaudwell/Gource.git"
_gitname="gource"

build() {
	cd "$srcdir"
	msg "Connecting to the GIT server...."

	if [[ -d $srcdir/$_gitname ]] ; then
		cd $_gitname
		git pull origin
		msg "The local files are updated."
	else
		git clone  $_gitroot $_gitname --recursive --depth 1
	fi

	msg "GIT checkout done"
	msg "Starting make..."
	cd "$srcdir"/"$_gitname"

	msg "Running autoreconf..."
	autoreconf -f -i
	msg "Running configure..."
	./configure --prefix=/usr
	msg "Running make..."
	make
}

package() {
	cd "$srcdir"/"$_gitname"
	make DESTDIR=$pkgdir install
	msg "Compiling done."
}
