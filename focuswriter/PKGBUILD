# Maintainer: Graeme Gott <graeme@gottcode.org>

pkgname=focuswriter
pkgver=1.4.2
pkgrel=2
pkgdesc="A simple fullscreen word processor"
arch=('i686' 'x86_64')
url="http://gottcode.org/${pkgname}/"
license=('GPL3')
if [ -e /usr/bin/qmake ] || [ ! -e /usr/bin/qmake-qt4 ]; then
  depends=('qt5-base' 'enchant' 'libzip')
else
  depends=('qt4' 'enchant' 'libzip')
fi
optdepends=('sdl_mixer: typewriter sound effects')
install=${pkgname}.install
source=(http://gottcode.org/${pkgname}/${pkgname}-${pkgver}-src.tar.bz2)
md5sums=('a43eb5b326f62906aaa2de120f94d636')
sha256sums=('f41ccf226cb5e4181162d37b2a4b5598be202bb184e2f7c26412dc371c9ef328')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  if [ -e /usr/bin/qmake ]; then
    /usr/bin/qmake PREFIX=/usr
  else
    qmake-qt4 PREFIX=/usr
  fi
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make INSTALL_ROOT="${pkgdir}/" install
}
