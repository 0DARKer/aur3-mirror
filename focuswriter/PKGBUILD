# Maintainer: Graeme Gott <graeme@gottcode.org>

pkgname=focuswriter
pkgver=1.4.4
pkgrel=1
pkgdesc="A simple fullscreen word processor"
arch=('i686' 'x86_64')
url="http://gottcode.org/${pkgname}/"
license=('GPL3')
if [ -e /usr/bin/qmake-qt5 ] || [ ! -e /usr/bin/qmake-qt4 ]; then
  depends=('qt5-base' 'enchant' 'libzip')
else
  depends=('qt4' 'enchant' 'libzip')
fi
optdepends=('sdl_mixer: typewriter sound effects')
install=${pkgname}.install
source=(http://gottcode.org/${pkgname}/${pkgname}-${pkgver}-src.tar.bz2)
md5sums=('f9145606596dbd0fe66cdefc5c1754c2')
sha256sums=('b574d6b76609078e36c38dd3b632cdfe1360bfb3a71221c3afc89b1c01d7670f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  if [ -e /usr/bin/qmake-qt5 ]; then
    qmake-qt5 PREFIX=/usr
  else
    qmake-qt4 PREFIX=/usr
  fi
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make INSTALL_ROOT="${pkgdir}/" install
}
