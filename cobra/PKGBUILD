# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Johannes Krampf <mail@johkra.de>

pkgname=cobra
pkgver=0.9.4
pkgrel=1
epoch=2
pkgdesc="Clean and expressive language for .NET and Mono"
arch=('i686' 'x86_64')
url="http://cobra-language.com/"
license=('custom:MIT')
depends=(mono)
install=cobra.install
source=(http://cobra-language.com/downloads/Cobra-$pkgver.tar.gz install.patch)
sha512sums=('a20a634d1cd770816e7c42e92cb00fac5420aecbf5238994f8ff9ba5951b7d171650aa44f464c71f6fe94833a5a52811e23938c27b60d53947510329f8ef1b0f'
            '6cbf59910799e809c2df8a416a9de915a754fc60b7a08e9f4d93ee45a6a34fffc42eb469c5d1cc8547858d9149a86b6ffcc83216cd3c50f7189b6a9e622052a3')

prepare() {
	cd Cobra-$pkgver
	patch -p1 < "$srcdir"/install.patch

	cd Source
	echo "$pkgdir"/opt/cobra > install-directory.text
}

package() {
	cd Cobra-$pkgver/Source

	yes | bin/install-from-workspace

	cd "$pkgdir"

	sed -i "s|$pkgdir||" opt/cobra/bin/cobra

	install --directory usr/bin
	ln -s /opt/cobra/bin/cobra usr/bin/cobra

	install --directory usr/share/licenses/$pkgname
	cp "$srcdir"/Cobra-$pkgver/License.text usr/share/licenses/$pkgname/
}
