# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Arch Haskell Team <>

pkgname=haskell-authenticate
_hkgname=authenticate
_licensefile=LICENSE
pkgver=1.3.0.1
pkgrel=1
pkgdesc="Authentication methods for Haskell web applications."
url="http://github.com/snoyberg/authenticate/tree/master"
license=('BSD3')
arch=('i686' 'x86_64')
makedepends=('ghc')
depends=('haskell-aeson'
         'haskell-attoparsec'
         'haskell-blaze-builder'
         'haskell-blaze-conduit'
         'haskell-bytestring'
         'haskell-case-insensitive'
         'haskell-conduit'
         'haskell-containers'
         'haskell-http-conduit'
         'haskell-http-types'
         'haskell-monad-control'
         'haskell-network'
         'haskell-resourcet'
         'haskell-tagsoup'
         'haskell-text'
         'haskell-transformers'
         'haskell-unordered-containers'
         'haskell-xml-conduit')
options=('strip')
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
install="${pkgname}.install"
sha256sums=('83b6113582f24009138aff021819a424281e26439a649b83e6aa423045cc418b')

build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    
    runhaskell Setup configure -O ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } --enable-split-objs --enable-shared \
        --prefix=/usr --docdir=/usr/share/doc/${pkgname} \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 ${_licensefile} ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/${_licensefile}
}
