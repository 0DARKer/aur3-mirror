# Contributor: Bernhard Walle   <bwalle.de: bernhard            >
# Contributor: Jesse Jaara      <gmail.com, mail.ru: jesse.jaara>
# Contributor: Armin Wehrfritz
# Maintainer:  Patrick McCarty  <pnorcks at gmail dot com>

pkgname=osc
pkgver=0.139.0
_gittaghash=0110d3d
pkgrel=1
pkgdesc="Command line client for the openSUSE Build Service"
arch=(any)
url="http://openbuildservice.org/"
license=('GPL')
depends=('python2'
         'python2-m2crypto'
         'urlgrabber')
optdepends=('obs-build: required to run local builds'
            'obs-service-format_spec_file: for running the format_spec_file source service')
conflicts=('osc-git')
source=(https://github.com/openSUSE/${pkgname}/tarball/${pkgver})
sha256sums=('2df6699f380f54f850d24c709fa646b57ccceb4b1e5007b4b3bc131ad894a9a3')

build() {
  cd "${srcdir}/openSUSE-${pkgname}-${_gittaghash}"

  python2 setup.py build
}

package() {
  cd "${srcdir}/openSUSE-${pkgname}-${_gittaghash}"

  python2 setup.py install --root="${pkgdir}/" --optimize=1
  cd "${pkgdir}/usr/bin"
  ln -s osc-wrapper.py osc
}
