# Contributor: Bernhard Walle   <bwalle.de: bernhard            >
# Contributor: Jesse Jaara      <gmail.com, mail.ru: jesse.jaara>
# Contributor: Armin Wehrfritz
# Maintainer:  Patrick McCarty  <pnorcks at gmail dot com>
 
pkgname=osc
pkgver=0.137.0
_gittaghash=d1bc8ad
pkgrel=1
pkgdesc="Command line client for the openSUSE Build Service"
arch=(any)
url="http://openbuildservice.org/"
license=('GPL')
depends=('python2' 'python-m2crypto' 'urlgrabber')
optdepends=('osc-build-git: required to run local builds')
conflicts=('osc-git')
source=(https://github.com/openSUSE/${pkgname}/tarball/${pkgver})
sha256sums=('88644b2d5ad4ad408d5a20b2520fcb3d60d3b1f92647fd35d0a0c39ecd9c3955')

build() {
  cd "${srcdir}/openSUSE-${pkgname}-${_gittaghash}"
 
  python2 setup.py build
}
 
package() {
  cd "${srcdir}/openSUSE-${pkgname}-${_gittaghash}"
 
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  cd "${pkgdir}/usr/bin"
  ln -s osc-wrapper.py osc
}
