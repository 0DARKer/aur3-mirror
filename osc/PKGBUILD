# Contributor: Bernhard Walle   <bwalle.de: bernhard            >
# Contributor: Jesse Jaara      <gmail.com, mail.ru: jesse.jaara>
# Contributor: Armin Wehrfritz
# Maintainer:  Patrick McCarty  <pnorcks at gmail dot com>
# AUR Category: devel
 
##########################################
# !!! Build with --holdver option, e.g.: #
# makepkg --holdver --syncdeps --log     #
##########################################
 
_srcver=0.133.1
 
pkgname=osc
pkgver=${_srcver}
pkgrel=2
pkgdesc="Command line client for the openSUSE Build Service"
arch=(any)
url="http://build.opensuse.org"
license=('GPL')
depends=('python2' 'python-m2crypto' 'urlgrabber')
makedepends=('git')
optdepends=('osc-build-git: required to run local builds')
conflicts=('osc-git')
source=()
md5sums=()

_gitroot="git://github.com/openSUSE/osc.git"
_gitname="osc"
 
build() {
  cd "${srcdir}"
  msg "Connecting to GIT server..."

  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}" && git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  git checkout ${_srcver} --
 
  python2 setup.py build
}
 
package() {
  cd "${srcdir}/${_gitname}-build"
 
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  cd "${pkgdir}/usr/bin"
  ln -s osc-wrapper.py osc
}
