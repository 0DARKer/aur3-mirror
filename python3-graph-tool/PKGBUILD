# Maintainer: Daniel MÃ¼llner <muellner@math.stanford.edu>

# Remove the --enable-openmp flag below if you do not want parallelization.

# Since the current stable release 2.2.18 does not work with Python3, this
# package uses the latest development snapshot. Future versions will use
# the stable releases when they become compatible with Python3.

pkgname=python3-graph-tool
pkgver=20121219
pkgrel=1
pkgdesc='A Python module for manipulation and statistical analysis of graphs'
arch=('i686' 'x86_64')
url='http://projects.skewed.de/graph-tool/'
license=(GPL3)
depends=(gcc boost python3 expat python3-scipy python3-numpy cgal graphviz)
makedepends=(git)
optdepends=('cairomm: graph drawing'
'python-cairo: graph drawing'
'python-matplotlib: graph drawing')
options=(!libtool)
source=()
sha256sums=()

_gitroot='git://git.skewed.de/graph-tool'
_gitname='graph-tool'

build() {
  cd "$srcdir"
  msg "Connecting to GIT server..."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files have been updated."
  else
    git clone $_gitroot
  fi

  msg "Configure..."
  cd "$srcdir/$_gitname"
  ./autogen.sh
  ./configure --enable-openmp --prefix='/usr' PYTHON=python3 PYTHON_LDFLAGS='-L/usr/lib -lpython3.3m' --with-boost-python=boost_python3
  make
}

package() {
  cd "$srcdir/$_gitname"
  # Remove information about the source directory, which is temporary.
  sed -i 's/#define PACKAGE_SOURCE_DIR ".*"/#define PACKAGE_SOURCE_DIR ""/' config.h
  #sed -i "s/#define PACKAGE_DOC_DIR \"\/usr\/doc\/graph-tool\"/#define PACKAGE_DOC_DIR \"\/usr\/share\/doc\/$pkgname\"/" config.h
  make install DESTDIR="$pkgdir/" PACKAGE_DOC_DIR="/usr/share/doc/$pkgname"
}
