# Maintainer: Daniel MÃ¼llner <muellner@math.stanford.edu>

# Remove the --enable-openmp flag below if you do not want parallelization.

pkgname=python3-graph-tool
pkgver=2.2.19
pkgrel=1
pkgdesc='A Python module for manipulation and statistical analysis of graphs'
arch=('i686' 'x86_64')
url='http://projects.skewed.de/graph-tool/'
license=(GPL3)
depends=(boost python3 expat python3-scipy python3-numpy cgal graphviz)
makedepends=(sparsehash)
optdepends=('cairomm: graph drawing'
'python-cairo: graph drawing'
'python-matplotlib: graph drawing')
options=(!libtool)
source=("http://downloads.skewed.de/graph-tool/graph-tool-$pkgver.tar.bz2")
sha256sums=(7caf1d120cffe8080dcfb2d6880b47bfee05a84956ee73a5300d167864df9e7b)

build() {
  cd "$srcdir/graph-tool-$pkgver"
  ./configure --enable-openmp --prefix='/usr' --docdir="/share/doc/$pkgname" PYTHON=python3 PYTHON_LDFLAGS='-L/usr/lib -lpython3.3m' --with-boost-python=boost_python3
  make
}

package() {
  cd "$srcdir/graph-tool-$pkgver"
  # Remove information about the source directory, which is temporary.
  sed -i 's/#define PACKAGE_SOURCE_DIR ".*"/#define PACKAGE_SOURCE_DIR ""/' config.h
  make DESTDIR="$pkgdir/" install
}
