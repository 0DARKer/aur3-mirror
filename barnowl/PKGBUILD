# Maintainer: karnath <karnathtorjian@gmail.com>
# Contributor: TDY <tdy@gmx.com>

pkgname=barnowl
pkgver=1.8.1
pkgrel=1
pkgdesc="A console chat client for the AIM, IRC, Jabber, Zephyr, Twitter"
arch=('x86' 'x86_64')
url="http://barnowl.mit.edu/"
license=('BSD' 'GPL')
depends=('glib-perl' 'perl-authen-sasl' 'perl-digest-sha1'
         'perl-io-socket-ssl' 'perl-class-accessor' 'perl-net-dns'
         'perl-par' 'perl-json' 'perl-net-twitter-lite'
         'perl-text-autoformat' 'perl-anyevent' 
         'perl-anyevent-irc' 'zephyr')
makedepends=('zip')
source=(http://$pkgname.mit.edu/dist/$pkgname-$pkgver-src.tgz)
sha256sums=('68e113d91ae62fe283de3162d80fcca08cedc11e338d449fae6a3e2045a602c0')

build() {
  cd $srcdir/$pkgname-$pkgver-src
  sed -i "s/ncursesw\///" owl.h
  sed -i "s/ncursesw\///" tester.c
  ./configure --prefix=/usr --includedir=/usr/include
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver-src
  make DESTDIR=$pkgdir install
  install -dm755 $pkgdir/usr/share/{$pkgname/examples,doc/$pkgname}
  install -m644 examples/* $pkgdir/usr/share/$pkgname/examples/
  install -m644 doc/*.txt $pkgdir/usr/share/doc/$pkgname/
  install -Dm644 doc/$pkgname.1 "$pkgdir/usr/share/man/man1/$pkgname.1"
  install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}
