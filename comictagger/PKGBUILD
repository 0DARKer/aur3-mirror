# Contributor: Peter van Liesdonk <peter@liesdonk.nl>

pkgname=comictagger
pkgver=1.1.14_beta
pkgrel=1
pkgdesc="A cross-platform GUI/CLI app for writing metadata to comic archives."
url="https://code.google.com/p/comictagger/"
arch=('any')
license=('Apache')
depends=("python2-configparser" "python2-beautifulsoup4" "python2-pillow" "python2-pyqt4")
makedepends=('python2-setuptools')
options=!strip
source=("https://googledrive.com/host/0Bw4IursaqWhhOHF6Wk9ab3FkejQ/${pkgver//_/-}/$pkgname-${pkgver//_/-}.zip")
sha256sums=('59bab5a1ec7dfba0e68b25aa1079872e3dd99854ee9fe6c82f5aa058b1600c3c')


build() {
  cd "${srcdir}"/$pkgname-${pkgver//_/-}

  python2 setup.py build
}

package() {
  cd "${srcdir}"/$pkgname-${pkgver//_/-}

  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1

  sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    $(find "${pkgdir}" -name '*.py')

}
