# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgname=vim-bash-support
pkgver=3.12.1
_srcid=19532
pkgrel=2
pkgdesc='BASH-IDE - Write and run BASH-scripts using menus and hotkeys. '
arch=('any')
url='http://vim.sourceforge.net/scripts/script.php?script_id=365'
license=('custom:vim')
depends=('vim-vim-support')
optdepends=('bashdb: bash debugger')
install='vimdoc.install'
source=("${pkgname}-${pkgver}.zip::http://www.vim.org/scripts/download_script.php?src_id=$_srcid")
noextract=("vim-bash-support-${pkgver}.zip")
sha256sums=('1d0995ee8fe31fcc0e5e9f6cc4b942830b137608eb2fb1034495d0466589e181')

package() {
  _DESTDIR="$pkgdir"/usr/share/vim/vimfiles

  install -dm755 "${_DESTDIR}"
  bsdtar -xf ${pkgname}-${pkgver}.zip -C "${_DESTDIR}" \
    --exclude "doc/templatesupport.txt" \
    --exclude "autoload/mmtemplates/core.vim"

  # Fix perms
  find "${_DESTDIR}" -type d -print0|xargs -0 chmod 0755
  find "${_DESTDIR}" -type f -print0|xargs -0 chmod 0644
}
