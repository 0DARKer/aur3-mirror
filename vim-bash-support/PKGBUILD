# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgname=vim-bash-support
pkgver=3.12
_srcid=19313
pkgrel=1
pkgdesc='BASH-IDE - Write and run BASH-scripts using menus and hotkeys. '
arch=('any')
url='http://vim.sourceforge.net/scripts/script.php?script_id=365'
license=('custom:vim')
depends=('vim')
optdepends=('bashdb: bash debugger')
install='vimdoc.install'
source=("vim-bash-support-${pkgver}.zip::http://www.vim.org/scripts/download_script.php?src_id=$_srcid")
sha256sums=('caf5ca8a0a718d68030bd7787c4a2aa3010954ebb4113bc0460c66749f351f38')

package() {
  cd "$srcdir"

  _VIM_FILES_DIR="$pkgdir"/usr/share/vim/vimfiles/

  install -dm755 "${_VIM_FILES_DIR}"

  find {bash-support,doc,ftplugin,plugin} -type d \
    -exec install -dm755 "${_VIM_FILES_DIR}/{}" \;
  find {bash-support,doc,ftplugin,plugin} -not -type d \
    -exec install -m644 "{}" "${_VIM_FILES_DIR}/{}" \;
}
