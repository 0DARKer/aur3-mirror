# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgname=vim-bash-support
pkgver=4.1
_srcid=21180
pkgrel=1
pkgdesc='BASH-IDE - Write and run BASH-scripts using menus and hotkeys. '
arch=('any')
url='http://vim.sourceforge.net/scripts/script.php?script_id=365'
license=('custom:vim')
depends=('vim-vim-support')
optdepends=('bashdb: bash debugger')
install='vimdoc.install'
source=("${pkgname}-${_srcid}.zip::http://www.vim.org/scripts/download_script.php?src_id=$_srcid")
noextract=("${pkgname}-${_srcid}.zip")
sha256sums=('0c1c97fbdb1bb8a3488b58e9bee0c7123e295124083de631c84a483deac6daa7')

package() {
  _DESTDIR="$pkgdir"/usr/share/vim/vimfiles

  install -dm755 "${_DESTDIR}"
  bsdtar -xf ${pkgname}-${_srcid}.zip -C "${_DESTDIR}" \
    --exclude "autoload/mmtemplates/core.vim" \
    --exclude "doc/templatesupport.txt" \
    --exclude "syntax/template.vim"

  # Fix perms
  find "${_DESTDIR}" -type d -print0 | xargs -0 chmod 0755
  find "${_DESTDIR}" -type f -print0 | xargs -0 chmod 0644
}
